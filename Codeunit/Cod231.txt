OBJECT Codeunit 231 Gen. Jnl.-Post
{
  OBJECT-PROPERTIES
  {
    Date=03/08/10;
    Time=16:31:49;
    Version List=NAVW13.00,ISA6.00;
  }
  PROPERTIES
  {
    TableNo=81;
    OnRun=BEGIN
            GenJnlLine.COPY(Rec);
            Code;
            Rec.COPY(GenJnlLine);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=cannot be filtered when posting recurring journals;FRA=ne peut pas ˆtre filtr‚(e) lors de la validation de feuilles abonnement';
      Text001@1001 : TextConst 'ENU=Do you want to post the journal lines?;FRA=Souhaitez-vous valider les lignes de la feuille ?';
      Text002@1002 : TextConst 'ENU=There is nothing to post.;FRA=Il n''y a rien … valider.';
      Text003@1003 : TextConst 'ENU=The journal lines were successfully posted.;FRA=Les lignes de la feuille ont ‚t‚ valid‚es avec succŠs.';
      Text004@1004 : TextConst 'ENU=The journal lines were successfully posted. You are now in the %1 journal.;FRA=Les lignes de la feuille ont ‚t‚ valid‚es avec succŠs. Vous vous trouvez maintenant dans la feuille %1.';
      GenJnlTemplate@1005 : Record 80;
      GenJnlLine@1006 : Record 81;
      GenJnlPostBatch@1007 : Codeunit 13;
      TempJnlBatchName@1008 : Code[10];
      Text005@8016602 : TextConst 'ENU=Using %1 for Declining Balance may result in misleading numbers for subsequent years. You should manually check the postings and correct if necessary.';

    LOCAL PROCEDURE Code@1();
    VAR
      FALedgEntry@8016602 : Record 5601;
    BEGIN
      WITH GenJnlLine DO BEGIN
        GenJnlTemplate.GET("Journal Template Name");
        GenJnlTemplate.TESTFIELD("Force Posting Report",FALSE);
        IF GenJnlTemplate.Recurring AND (GETFILTER("Posting Date") <> '') THEN
          FIELDERROR("Posting Date",Text000);

        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      //<<ISA01.ST
        IF "Account Type" = "Account Type"::"Fixed Asset" THEN BEGIN
          FALedgEntry.SETRANGE("FA No.","Account No.");
          FALedgEntry.SETRANGE("FA Posting Type","FA Posting Type"::"Acquisition Cost");
          IF FALedgEntry.FINDFIRST AND "Depr. Acquisition Cost" THEN
            IF NOT CONFIRM(Text005,FALSE,FIELDCAPTION("Depr. Acquisition Cost")) THEN
              EXIT;
        END;
      //>>ISA01.ST
        TempJnlBatchName := "Journal Batch Name";

        GenJnlPostBatch.RUN(GenJnlLine);

        IF "Line No." = 0 THEN
          MESSAGE(Text002)
        ELSE
          IF TempJnlBatchName = "Journal Batch Name" THEN
            MESSAGE(Text003)
          ELSE
            MESSAGE(
              Text004,
              "Journal Batch Name");

        IF NOT FIND('=><') OR (TempJnlBatchName <> "Journal Batch Name") THEN BEGIN
          RESET;
          FILTERGROUP(2);
          SETRANGE("Journal Template Name","Journal Template Name");
          SETRANGE("Journal Batch Name","Journal Batch Name");
          FILTERGROUP(0);
          "Line No." := 1;
        END;
      END;
    END;

    BEGIN
    {
      /**@r ISA01 @d 03/08/10 @a ISAT.ST @v ISA6.00.00.09 @s Correctif MS
         @c KB980559 Controle ‚critures immos */
    }
    END.
  }
}