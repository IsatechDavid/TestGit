OBJECT Codeunit 410 Update Analysis View
{
  OBJECT-PROPERTIES
  {
    Date=14/10/09;
    Time=12:00:00;
    Version List=NAVW16.00.01,ISA6.00;
  }
  PROPERTIES
  {
    TableNo=363;
    Permissions=TableData 17=r,
                TableData 363=rm,
                TableData 364=r,
                TableData 365=rimd,
                TableData 366=rimd;
    OnRun=BEGIN
            MaxNumber := 2147483647;
            MaxSimulNumber := -2147483647; // ISA02.ISAT.MA
            IF Code <> '' THEN
              IF CONFIRM(Text000,TRUE,TABLECAPTION,Code) THEN BEGIN
                Rec.MODIFY;
                UpdateOne(Rec,2,TRUE);
              END;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Do you want to update %1 %2?;FRA=Souhaitez-vous mettre … jour %1 %2 ?';
      Text005@1001 : TextConst 'ENU=Analysis View     #1############################\\;FRA=Vue d''analyse         #1############################\\';
      Text006@1002 : TextConst 'ENU=Updating table    #2############################\;FRA=Mise … jour table     #2############################\';
      Text007@1003 : TextConst 'ENU=Speed: (Entries/s)#4########\;FRA=Vitesse : (Ecriture/s)#4########\';
      Text008@1004 : TextConst 'ENU=Average Speed     #5########;FRA=Vitesse moyenne       #5########';
      AnalysisView@1005 : Record 363;
      GLEntry@1006 : Record 17;
      GLBudgetEntry@1007 : Record 96;
      TempAllocationEntry@8016602 : TEMPORARY Record 365;
      AnalysisViewEntry@1008 : Record 365;
      AnalysisViewBudgetEntry@1009 : Record 366;
      TempAnalysisViewEntry@1010 : TEMPORARY Record 365;
      TempAnalysisViewBudgetEntry@1011 : TEMPORARY Record 366;
      TempAnalysisViewFilter@1012 : TEMPORARY Record 364;
      TempDimBuf@1013 : TEMPORARY Record 360;
      FilterIsInitialized@1014 : Boolean;
      MaxNumber@1015 : Integer;
      LedgEntryDimEntryNo@1016 : Integer;
      PrevPostingDate@1017 : Date;
      PrevCalculatedPostingDate@1018 : Date;
      NoOfEntries@1019 : Integer;
      Window@1020 : Dialog;
      ShowProgressWindow@1021 : Boolean;
      WinLastEntryNo@1022 : Integer;
      WinPrevEntryNo@1023 : Integer;
      WinUpdateCounter@1024 : Integer;
      WinTotalCounter@1025 : Integer;
      WinTime0@1026 : Time;
      WinTime1@1027 : Time;
      WinTime2@1028 : Time;
      Text009@1030 : TextConst 'ENU=#6############### @3@@@@@@@@@@@@@@@@@@@@@@@@@@@@\\;FRA=#6###############     @3@@@@@@@@@@@@@@@@@@@@@@@@@@@@\\';
      Text010@1031 : TextConst 'ENU=Summarizing;FRA=Cumul';
      Text011@1032 : TextConst 'ENU=Updating Database;FRA=Mise … jour de la base de donn‚es';
      GLSimulEntry@8016603 : Record 17;
      MaxSimulNumber@8016604 : Integer;

    PROCEDURE UpdateAll@15(Which@1000 : 'Ledger Entries,Budget Entries,Both';DirectlyFromPosting@1001 : Boolean);
    VAR
      AnalysisView2@1002 : Record 363;
    BEGIN
      MaxNumber := 2147483647;
      MaxSimulNumber := -2147483647; // ISA02.ISAT.MA
      AnalysisView2.SETRANGE(Blocked,FALSE);
      IF DirectlyFromPosting THEN
        AnalysisView2.SETRANGE("Update on Posting",TRUE);

      IF AnalysisView2.ISEMPTY THEN
        EXIT;
      AnalysisView2.LOCKTABLE;
      IF AnalysisView2.FINDSET THEN
        REPEAT
          UpdateOne(AnalysisView2,Which,NOT DirectlyFromPosting);
        UNTIL AnalysisView2.NEXT = 0;
    END;

    PROCEDURE Update@23(VAR NewAnalysisView@1000 : Record 363;Which@1001 : 'Ledger Entries,Budget Entries,Both';ShowWindow@1002 : Boolean);
    BEGIN
      MaxNumber := 2147483647;
      MaxSimulNumber := -2147483647; // ISA02.ISAT.MA
      NewAnalysisView.TESTFIELD(Blocked,FALSE);
      UpdateOne(NewAnalysisView,Which,ShowWindow);
    END;

    LOCAL PROCEDURE UpdateOne@11(VAR NewAnalysisView@1000 : Record 363;Which@1001 : 'Ledger Entries,Budget Entries,Both';ShowWindow@1002 : Boolean);
    VAR
      LastEntryNo@1003 : Integer;
    BEGIN
      AnalysisView := NewAnalysisView;
      AnalysisView.TESTFIELD(Blocked,FALSE);
      ShowProgressWindow := ShowWindow;
      IF ShowProgressWindow THEN
        InitWindow;

      IF Which IN [Which::"Ledger Entries",Which::Both] THEN BEGIN
        GLEntry.RESET;
        GLEntry.LOCKTABLE;
        GLEntry.SETRANGE("Entry No.",AnalysisView."Last Entry No." + 1,MaxNumber);
        IF GLEntry.FINDLAST THEN BEGIN
          LastEntryNo := GLEntry."Entry No.";
          IF ShowProgressWindow THEN
            UpdateWindowHeader(DATABASE::"Analysis View Entry",GLEntry."Entry No.");
          GLEntry.SETRANGE("Entry No.",AnalysisView."Last Entry No." + 1,LastEntryNo);
          UpdateEntries; //HM
          AnalysisView."Last Entry No." := LastEntryNo;
        END;
      END;

      IF (Which IN [Which::"Budget Entries",Which::Both]) AND
         NewAnalysisView."Include Budgets"
      THEN BEGIN
        GLBudgetEntry.RESET;
        GLBudgetEntry.LOCKTABLE;
        IF GLBudgetEntry.FINDLAST THEN BEGIN
          LastEntryNo := GLBudgetEntry."Entry No.";
          GLBudgetEntry.SETRANGE("Entry No.",AnalysisView."Last Budget Entry No." + 1,LastEntryNo);
          IF ShowProgressWindow THEN
            UpdateWindowHeader(DATABASE::"Analysis View Budget Entry",GLBudgetEntry."Entry No.");
          UpdateBudgetEntries(AnalysisView."Last Budget Entry No." + 1);
          AnalysisView."Last Budget Entry No." := LastEntryNo;
        END;
      END;


      // <<ISA02.ISAT.MA
      IF NewAnalysisView."Simulation Include" THEN BEGIN
        GLSimulEntry.RESET;
        GLSimulEntry.SETRANGE("Entry No.",MaxSimulNumber,-1);
        IF GLSimulEntry.FIND('-') THEN BEGIN
        LastEntryNo := GLSimulEntry."Entry No.";
        IF ShowProgressWindow THEN
            UpdateWindowHeader(DATABASE::"Analysis View Entry",GLSimulEntry."Entry No.");
          UpdateSimulationEntries;
        END;
      END;
      // >>ISA02.ISAT.MA


      AnalysisView."Last Date Updated" := TODAY;
      AnalysisView.MODIFY;

      IF ShowProgressWindow THEN
        Window.CLOSE;
    END;

    LOCAL PROCEDURE UpdateEntries@9();
    VAR
      LedgEntryDim1@1000 : Record 355;
      LedgEntryDim2@1001 : Record 355;
      LedgEntryDim3@1002 : Record 355;
      LedgEntryDim4@1003 : Record 355;
    BEGIN
      FilterIsInitialized := FALSE;
      LedgEntryDimEntryNo := 0;
      GLEntry.FILTERGROUP(2);
      GLEntry.SETFILTER("G/L Account No.",'<>%1','');
      GLEntry.FILTERGROUP(0);
      IF AnalysisView."G/L Account Filter" <> '' THEN
        GLEntry.SETFILTER("G/L Account No.",AnalysisView."G/L Account Filter");
      IF AnalysisView."Business Unit Filter" <> '' THEN
        GLEntry.SETFILTER("Business Unit Code",AnalysisView."Business Unit Filter");
      IF NOT GLEntry.FINDSET(TRUE) THEN
        EXIT;

      InitDimEntry(LedgEntryDim1,AnalysisView."Dimension 1 Code");
      InitDimEntry(LedgEntryDim2,AnalysisView."Dimension 2 Code");
      InitDimEntry(LedgEntryDim3,AnalysisView."Dimension 3 Code");
      InitDimEntry(LedgEntryDim4,AnalysisView."Dimension 4 Code");
      REPEAT
        IF LedgEntryDimEntryNo < GLEntry."Entry No." THEN
          FindNextLedgerEntryNo(DATABASE::"Ledger Entry Dimension",GLEntry."Entry No.");
        IF (LedgEntryDimEntryNo = GLEntry."Entry No.") OR (LedgEntryDimEntryNo = MaxNumber) THEN
      //<<ISA01.ST
          IF AnalysisView."Entries Allocate" THEN BEGIN
            InitTempAllocation(GetDimEntry(LedgEntryDim1),GetDimEntry(LedgEntryDim2),
                               GetDimEntry(LedgEntryDim3),GetDimEntry(LedgEntryDim4));
            TempAllocationEntry.FIND('-');
            REPEAT
              UpdateAnalysisViewEntry(TempAllocationEntry."Dimension 1 Value Code",TempAllocationEntry."Dimension 2 Value Code",
                         TempAllocationEntry."Dimension 3 Value Code",TempAllocationEntry."Dimension 4 Value Code",
                         TempAllocationEntry.Amount);
            UNTIL TempAllocationEntry.NEXT = 0;
          END ELSE BEGIN
      //>>ISA01.ST
          UpdateAnalysisViewEntry(
            GetDimEntry(LedgEntryDim1),GetDimEntry(LedgEntryDim2),GetDimEntry(LedgEntryDim3),GetDimEntry(LedgEntryDim4),
            1); //ISA01.ST
        END;
        IF ShowProgressWindow THEN
          UpdateWindowCounter(GLEntry."Entry No.");
        IF LedgEntryDimEntryNo < MaxNumber THEN
          GLEntry.SETRANGE("Entry No.",LedgEntryDimEntryNo,MaxNumber);
      UNTIL (GLEntry.NEXT = 0) OR (LedgEntryDimEntryNo = 0);
      IF ShowProgressWindow THEN
        UpdateWindowCounter(GLEntry."Entry No.");
      FlushAnalysisViewEntry;
    END;

    LOCAL PROCEDURE UpdateBudgetEntries@10(DeleteFromEntry@1000 : Integer);
    VAR
      GLBudgetDim1@1001 : Record 361;
      GLBudgetDim2@1002 : Record 361;
      GLBudgetDim3@1003 : Record 361;
      GLBudgetDim4@1004 : Record 361;
    BEGIN
      AnalysisViewBudgetEntry.SETRANGE("Analysis View Code",AnalysisView.Code);
      AnalysisViewBudgetEntry.SETRANGE("Entry No.",DeleteFromEntry,MaxNumber);
      AnalysisViewBudgetEntry.DELETEALL;
      AnalysisViewBudgetEntry.RESET;

      FilterIsInitialized := FALSE;
      LedgEntryDimEntryNo := 0;
      IF AnalysisView."G/L Account Filter" <> '' THEN
        GLBudgetEntry.SETFILTER("G/L Account No.",AnalysisView."G/L Account Filter");
      IF AnalysisView."Business Unit Filter" <> '' THEN
        GLBudgetEntry.SETFILTER("Business Unit Code",AnalysisView."Business Unit Filter");
      IF NOT GLBudgetEntry.FINDSET(TRUE) THEN
        EXIT;

      InitBudgetDim(GLBudgetDim1,AnalysisView."Dimension 1 Code");
      InitBudgetDim(GLBudgetDim2,AnalysisView."Dimension 2 Code");
      InitBudgetDim(GLBudgetDim3,AnalysisView."Dimension 3 Code");
      InitBudgetDim(GLBudgetDim4,AnalysisView."Dimension 4 Code");
      REPEAT
        IF LedgEntryDimEntryNo < GLBudgetEntry."Entry No." THEN
          FindNextLedgerEntryNo(DATABASE::"G/L Budget Dimension",GLBudgetEntry."Entry No.");
        IF (LedgEntryDimEntryNo = GLBudgetEntry."Entry No.") OR (LedgEntryDimEntryNo = MaxNumber) THEN
          //<<ISA01.ST
          IF AnalysisView."Entries Allocate" THEN BEGIN
            InitTempAllocation(
              GLBudgetDim1."Dimension Value Code",
              GLBudgetDim2."Dimension Value Code",
              GLBudgetDim3."Dimension Value Code",
              GLBudgetDim4."Dimension Value Code");
            TempAllocationEntry.FIND('-');
            REPEAT
              UpdateAnalysisViewBudgetEntry(
                TempAllocationEntry."Dimension 1 Value Code",
                TempAllocationEntry."Dimension 2 Value Code",
                TempAllocationEntry."Dimension 3 Value Code",
                TempAllocationEntry."Dimension 4 Value Code",
                TempAllocationEntry.Amount);
            UNTIL TempAllocationEntry.NEXT = 0;
          END ELSE BEGIN
          //>>ISA01.ST
          UpdateAnalysisViewBudgetEntry(
            GetBudgetDim(GLBudgetDim1),
            GetBudgetDim(GLBudgetDim2),
            GetBudgetDim(GLBudgetDim3),
            GetBudgetDim(GLBudgetDim4),
            1);//ISA01.ST
          END;
       IF ShowProgressWindow THEN
         UpdateWindowCounter(GLBudgetEntry."Entry No.");
       IF LedgEntryDimEntryNo < MaxNumber THEN
         GLBudgetEntry.SETRANGE("Entry No.",LedgEntryDimEntryNo,MaxNumber);
      UNTIL (GLBudgetEntry.NEXT = 0) OR (LedgEntryDimEntryNo = 0);
      IF ShowProgressWindow THEN
        UpdateWindowCounter(GLBudgetEntry."Entry No.");
      FlushAnalysisViewBudgetEntry;
    END;

    LOCAL PROCEDURE FindNextLedgerEntryNo@8(FindInTableID@1000 : Integer;ActualEntryNo@1001 : Integer);
    VAR
      LedgEntryDim@1002 : Record 355;
      GLBudgetDim@1003 : Record 361;
      AnalysisViewFilter@1004 : Record 364;
      Found@1005 : Boolean;
    BEGIN
      IF NOT FilterIsInitialized THEN BEGIN
        LedgEntryDimEntryNo := 0;
        TempAnalysisViewFilter.DELETEALL;
        TempDimBuf.DELETEALL;
        AnalysisViewFilter.SETRANGE("Analysis View Code",AnalysisView.Code);
        AnalysisViewFilter.SETFILTER("Dimension Value Filter",'<>%1','');
        IF AnalysisViewFilter.FINDSET THEN
          REPEAT
            TempAnalysisViewFilter.INIT;
            TempAnalysisViewFilter."Dimension Code" := AnalysisViewFilter."Dimension Code";
            TempAnalysisViewFilter."Dimension Value Filter" := AnalysisViewFilter."Dimension Value Filter";
            TempAnalysisViewFilter.INSERT;
            TempDimBuf.INIT;
            TempDimBuf."Dimension Code" := AnalysisViewFilter."Dimension Code";
            IF FilterIncludesBlanks(AnalysisViewFilter."Dimension Value Filter") THEN
              TempDimBuf."Dimension Value Code" := '*';
            TempDimBuf.INSERT;
          UNTIL AnalysisViewFilter.NEXT = 0
        ELSE
          LedgEntryDimEntryNo := MaxNumber;  // No filters are set
        FilterIsInitialized := TRUE;
      END;
      // Synchronize TempDimBuf with ActualEntryNo and check for match
      IF LedgEntryDimEntryNo < ActualEntryNo THEN BEGIN
        Found := FALSE;
        LedgEntryDimEntryNo := ActualEntryNo;
        WHILE (NOT Found) AND (LedgEntryDimEntryNo > 0) DO BEGIN
          Found := TRUE;
          IF TempDimBuf.RECORDLEVELLOCKING THEN
            TempDimBuf.LOCKTABLE;
          IF TempDimBuf.FINDSET THEN
            REPEAT
              IF TempDimBuf."Line No." < LedgEntryDimEntryNo THEN BEGIN
                TempAnalysisViewFilter.GET('',TempDimBuf."Dimension Code");
                CASE FindInTableID OF
                  DATABASE::"Ledger Entry Dimension":
                    BEGIN
                      LedgEntryDim.SETRANGE("Table ID",DATABASE::"G/L Entry");
                      LedgEntryDim.SETRANGE("Entry No.",LedgEntryDimEntryNo,MaxNumber);
                      LedgEntryDim.SETRANGE("Dimension Code",TempAnalysisViewFilter."Dimension Code");
                      LedgEntryDim.SETFILTER(
                        "Dimension Value Code",TempAnalysisViewFilter."Dimension Value Filter");
                      IF LedgEntryDim.FINDFIRST THEN
                        TempDimBuf."Line No." := LedgEntryDim."Entry No."
                      ELSE
                        IF TempDimBuf."Dimension Value Code" = '*' THEN
                          TempDimBuf."Line No." := MaxNumber
                        ELSE
                          TempDimBuf."Line No." := 0;
                      IF TempDimBuf."Dimension Value Code" = '*' THEN BEGIN
                        LedgEntryDim.SETRANGE("Dimension Value Code");
                        IF LedgEntryDim.FINDFIRST THEN
                          IF (LedgEntryDim."Entry No." < TempDimBuf."Line No.") THEN
                            TempDimBuf."Line No." := LedgEntryDim."Entry No." - 1;
                      END;
                    END;
                  DATABASE::"G/L Budget Dimension":
                    BEGIN
                      GLBudgetDim.SETRANGE("Entry No.",LedgEntryDimEntryNo,MaxNumber);
                      GLBudgetDim.SETRANGE("Dimension Code",TempAnalysisViewFilter."Dimension Code");
                      GLBudgetDim.SETFILTER(
                        "Dimension Value Code",TempAnalysisViewFilter."Dimension Value Filter");
                      IF GLBudgetDim.FINDFIRST THEN
                        TempDimBuf."Line No." := GLBudgetDim."Entry No."
                      ELSE
                        IF TempDimBuf."Dimension Value Code" = '*' THEN
                          TempDimBuf."Line No." := MaxNumber
                        ELSE
                          TempDimBuf."Line No." := 0;
                      IF TempDimBuf."Dimension Value Code" = '*' THEN BEGIN
                        GLBudgetDim.SETRANGE("Dimension Value Code");
                        IF GLBudgetDim.FINDFIRST THEN
                          IF (GLBudgetDim."Entry No." < TempDimBuf."Line No.") THEN
                            TempDimBuf."Line No." := GLBudgetDim."Entry No." - 1;
                      END;
                    END;
                END;
                TempDimBuf.MODIFY;
              END;
              Found :=
                Found AND
                ((TempDimBuf."Line No." = LedgEntryDimEntryNo) OR
                (TempDimBuf."Dimension Value Code" = '*') AND
                (TempDimBuf."Line No." >= LedgEntryDimEntryNo));

              IF TempDimBuf."Dimension Value Code" = '*' THEN BEGIN
                IF (TempDimBuf."Line No." = MaxNumber) OR
                   (LedgEntryDimEntryNo > 0) AND (TempDimBuf."Line No." < LedgEntryDimEntryNo)
                THEN
                  LedgEntryDimEntryNo := LedgEntryDimEntryNo + 1;
              END ELSE BEGIN
                IF (TempDimBuf."Line No." = 0) OR
                   (LedgEntryDimEntryNo > 0) AND (TempDimBuf."Line No." > LedgEntryDimEntryNo)
                THEN
                  LedgEntryDimEntryNo := TempDimBuf."Line No.";
              END;
            UNTIL (TempDimBuf.NEXT = 0) OR (LedgEntryDimEntryNo = 0);
        END;
      END;
    END;

    LOCAL PROCEDURE InitDimEntry@2(VAR LedgEntryDim@1000 : Record 355;DimCode@1001 : Code[20]);
    BEGIN
      IF DimCode = '' THEN
        EXIT;
      WITH LedgEntryDim DO BEGIN
        SETRANGE("Table ID",DATABASE::"G/L Entry");
        SETRANGE("Dimension Code",DimCode);
        SETRANGE("Entry No.",AnalysisView."Last Entry No." + 1,MaxNumber);
        IF FINDFIRST THEN;
      END;
    END;

    LOCAL PROCEDURE InitBudgetDim@12(VAR GLBudgetDim@1000 : Record 361;DimCode@1001 : Code[20]);
    BEGIN
      IF DimCode = '' THEN
        EXIT;
      WITH GLBudgetDim DO BEGIN
        SETRANGE("Dimension Code",DimCode);
        SETRANGE("Entry No.",AnalysisView."Last Budget Entry No." + 1,MaxNumber);
        IF FINDFIRST THEN;
      END;
    END;

    LOCAL PROCEDURE UpdateAnalysisViewEntry@3(DimValue1@1000 : Code[20];DimValue2@1001 : Code[20];DimValue3@1002 : Code[20];DimValue4@1003 : Code[20];AllocShare@8016602 : Decimal);
    VAR
      PostingDate@1004 : Date;
      EntryNo@1005 : Integer;
    BEGIN
      PostingDate := GLEntry."Posting Date";
      IF PostingDate < AnalysisView."Starting Date" THEN BEGIN
        PostingDate := AnalysisView."Starting Date" - 1;
        EntryNo := 0;
      END ELSE BEGIN
        PostingDate := CalculatePeriodStart(PostingDate,AnalysisView."Date Compression");
        IF PostingDate < AnalysisView."Starting Date" THEN
          PostingDate := AnalysisView."Starting Date";
        IF AnalysisView."Date Compression" = AnalysisView."Date Compression"::None THEN
          EntryNo := GLEntry."Entry No."
        ELSE
          EntryNo := 0;
      END;
      TempAnalysisViewEntry."Analysis View Code" := AnalysisView.Code;
      TempAnalysisViewEntry."Business Unit Code" := GLEntry."Business Unit Code";
      TempAnalysisViewEntry."G/L Account No." := GLEntry."G/L Account No.";
      TempAnalysisViewEntry."Posting Date" := PostingDate;
      TempAnalysisViewEntry."Dimension 1 Value Code" := DimValue1;
      TempAnalysisViewEntry."Dimension 2 Value Code" := DimValue2;
      TempAnalysisViewEntry."Dimension 3 Value Code" := DimValue3;
      TempAnalysisViewEntry."Dimension 4 Value Code" := DimValue4;
      TempAnalysisViewEntry."Entry No." := EntryNo;

      //ISA01.ST multiplication par coeff AllocShare coeff de ventilation (=1 par d‚faut)
      IF TempAnalysisViewEntry.FIND THEN BEGIN
        TempAnalysisViewEntry."Debit Amount" := TempAnalysisViewEntry."Debit Amount" + GLEntry."Debit Amount"*AllocShare;
        TempAnalysisViewEntry."Credit Amount" := TempAnalysisViewEntry."Credit Amount" + GLEntry."Credit Amount"*AllocShare;
        TempAnalysisViewEntry.Amount := TempAnalysisViewEntry.Amount + GLEntry.Amount*AllocShare;
        TempAnalysisViewEntry."Add.-Curr. Amount" :=
          TempAnalysisViewEntry."Add.-Curr. Amount" + GLEntry."Additional-Currency Amount"*AllocShare;
        TempAnalysisViewEntry."Add.-Curr. Debit Amount" :=
          TempAnalysisViewEntry."Add.-Curr. Debit Amount" + GLEntry."Add.-Currency Debit Amount"*AllocShare;
        TempAnalysisViewEntry."Add.-Curr. Credit Amount" :=
          TempAnalysisViewEntry."Add.-Curr. Credit Amount" + GLEntry."Add.-Currency Credit Amount"*AllocShare;
        TempAnalysisViewEntry.MODIFY;
      END ELSE BEGIN
        TempAnalysisViewEntry."Debit Amount" := GLEntry."Debit Amount"*AllocShare;
        TempAnalysisViewEntry."Credit Amount" := GLEntry."Credit Amount"*AllocShare;
        TempAnalysisViewEntry.Amount := GLEntry.Amount*AllocShare;
        TempAnalysisViewEntry."Add.-Curr. Amount" := GLEntry."Additional-Currency Amount"*AllocShare;
        TempAnalysisViewEntry."Add.-Curr. Debit Amount" := GLEntry."Add.-Currency Debit Amount"*AllocShare;
        TempAnalysisViewEntry."Add.-Curr. Credit Amount" := GLEntry."Add.-Currency Credit Amount"*AllocShare;
        TempAnalysisViewEntry.INSERT;
        NoOfEntries := NoOfEntries + 1;
      END;
      IF NoOfEntries >= 10000 THEN
        FlushAnalysisViewEntry;
    END;

    LOCAL PROCEDURE UpdateAnalysisViewBudgetEntry@18(DimValue1@1000 : Code[20];DimValue2@1001 : Code[20];DimValue3@1002 : Code[20];DimValue4@1003 : Code[20];AllocShare@8016602 : Decimal);
    BEGIN
      TempAnalysisViewBudgetEntry."Analysis View Code" := AnalysisView.Code;
      TempAnalysisViewBudgetEntry."Budget Name" := GLBudgetEntry."Budget Name";
      TempAnalysisViewBudgetEntry."Business Unit Code" := GLBudgetEntry."Business Unit Code";
      TempAnalysisViewBudgetEntry."G/L Account No." := GLBudgetEntry."G/L Account No.";
      IF GLBudgetEntry.Date < AnalysisView."Starting Date" THEN
        TempAnalysisViewBudgetEntry."Posting Date" := AnalysisView."Starting Date" - 1
      ELSE BEGIN
        TempAnalysisViewBudgetEntry."Posting Date" :=
          CalculatePeriodStart(GLBudgetEntry.Date,AnalysisView."Date Compression");
        IF TempAnalysisViewBudgetEntry."Posting Date" < AnalysisView."Starting Date" THEN
          TempAnalysisViewBudgetEntry."Posting Date" := AnalysisView."Starting Date";
      END;
      TempAnalysisViewBudgetEntry."Dimension 1 Value Code" := DimValue1;
      TempAnalysisViewBudgetEntry."Dimension 2 Value Code" := DimValue2;
      TempAnalysisViewBudgetEntry."Dimension 3 Value Code" := DimValue3;
      TempAnalysisViewBudgetEntry."Dimension 4 Value Code" := DimValue4;
      TempAnalysisViewBudgetEntry."Entry No." := GLBudgetEntry."Entry No.";

      //ISA01.ST multiplication par coeff AllocShare coeff de ventilation (=1 par d‚faut)
      IF TempAnalysisViewBudgetEntry.FIND THEN BEGIN
        TempAnalysisViewBudgetEntry.Amount := TempAnalysisViewBudgetEntry.Amount + GLBudgetEntry.Amount*AllocShare;
        TempAnalysisViewBudgetEntry.MODIFY;
      END ELSE BEGIN
        TempAnalysisViewBudgetEntry.Amount := GLBudgetEntry.Amount*AllocShare;
        TempAnalysisViewBudgetEntry.INSERT;
        NoOfEntries := NoOfEntries + 1;
      END;
      IF NoOfEntries >= 10000 THEN
        FlushAnalysisViewBudgetEntry;
    END;

    LOCAL PROCEDURE CalculatePeriodStart@4(PostingDate@1000 : Date;DateCompression@1001 : Integer) : Date;
    VAR
      AccountingPeriod@1002 : Record 50;
    BEGIN
      IF PostingDate = CLOSINGDATE(PostingDate) THEN
        EXIT(PostingDate);

      CASE DateCompression OF
        AnalysisView."Date Compression"::Week :
          PostingDate := CALCDATE('<CW+1D-1W>',PostingDate);
        AnalysisView."Date Compression"::Month :
          PostingDate := CALCDATE('<CM+1D-1M>',PostingDate);
        AnalysisView."Date Compression"::Quarter :
          PostingDate := CALCDATE('<CQ+1D-1Q>',PostingDate);
        AnalysisView."Date Compression"::Year :
          PostingDate := CALCDATE('<CY+1D-1Y>',PostingDate);
        AnalysisView."Date Compression"::Period :
          BEGIN
            IF PostingDate <> PrevPostingDate THEN BEGIN
              PrevPostingDate := PostingDate;
              AccountingPeriod.SETRANGE("Starting Date",0D,PostingDate);
              IF AccountingPeriod.FINDLAST THEN BEGIN
                PrevCalculatedPostingDate := AccountingPeriod."Starting Date"
              END ELSE
                PrevCalculatedPostingDate := PostingDate;
            END;
            PostingDate := PrevCalculatedPostingDate;
          END;
      END;
      EXIT(PostingDate);
    END;

    LOCAL PROCEDURE FlushAnalysisViewEntry@5();
    BEGIN
      IF ShowProgressWindow THEN
        Window.UPDATE(6,Text011);
      IF TempAnalysisViewEntry.FINDSET THEN
        REPEAT
          AnalysisViewEntry.INIT;
          AnalysisViewEntry := TempAnalysisViewEntry;
          IF NOT AnalysisViewEntry.INSERT THEN BEGIN
            AnalysisViewEntry.FIND;
            AnalysisViewEntry.Amount :=
              AnalysisViewEntry.Amount + TempAnalysisViewEntry.Amount;
            AnalysisViewEntry."Debit Amount" :=
              AnalysisViewEntry."Debit Amount" + TempAnalysisViewEntry."Debit Amount";
            AnalysisViewEntry."Credit Amount" :=
              AnalysisViewEntry."Credit Amount" + TempAnalysisViewEntry."Credit Amount";
            AnalysisViewEntry."Add.-Curr. Amount" :=
              AnalysisViewEntry."Add.-Curr. Amount" +
              TempAnalysisViewEntry."Add.-Curr. Amount";
            AnalysisViewEntry."Add.-Curr. Debit Amount" :=
              AnalysisViewEntry."Add.-Curr. Debit Amount" +
              TempAnalysisViewEntry."Add.-Curr. Debit Amount";
            AnalysisViewEntry."Add.-Curr. Credit Amount" :=
              AnalysisViewEntry."Add.-Curr. Credit Amount" +
              TempAnalysisViewEntry."Add.-Curr. Credit Amount";
            AnalysisViewEntry.MODIFY;
          END;
        UNTIL TempAnalysisViewEntry.NEXT = 0;
      TempAnalysisViewEntry.DELETEALL;
      NoOfEntries := 0;
      IF ShowProgressWindow THEN
        Window.UPDATE(6,Text010);
    END;

    LOCAL PROCEDURE FlushAnalysisViewBudgetEntry@25();
    BEGIN
      IF ShowProgressWindow THEN
        Window.UPDATE(6,Text011);
      IF TempAnalysisViewBudgetEntry.FINDSET THEN
        REPEAT
          AnalysisViewBudgetEntry.INIT;
          AnalysisViewBudgetEntry := TempAnalysisViewBudgetEntry;
          IF NOT AnalysisViewBudgetEntry.INSERT THEN BEGIN
            AnalysisViewBudgetEntry.FIND;
            AnalysisViewBudgetEntry.Amount :=
              AnalysisViewBudgetEntry.Amount + TempAnalysisViewBudgetEntry.Amount;
            AnalysisViewBudgetEntry.MODIFY;
          END;
        UNTIL TempAnalysisViewBudgetEntry.NEXT = 0;
      TempAnalysisViewBudgetEntry.DELETEALL;
      NoOfEntries := 0;
      IF ShowProgressWindow THEN
        Window.UPDATE(6,Text010);
    END;

    LOCAL PROCEDURE GetDimEntry@6(VAR LedgEntryDim@1000 : Record 355) : Code[20];
    BEGIN
      IF LedgEntryDim.GETFILTER("Dimension Code") = '' THEN
        EXIT('');
      IF LedgEntryDim."Entry No." > GLEntry."Entry No." THEN
        EXIT('');

      LedgEntryDim.SETRANGE("Entry No.",GLEntry."Entry No.",MaxNumber);
      IF NOT LedgEntryDim.FINDFIRST THEN
        LedgEntryDim."Entry No." := MaxNumber;
      IF LedgEntryDim."Entry No." = GLEntry."Entry No." THEN
        EXIT(LedgEntryDim."Dimension Value Code")
      ELSE
        EXIT('');
    END;

    LOCAL PROCEDURE GetBudgetDim@14(VAR GLBudgetDim@1000 : Record 361) : Code[20];
    BEGIN
      IF GLBudgetDim.GETFILTER("Dimension Code") = '' THEN
        EXIT('');
      IF GLBudgetDim."Entry No." > GLBudgetEntry."Entry No." THEN
        EXIT('');
      GLBudgetDim.SETRANGE("Entry No.",GLBudgetEntry."Entry No.",MaxNumber);
      IF NOT GLBudgetDim.FINDFIRST THEN
        GLBudgetDim."Entry No." := MaxNumber;
      IF GLBudgetDim."Entry No." = GLBudgetEntry."Entry No." THEN
        EXIT(GLBudgetDim."Dimension Value Code")
      ELSE
        EXIT('');
    END;

    LOCAL PROCEDURE InitWindow@1();
    BEGIN
      Window.OPEN(
        Text005 +
        Text006 +
        Text009 +
        Text007 +
        Text008);
      Window.UPDATE(6,Text010);
    END;

    LOCAL PROCEDURE UpdateWindowCounter@7(EntryNo@1000 : Integer);
    BEGIN
      WinUpdateCounter := WinUpdateCounter + 1;
      WinTime2 := TIME;
      IF (WinTime2 > WinTime1 + 1000) OR (EntryNo = WinLastEntryNo) THEN BEGIN
        IF WinLastEntryNo <> 0 THEN
          Window.UPDATE(3,ROUND(EntryNo / WinLastEntryNo*10000,1));
        WinPrevEntryNo := EntryNo;
        WinTotalCounter := WinTotalCounter + WinUpdateCounter;
        IF WinTime2 <> WinTime1 THEN
          Window.UPDATE(4,ROUND(WinUpdateCounter * (1000 / (WinTime2 - WinTime1)),1));
        IF WinTime2 <> WinTime0 THEN
          Window.UPDATE(5,ROUND(WinTotalCounter * (1000 / (WinTime2 - WinTime0)),1));
        WinTime1 := WinTime2;
        WinUpdateCounter := 0;
      END;
    END;

    LOCAL PROCEDURE UpdateWindowHeader@13(TableID@1000 : Integer;EntryNo@1001 : Integer);
    VAR
      Object@1002 : Record 2000000001;
    BEGIN
      WinLastEntryNo := EntryNo;
      WinPrevEntryNo := 0;
      WinTotalCounter := 0;
      Object.GET(Object.Type::Table,'',TableID);
      Window.UPDATE(1,AnalysisView.Code);
      Window.UPDATE(2,Object.Name);
      Window.UPDATE(3,0);
      Window.UPDATE(4,0);
      Window.UPDATE(5,0);
      WinTime0 := TIME;
      WinTime1 := WinTime0;
      WinTime2 := WinTime0;
    END;

    PROCEDURE SetLastBudgetEntryNo@16(NewLastBudgetEntryNo@1000 : Integer);
    VAR
      AnalysisView2@1001 : Record 363;
    BEGIN
      AnalysisView.SETRANGE("Last Budget Entry No.",NewLastBudgetEntryNo + 1,2147483647);
      AnalysisView.SETRANGE("Include Budgets",TRUE);
      IF AnalysisView.FINDSET(TRUE,TRUE) THEN
        REPEAT
          AnalysisView2 := AnalysisView;
          AnalysisView2."Last Budget Entry No." := NewLastBudgetEntryNo;
          AnalysisView2.MODIFY;
        UNTIL AnalysisView.NEXT = 0;
    END;

    LOCAL PROCEDURE FilterIncludesBlanks@17(VAR TheFilter@1000 : Code[250]) : Boolean;
    VAR
      TempDimBuf2@1001 : TEMPORARY Record 360;
    BEGIN
      WITH TempDimBuf2 DO BEGIN
        INIT;
        INSERT;
        SETFILTER("Dimension Code",TheFilter);
        EXIT(FINDFIRST);
      END;
    END;

    LOCAL PROCEDURE InitTempAllocation@8016602(DimValue1@8016602 : Code[20];DimValue2@8016603 : Code[20];DimValue3@8016604 : Code[20];DimValue4@8016605 : Code[20]);
    VAR
      DimValueAllocation1@8016606 : TEMPORARY Record 8016605;
      DimValueAllocation2@8016607 : TEMPORARY Record 8016605;
      DimValueAllocation3@8016608 : TEMPORARY Record 8016605;
      DimValueAllocation4@8016609 : TEMPORARY Record 8016605;
    BEGIN
      //ISA01.ST
      TempAllocationEntry.DELETEALL;
      TempAllocationEntry.RESET;
      //initialisation des ventilations par section : liste axe, section et coefficient multiplicateur
      InitDimValueAllocation(
        DimValueAllocation1,
        AnalysisView."Dimension 1 Code",
        DimValue1);
      InitDimValueAllocation(
        DimValueAllocation2,
        AnalysisView."Dimension 2 Code",
        DimValue2);
      InitDimValueAllocation(
        DimValueAllocation3,
        AnalysisView."Dimension 3 Code",
        DimValue3);
      InitDimValueAllocation(
        DimValueAllocation4,
        AnalysisView."Dimension 4 Code",
        DimValue4);
      //Produit cart‚sien des ventilations analytiques dans table temporaire
      DimValueAllocation1.FIND('-');
      REPEAT BEGIN
        DimValueAllocation2.FIND('-');
        REPEAT BEGIN
          DimValueAllocation3.FIND('-');
          REPEAT BEGIN
            DimValueAllocation4.FIND('-');
            REPEAT BEGIN
              TempAllocationEntry."Analysis View Code" := AnalysisView.Code;
              TempAllocationEntry."G/L Account No." := GLEntry."G/L Account No.";
              TempAllocationEntry."Dimension 1 Value Code" := DimValueAllocation1.Code;
              TempAllocationEntry."Dimension 2 Value Code" := DimValueAllocation2.Code;
              TempAllocationEntry."Dimension 3 Value Code" := DimValueAllocation3.Code;
              TempAllocationEntry."Dimension 4 Value Code" := DimValueAllocation4.Code;
              TempAllocationEntry.Amount :=
                DimValueAllocation1.Percentage*DimValueAllocation2.Percentage *
                DimValueAllocation3.Percentage*DimValueAllocation4.Percentage;
              TempAllocationEntry.INSERT;
            END UNTIL DimValueAllocation4.NEXT = 0;
          END UNTIL DimValueAllocation3.NEXT = 0;
        END UNTIL DimValueAllocation2.NEXT = 0;
      END UNTIL DimValueAllocation1.NEXT = 0;
    END;

    PROCEDURE InitDimValueAllocation@8016603(VAR DimValueAllocationI@8016602 : TEMPORARY Record 8016605;DimI@8016603 : Code[20];DimValueI@8016604 : Code[20]);
    VAR
      DimValue@8016605 : Record 349;
      DimValueAllocation@8016606 : Record 8016605;
    BEGIN
      //ISA01.ST
      DimValue.SETRANGE("Dimension Code",DimI);
      DimValue.SETRANGE(Code,DimValueI);
      IF DimValue.FIND('-') THEN;
      DimValueAllocation.SETRANGE("Main Dimension Code",DimI);
      DimValueAllocation.SETRANGE("Main Code",DimValueI);

      IF DimValueAllocation.FIND('-') THEN BEGIN
        DimValue.TESTFIELD(Blocked,FALSE);
        DimValue.TESTFIELD(Status,DimValue.Status::Valid‚e);
        DimValueAllocationI.DELETEALL;
        REPEAT BEGIN
          DimValueAllocationI."Main Dimension Code" := DimI;
          DimValueAllocationI."Main Code" := DimValueI;
          DimValueAllocationI.Code := DimValueAllocation.Code;
          IF DimValue."Entry Type" = DimValue."Entry Type"::Pourcentage THEN
            DimValueAllocationI.Percentage := DimValueAllocation.Percentage/100
          ELSE
            DimValueAllocationI.Percentage :=DimValueAllocation."Applied Quantity"/DimValue."Total Quantity";
          DimValueAllocationI.INSERT;
        END UNTIL DimValueAllocation.NEXT=0;
      END ELSE BEGIN
        DimValueAllocationI."Main Dimension Code" := DimI;
        DimValueAllocationI."Main Code" := DimValueI;
        DimValueAllocationI.Code := DimValueI;
        DimValueAllocationI.Percentage :=1;
        DimValueAllocationI.INSERT;
      END;
    END;

    PROCEDURE UpdateSimulationEntries@8016604();
    VAR
      lLESimulDim1@8016602 : Record 355;
      lLESimulDim2@8016603 : Record 355;
      lLESimulDim3@8016604 : Record 355;
      lLESimulDim4@8016605 : Record 355;
    BEGIN
      // ISA02.ISAT.MA
      AnalysisViewEntry.SETRANGE("Analysis View Code",AnalysisView.Code);
      AnalysisViewEntry.SETRANGE("Entry No.",MaxSimulNumber,-1);
      AnalysisViewEntry.DELETEALL;
      AnalysisViewEntry.RESET;

      FilterIsInitialized := FALSE;
      LedgEntryDimEntryNo := 0;
      GLSimulEntry.FILTERGROUP(2);
      GLSimulEntry.SETFILTER("G/L Account No.",'<>%1','');
      GLSimulEntry.FILTERGROUP(0);
      IF AnalysisView."G/L Account Filter" <> '' THEN
        GLSimulEntry.SETFILTER("G/L Account No.",AnalysisView."G/L Account Filter");
      IF AnalysisView."Business Unit Filter" <> '' THEN
        GLSimulEntry.SETFILTER("Business Unit Code",AnalysisView."Business Unit Filter");
      IF NOT GLSimulEntry.FIND('-') THEN
        EXIT;

      InitSimulDimEntry(lLESimulDim1,AnalysisView."Dimension 1 Code");
      InitSimulDimEntry(lLESimulDim2,AnalysisView."Dimension 2 Code");
      InitSimulDimEntry(lLESimulDim3,AnalysisView."Dimension 3 Code");
      InitSimulDimEntry(lLESimulDim4,AnalysisView."Dimension 4 Code");
      REPEAT
        UpdateAnalysisViewSimulEntry(
            GetSimulDimEntry(lLESimulDim1),
            GetSimulDimEntry(lLESimulDim2),
            GetSimulDimEntry(lLESimulDim3),
            GetSimulDimEntry(lLESimulDim4));
      UNTIL GLSimulEntry.NEXT = 0;
      IF ShowProgressWindow THEN
        UpdateWindowCounter(GLSimulEntry."Entry No.");
      FlushAnalysisViewEntry;
    END;

    PROCEDURE InitSimulDimEntry@8016605(VAR pLESimulDim@8016602 : Record 355;pDimCode@8016603 : Code[20]);
    BEGIN
      // ISA02.ISAT.MA
      IF pDimCode = '' THEN
        EXIT;
      WITH pLESimulDim DO BEGIN
        SETRANGE("Table ID",DATABASE::"G/L Entry");
        SETRANGE("Dimension Code",pDimCode);
        SETRANGE("Entry No.",MaxSimulNumber,-1);
        IF FIND('-') THEN;
      END;
    END;

    PROCEDURE UpdateAnalysisViewSimulEntry@8016606(DimValue1@8016602 : Code[20];DimValue2@8016603 : Code[20];DimValue3@8016604 : Code[20];DimValue4@8016605 : Code[20]);
    VAR
      lPostingDate@8016606 : Date;
      lEntryNo@8016607 : Integer;
    BEGIN
      // ISA02.ISAT.MA

      lPostingDate := GLSimulEntry."Posting Date";
      IF lPostingDate < AnalysisView."Starting Date" THEN BEGIN
        lPostingDate := AnalysisView."Starting Date" - 1;
        lEntryNo := 0;
      END ELSE BEGIN
        lPostingDate := CalculatePeriodStart(lPostingDate,AnalysisView."Date Compression");
        IF lPostingDate < AnalysisView."Starting Date" THEN
          lPostingDate := AnalysisView."Starting Date";
        IF AnalysisView."Date Compression" = AnalysisView."Date Compression"::None THEN
          lEntryNo := GLSimulEntry."Entry No."
        ELSE
          lEntryNo := -1;
      END;
      TempAnalysisViewEntry."Analysis View Code" := AnalysisView.Code;
      TempAnalysisViewEntry."Business Unit Code" := GLSimulEntry."Business Unit Code";
      TempAnalysisViewEntry."G/L Account No." := GLSimulEntry."G/L Account No.";
      TempAnalysisViewEntry."Posting Date" := lPostingDate;
      TempAnalysisViewEntry."Dimension 1 Value Code" := DimValue1;
      TempAnalysisViewEntry."Dimension 2 Value Code" := DimValue2;
      TempAnalysisViewEntry."Dimension 3 Value Code" := DimValue3;
      TempAnalysisViewEntry."Dimension 4 Value Code" := DimValue4;
      TempAnalysisViewEntry."Entry No." := lEntryNo;

      IF TempAnalysisViewEntry.FIND THEN BEGIN
        TempAnalysisViewEntry."Debit Amount" := TempAnalysisViewEntry."Debit Amount" + GLSimulEntry."Debit Amount";
        TempAnalysisViewEntry."Credit Amount" := TempAnalysisViewEntry."Credit Amount" + GLSimulEntry."Credit Amount";
        TempAnalysisViewEntry.Amount := TempAnalysisViewEntry.Amount + GLSimulEntry.Amount;
        TempAnalysisViewEntry."Add.-Curr. Amount" :=
          TempAnalysisViewEntry."Add.-Curr. Amount" + GLSimulEntry."Additional-Currency Amount";
        TempAnalysisViewEntry."Add.-Curr. Debit Amount" :=
          TempAnalysisViewEntry."Add.-Curr. Debit Amount" + GLSimulEntry."Add.-Currency Debit Amount";
        TempAnalysisViewEntry."Add.-Curr. Credit Amount" :=
          TempAnalysisViewEntry."Add.-Curr. Credit Amount" + GLSimulEntry."Add.-Currency Credit Amount";
        TempAnalysisViewEntry.MODIFY;
      END ELSE BEGIN
        TempAnalysisViewEntry."Debit Amount" := GLSimulEntry."Debit Amount";
        TempAnalysisViewEntry."Credit Amount" := GLSimulEntry."Credit Amount";
        TempAnalysisViewEntry.Amount := GLSimulEntry.Amount;
        TempAnalysisViewEntry."Add.-Curr. Amount" := GLSimulEntry."Additional-Currency Amount";
        TempAnalysisViewEntry."Add.-Curr. Debit Amount" := GLSimulEntry."Add.-Currency Debit Amount";
        TempAnalysisViewEntry."Add.-Curr. Credit Amount" := GLSimulEntry."Add.-Currency Credit Amount";
        TempAnalysisViewEntry.INSERT;
        NoOfEntries := NoOfEntries + 1;
      END;
      IF NoOfEntries >= 10000 THEN
        FlushAnalysisViewEntry;
    END;

    PROCEDURE GetSimulDimEntry@8016607(pLESimulDim@8016602 : Record 355) : Code[20];
    BEGIN
      // ISA02.ISAT.MA
      IF pLESimulDim.GETFILTER("Dimension Code") = '' THEN
        EXIT('');
      IF pLESimulDim."Entry No." > GLSimulEntry."Entry No." THEN
        EXIT('');

      pLESimulDim.SETRANGE("Entry No.",MaxSimulNumber,GLSimulEntry."Entry No.");
      IF NOT pLESimulDim.FIND('+') THEN
        pLESimulDim."Entry No." := MaxSimulNumber;
      IF pLESimulDim."Entry No." = GLSimulEntry."Entry No." THEN
        EXIT(pLESimulDim."Dimension Value Code")
      ELSE
        EXIT('');
    END;

    BEGIN
    {
      /** @r ISA02 @d 22/07/09 @a ISAT.MA @v ISA6.00.00.02 @s Inclusion des ‚critures simulations */
      /** @r ISA01 @d 06/07/09 @a ISAT.ST @v ISA6.00.00.01 @s migration ISATECH +
          @c prise en compte de la ventilation analytique
          @c Ajout permission lecture T17 */
    }
    END.
  }
}