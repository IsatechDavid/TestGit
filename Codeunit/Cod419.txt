OBJECT Codeunit 419 3-Tier Automation Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=19/04/12;
    Time=15:10:29;
    Modified=Yes;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      Text001@1004 : TextConst 'ENU=Default;FRA=Par d‚faut';
      Text006@1003 : TextConst 'ENU=Export;FRA=Exporter';
      Text007@1002 : TextConst 'ENU=Import;FRA=Importer';
      Text009@1000 : TextConst 'ENU=All Files (*.*)|*.*;FRA=Tous les fichiers (*.*)|*.*';
      CommonDialogMgnt@1005 : Codeunit 412;
      ClientTempPath@1006 : Text[1024];

    PROCEDURE BLOBImport@3(VAR BLOBRef@1000 : TEMPORARY Record 99008535;Name@1002 : Text[1024];CommonDialog@1003 : Boolean) : Text[1024];
    VAR
      NVInStream@1001 : InStream;
      NVOutStream@1004 : OutStream;
    BEGIN
      IF NOT ISSERVICETIER THEN BEGIN
        IF CommonDialog THEN BEGIN
          Name := CommonDialogMgnt.OpenFileWithName(Name);
          IF STRPOS(Name,'*') > 0 THEN
            EXIT('');
        END;
        BLOBRef.Blob.IMPORT(Name,FALSE);
        EXIT(Name);
      END
      ELSE BEGIN
        IF UPLOADINTOSTREAM(Text007,'',Text009,Name,NVInStream) THEN BEGIN
          BLOBRef.Blob.CREATEOUTSTREAM(NVOutStream);
          COPYSTREAM(NVOutStream,NVInStream);
          EXIT(Name);
        END;
        EXIT('');
      END
    END;

    PROCEDURE BLOBExport@4(VAR BLOBRef@1002 : TEMPORARY Record 99008535;Name@1001 : Text[1024];CommonDialog@1000 : Boolean) : Text[1024];
    VAR
      NVInStream@1003 : InStream;
      ToFile@1004 : Text[1024];
      p@1005 : Integer;
      Path@1006 : Text[1024];
    BEGIN
      IF NOT ISSERVICETIER THEN
        EXIT(BLOBRef.Blob.EXPORT(Name,CommonDialog))
      ELSE BEGIN
        BLOBRef.Blob.CREATEINSTREAM(NVInStream);
        IF STRPOS(Name,'*') = 0 THEN
          ToFile := Name
        ELSE BEGIN
          ToFile := INSSTR(Name,Text001,1);
          ToFile := DELCHR(ToFile,'=','*');
        END;
        WHILE STRPOS(ToFile,'\')<>0 DO BEGIN
          p := STRPOS(ToFile,'\');
          ToFile := COPYSTR(ToFile, p+1);
        END;
        Path := 'C:\TEMP';
        IF NOT CommonDialog THEN
          Path := Magicpath;
        DOWNLOADFROMSTREAM(NVInStream,Text006,Path,Text009,ToFile);
        EXIT(ToFile);
      END
    END;

    PROCEDURE EnvironFileName@1(PassedString@1001 : Text[250];FileExtension@1002 : Text[250]) FileName : Text[1024];
    BEGIN
      IF NOT ISSERVICETIER THEN BEGIN
        IF STRPOS(PassedString,'%1') = 0 THEN
          FileName := ENVIRON('TEMP') + PassedString + FileExtension
        ELSE
          FileName := STRSUBSTNO(ENVIRON('TEMP') + PassedString,FileExtension)
      END ELSE
        EXIT(ServerTempFileName(PassedString,FileExtension));
    END;

    PROCEDURE ServerTempFileName@5(PassedString@1001 : Text[250];FileExtension@1002 : Text[250]) FileName : Text[1024];
    VAR
      TempFile@1000 : File;
    BEGIN
      TempFile.CREATETEMPFILE;
      FileName := TempFile.NAME + '.' + FileExtension;
      TempFile.CLOSE;
    END;

    PROCEDURE ClientTempFileName@6(PassedString@1001 : Text[250];FileExtension@1002 : Text[250]) ClientFileName : Text[1024];
    VAR
      TempFile@1004 : File;
    BEGIN
      IF ISSERVICETIER THEN BEGIN
        IF ClientTempPath = '' THEN BEGIN
          TempFile.CREATETEMPFILE;
          ClientFileName := TempFile.NAME + '.' + FileExtension;
          TempFile.CLOSE;
          TempFile.CREATE(ClientFileName);
          TempFile.CLOSE;
          ClientTempPath := Path(DownloadTempFile(ClientFileName));
        END;
        ClientFileName := ClientTempPath + '\' + FORMAT(CREATEGUID) + '.' + FileExtension;
      END ELSE
        ClientFileName := ServerTempFileName(PassedString, FileExtension);
    END;

    PROCEDURE DownloadTempFile@7(ServerFileName@1001 : Text[1024]) : Text[1024];
    VAR
      FileName@1102601003 : Text[1024];
      Path@1102601004 : Text[1024];
    BEGIN
      IF NOT ISSERVICETIER THEN
        EXIT(ServerFileName);

      FileName := ServerFileName;
      Path := Magicpath;
      DOWNLOAD(ServerFileName,'',Path,Text009,FileName);
      EXIT(FileName);
    END;

    PROCEDURE Path@8(Filename@1102601000 : Text[1024]) Path : Text[1024];
    BEGIN
      Filename := DELCHR(Filename,'<');
      Path := Filename;
      WHILE STRPOS(Filename,'\')<>0 DO BEGIN
        Filename := COPYSTR(Filename, STRPOS(Filename,'\')+1);
      END;
      IF STRLEN(Path) > STRLEN(Filename) THEN
        EXIT(COPYSTR(Path,1,STRLEN(Path)-STRLEN(Filename)))
      ELSE
        EXIT('');
    END;

    PROCEDURE Magicpath@9() : Text[1024];
    BEGIN
      EXIT('<TEMP>');   // MAGIC PATH makes sure we don't get a prompt
    END;

    PROCEDURE UploadTempFile@1100281000(pClientFileName@1100281001 : Text[1024];pServerFileName@1100281000 : Text[1024];pExtension@1100281005 : Text[5]) : Text[1024];
    VAR
      lTempBLOB@1100281002 : TEMPORARY Record 99008535;
      lInStream@1100281003 : InStream;
      lOutStream@1100281004 : OutStream;
    BEGIN
      //IMH01.ST
      IF NOT ISSERVICETIER THEN
        EXIT(pClientFileName);
      IF pServerFileName = '' THEN
        pServerFileName := ServerTempFileName('', pExtension);

      UPLOAD('Upload', Path(pClientFileName), '',pClientFileName, pServerFileName);
      EXIT(pServerFileName);
    END;

    BEGIN
    {
      /** @r IMH01 @d 19/04/12 @a ISAT.ST @v IMH6.01.201 @s Reporting DPR @h mantis 1774
          @c Gestion de mode OnServer */
    }
    END.
  }
}