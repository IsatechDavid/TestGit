OBJECT Codeunit 50009 Completness Mgt.
{
  OBJECT-PROPERTIES
  {
    Date=17/04/13;
    Time=17:11:34;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      DialBox@1100281000 : Dialog;
      TotRec@1100281001 : Integer;
      CurrRec@1100281002 : Integer;
      Pct@1100281003 : Integer;
      Text001@1100281004 : TextConst 'FRA=Traitement en cours\@1@@@@@@@@@@@@@@@@';
      ReminderTerms@1100281005 : Record 292;
      Text002@1100281006 : TextConst 'ENU=Do you want to update all providers?;FRA=Souhaitez-vous mettre … jour tous les prestataires?';

    PROCEDURE InitCompletnessTask@1100281000(pProvider@1100281000 : Record 50000;pWithoutMessage@1100281005 : Boolean);
    VAR
      lCompletnessTaskSetup@1100281001 : Record 50003;
      lCompletnessTask@1100281002 : Record 50004;
      lProviderSpecialization@1100281003 : Record 50006;
      lCompletnessTask2@1100281004 : Record 50004;
    BEGIN
      WITH lCompletnessTaskSetup DO BEGIN

        SETRANGE(Category,pProvider.Category);

        IF GUIALLOWED AND NOT pWithoutMessage THEN BEGIN
          TotRec := COUNT;
          CurrRec := 0;
          Pct := ROUND(TotRec / 100,1,'>');
          IF Pct = 0 THEN
            Pct := 1;
          DialBox.OPEN(Text001);
        END;

        lCompletnessTask.RESET;
        lCompletnessTask.SETRANGE("Provider No.",pProvider."No.");
        lCompletnessTask.DELETEALL;

        IF FINDSET THEN
          REPEAT
            IF GUIALLOWED AND NOT pWithoutMessage THEN BEGIN
              CurrRec += 1;
              IF CurrRec MOD Pct = 0 THEN
                DialBox.UPDATE(1,ROUND(CurrRec * 10000 / TotRec,1));
            END;

            IF NOT lCompletnessTask.GET(pProvider."No.",Code,"Specialization Code") THEN BEGIN
              IF ("Specialization Code" = '') OR
                 (("Specialization Code" <> '') AND
                  lProviderSpecialization.GET(pProvider."No.","Specialization Code"))
              THEN BEGIN
                InsertCompletnessTask(pProvider."No.",lCompletnessTaskSetup,lCompletnessTask2);
              END;
            END;
          UNTIL NEXT = 0;
        IF GUIALLOWED AND NOT pWithoutMessage THEN
          DialBox.CLOSE;

      END;
    END;

    PROCEDURE InitSpecialityCompletnessTask@1100281002(pProviderNo@1100281000 : Code[20];pSpecialityCode@1100281006 : Code[10];pWithoutMessage@1100281005 : Boolean);
    VAR
      lProvider@1100281007 : Record 50000;
      lCompletnessTaskSetup@1100281001 : Record 50003;
      lCompletnessTask@1100281002 : Record 50004;
      lProviderSpecialization@1100281003 : Record 50006;
      lCompletnessTask2@1100281004 : Record 50004;
    BEGIN
      //IMH04
      IF NOT lProvider.GET(pProviderNo) THEN
        EXIT;
      IF pSpecialityCode ='' THEN
        EXIT;

      WITH lCompletnessTaskSetup DO BEGIN
        SETRANGE(Category,lProvider.Category);
        lCompletnessTaskSetup.SETRANGE("Specialization Code", pSpecialityCode);

        IF GUIALLOWED AND NOT pWithoutMessage THEN BEGIN
          TotRec := COUNT;
          CurrRec := 0;
          Pct := ROUND(TotRec / 100,1,'>');
          IF Pct = 0 THEN
            Pct := 1;
          DialBox.OPEN(Text001);
        END;

        IF FINDSET THEN
          REPEAT
            IF GUIALLOWED AND NOT pWithoutMessage THEN BEGIN
              CurrRec += 1;
              IF CurrRec MOD Pct = 0 THEN
                DialBox.UPDATE(1,ROUND(CurrRec * 10000 / TotRec,1));
            END;

            IF NOT lCompletnessTask.GET(lProvider."No.",Code,"Specialization Code") THEN BEGIN
              InsertCompletnessTask(lProvider."No.",lCompletnessTaskSetup,lCompletnessTask2);
            END;
          UNTIL NEXT = 0;
        IF GUIALLOWED AND NOT pWithoutMessage THEN
          DialBox.CLOSE;
      END;
    END;

    PROCEDURE InsertCompletnessTask@1100281001(pProviderNo@1100281000 : Code[10];pCompletnessTaskSetup@1100281001 : Record 50003;VAR pCompletnessTask@1100281003 : Record 50004);
    BEGIN
      WITH pCompletnessTask DO BEGIN
        INIT;
        "Provider No." := pProviderNo;
        "Document Code" := pCompletnessTaskSetup.Code;
        "Specialization Code" := pCompletnessTaskSetup."Specialization Code";  // IMH02.isat.cc
        "Specialization Code" := pCompletnessTaskSetup."Specialization Code";
        Description := pCompletnessTaskSetup.Description;
        Mandatory := pCompletnessTaskSetup.Mandatory;
        "Renew Mandatory" := pCompletnessTaskSetup."Renew Mandatory";
        "Renew Period" := pCompletnessTaskSetup."Renew Period";
        //"Provider Category" := pCompletnessTaskSetup.Category; //IMH03  //IMH04 d‚sactiv‚
        IF INSERT(TRUE) THEN;
      END;
    END;

    PROCEDURE UpdateCompletnessTaskWithCat@1100281003(pProvider@1100281001 : Record 50000;pWithoutMessage@1100281000 : Boolean);
    VAR
      lCompletnessTaskSetup@1100281005 : Record 50003;
      lCompletnessTask@1100281004 : Record 50004;
      lProviderSpecialization@1100281003 : Record 50006;
      lCompletnessTask2@1100281002 : Record 50004;
      lCompletnessTaskTemp@1100281006 : TEMPORARY Record 50004;
    BEGIN
      WITH lCompletnessTaskSetup DO BEGIN

        SETRANGE(Category,pProvider.Category);

        IF GUIALLOWED AND NOT pWithoutMessage THEN BEGIN
          TotRec := COUNT;
          CurrRec := 0;
          Pct := ROUND(TotRec / 100,1,'>');
          IF Pct = 0 THEN
            Pct := 1;
          DialBox.OPEN(Text001);
        END;

        lCompletnessTaskTemp.DELETEALL;

        IF FINDSET THEN
        BEGIN
          REPEAT
            IF GUIALLOWED AND NOT pWithoutMessage THEN BEGIN
              CurrRec += 1;
              IF CurrRec MOD Pct = 0 THEN
                DialBox.UPDATE(1,ROUND(CurrRec * 10000 / TotRec,1));
            END;

            IF ("Specialization Code" = '') OR
               (("Specialization Code" <> '') AND
                lProviderSpecialization.GET(pProvider."No.","Specialization Code"))
            THEN BEGIN
              lCompletnessTask.RESET;
              lCompletnessTask.SETRANGE("Provider No.",pProvider."No.");
              lCompletnessTask.SETRANGE("Document Code",lCompletnessTaskSetup.Code);
              lCompletnessTask.SETRANGE("Specialization Code",lCompletnessTaskSetup."Specialization Code");
              IF lCompletnessTask.FINDFIRST THEN
              BEGIN
                lCompletnessTaskTemp := lCompletnessTask;
                lCompletnessTaskTemp.INSERT;
              END ELSE BEGIN
                lCompletnessTaskTemp.INIT;
                lCompletnessTaskTemp."Provider No." := pProvider."No.";
                lCompletnessTaskTemp."Document Code" := lCompletnessTaskSetup.Code;
                lCompletnessTaskTemp."Specialization Code" := lCompletnessTaskSetup."Specialization Code";
                lCompletnessTaskTemp.Description := lCompletnessTaskSetup.Description;
                lCompletnessTaskTemp.INSERT(TRUE);
              END;
              lCompletnessTaskTemp.Mandatory := lCompletnessTaskSetup.Mandatory;
              lCompletnessTaskTemp."Renew Mandatory" := lCompletnessTaskSetup."Renew Mandatory";
              IF FORMAT(lCompletnessTaskTemp."Renew Period") = '' THEN
                lCompletnessTaskTemp."Renew Period" := lCompletnessTaskSetup."Renew Period";
              //lCompletnessTaskTemp."Provider Category" := lCompletnessTaskSetup.Category; //IMH03   //IMH04 d‚sactiv‚
              lCompletnessTaskTemp.MODIFY;
            END;
          UNTIL NEXT = 0;

          lCompletnessTask.RESET;
          lCompletnessTask.SETRANGE("Provider No.",pProvider."No.");
          lCompletnessTask.DELETEALL;

          lCompletnessTaskTemp.RESET;
          IF lCompletnessTaskTemp.FINDFIRST THEN
          REPEAT
            lCompletnessTask.INIT;
            lCompletnessTask := lCompletnessTaskTemp;
            lCompletnessTask.INSERT;
          UNTIL (lCompletnessTaskTemp.NEXT = 0);
        END;

        IF GUIALLOWED AND NOT pWithoutMessage THEN
          DialBox.CLOSE;

      END;
    END;

    PROCEDURE UpdtRenewPeriodCompTask@1100281005(pCompletnessTaskSetup@1100281000 : Record 50003);
    VAR
      lCompletnessTask@1100281004 : Record 50004;
    BEGIN
      lCompletnessTask.SETRANGE("Document Code", pCompletnessTaskSetup.Code);
      lCompletnessTask.SETRANGE("Specialization Code", pCompletnessTaskSetup."Specialization Code");
      lCompletnessTask.SETRANGE("Provider Category", pCompletnessTaskSetup.Category);

      IF GUIALLOWED THEN BEGIN
        IF NOT CONFIRM(Text002) THEN
          EXIT;
        TotRec := lCompletnessTask.COUNT;
        CurrRec := 0;
        Pct := ROUND(TotRec / 100,1,'>');
        IF Pct = 0 THEN
          Pct := 1;
        DialBox.OPEN(Text001);
      END;

      IF lCompletnessTask.FINDSET THEN
        REPEAT
          IF GUIALLOWED THEN BEGIN
            CurrRec += 1;
            IF CurrRec MOD Pct = 0 THEN
              DialBox.UPDATE(1,ROUND(CurrRec * 10000 / TotRec,1));
          END;
          lCompletnessTask."Renew Period" := pCompletnessTaskSetup."Renew Period";
          lCompletnessTask.VALIDATE("Validity Start Date");
          lCompletnessTask.MODIFY(TRUE);
        UNTIL (lCompletnessTask.NEXT = 0);

      IF GUIALLOWED THEN
        DialBox.CLOSE;
    END;

    BEGIN
    {
      /** @r IMH04 @d 17/04/13 @a ISAT.ZW @v IMH6.01.345 @s MAI @h 4225
          @c Provider Catecory passer en flowfield */
      /** @r IHM03 @d 17/05/11 @a ISAT.ZW @v IMH6.01.021 @s Compl‚tude @c Ajout fonction InitSpecialityCompletnessTask */
      /** @r IHM03 @d 21/02/11 @a ISAT.ZW @v IMH6.01.021 @s Compl‚tude @c Ajout champ provider category */
      /** @r IHM02 @d 18/10/10 @a ISAT.cc @v IMH6.01.021 @s Compl‚tude @c Ajout du champ "Speciality Code" */
      /** @r IHM01 @d 14/10/10 @a ISAT.cc @v IMH6.01.021 @s Compl‚tude @c Creation */
    }
    END.
  }
}