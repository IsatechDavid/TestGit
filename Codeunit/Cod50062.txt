OBJECT Codeunit 50062 Service Header InfoPane
{
  OBJECT-PROPERTIES
  {
    Date=06/05/14;
    Time=12:29:29;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    OnRun=VAR
            lServiceHeader@1100281001 : Record 5900;
          BEGIN
            //IMH09
            lServiceHeader.SETCURRENTKEY("Service Creation Date");
            lServiceHeader.FINDLAST;
            PAGE.RUN(0, lServiceHeader);
          END;

  }
  CODE
  {

    PROCEDURE CalcNoOfIntervention@1100281000(pServHeader@1100281002 : Record 5900) : Integer;
    VAR
      lInterventionLine@1100281000 : Record 50075;
    BEGIN
      IF pServHeader."No." = '' THEN
        EXIT;
      lInterventionLine.SETRANGE("Service Header No.", pServHeader."No.");
      EXIT(lInterventionLine.COUNT);
    END;

    PROCEDURE LookupIntervention@1100281004(pServHeader@1100281001 : Record 5900);
    VAR
      lInterventionLine@1100281002 : Record 50075;
    BEGIN
      IF pServHeader."No." = '' THEN
        EXIT;
      lInterventionLine.FILTERGROUP(2);
      lInterventionLine.SETRANGE("Service Header No.", pServHeader."No.");
      lInterventionLine.FILTERGROUP(0);
      IF lInterventionLine.ISEMPTY THEN
        EXIT;
      IF lInterventionLine.COUNT = 1 THEN BEGIN
        lInterventionLine.FINDFIRST;
        lInterventionLine.FILTERGROUP(2);
        lInterventionLine.SETRECFILTER;
        lInterventionLine.FILTERGROUP(0);
      //<<IMH08.ST
      //  FORM.RUN(FORM::"Intervention Line Card",lInterventionLine);
        lInterventionLine.ShowCard(FALSE);
      //>>IMH08.ST
      END
      ELSE
        FORM.RUN(0,lInterventionLine)
    END;

    PROCEDURE CalcNoOfTrayLines@1100281005(pServHeader@1100281001 : Record 5900;pUnclosed@1100281002 : Boolean) : Integer;
    VAR
      lTrayLine@1100281000 : Record 50048;
    BEGIN
      //IMH02 isat.zw
      IF pServHeader."No." = '' THEN
        EXIT;

      lTrayLine.SETCURRENTKEY("Service No.","Service Header No.","Intervention Line No.");
      lTrayLine.SETRANGE("Service No.", pServHeader."Service No.");
      lTrayLine.SETRANGE("Service Header No.", pServHeader."No.");
      IF pUnclosed THEN
        lTrayLine.SETRANGE(Closed, FALSE);
      EXIT(lTrayLine.COUNT);
    END;

    PROCEDURE LookupTrayLines@1100281001(pServHeader@1100281000 : Record 5900;pUnclosed@1100281002 : Boolean);
    VAR
      lTrayLine@1100281001 : Record 50048;
    BEGIN
      //IMH02 isat.zw
      IF pServHeader."No." = '' THEN
        EXIT;

      lTrayLine.SETCURRENTKEY("Service No.","Service Header No.","Intervention Line No.");
      lTrayLine.FILTERGROUP(2);
      lTrayLine.SETRANGE("Service No.", pServHeader."Service No.");
      lTrayLine.SETRANGE("Service Header No.", pServHeader."No.");
      lTrayLine.FILTERGROUP(0);
      IF pUnclosed THEN
        lTrayLine.SETRANGE(Closed, FALSE);
      IF lTrayLine.ISEMPTY THEN
        EXIT;

      FORM.RUN(0, lTrayLine);
    END;

    PROCEDURE LookupTrayLineHist@1100281006(pServHeader@1100281000 : Record 5900);
    VAR
      lTrayLine@1100281001 : Record 50049;
    BEGIN
      //IMH03.ST
      IF pServHeader."No." = '' THEN
        EXIT;
      lTrayLine.SETCURRENTKEY("Service No.","Service Header No.","Intervention Line No.");
      lTrayLine.FILTERGROUP(2);
      lTrayLine.SETRANGE("Service No.", pServHeader."Service No.");
      lTrayLine.SETRANGE("Service Header No.", pServHeader."No.");
      lTrayLine.FILTERGROUP(0);
      FORM.RUN(0, lTrayLine);
    END;

    PROCEDURE CalcNoOfDiagElement@1100281002(pServNo@1100281000 : Code[20]) : Integer;
    VAR
      lDiagElement@1100281001 : Record 50074;
    BEGIN
      //IMH04.ST
      IF pServNo = '' THEN
        EXIT;
      lDiagElement.SETRANGE("Service Header No.", pServNo);
      lDiagElement.SETRANGE("Diagnosis Status", lDiagElement."Diagnosis Status"::Yes);
      EXIT(lDiagElement.COUNT);
    END;

    PROCEDURE LookupDiagElement@1100281003(pServNo@1100281000 : Code[20]);
    VAR
      lDiagElement@1100281001 : Record 50074;
    BEGIN
      //IMH04.ST
      IF pServNo = '' THEN
        EXIT;
      lDiagElement.FILTERGROUP(2);
      lDiagElement.SETRANGE("Service Header No.", pServNo);
      lDiagElement.FILTERGROUP(0);
      lDiagElement.SETRANGE("Diagnosis Status", lDiagElement."Diagnosis Status"::Yes);
      FORM.RUN(0, lDiagElement);
    END;

    PROCEDURE GetProviderName@1100281007(pServHeader@1100281001 : Record 5900) : Text[50];
    VAR
      lProvider@1100281000 : Record 50000;
    BEGIN
      //IMH05
      IF pServHeader."No." = '' THEN
        EXIT;
      IF lProvider.GET(pServHeader."Provider No.") THEN
        EXIT(lProvider.Name);
    END;

    PROCEDURE LookupProvider@1100281008(pServHeader@1100281000 : Record 5900);
    VAR
      lProvider@1100281001 : Record 50000;
    BEGIN
      //IMH05
      IF pServHeader."No." = '' THEN
        EXIT;
      IF lProvider.GET(pServHeader."Provider No.") THEN
        FORM.RUN(FORM::"Provider Card",lProvider);
    END;

    PROCEDURE GetClaimCause@1100281010(pServHeader@1100281001 : Record 5900) : Text[50];
    VAR
      lServiceHeaderClaims@1100281000 : Record 50100;
    BEGIN
      //IMH05
      IF pServHeader."No." = '' THEN
        EXIT;
      IF NOT pServHeader.Claim THEN
        EXIT;
      IF NOT lServiceHeaderClaims.GET(pServHeader."No.") THEN
        EXIT;
      lServiceHeaderClaims.CALCFIELDS("Claims Cause Description");
      EXIT(lServiceHeaderClaims."Claims Cause Description");
    END;

    PROCEDURE LookupClaimCause@1100281009(pServHeader@1100281000 : Record 5900);
    VAR
      lProvider@1100281001 : Record 50000;
    BEGIN
      //IMH05
      IF pServHeader."No." = '' THEN
        EXIT;
      //************** TO DO ******************/
    END;

    PROCEDURE GetClaimSource@1100281012(pServHeader@1100281001 : Record 5900) : Text[50];
    VAR
      lIntervLine@1100281000 : Record 50075;
      lServiceHeader@1000000000 : Record 5900;
    BEGIN
      //IMH06
      IF pServHeader."No." = '' THEN
        EXIT;
      IF NOT pServHeader.Claim THEN
        EXIT;

      lIntervLine.SETCURRENTKEY("Service No.");
      lIntervLine.SETRANGE("Service No.", pServHeader."Service No.");
      lIntervLine.SETRANGE("Service Header Claims No.", pServHeader."No.");
      IF lIntervLine.ISEMPTY THEN BEGIN
        lServiceHeader.SETCURRENTKEY("Service No.");
        lServiceHeader.SETRANGE("Service No.", pServHeader."Service No.");
        lServiceHeader.SETRANGE("Service Header Claims No.", pServHeader."No.");
        IF lServiceHeader.FINDFIRST THEN
          EXIT(lServiceHeader."No.");
      END ELSE BEGIN
        lIntervLine.FINDFIRST;
        EXIT(lIntervLine."Service Header No.");
      END;
    END;

    PROCEDURE LookupClaimSource@1100281011(pServHeader@1100281000 : Record 5900);
    VAR
      lIntervLine@1100281002 : Record 50075;
      lServiceHeader@1000000000 : Record 5900;
    BEGIN
      //IMH06
      IF pServHeader."No." = '' THEN
        EXIT;
      IF NOT pServHeader.Claim THEN
        EXIT;

      lIntervLine.SETCURRENTKEY("Service No.");
      lIntervLine.FILTERGROUP(2);
      lIntervLine.SETRANGE("Service No.", pServHeader."Service No.");
      lIntervLine.SETRANGE("Service Header Claims No.", pServHeader."No.");
      lIntervLine.FILTERGROUP(0);
      IF NOT lIntervLine.ISEMPTY THEN BEGIN
        IF lIntervLine.COUNT = 1 THEN BEGIN
          lIntervLine.FINDFIRST;
          lIntervLine.SETRECFILTER;
      //<<IMH08.ST
      //    FORM.RUN(FORM::"Intervention Line Card", lIntervLine);
          lIntervLine.ShowCard(FALSE);
      //>>
        END
        ELSE BEGIN
          FORM.RUN(0, lIntervLine);
        END;
      END ELSE BEGIN
        lServiceHeader.SETCURRENTKEY("Service No.");
        lServiceHeader.FILTERGROUP(2);
        lServiceHeader.SETRANGE("Service No.", pServHeader."Service No.");
        lServiceHeader.SETRANGE("Service Header Claims No.", pServHeader."No.");
        lServiceHeader.FILTERGROUP(0);
        IF lServiceHeader.COUNT = 1 THEN BEGIN
          lServiceHeader.FINDFIRST;
          lServiceHeader.ShowCard(FALSE);
        END ELSE
          FORM.RUN(0, lServiceHeader);
      END;
    END;

    PROCEDURE CorporateInfoExist@1100281013(pServHeader@1100281000 : Record 5900) : Boolean;
    VAR
      lAdvCommentLine@1100281001 : Record 50056;
    BEGIN
      lAdvCommentLine.SETRANGE("Source Table ID", DATABASE::"Product Catalog");
      lAdvCommentLine.SETRANGE("Source No.", pServHeader."Corporate No.");
      lAdvCommentLine.SETRANGE("Source No. 2", pServHeader."Service Order Type");
      EXIT(NOT lAdvCommentLine.ISEMPTY);
    END;

    PROCEDURE LookupCorporateInfo@1100281014(pServHeader@1100281000 : Record 5900);
    VAR
      lAdvCommentLine@1100281001 : Record 50056;
    BEGIN
      lAdvCommentLine.FILTERGROUP(2);
      lAdvCommentLine.SETRANGE("Source Table ID", DATABASE::"Product Catalog");
      lAdvCommentLine.SETRANGE("Source No.", pServHeader."Corporate No.");
      lAdvCommentLine.SETRANGE("Source No. 2", pServHeader."Service Order Type");
      lAdvCommentLine.FILTERGROUP(0);
      FORM.RUN(0, lAdvCommentLine);
    END;

    PROCEDURE CalcNoOfSpec@1100281015(pServHeader@1100281000 : Record 5900;pRemainingOnly@1100281001 : Boolean) : Integer;
    VAR
      lIntervMgt@1100281002 : Codeunit 50014;
      lSearchBasket@1100281003 : TEMPORARY Record 51002;
      lServOrderType@1100281004 : Record 5903;
    BEGIN
      IF pServHeader."No." = '' THEN
        EXIT;
      IF NOT lServOrderType.GET(pServHeader."Service Order Type") THEN
        CLEAR(lServOrderType);
      lIntervMgt.LoadSpec(pServHeader."No.", lSearchBasket, lServOrderType, FALSE, pRemainingOnly);
      EXIT(lSearchBasket.COUNT);
    END;

    BEGIN
    {
      /** @r IMH09 @d 05/05/14 @a ISAT.ZW @v IMH6.01.5071 @s Tri Service @h 5071
          @c Affichage liste des services dans la trigger OnRun */
      /** @r IMH08 @d 04/04/2012 @a ISAT.ST @v IMH6.01.011 @s Gestion du service @c Appel ShowCard sur intervention */
      /** @r IMH07 @d 16/12/2012 @a ISAT.ZW @v IMH6.01.002 @s Gestion du service @c gestion affichage service r‚cla */
      /** @r IMH06 @d 07/04/2011 @a ISAT.ST @v IMH6.01.002 @s Gestion du service
          @c Ajout GetClaimSource, LookupClaimSource */
      /** @r IMH05 @d 17/02/2011 @a ISAT.LLT @v IMH6.01.002 @s Gestion du service
          @c Ajout fonctions pour gestion r‚clamation */
      /** @r IMH04 @d 16/02/2011 @a ISAT.ST @v IMH6.01.002 @s Gestion du service
          @c Ajout fonctions diag elements */
      /** @r IMH03 @d 10/12/2010 @a ISAT.ST @v IMH6.01.002 @s Gestion du service
          @c Affichage de fiche en non modal */
      /** @r IMH02 @d 27/10/2010 @a ISAT.ZW @v IMH6.01.013 @s Gestion des corbeilles @c table TODO --> table Tray Line */
      /** @r IMH01 @d 14/10/2010 @a ISAT.ST @v IMH6.01.011 @s Gestion du service
          @c Cr‚ation objet pour info pane/factbox service */
    }
    END.
  }
}