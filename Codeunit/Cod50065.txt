OBJECT Codeunit 50065 Customer InfoPane
{
  OBJECT-PROPERTIES
  {
    Date=04/08/11;
    Time=17:15:50;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE CalcNoOfService@1100281008(pCust@1100281002 : Record 18;pUnclosed@1100281001 : Boolean) : Integer;
    VAR
      lService@1100281000 : Record 50066;
    BEGIN
      IF pCust."No." = '' THEN
        EXIT;
      lService.SETCURRENTKEY("Corporate No.");
      lService.SETRANGE("Corporate No.", pCust."No.");
      IF pUnclosed THEN
        lService.SETFILTER(Status, '<%1',lService.Status::Finished);
      EXIT(lService.COUNT);
    END;

    PROCEDURE LookupService@1100281005(pCust@1100281000 : Record 18;pUnclosed@1100281001 : Boolean);
    VAR
      lService@1100281002 : Record 50066;
    BEGIN
      IF pCust."No." = '' THEN
        EXIT;

      lService.SETCURRENTKEY("Corporate No.", Status);
      lService.ASCENDING(FALSE);//IMH03
      lService.FILTERGROUP(2);
      lService.SETRANGE("Corporate No.", pCust."No.");
      lService.FILTERGROUP(0);
      IF pUnclosed THEN
        lService.SETFILTER(Status, '<%1',lService.Status::Finished);
      IF lService.ISEMPTY THEN
        EXIT;
      IF lService.COUNT = 1 THEN
        FORM.RUN(FORM::"Service Card", lService)
      ELSE
        FORM.RUNMODAL(0, lService);
    END;

    PROCEDURE CalcNoOfServiceQuoteOnHold@1100281001(pCust@1100281002 : Record 18) : Integer;
    VAR
      lServiceQuote@1100281000 : Record 50076;
    BEGIN
      IF pCust."No." = '' THEN
        EXIT;
      WITH lServiceQuote DO BEGIN
        SETCURRENTKEY("Corporate No.");
        SETRANGE("Corporate No.", pCust."No.");
        SETRANGE("Approval Status","Approval Status"::WaitAdjuster);
        EXIT(COUNT);
      END;
    END;

    PROCEDURE LookupServiceQuoteOnHold@1100281000(pCust@1100281000 : Record 18);
    VAR
      lServiceQuote@1100281002 : Record 50076;
    BEGIN
      IF pCust."No." = '' THEN
        EXIT;
      WITH lServiceQuote DO BEGIN
        SETCURRENTKEY("Corporate No.");
        FILTERGROUP(2);
        SETRANGE("Corporate No.", pCust."No.");
        SETRANGE("Approval Status","Approval Status"::WaitAdjuster);
        FILTERGROUP(0);
        IF ISEMPTY THEN
          EXIT;
        IF COUNT = 1 THEN
          FORM.RUN(FORM::"Serv. Intervention Quote Card", lServiceQuote)
        ELSE
          FORM.RUNMODAL(0, lServiceQuote);
      END;
    END;

    PROCEDURE CalcNoOfHomeService@1100281002(pCust@1100281000 : Record 18) : Integer;
    VAR
      lServiceHeader@1100281001 : Record 5900;
    BEGIN
      IF pCust."No." = '' THEN
        EXIT;
      lServiceHeader.SETCURRENTKEY("Corporate No.");
      lServiceHeader.SETRANGE("Corporate No.", pCust."No.");
      lServiceHeader.SETFILTER("Service Order Type", GetHomeServiceFilter);
      EXIT(lServiceHeader.COUNT);
    END;

    PROCEDURE LookupHomeService@1100281003(pCust@1100281000 : Record 18);
    VAR
      lServiceHeader@1100281002 : Record 5900;
    BEGIN
      IF pCust."No." = '' THEN
        EXIT;

      lServiceHeader.SETCURRENTKEY("Corporate No.");
      lServiceHeader.ASCENDING(FALSE);//IMH03
      lServiceHeader.FILTERGROUP(2);
      lServiceHeader.SETRANGE("Corporate No.", pCust."No.");
      lServiceHeader.SETFILTER("Service Order Type", GetHomeServiceFilter);
      lServiceHeader.FILTERGROUP(0);
      IF lServiceHeader.ISEMPTY THEN
        EXIT;
      IF lServiceHeader.COUNT = 1 THEN BEGIN
        lServiceHeader.FINDFIRST;
        lServiceHeader.ShowCard(FALSE);
      END ELSE BEGIN
        FORM.RUNMODAL(0, lServiceHeader);
      END;
    END;

    PROCEDURE CalcNoOfClaim@1100281007(pCust@1100281000 : Record 18) : Integer;
    VAR
      lServiceHeader@1100281001 : Record 5900;
    BEGIN
      IF pCust."No." = '' THEN
        EXIT;
      lServiceHeader.SETCURRENTKEY("Corporate No.");
      lServiceHeader.SETRANGE("Corporate No.", pCust."No.");
      lServiceHeader.SETFILTER("Service Order Type", GetClaimFilter);
      EXIT(lServiceHeader.COUNT);
    END;

    PROCEDURE LookupClaim@1100281006(pCust@1100281000 : Record 18);
    VAR
      lServiceHeader@1100281002 : Record 5900;
    BEGIN
      IF pCust."No." = '' THEN
        EXIT;

      lServiceHeader.SETCURRENTKEY("Corporate No.");
      lServiceHeader.FILTERGROUP(2);
      lServiceHeader.SETRANGE("Corporate No.", pCust."No.");
      lServiceHeader.SETFILTER("Service Order Type", GetClaimFilter);
      lServiceHeader.FILTERGROUP(0);
      IF lServiceHeader.ISEMPTY THEN
        EXIT;
      IF lServiceHeader.COUNT = 1 THEN BEGIN
        lServiceHeader.FINDFIRST;
        lServiceHeader.ShowCard(FALSE);
      END ELSE BEGIN
        FORM.RUNMODAL(0, lServiceHeader);
      END;
    END;

    LOCAL PROCEDURE GetClaimFilter@1100281004() rValue : Code[1024];
    VAR
      lServOrderType@1100281000 : Record 5903;
    BEGIN
      lServOrderType.SETRANGE(Claim, TRUE);
      IF lServOrderType.FINDSET THEN
        REPEAT
          IF rValue = '' THEN
            rValue := lServOrderType.Code
          ELSE
            rValue += '|' + lServOrderType.Code;
        UNTIL lServOrderType.NEXT=0;
    END;

    LOCAL PROCEDURE GetHomeServiceFilter@1100281009() rValue : Code[1024];
    VAR
      lServOrderType@1100281000 : Record 5903;
    BEGIN
      lServOrderType.SETRANGE("Home Service", TRUE);
      IF lServOrderType.FINDSET THEN
        REPEAT
          IF rValue = '' THEN
            rValue := lServOrderType.Code
          ELSE
            rValue += '|' + lServOrderType.Code;
        UNTIL lServOrderType.NEXT=0;
    END;

    BEGIN
    {
      /** @r IMH03 @d 24/01/11 @a ISAT.LLT @v IMH6.01.002 @s Modif tri */
      /** @r IMH02 @d 10/12/2010 @a ISAT.ST @v IMH6.01.002 @s Gestion du service
          @c Affichage de fiche en non modal */
      /** @r IMH01 @d 09/11/2010 @a ISAT.LLT @v IMH6.01.011 @s Gestion du service @c cr‚ation */
    }
    END.
  }
}