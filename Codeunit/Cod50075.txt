OBJECT Codeunit 50075 DPS Code Setup Management
{
  OBJECT-PROPERTIES
  {
    Date=08/08/11;
    Time=17:33:46;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    OnRun=VAR
            lTempCodeList@1100281000 : TEMPORARY Record 50078;
            lCodeUsage@1100281001 : Record 50078;
          BEGIN
            lCodeUsage.SETCURRENTKEY("Setup Order");
            lCodeUsage.SETRANGE("Show In DGS", TRUE);
            SelectCodeList(lCodeUsage,10);
          END;

  }
  CODE
  {
    VAR
      Text000@1100281000 : TextConst 'ENU=Address Type,Occupier Nature,Residence Type,Housing Type,Property Type,Information Type,Job Amount Class,Project Type,Service Quote Code,Service Channel,Next...;FRA=Type d''adresse,Nature de l''occupant,Type de r‚sidence,Type d''habitation,Type de propri‚t‚,Type information,Montant du projet habitat,Type projet,Code devis produit,Canal service,Suite...';
      Text001@1100281001 : TextConst 'ENU=Service Requisitioner,Expense Payment,Role Code,VAT,Location Detail Code,Service Device Family,Service Accomm. Option,Diagnosis Element Code,Service Diagnosis Element Refusal Reason,Service Intervention Refusal Reason,Next...;FRA=Demandeur service,Paiement des frais,Code r“le,TVA,Code d‚tail lieu,Famille appareil,Option garantie compl‚mentaire,Code ‚l‚ment diagnostic,Code motif refus ‚l‚ment de diagnostic,Code motif refus intervention,Suite...';
      Text002@1100281002 : TextConst 'ENU=Quote External Refused Reason,Way Type,Claims Cause,No Restart Work Cause,Other Provider Cause;FRA=Code refus validation externe devis,Type de voie,Motif r‚clamation,Motif non reprise travaux,Motif autre prestataire';
      NextOption@1100281003 : TextConst 'ENU=Next...;FRA=Suite...';

    PROCEDURE SetParameters@1100281000();
    VAR
      lOptionSelect@1100281016 : Option;
      lOptionAll@1100281000 : 'Cancel,Add,OccNat,Resid,Hous,Prpty,Info,JobAmnt,Proj,Quote,Channel,Requ,ExpPaymt,Role,VAT,Locat,DevFam,AccOpt,DiagElmt,DiagElmtRfu,IntervRfu,QutExtRfu,Way,Claims,NoRestWork,OthProv';
      lCode@1100281001 : Record 50079;
      lContactAltAddress@1100281002 : Record 5051;
      lServiceHeader@1100281003 : Record 5900;
      lServiceQuoteCodes@1100281004 : Record 50062;
      lService@1100281005 : Record 50066;
      lServiceHeaderAddress@1100281006 : Record 50068;
      lInterventionDescription@1100281007 : Record 50070;
      lServiceDevice@1100281008 : Record 50071;
      lServiceAccommOption@1100281009 : Record 50073;
      lServiceDiagnosisElement@1100281010 : Record 50074;
      lServiceInterventionLine@1100281011 : Record 50075;
      lServiceInterventionQuote@1100281012 : Record 50076;
      lAddressReference@1100281013 : Record 50077;
      lServiceHeaderClaims@1100281014 : Record 50100;
    BEGIN
      // Param‚trage des codes en RTC
      lOptionSelect := STRMENU(Text000);
      IF lOptionSelect = 0 THEN
        EXIT;
      lOptionAll := lOptionSelect;

      IF lOptionSelect = 11 THEN BEGIN
        lOptionSelect := STRMENU(Text001);
        IF lOptionSelect = 0 THEN
          EXIT
        ELSE
          lOptionAll := lOptionSelect + 10;
      END;

      IF lOptionSelect = 11 THEN BEGIN
        lOptionSelect := STRMENU(Text002);
        IF lOptionSelect = 0 THEN
          EXIT
        ELSE
          lOptionAll := lOptionSelect + 20;
      END;

      CASE lOptionAll OF
        lOptionAll::Add :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Contact Alt. Address");
          lCode.SETRANGE("Field ID",lContactAltAddress.FIELDNO("Address Type"));
        END;
        lOptionAll::OccNat :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Contact Alt. Address");
          lCode.SETRANGE("Field ID",lContactAltAddress.FIELDNO("Occupier Nature"));
        END;
        lOptionAll::Resid :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Contact Alt. Address");
          lCode.SETRANGE("Field ID",lContactAltAddress.FIELDNO("Residence Type"));
        END;
        lOptionAll::Hous :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Contact Alt. Address");
          lCode.SETRANGE("Field ID",lContactAltAddress.FIELDNO("Housing Type"));
        END;
        lOptionAll::Prpty :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Contact Alt. Address");
          lCode.SETRANGE("Field ID",lContactAltAddress.FIELDNO("Property Type"));
        END;
        lOptionAll::Info :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Header");
          lCode.SETRANGE("Field ID",lServiceHeader.FIELDNO("Information Type"));
        END;
        lOptionAll::JobAmnt :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Header");
          lCode.SETRANGE("Field ID",lServiceHeader.FIELDNO("Job Amount Class"));
        END;
        lOptionAll::Proj :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Header");
          lCode.SETRANGE("Field ID",lServiceHeader.FIELDNO("Project Type"));
        END;
        lOptionAll::Quote :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Quote Codes");
          lCode.SETRANGE("Field ID",lServiceQuoteCodes.FIELDNO(Code));
        END;
        lOptionAll::Channel :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::Service);
          lCode.SETRANGE("Field ID",lService.FIELDNO(Channel));
        END;
        lOptionAll::Requ :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::Service);
          lCode.SETRANGE("Field ID",lService.FIELDNO(Requisitioner));
        END;
        lOptionAll::ExpPaymt :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::Service);
          lCode.SETRANGE("Field ID",lService.FIELDNO("Expense Payment"));
        END;
        lOptionAll::Role :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Header Address");
          lCode.SETRANGE("Field ID",lServiceHeaderAddress.FIELDNO("Role Code"));
        END;
        lOptionAll::VAT :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Intervention Description");
          lCode.SETRANGE("Field ID",lInterventionDescription.FIELDNO(VAT));
        END;
        lOptionAll::Locat :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Intervention Description");
          lCode.SETRANGE("Field ID",lInterventionDescription.FIELDNO("Location Detail Code"));
        END;
        lOptionAll::DevFam :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Device");
          lCode.SETRANGE("Field ID",lServiceDevice.FIELDNO(Family));
        END;
        lOptionAll::AccOpt :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Accomm. Option");
          lCode.SETRANGE("Field ID",lServiceAccommOption.FIELDNO("Option Code"));
        END;
        lOptionAll::DiagElmt :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Diagnosis Element");
          lCode.SETRANGE("Field ID",lServiceDiagnosisElement.FIELDNO("Diagnosis Element Code"));
        END;
        lOptionAll::DiagElmtRfu :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Diagnosis Element");
          lCode.SETRANGE("Field ID",lServiceDiagnosisElement.FIELDNO("Refusal Reason Code"));
        END;
        lOptionAll::IntervRfu :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Intervention Line");
          lCode.SETRANGE("Field ID",lServiceInterventionLine.FIELDNO("Refusal Reason Code"));
        END;
        lOptionAll::QutExtRfu :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Intervention Quote");
          lCode.SETRANGE("Field ID",lServiceInterventionQuote.FIELDNO("External Refused Reason"));
        END;
        lOptionAll::Way :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Address Reference");
          lCode.SETRANGE("Field ID",lAddressReference.FIELDNO("Way Type"));
        END;
        lOptionAll::Claims :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Header Claims");
          lCode.SETRANGE("Field ID",lServiceHeaderClaims.FIELDNO("Claims Cause"));
        END;
        lOptionAll::NoRestWork :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Header Claims");
          lCode.SETRANGE("Field ID",lServiceHeaderClaims.FIELDNO("No Restart Work Cause"));
        END;
        lOptionAll::OthProv :
        BEGIN
          lCode.SETRANGE("Table ID",DATABASE::"Service Header Claims");
          lCode.SETRANGE("Field ID",lServiceHeaderClaims.FIELDNO("Other Provider Cause"));
        END;
      END;
      FORM.RUNMODAL(FORM::Code,lCode);
    END;

    PROCEDURE LoadCodeList@1100281001(VAR pTempCodeUsage@1100281001 : Record 50078);
    VAR
      lCodeUsage@1100281000 : Record 50078;
      lint@1100281002 : Integer;
    BEGIN
      pTempCodeUsage.RESET;
      IF pTempCodeUsage.FINDLAST THEN
        lint := pTempCodeUsage."Setup Order" + 1
      ELSE
        lint := 1;
      lCodeUsage.SETRANGE("Show In DGS", TRUE);
      IF lCodeUsage.FINDSET THEN
        REPEAT
          pTempCodeUsage := lCodeUsage;
          pTempCodeUsage."Setup Order" := lint;
          pTempCodeUsage.INSERT;
        UNTIL lCodeUsage.NEXT=0;
    END;

    PROCEDURE SelectCodeList@1100281002(VAR pCodeUsage@1100281000 : Record 50078;pMaxLen@1100281003 : Integer);
    VAR
      lInt@1100281001 : Integer;
      lTempCodeUsage@1100281002 : TEMPORARY Record 50078;
      lOptionText@1100281004 : Text[360];
      lText@1100281005 : Text[360];
      lOptionSelect@1100281006 : Integer;
      lCount@1100281007 : Integer;
      lTotalShow@1100281008 : Integer;
    BEGIN
      lInt := 0;
      lCount := pCodeUsage.COUNT;
      IF lCount = 0 THEN
        EXIT;
      WITH pCodeUsage DO BEGIN
        FINDSET;
        REPEAT
          lInt += 1;
          IF Description = '' THEN BEGIN
            CALCFIELDS("Field Caption", "Table Name");
            lText := STRSUBSTNO('%1 (%2)', "Field Caption", "Table Name");
          END
          ELSE
            lText := Description;
          lText := DELCHR(lText,'=',',');
          IF STRLEN(lOptionText + lText + NextOption) + 2 > MAXSTRLEN(lOptionText) THEN BEGIN
            IF SelectOption(lTempCodeUsage, lOptionText,  lTotalShow <> lCount) >= 0 THEN
              EXIT;
            lInt := 1;
          END;

          IF lOptionText = '' THEN
            lOptionText := lText
          ELSE
            lOptionText += ',' + lText;
          lTempCodeUsage := pCodeUsage;
          lTempCodeUsage."Setup Order" := lInt;
          lTempCodeUsage.INSERT;

          lTotalShow += 1;
          IF lInt = pMaxLen THEN BEGIN
            IF SelectOption(lTempCodeUsage, lOptionText,  lTotalShow <> lCount) >= 0 THEN
              EXIT;
            lInt := 0;
          END;
        UNTIL NEXT=0;
      END;
      IF lInt <> 0 THEN
        SelectOption(lTempCodeUsage, lOptionText, FALSE);
    END;

    LOCAL PROCEDURE ShowCodeList@1100281008(pTableID@1100281000 : Integer;pFieldId@1100281001 : Integer);
    VAR
      lCode@1100281002 : Record 50079;
    BEGIN
      lCode.FILTERGROUP(2);
      lCode.SETRANGE("Table ID", pTableID);
      lCode.SETRANGE("Field ID", pFieldId);
      lCode.FILTERGROUP(0);
      FORM.RUN(0, lCode);
    END;

    PROCEDURE SelectOption@1100281003(VAR pTempCodeList@1100281000 : Record 50078;VAR pOptionString@1100281001 : Text[400];pAddNext@1100281002 : Boolean) rValue : Integer;
    VAR
      lOptionSelected@1100281003 : Integer;
    BEGIN
      IF pAddNext THEN
        pOptionString += ',' + NextOption;
      rValue := STRMENU(pOptionString);
      IF rValue <> 0 THEN BEGIN
        pTempCodeList.SETRANGE("Setup Order", rValue);
        IF pTempCodeList.FINDFIRST THEN
          ShowCodeList(pTempCodeList."Table ID", pTempCodeList."Field ID")
        ELSE
          rValue := -1;
      END;
      pTempCodeList.RESET;
      pTempCodeList.DELETEALL;
      pOptionString := '';
    END;

    BEGIN
    {
      /** @r IMH01 @d 28/02/11 @a ISAT.ZW @v IMH6.01.005 @s Gestion des codes
          @c Cr‚ation */
    }
    END.
  }
}