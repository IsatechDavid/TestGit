OBJECT Codeunit 50146 ANSAAP Explode Rcvd Error
{
  OBJECT-PROPERTIES
  {
    Date=01/08/11;
    Time=11:32:44;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    TableNo=8016729;
    OnRun=VAR
            lText000@1100481000 : TextConst 'ENU=%1 - %2 : XML Generating Handler started;FRA=%1 - %2 : D‚marrage du handler de g‚n‚ration XML';
            lEAIRcvdMQ3@1100281000 : Record 8016729;
          BEGIN
            WITH Rec DO BEGIN
              SETRANGE(Status,Status::Error);
              SETFILTER("No. Of Lines", '<>1');
              TotalCounter := COUNT;
              CurrentCounter := 0;
              ModuloCounter := ROUND(TotalCounter * 5 / 100,1,'>');
              WDialog.OPEN(Text001);
              IF FINDSET THEN BEGIN
                REPEAT
                  lEAIRcvdMQ3 := Rec;
                  ExplodeRec(lEAIRcvdMQ3);
                  COMMIT;
                  CurrentCounter += 1;
                  IF CurrentCounter MOD ModuloCounter = 0 THEN BEGIN
                    WDialog.UPDATE(1,ROUND(CurrentCounter / TotalCounter * 9999,1));
                  END;
                UNTIL NEXT = 0;
              END;
            END;
          END;

  }
  CODE
  {
    VAR
      Text001@1100281005 : TextConst 'FRA=Traitement...@1@@@@@@@@@@@@@@@@@@@@';
      ROOT@1100281001 : TextConst 'FRA=MESSAGE';
      EAIMessageQueue@1100281007 : Record 8016704;
      TotalCounter@1100281002 : Integer;
      CurrentCounter@1100281003 : Integer;
      ModuloCounter@1100281004 : Integer;
      WDialog@1100281006 : Dialog;

    PROCEDURE ExplodeRec@1100281000(pEAIRcvdMQ3@1100281000 : Record 8016729);
    VAR
      lEAIXMLMgt@1100281007 : Codeunit 8016710;
      lXMLDocument@1100281011 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
      lXMLDocumentNode@1100281010 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXMLNode@1100281004 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXMLNodeList@1100281001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList";
      lOutStream@1100281002 : OutStream;
    BEGIN
      IF pEAIRcvdMQ3."No. Of Lines" = 1 THEN
        EXIT;
      WITH lEAIXMLMgt DO BEGIN
        IF ISCLEAR(lXMLDocument) THEN BEGIN
          CREATE(lXMLDocument);
        END;

        pEAIRcvdMQ3.CALCFIELDS("XML Source");
        pEAIRcvdMQ3."XML Source".CREATEOUTSTREAM(lOutStream);
        lXMLDocument.load(lOutStream);
        lXMLDocumentNode := lXMLDocument.documentElement;

        IF FindNodes(lXMLDocumentNode,'MembCont',lXMLNodeList) THEN BEGIN
          lXMLNodeList.reset;
          IF lXMLNodeList.length > 1 THEN BEGIN
            lXMLNode := lXMLNodeList.nextNode;
            WHILE NOT ISCLEAR(lXMLNode) DO BEGIN
              CreateXMLdoc(lXMLNode, pEAIRcvdMQ3);
              lXMLNode := lXMLNodeList.nextNode;
            END;
            pEAIRcvdMQ3.DELETE;
          END;
        END;
      END;
    END;

    LOCAL PROCEDURE CreateXMLdoc@1100281002(VAR pXMLNode@1100281001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";pEAIRcvdMQ3@1100281009 : Record 8016729);
    VAR
      lEAIRcvdMQ3@1100281004 : Record 8016729;
      lEAIXMLMgt@1100281007 : Codeunit 8016710;
      lEAIMQBufferMgt@1100281006 : Codeunit 8016708;
      NoSeriesMgt@1100281008 : Codeunit 396;
      lXMLDocument@1100481006 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
      lXMLRootNode@1100481009 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXMLDocumentNode@1100481007 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXMLNewNode@1100281000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lRecID@1100481003 : RecordID;
      lRecRef@1100481002 : RecordRef;
      lInStr@1100481005 : InStream;
      lNameSpace@1100481011 : Text[30];
      lReturnTableName@1100281002 : Text[250];
      lCode@1100281003 : Code[20];
    BEGIN
      CLEAR(lXMLDocument);
      CREATE(lXMLDocument);

      lNameSpace := '';

      IF NOT lXMLDocument.loadXML('<?xml version="1.0" encoding="ISO-8859-1" ?><' + ROOT + '/>') THEN BEGIN
        EXIT;
      END;

      lXMLRootNode := lXMLDocument.documentElement;
      lXMLRootNode.appendChild(pXMLNode);

      lEAIRcvdMQ3."XML Source".CREATEINSTREAM(lInStr);
      lXMLDocument.save(lInStr);
      CLEAR(lXMLDocument);
      lEAIRcvdMQ3 := pEAIRcvdMQ3;
      lEAIRcvdMQ3."Entry No." := '';
      lEAIRcvdMQ3.Step := lEAIRcvdMQ3.Step::Validation;
      lEAIRcvdMQ3.Status := lEAIRcvdMQ3.Status::Unprocessed;
      lEAIRcvdMQ3."Comment Status" := '';
      lEAIRcvdMQ3."No. Of Lines" := 1;
      IF lEAIRcvdMQ3."No. Series" = '' THEN BEGIN
        IF EAIMessageQueue.Code <> pEAIRcvdMQ3."EAI MQ Code" THEN
          EAIMessageQueue.GET(pEAIRcvdMQ3."EAI MQ Code");
        NoSeriesMgt.InitSeries(EAIMessageQueue."Message No. Series",'',0D,lEAIRcvdMQ3."Entry No.",lCode);
      END
      ELSE
        NoSeriesMgt.InitSeries(lEAIRcvdMQ3."No. Series",'',0D,lEAIRcvdMQ3."Entry No.",lCode);
      lEAIRcvdMQ3.INSERT;
    END;

    BEGIN
    {
      /** @r IMH01 @d 12/07/11 @a ISAT.PC @v IMH6.01.133 @s Fichier Societaire */
    }
    END.
  }
}