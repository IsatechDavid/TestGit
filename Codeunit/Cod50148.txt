OBJECT Codeunit 50148 Provider Notification Mgt
{
  OBJECT-PROPERTIES
  {
    Date=04/06/13;
    Time=17:10:14;
    Modified=Yes;
    Version List=IMH6.01,JUIN;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {
    VAR
      InterfaceSetup@1100281000 : Record 50139;
      InterfaceSetupOK@1100281001 : Boolean;

    PROCEDURE CreateNotifFromServHeader@1100281003(pServiceHeader@1100281000 : Record 5900;pXServiceHeader@1100281003 : Record 5900);
    VAR
      lText@1000000000 : Text[1024];
    BEGIN
      // g‚n‚re des notifications pour les prestataires affect‚s … cette ligne de produit lorsque l'adresse MEO
      // change
      WITH pServiceHeader DO BEGIN
        IF ("Ship-to Address" = pXServiceHeader."Ship-to Address") AND
           ("Ship-to Address 2" = pXServiceHeader."Ship-to Address 2") AND
           ("Ship-to Address 3" = pXServiceHeader."Ship-to Address 3") AND
           ("Ship-to City" = pXServiceHeader."Ship-to City") AND
           ("Ship-to Post Code" = pXServiceHeader."Ship-to Post Code") AND
           ("Ship-to E-Mail" = pXServiceHeader."Ship-to E-Mail") AND
           ("Ship-to Phone" = pXServiceHeader."Ship-to Phone") AND
           ("Ship-to Country/Region Code" = pXServiceHeader."Ship-to Country/Region Code") AND //IMH04.ST
           ("Ship-to Mobile Phone No." = pXServiceHeader."Ship-to Mobile Phone No.") //IMH04.ST
        THEN BEGIN
          EXIT;
        END;
        GetInterfaceSetup;
        lText := InterfaceSetup."Ship-to Address Mod. Descr.";
      //<<IMH05.ST
        IF "Ship-to Address" <> pXServiceHeader."Ship-to Address" THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to Address"), "Ship-to Address"),lText);
        IF "Ship-to Address 2" <> pXServiceHeader."Ship-to Address 2" THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to Address 2"), "Ship-to Address 2"),lText);
        IF "Ship-to Address 3" <> pXServiceHeader."Ship-to Address 3" THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to Address 3"), "Ship-to Address 3"),lText);
        IF "Ship-to City" <> pXServiceHeader."Ship-to City" THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to City"), "Ship-to City"),lText);
        IF "Ship-to Post Code" <> pXServiceHeader."Ship-to Post Code" THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to Post Code"), "Ship-to Post Code"),lText);
        IF "Ship-to E-Mail" <> pXServiceHeader."Ship-to E-Mail" THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to E-Mail"), "Ship-to E-Mail"),lText);
        IF "Ship-to Phone" <> pXServiceHeader."Ship-to Phone" THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to Phone"), "Ship-to Phone"),lText);
        IF "Ship-to Country/Region Code" <> pXServiceHeader."Ship-to Country/Region Code" THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to Country/Region Code"), "Ship-to Country/Region Code"),lText);
        IF "Ship-to Mobile Phone No." <> pXServiceHeader."Ship-to Mobile Phone No." THEN
          AddTextline(STRSUBSTNO('%1 : %2', FIELDCAPTION("Ship-to Mobile Phone No."), "Ship-to Mobile Phone No."),lText);
      //>>IMH05.ST
        CreateNotifOnAllIntervention("No.",lText,0);
      END;
    END;

    PROCEDURE CreateNotifFromServHeaderAddr@1100281006(pServiceHeaderAddress@1100281000 : Record 50068;pXServiceHeaderAddress@1100281003 : Record 50068;pNewContact@1100281001 : Boolean);
    BEGIN
      // g‚n‚re des notifications pour les contacts annexes affect‚s … cette ligne de produit lorsque l'adresse MEO

      WITH pServiceHeaderAddress DO BEGIN
        IF pServiceHeaderAddress."Address Type" <> pServiceHeaderAddress."Address Type"::"Annexe Contact" THEN BEGIN
          EXIT;
        END;
        GetInterfaceSetup;
        //<<IMH06.PC
        //IF (pServiceHeaderAddress."Address No." = 0) THEN BEGIN
        IF pNewContact THEN BEGIN
        //>>IMH06.PC
          CreateNotifOnAllIntervention("Service Header No.",InterfaceSetup."New Serv. Hd. Add. Descr.","Address No.");
        END ELSE BEGIN
          IF (Address = pXServiceHeaderAddress.Address) AND
             ("Address 2" = pXServiceHeaderAddress."Address 2") AND
             (City = pXServiceHeaderAddress.City) AND
             ("Post Code" = pXServiceHeaderAddress."Post Code") AND
             ("Phone No." = pXServiceHeaderAddress."Phone No.")
          THEN BEGIN
            EXIT;
          END;
          CreateNotifOnAllIntervention("Service Header No.",InterfaceSetup."Serv. Hd. Addr. Mod. Descr.","Address No.");
        END;
      END;
    END;

    LOCAL PROCEDURE CreateNotifOnAllIntervention@1100281004(pServiceHeaderNo@1100281000 : Code[20];pCommentText@1100281004 : Text[1024];pAddressNo@1100281001 : Integer);
    VAR
      lServiceInterventionLine@1100281003 : Record 50075;
      lProvider@1100281002 : Record 50000;
    BEGIN
      // Parcours des interventions d'une ligne de produit afin de cr‚er des notifs extranet

      WITH lServiceInterventionLine DO BEGIN
        SETRANGE("Service Header No.",pServiceHeaderNo);
        SETFILTER("Intervention Status",'<%1',"Intervention Status"::Closed);
        SETFILTER("Mission Status",'<>%1',"Mission Status"::Canceled);
        SETFILTER("Provider No.",'<>%1','');
        IF FINDSET THEN BEGIN
          REPEAT
            lProvider.GET("Provider No.");
            IF lProvider.Extranet THEN BEGIN
              CreateCommentToNotification(
                "Provider No.",
                "Service No.",
                "Service Header No.",
                "Line No.",
                DATABASE::"Service Intervention Line",
                0,
                pCommentText,
                pAddressNo);
            END;
          UNTIL NEXT = 0;
        END;
      END;
    END;

    PROCEDURE CreateNotifFromInteraction@1100281000(pServiceHeaderNo@1100281000 : Code[20];pServiceInterventionLineNo@1100281004 : Integer;pInteractionDescr@1100281005 : Text[80]);
    VAR
      lServiceInterventionLine@1100281003 : Record 50075;
      lProvider@1100281002 : Record 50000;
      lInterfaceSetup@1100281001 : Record 50139;
      lDescription@1100281006 : Text[1024];
    BEGIN
      // IMH02.PC
      // Ajout d'une notification suite ajout d'une interaction dont le modŠle est top‚ "… notifier … l'extranet"

      WITH lServiceInterventionLine DO BEGIN
        IF NOT GET(pServiceHeaderNo,pServiceInterventionLineNo) THEN BEGIN
          EXIT;
        END;
        IF ("Intervention Status" >= "Intervention Status"::Closed) OR
           ("Mission Status" = "Mission Status"::Canceled) OR
           ("Provider No." = '')
        THEN BEGIN
          EXIT;
        END;

        lInterfaceSetup.GET;
      //<<IMH03.ST
        lDescription := lInterfaceSetup."Add Doc. Descr.";
        IF (lDescription <> '') AND (pInteractionDescr <> '') THEN
          lDescription := lDescription + ' : ' + pInteractionDescr;
      //>>IMH03.ST

        lProvider.GET("Provider No.");
        IF lProvider.Extranet THEN BEGIN
          CreateCommentToNotification(
            "Provider No.",
            "Service No.",
            "Service Header No.",
            "Line No.",
            DATABASE::"Service Intervention Line",
            0,
      //<<IMH03.ST
      //      lInterfaceSetup."Add Doc. Descr.",
            lDescription,
      //>>IMH03.ST
            0);
        END;
      END;
    END;

    PROCEDURE CreateCommentToNotification@1100281001(pProviderNo@1100281001 : Code[20];pServiceNo@1100281002 : Code[20];pServiceHeaderNo@1100281003 : Code[20];pInterventionLineNo@1100281004 : Integer;pSourceTableID@1100281005 : Integer;pType@1100281006 : 'Intervention,Contact,Provider';pDescription@1100281007 : Text[1024];pAddressNo@1100281008 : Integer);
    VAR
      lAdvCommentLine@1100281010 : Record 50056;
      lProvider@1100281017 : Record 50000;
      lTextMgt@1100281012 : Codeunit 50085;
    BEGIN
      // Creation d'un commentaire de type publication extranet

      IF pProviderNo = '' THEN BEGIN
        EXIT;
      END;
      IF NOT lProvider.GET(pProviderNo) THEN BEGIN
        EXIT;
      END;
      IF NOT lProvider.Extranet THEN BEGIN
        EXIT;
      END;

      WITH lAdvCommentLine DO BEGIN
        "Source Table ID" := pSourceTableID;
        "Source No." := pServiceNo;
        "Source No. 2" := pServiceHeaderNo;
        "Source No. 3" := pInterventionLineNo;
        Publication := Publication::Extranet;
        "Address No." := pAddressNo;
      END;

      //<<IMH03.ST
      //lCommentBigText.ADDTEXT(pDescription);
      //lTextMgt.SaveBigText(lAdvCommentLine,WORKDATE,lCommentBigText);
      lTextMgt.SaveText(lAdvCommentLine, WORKDATE, pDescription);
      //>>IMH03.ST
    END;

    PROCEDURE CreateNotifFromComment@1100281002(pAdvCommentLine@1100281001 : Record 50056);
    VAR
      lProviderNotification@1100281000 : Record 50153;
      lAdvCommentLine@1100281010 : Record 50056;
      lServiceInterventionLine@1100281002 : Record 50075;
      lProvider@1100281003 : Record 50000;
      lTextMgt@1100281012 : Codeunit 50085;
      lCommentBigText@1100281011 : BigText;
    BEGIN
      // Creation d'une notification … partir d'un commentaire uniquement pour les prestataires extranet

      WITH pAdvCommentLine DO BEGIN
        IF Publication <> Publication::Extranet THEN BEGIN
          EXIT;
        END;
        IF "Attached To Line No." <> 0 THEN BEGIN
          EXIT;
        END;
        IF NOT lServiceInterventionLine.GET("Source No. 2","Source No. 3") THEN BEGIN
          EXIT;
        END;
        IF lServiceInterventionLine."Provider No." = '' THEN BEGIN
          EXIT;
        END;
        IF NOT lProvider.GET(lServiceInterventionLine."Provider No.") THEN BEGIN
          EXIT;
        END;
        IF NOT lProvider.Extranet THEN BEGIN
          EXIT;
        END;

        lProviderNotification.INIT;
        lProviderNotification."Provider No." := lServiceInterventionLine."Provider No.";
        lProviderNotification."Entry No." := 0;
        lProviderNotification."Service No." := "Source No.";
        lProviderNotification."Service Header No." := "Source No. 2";
        lProviderNotification."Intervention Line No." := "Source No. 3";
        lProviderNotification."Intervention No." := lServiceInterventionLine."Intervention No.";
        lProviderNotification.Description := Comment;
        lProviderNotification."Notification Type" := lProviderNotification."Notification Type"::Intervention;
        lProviderNotification."Contact No." := ''; //lServiceInterventionLine."Contact No.";
        lProviderNotification."Comment Source Table ID" := DATABASE::"Service Intervention Line";
        lProviderNotification."Comment Source No. 4" := "Source No. 4";
        lProviderNotification."Comment Source No. 5" := "Source No. 5";
        lProviderNotification."Comment Line No." := "Line No.";
        lProviderNotification."Address No." := "Address No.";
        lProviderNotification.INSERT(TRUE);
      END;
    END;

    PROCEDURE GetInterfaceSetup@1100281005();
    BEGIN
      IF InterfaceSetupOK THEN BEGIN
        EXIT;
      END;

      InterfaceSetupOK := TRUE;
      InterfaceSetup.GET;
    END;

    PROCEDURE AddTextline@1000000004(pText@1000000000 : Text[1024];VAR pToText@1000000001 : Text[1024]);
    VAR
      lCRLF@1000000002 : Text[2];
    BEGIN
      lCRLF[1] := 13;
      lCRLF[2] := 10;
      IF STRLEN(pToText) + STRLEN(pText) < MAXSTRLEN(pToText)-2 THEN
        pToText := pToText  + lCRLF + pText
      // pas de g‚n‚ration de la ligne sinon
    END;

    BEGIN
    {
      /** @r IMH06 @d 04/06/13 @a ISAT.PC @v IMH6.01.347 @s JUIN @h 4322
          @c Ajout du paramŠtre pNewContact dans la fonction CreateNotifFromServHeaderAddr */
      /** @r IMH05 @d 28/03/12 @a ISAT.ST @v IMH6.01.165 @s Extranet @h 1479 @c Ajout d‚tail modif */
      /** @r IMH04 @d 19/12/11 @a ISAT.ST @v IMH6.01.165 @s Extranet @h 1479 @c Ajout mobile MEO */
      /** @r IMH03 @d 14/11/11 @a ISAT.ST @v IMH6.01.165 @s Extranet @h mantis2026
          @c Ajout pInteractionDescr dans CreateNotifFromInteraction
          @c SaveText au lieu de SaveBigText */
      /** @r IHM02 @d 08/10/11 @a ISAT.PC @v IMH6.01.165 @s Extranet @h mantis1646 @c Ajout CreateNotifFromInteraction */
      /** @r IHM01 @d 06/07/11 @a ISAT.ZW @v IMH6.01.165 @s Extranet @c Creation */
    }
    END.
  }
}