OBJECT Codeunit 50160 DARVA Send Ack. Val. Act
{
  OBJECT-PROPERTIES
  {
    Date=24/05/13;
    Time=15:08:33;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    TableNo=8016720;
    OnRun=VAR
            lXMLDocument@1100281005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
            lInStream@1100281004 : InStream;
            lXMLDocument2@1100281003 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
            lInStream2@1100281002 : InStream;
            lOutStream2@1100281001 : OutStream;
            lFile@1100281000 : File;
          BEGIN
            Code(Rec);
          END;

  }
  CODE
  {

    PROCEDURE Code@1100281001(VAR pEAISentMQBuffer@1100281007 : Record 8016720);
    VAR
      lEAIExchangeType@1100281003 : Record 8016717;
      lEAIXMLDocMgt@1100281008 : Codeunit 8016713;
      lWinHTTPService@1100281002 : Automation "{662901FC-6951-4854-9EB2-D9A2570F2B2E} 5.1:{2087C2F4-2CEF-4953-A8AB-66779B670495}:'Microsoft WinHTTP Services, version 5.1'.WinHttpRequest";
      lXmlDoc@1100281005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";
      lXmlNewDoc@1100281006 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";
      lXmlNewChild1@1100281001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXmlNode@1100281009 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lInStream@1100281004 : InStream;
      lOutStream@1100281000 : OutStream;
      lError001@1100281010 : TextConst 'ENU=Error from Web Service;FRA=Erreur au niveau du Web Service';
    BEGIN
      lEAIExchangeType.GET(lEAIExchangeType.Type::Outbound,pEAISentMQBuffer."EAI Exchange Type Code");
      lEAIExchangeType.TESTFIELD("Outgoing Queue Label");
      lEAIExchangeType.TESTFIELD(Enable);

      IF ISCLEAR(lWinHTTPService) THEN BEGIN
        CREATE(lWinHTTPService);
      END;

      lWinHTTPService.Open('POST',lEAIExchangeType."Outgoing Queue Label",0);
      lWinHTTPService.SetRequestHeader('Content-type','application/soap+xml; charset=UTF-8');
      lWinHTTPService.SetRequestHeader('SOAPAction','""');
      lWinHTTPService.SetTimeouts(10000,10000,10000,0);

      CREATE(lXmlDoc);
      pEAISentMQBuffer.CALCFIELDS("XML Source");
      pEAISentMQBuffer."XML Source".CREATEINSTREAM(lInStream);
      lXmlDoc.load(lInStream);
      lWinHTTPService.Send(lXmlDoc);
      CLEAR(lInStream);
      CLEAR(lXmlDoc);

      IF NOT lWinHTTPService.WaitForResponse(60) THEN BEGIN
        ERROR('Request timed out.');
      END;

      IF lWinHTTPService.Status <> 200 THEN BEGIN
        CLEAR(pEAISentMQBuffer."Response XML Source");
        CREATE(lXmlDoc);
        lXmlDoc.load(lWinHTTPService.ResponseStream);
        lXmlNode := lXmlDoc.documentElement;
        IF NOT ISCLEAR(lXmlNode) THEN BEGIN
          pEAISentMQBuffer."Response XML Source".CREATEOUTSTREAM(lOutStream);
          CREATE(lXmlNewDoc);
          lEAIXMLDocMgt.CreateXmlByRootNodeName2(lXmlNewDoc,'Retour_WS',lXmlNewChild1);
          lXmlNewChild1.appendChild(lXmlNode);
          lXmlNewDoc.save(lOutStream);
        END;
        pEAISentMQBuffer.Status := pEAISentMQBuffer.Status::Error;
        pEAISentMQBuffer."Not Change Status" := TRUE;
        pEAISentMQBuffer."Comment Status" := COPYSTR(lWinHTTPService.StatusText,1,MAXSTRLEN(pEAISentMQBuffer."Comment Status"));
      END;
    END;

    BEGIN
    {
      /** @r IMH03 @d 06/05/13 @a ISAT.PC @v IMH6.01.222 @s Uniformisation Darva @c r‚ecriture complŠte*/
      /** @r IMH02 @d 26/08/11 @a ISAT.ST @v IMH6.01.134 @s DARVA Management @c Ajout Response XML Source */
      /** @r IMH01 @d 13/07/11 @a ISAT.MA @v IMH6.01.134 @s DARVA Management @c Cr‚ation */
    }
    END.
  }
}