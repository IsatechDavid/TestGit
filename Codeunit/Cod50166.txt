OBJECT Codeunit 50166 DARVA Rcvd SDR45 Post Act
{
  OBJECT-PROPERTIES
  {
    Date=25/02/14;
    Time=16:15:14;
    Modified=Yes;
    Version List=IMH6.01,SysAgDarva;
  }
  PROPERTIES
  {
    TableNo=8016721;
    OnRun=VAR
            lEAIRcvdMQBuffer@1100281000 : TEMPORARY Record 8016721;
          BEGIN
            lEAIRcvdMQBuffer := Rec;
            Rec.CALCFIELDS("XML Source");
            lEAIRcvdMQBuffer."XML Source" := "XML Source";
            Code(lEAIRcvdMQBuffer);
            Rec := lEAIRcvdMQBuffer;
          END;

  }
  CODE
  {
    VAR
      TmpCommentLine@1100281006 : TEMPORARY Record 50056;
      DARVAMgt@1100281001 : Codeunit 50150;
      DARVAXMLMgt@1100281002 : Codeunit 50154;
      TrayLineMgt@1100281000 : Codeunit 50050;
      XMLDom@1100281003 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";
      InterfaceSetup@1100281004 : Record 50139;
      Text001@1100281007 : TextConst 'ENU=DARVA Cancel;FRA=Annulation Darva';
      Text002@1100281008 : TextConst 'ENU=Comments;FRA=Cf commentaire';
      Text003@1100281005 : TextConst 'ENU=GTA reference %1 doesn''t exists.;FRA=La r‚f‚rence GTA %1 n''est pas d‚finie.';
      Text004@1100281009 : TextConst 'ENU=It is impossible to get service header from service %1 with mission no. %2;FRA=Impossible d''idenfier la ligne produit du service %1 portant le nø de mission %2';
      Text012@1100281011 : TextConst 'ENU=Error in attachment rec research;FRA=Erreur de la recherche du flux piŠce jointe';
      Text013@1100281010 : TextConst 'ENU=Attachment difference between DT (%1) and DARVA request (%2);FRA=Diff‚rence de nb PJ entre les infos DT (%1) et la demande DARVA (%2)';

    PROCEDURE Code@1100281000(VAR pEAIRcvdMQBuffer@1100281000 : Record 8016721);
    VAR
      lService@1100281009 : Record 50066;
      lServiceHeader@1100281008 : Record 5900;
      lEAIPartnerCrossRef@1100281004 : Record 8016748;
      lEAIAct@1100281017 : Record 8016706;
      lTrayLine@1100281016 : Record 50048;
      lCustomer@1100281018 : Record 18;
      lDARVAMission@1100281020 : Record 50168;
      lAdvCommentLineBuffer@1100281021 : TEMPORARY Record 50056;
      lEAIDocEntryMgt@1100281007 : Codeunit 8016707;
      lXmlDom@1100281002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";
      lXmlDomElement@1100281005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";
      lXmlDomElement2@1100281015 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";
      lXmlDomElement3@1100281014 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";
      lXmlNodeList@1100281024 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList";
      lXmlNodeList2@1100281006 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList";
      lAttributeNode@1100281013 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lInStream@1100281001 : InStream;
      lCorporateNo@1100281012 : Code[20];
      lServiceRefNo@1100281011 : Code[20];
      lMissionOrder@1100281003 : Code[20];
      lDARVACorporateNo@1100281010 : Code[20];
      lCorporateRegionCode@1100281019 : Code[20];
      lResult@1100281023 : ' ,Quit,NoAttach,CreateMessage,ToTreat';
      lAttachNb@1100281022 : Integer;
      lDarvaSwitchSetup@1000000000 : Record 50180;
    BEGIN
      CREATE(lXmlDom);
      pEAIRcvdMQBuffer.CALCFIELDS("XML Source");
      pEAIRcvdMQBuffer."XML Source".CREATEINSTREAM(lInStream);
      lXmlDom.load(lInStream);
      InterfaceSetup.GET;
      DARVAXMLMgt.InitXMLDom(lXmlDom,XMLDom);

      //<<IMH11.PC
      lAttachNb := CheckAttachment(lXmlDom,pEAIRcvdMQBuffer,lResult);
      CASE lResult OF
        lResult::" " : ERROR(Text012);
        lResult::Quit : BEGIN
          pEAIRcvdMQBuffer."Not Change Status" := TRUE;
          EXIT;
        END;
        lResult::NoAttach : ;
        lResult::CreateMessage : BEGIN
          CreateAttachmentDemand(pEAIRcvdMQBuffer,lAttachNb);
          pEAIRcvdMQBuffer."Not Change Status" := TRUE;
          pEAIRcvdMQBuffer."Attachment Nb" := lAttachNb;
          EXIT;
        END;
        lResult::ToTreat : GetEAIRcvdPJ(pEAIRcvdMQBuffer,lXmlNodeList2);
      END;
      //>>IMH11.PC

      lXmlNodeList := XMLDom.getElementsByTagName('GR010101');
      lXmlDomElement := lXmlNodeList.item(0);

      lXmlDomElement2 := lXmlDomElement.selectSingleNode('SE0020101');

      lDARVACorporateNo := DARVAMgt.GetNodeValue(lXmlDomElement2,'DE00460101');
      IF NOT lEAIPartnerCrossRef.GetFromCrossRef(
               DATABASE::Customer,pEAIRcvdMQBuffer."EAI Partner Code",
               lDARVACorporateNo,lEAIPartnerCrossRef)
      THEN BEGIN
        ERROR(Text003,lDARVACorporateNo);
      END;

      lEAIPartnerCrossRef.TESTFIELD("Source No.");

      IF lCustomer.GET(lEAIPartnerCrossRef."Source No.") THEN BEGIN
        IF lCustomer."Parent Customer No." <> '' THEN BEGIN
          lCorporateNo := lCustomer."Parent Customer No."
        END ELSE BEGIN
          lCorporateNo := lCustomer."No.";
        END;
        lCorporateRegionCode := lCustomer."Corporate Region Code";
      END ELSE BEGIN
        lCorporateNo := lEAIPartnerCrossRef."Source No.";
      END;

      lServiceRefNo := DARVAMgt.GetNodeValue(lXmlDomElement2,'DE00470101');
      lServiceRefNo := ReferenceNoWithOutZero(lServiceRefNo);
      lMissionOrder := DARVAMgt.GetNodeValue(lXmlDomElement2,'DE00530101');
      pEAIRcvdMQBuffer."Reference No." := lServiceRefNo;
      pEAIRcvdMQBuffer."Your Reference" := lMissionOrder;

      //<<IMH12.ACO
      lDARVAMission.SETCURRENTKEY("GTA Code", "Reference No.", "Mission No.");
      lDARVAMission.SETRANGE("GTA Code", DARVAXMLMgt.GetDARVAGTACode(XMLDom));
      lDARVAMission.SETRANGE("Reference No.", pEAIRcvdMQBuffer."Reference No.");
      lDARVAMission.SETRANGE("Mission No.", pEAIRcvdMQBuffer."Your Reference");
      IF lDARVAMission.FINDFIRST THEN BEGIN
        lServiceHeader.SETRANGE("No.", lDARVAMission."Service Header No.");
        IF lServiceHeader.FINDFIRST THEN BEGIN
          lDarvaSwitchSetup.SETCURRENTKEY("Message Code", "Message Version", "Message Release", "Message Sub-type");
          lDarvaSwitchSetup.SETRANGE("Message Code", pEAIRcvdMQBuffer."Message Code");
          lDarvaSwitchSetup.SETRANGE("Message Version", pEAIRcvdMQBuffer."Message Version");
          lDarvaSwitchSetup.SETRANGE("Message Release", pEAIRcvdMQBuffer."Message Release");
          lDarvaSwitchSetup.SETRANGE("Message Sub-type", pEAIRcvdMQBuffer."Message Sub-type");
          lDarvaSwitchSetup.SETRANGE("Line Type", lDarvaSwitchSetup."Line Type"::"Service Order Type");
          lDarvaSwitchSetup.SETRANGE("Service Order Type", lServiceHeader."Service Order Type");
          lDarvaSwitchSetup.SETRANGE("Corporate No.", lCorporateNo);
          lDarvaSwitchSetup.SETRANGE("Corporate Region Code", lCorporateRegionCode);
          lDarvaSwitchSetup.SETRANGE(Direction, lDarvaSwitchSetup.Direction::Receipt);

          IF lDarvaSwitchSetup.ISEMPTY THEN
            lDarvaSwitchSetup.SETRANGE("Corporate Region Code");

          IF lDarvaSwitchSetup.FINDFIRST THEN BEGIN
            IF pEAIRcvdMQBuffer."EAI Act Code" <>  lDarvaSwitchSetup."Act Code" THEN BEGIN
               pEAIRcvdMQBuffer."EAI Act Code" := lDarvaSwitchSetup."Act Code";
               pEAIRcvdMQBuffer."Not Change Status" := TRUE;
               EXIT;
            END;
          END;
        END;
      END;
      //>>IMH12.ACO

      lTrayLine.INIT;
      lTrayLine."Service Reference No." := lServiceRefNo;
      lTrayLine."Mission No." := lMissionOrder;
      lTrayLine."Corporate No." := lCorporateNo;
      lTrayLine."DARAV Message ID" := pEAIRcvdMQBuffer."Message ID";
      lTrayLine."EAI Entry No." := pEAIRcvdMQBuffer."Entry No.";

      lEAIAct.GET(pEAIRcvdMQBuffer."EAI Act Type Code", pEAIRcvdMQBuffer."EAI Act Code");
      lEAIAct.TESTFIELD("Create Tray Line On Receive", TRUE); // cr‚ation activit‚ obligatoire pour ce message

      lService.SETCURRENTKEY("Reference No.", "Corporate No.");
      lService.SETFILTER("Reference No.",lServiceRefNo);
      lService.SETRANGE("Corporate No.",lCorporateNo);
      IF lCorporateRegionCode <> '' THEN BEGIN
        lService.SETRANGE("Corporate Region Code",lCorporateRegionCode);
      END;
      lService.FINDFIRST;
      pEAIRcvdMQBuffer."Service No." := lService."Service No.";

      lServiceHeader.SETCURRENTKEY("Service No.");
      lServiceHeader.SETRANGE("Service No.",lService."Service No.");
      lServiceHeader.SETRANGE("Your Reference",lMissionOrder);

      IF lServiceHeader.ISEMPTY THEN BEGIN
        lDARVAMission.RESET;
        lDARVAMission.SETRANGE("Service No." , lService."Service No.");
        lDARVAMission.SETRANGE("Mission No.",lMissionOrder + '*');
        IF lDARVAMission.ISEMPTY THEN BEGIN
          lDARVAMission.SETRANGE("Mission No.",lMissionOrder);
          IF lDARVAMission.FINDFIRST THEN BEGIN
            lServiceHeader.GET(lServiceHeader."Document Type"::Invoice,lDARVAMission."Service Header No.");
          END ELSE BEGIN
            ERROR(Text004,lService."Service No.",lMissionOrder);
          END;
        END;
      END ELSE BEGIN
        lServiceHeader.FINDFIRST;
      END;

      pEAIRcvdMQBuffer."Service Header No." := lServiceHeader."No.";

      lEAIAct.GET(pEAIRcvdMQBuffer."EAI Act Type Code", pEAIRcvdMQBuffer."EAI Act Code");
      lEAIAct.TESTFIELD("Create Tray Line On Receive", TRUE);
      TrayLineMgt.InitTrayLine(lTrayLine);
      //<<IMH13
      TrayLineMgt.CreateTLFromServHeader(
        //lServiceHeader,lServiceHeader."Default Team Code",lEAIAct."Tray Line Task Code On Receive",FALSE);
        lServiceHeader,
        lServiceHeader.GetTeamByServiceTask(lServiceHeader, lEAIAct."Tray Line Task Code On Receive"),
        lEAIAct."Tray Line Task Code On Receive",FALSE);
      //>>IMH13

      lEAIDocEntryMgt.AddEntryFromRcvdBuffer(
        DATABASE::"Service Header",
        lServiceHeader."Document Type",lServiceHeader."No.",'',0,pEAIRcvdMQBuffer);

      pEAIRcvdMQBuffer.CALCFIELDS("XML Source");
      pEAIRcvdMQBuffer."XML Source".CREATEINSTREAM(lInStream);
      lXmlDom.load(lInStream);
      DARVAXMLMgt.InitXMLDom(lXmlDom,XMLDom);
      GetComments(XMLDom,lAdvCommentLineBuffer);
      SaveComments(lAdvCommentLineBuffer,lServiceHeader);
      // Partie piŠce jointe
      pEAIRcvdMQBuffer.CALCFIELDS("XML Source");
      pEAIRcvdMQBuffer."XML Source".CREATEINSTREAM(lInStream);
      lXmlDom.load(lInStream);
      //<<IMH11.PC
      //CreateAttachmentFile(lServiceHeader,lXmlDom);
      CreateAttachmentFile(lServiceHeader,lXmlDom,lXmlNodeList2);
      //>>IMH11.PC
    END;

    PROCEDURE GetComments@1100281002(pXmlDom@1100281005 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";VAR pAdvCommentLine@1100281006 : Record 50056);
    VAR
      lXmlDomNodeList@1100281003 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList";
      lXmlDomElement@1100281002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";
      lXmlDomElement2@1100281004 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF86-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMElement";
      i@1100281000 : Integer;
      lLineNo@1100281008 : Integer;
    BEGIN
      lXmlDomNodeList := pXmlDom.getElementsByTagName('DE00080201');
      IF ISCLEAR(lXmlDomNodeList) THEN BEGIN
        EXIT;
      END;
      IF pAdvCommentLine.FINDLAST THEN BEGIN
        lLineNo := pAdvCommentLine."Line No.";
      END ELSE BEGIN
        lLineNo := 1;
      END;
      FOR i := 0 TO lXmlDomNodeList.length - 1 DO BEGIN
        lXmlDomElement2 := lXmlDomNodeList.item(i);
        pAdvCommentLine."Line No." := lLineNo + i;
        pAdvCommentLine.Comment := lXmlDomElement2.text;
        pAdvCommentLine.INSERT;
      END;
    END;

    PROCEDURE SaveComments@1100281001(VAR pAdvCommentLine@1100281000 : Record 50056;pServiceHeader@1100281001 : Record 5900);
    VAR
      lAdvCommentLine@1100281005 : Record 50056;
      lServiceCommentLine@1100281010 : Record 5906;
      lLineNo@1100281006 : Integer;
      lLastLine@1100281007 : Integer;
      lCommentLineCount@1100281008 : Integer;
      lCommentNbCountToAdd@1100281011 : Integer;
      lCommentNbCountActual@1100281012 : Integer;
      lDate@1100281009 : Date;
      lCommentToAdd@1100281013 : Text[250];
    BEGIN
      pAdvCommentLine.RESET;
      lCommentLineCount  := pAdvCommentLine.COUNT;
      IF lCommentLineCount = 0 THEN
        EXIT;

      // Calcul du nombre de caractŠre commentaires … ajouter
      lCommentNbCountToAdd := 0;
      pAdvCommentLine.FINDSET;
      REPEAT
        lCommentNbCountToAdd += STRLEN(pAdvCommentLine.Comment);
      UNTIL pAdvCommentLine.NEXT = 0;

      // V‚rification du nb de commentaire au niveau du champ contexte de la ligne de produit
      lCommentNbCountActual := 0;
      lServiceCommentLine.SETRANGE("Table Name",lServiceCommentLine."Table Name"::"Service Header");
      lServiceCommentLine.SETRANGE("Table Subtype",pServiceHeader."Document Type");
      lServiceCommentLine.SETRANGE("No.",pServiceHeader."No.");
      lServiceCommentLine.SETRANGE(Type,lServiceCommentLine.Type::General);
      lServiceCommentLine.SETRANGE("Table Line No.",0);
      lServiceCommentLine.SETRANGE("Line No.",-50012);
      IF lServiceCommentLine.FINDFIRST THEN BEGIN
        lCommentNbCountActual := STRLEN(lServiceCommentLine.Text);
      END;

      // 233 = 250 - 17 caractŠre de ' - Cf commentaire'
      // (lCommentLineCount - 1) = nb espace … ajouter entre chaque ligne
      IF lCommentNbCountToAdd + lCommentNbCountActual + (lCommentLineCount - 1) <= (250 - STRLEN(Text002)) THEN BEGIN
        pAdvCommentLine.FINDSET;
        REPEAT
          IF STRLEN(lCommentToAdd) = 0 THEN BEGIN
            lCommentToAdd := pAdvCommentLine.Comment;
          END ELSE BEGIN
            lCommentToAdd += ' ' + pAdvCommentLine.Comment;
          END;
        UNTIL pAdvCommentLine.NEXT = 0;
        IF lCommentNbCountActual = 0 THEN BEGIN
          lServiceCommentLine."Table Name" := lServiceCommentLine."Table Name"::"Service Header";
          lServiceCommentLine."Table Subtype" := pServiceHeader."Document Type";
          lServiceCommentLine."No." := pServiceHeader."No.";
          lServiceCommentLine.Type := lServiceCommentLine.Type::General;
          lServiceCommentLine."Table Line No." := 0;
          lServiceCommentLine."Line No." := -50012;
          lServiceCommentLine.Text := lCommentToAdd;
          lServiceCommentLine.INSERT;
        END ELSE BEGIN
          lServiceCommentLine.Text += ' ' + lCommentToAdd;
          lServiceCommentLine.MODIFY;
        END;
        pAdvCommentLine.DELETEALL;
      END ELSE BEGIN
        IF lCommentNbCountActual = 0 THEN BEGIN
          lServiceCommentLine."Table Name" := lServiceCommentLine."Table Name"::"Service Header";
          lServiceCommentLine."Table Subtype" := pServiceHeader."Document Type";
          lServiceCommentLine."No." := pServiceHeader."No.";
          lServiceCommentLine.Type := lServiceCommentLine.Type::General;
          lServiceCommentLine."Table Line No." := 0;
          lServiceCommentLine."Line No." := -50012;
          lServiceCommentLine.Text := Text002;
          lServiceCommentLine.INSERT;
        END ELSE BEGIN
          IF lCommentNbCountActual + 3 + STRLEN(Text002) <= 250 THEN BEGIN
            lServiceCommentLine.Text += ' - ' + Text002;
            lServiceCommentLine.MODIFY;
          END;
        END;

        lAdvCommentLine.SETRANGE("Source Table ID",DATABASE::"Service Header");
        lAdvCommentLine.SETRANGE("Source No.",pServiceHeader."Service No.");
        lAdvCommentLine.SETRANGE("Source No. 2",pServiceHeader."No.");
        lAdvCommentLine.SETRANGE("Source No. 3",0);
        IF lAdvCommentLine.FINDLAST THEN BEGIN
          lLineNo := lAdvCommentLine."Line No." + 10000
        END ELSE BEGIN
          lLineNo := 10000;
        END;

        lLastLine := lLineNo + lCommentLineCount -1;
        lDate := TODAY;
        pAdvCommentLine.FIND('+');
        REPEAT
          lAdvCommentLine := pAdvCommentLine;
          lAdvCommentLine."Source Table ID" := DATABASE::"Service Header";
          lAdvCommentLine."Source No." := pServiceHeader."Service No.";
          lAdvCommentLine."Source No. 2" := pServiceHeader."No.";
          lAdvCommentLine."Source No. 3" := 0;
          lAdvCommentLine."Line No." := lLineNo;
          IF lLineNo <> lLastLine THEN
            lAdvCommentLine."Attached To Line No." := lLastLine
          ELSE
            lAdvCommentLine.Date := lDate;
          lAdvCommentLine.Reception := lAdvCommentLine.Reception::Darva;
          lAdvCommentLine.Code := '';
          lAdvCommentLine.INSERT(TRUE);
          lLineNo += 1;
        UNTIL pAdvCommentLine.NEXT(-1) = 0;
        pAdvCommentLine.DELETEALL;
      END;
    END;

    PROCEDURE CreateAttachmentFile@1100281012(pServiceHeader@1100281016 : Record 5900;pXMLDom@1100281001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";VAR pXmlNodeList@1100281018 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList");
    VAR
      lXMLDOMNode@1100281017 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXMLDOMNode1@1100281014 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXMLDOMNode2@1100281010 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXmlNodeList@1100281000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList";
      lAdoStream@1100281007 : Automation "{2A75196C-D9EB-4129-B803-931327F72D5C} 2.8:{00000566-0000-0010-8000-00AA006D2EA4}:'Microsoft ActiveX Data Objects 2.8 Library'.Stream";
      lTempFile@1100281003 : File;
      lFile@1100281009 : File;
      lFileName@1100281002 : Text[250];
      lFileType@1100281013 : Text[10];
      lDocAttach@1100281008 : Record 50021;
      lDocAttach2@1100281015 : Record 50021;
      lSourceAttchMgt@1100281006 : Codeunit 50003;
      lAttachmentTemp@1100281005 : TEMPORARY Record 5062;
      lAttachMgt@1100281004 : Codeunit 50002;
      lLength@1100281012 : Integer;
      i@1100281011 : Integer;
    BEGIN

      lXMLDOMNode := pXMLDom.getElementsByTagName('ns2:formatPdf').item(0);
      IF ISCLEAR(lXMLDOMNode) THEN BEGIN
        EXIT;
      END;

      lXMLDOMNode.dataType := 'bin.base64';
      CREATE(lAdoStream);
      lAdoStream.Open;
      lAdoStream.Type := 1;
      lAdoStream.Write(lXMLDOMNode.nodeTypedValue);
      lTempFile.CREATETEMPFILE;
      lFileName := COPYSTR(lTempFile.NAME,1,STRLEN(lTempFile.NAME) - 4) + '.pdf';
      lTempFile.CLOSE;
      lAdoStream.SaveToFile(lFileName);
      lAdoStream.Close;

      WITH lDocAttach DO BEGIN
        SETCURRENTKEY("Service Document No.","Service Intervention Line No.","Intervention Quote Line No.");
        SETRANGE("Service Document No.",pServiceHeader."No.");
        lDocAttach."Key Integer 1" := 0;
        lDocAttach."Key Integer 2" := 0;
        lDocAttach."Key Code 1" := '';
        lDocAttach."Key Code 2" := '';
        Description := Text001;
      END;
      lSourceAttchMgt.CreateAttachment(lDocAttach,lFileName,lAttachmentTemp);

      //<<IMH11.PC
      CreateAttachment(pServiceHeader,pXmlNodeList);
      //>>IMH11.PC
    END;

    LOCAL PROCEDURE ReferenceNoWithOutZero@1100281004(pServiceRefNo@1100281000 : Code[20]) rValue : Code[20];
    BEGIN
      rValue := '';
      rValue := DARVAMgt.ReferenceNoWithOutZero(pServiceRefNo);
    END;

    LOCAL PROCEDURE CheckAttachment@1000000002(VAR pXmlDom@1000000001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";pEAIRcvdMQBuffer@1000000003 : Record 8016721;VAR pResult@1000000005 : ' ,Quit,NoAttach,CreateMessage,ToTreat') rValue : Integer;
    VAR
      lEAIMQBufferMgt@1000000000 : Codeunit 8016708;
      lXmlNodeList@1000000013 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList";
      lText001@1000000004 : TextConst 'ENU=Attachment Rec with ID %1 was error.;FRA=Le flux PJ portant l''ID %1 est en erreur.';
    BEGIN
      // IMH11.PC
      rValue := 0;
      pResult := pResult::" ";

      lXmlNodeList := pXmlDom.getElementsByTagName('ns2:pieceJointe');

      rValue := lXmlNodeList.length;
      IF rValue = 0 THEN BEGIN
        pResult := pResult::NoAttach;
        EXIT;
      END;

      // V‚rification de la pr‚sence du flux DARVA_PJ
      // pStep : 0 = Generate / 1 = Validation
      // pStatus : 0 = Non trait‚ / 1 = Trait‚ / 2 = Erreur / 3 = Test unitaire
      IF lEAIMQBufferMgt.CheckIfActExistsToNextAct(pEAIRcvdMQBuffer,FALSE,'','',TRUE,1,TRUE,1) THEN BEGIN
        pResult := pResult::ToTreat;
        EXIT;
      END;

      IF lEAIMQBufferMgt.CheckIfActExistsToNextAct(pEAIRcvdMQBuffer,FALSE,'','',FALSE,0,TRUE,2) THEN BEGIN
        ERROR(lText001,pEAIRcvdMQBuffer."Message ID");
      END;

      IF lEAIMQBufferMgt.CheckIfActExistsToNextAct(pEAIRcvdMQBuffer,FALSE,'','',FALSE,0,FALSE,2) THEN BEGIN
        pResult := pResult::Quit;
        EXIT;
      END ELSE BEGIN
        pResult := pResult::CreateMessage;
        EXIT;
      END;
    END;

    LOCAL PROCEDURE CreateAttachmentDemand@1000000004(pEAIRcvdMQBuffer@1000000000 : Record 8016721;pAttachNb@1100281000 : Integer);
    VAR
      lEAIRcvdMQBuffer@1000000001 : Record 8016721;
      lEAIAct@1000000002 : Record 8016706;
      lEAIMQBufferMgt@1000000003 : Codeunit 8016708;
    BEGIN
      // IMH11.PC

      lEAIRcvdMQBuffer := pEAIRcvdMQBuffer;
      WITH lEAIRcvdMQBuffer DO BEGIN
        lEAIAct.GET(pEAIRcvdMQBuffer."EAI Act Type Code",pEAIRcvdMQBuffer."EAI Act Code");
        lEAIAct.TESTFIELD("Next Act Type Code Af. Val.");
        lEAIAct.TESTFIELD("Next Act Code After Validation");
        "EAI Act Type Code" := lEAIAct."Next Act Type Code Af. Val.";
        "EAI Act Code" := lEAIAct."Next Act Code After Validation";
        "Entry No." := '';
        IF lEAIAct.GET("EAI Act Type Code","EAI Act Code") THEN BEGIN
          "Act Priority" := lEAIAct."Act Priority";
        END;
        "Date Creation" := WORKDATE;
        "Time Creation" := TIME;
        "User Creation" := USERID;
        Step := Step::Generation;
        Status := Status::Unprocessed;
        "Attachment Nb" := pAttachNb;
        //<<IMH12.ACO
        "Origin Entry No." := pEAIRcvdMQBuffer."Entry No.";
        "Origin EAI Act Code" := pEAIRcvdMQBuffer."EAI Act Code";
        //>>IMH12.ACO
        CLEAR("XML Source");
        CLEAR("Response XML Source");
        INSERT;
        lEAIMQBufferMgt.InsertReceivedBufferToMQCode("EAI MQ Code",lEAIRcvdMQBuffer);
        DELETE;
      END;
    END;

    LOCAL PROCEDURE CreateAttachment@1000000011(pServiceHeader@1100281002 : Record 5900;VAR pXmlNodeList@1100281000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList");
    VAR
      lAttachmentTemp@1000000004 : TEMPORARY Record 5062;
      lDocumentAttachmentBuffer@1000000007 : TEMPORARY Record 50021;
      lSourceAttchMgt@1000000005 : Codeunit 50003;
      lAttachMgt@1000000003 : Codeunit 50002;
      lXmlDOMNode@1000000016 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXmlDOMNode1@1000000015 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lXmlDOMNode2@1000000014 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lAdoStream@1000000012 : Automation "{2A75196C-D9EB-4129-B803-931327F72D5C} 2.8:{00000566-0000-0010-8000-00AA006D2EA4}:'Microsoft ActiveX Data Objects 2.8 Library'.Stream";
      lTempFile@1000000011 : File;
      lLength@1000000002 : Integer;
      i@1000000001 : Integer;
      j@1100281001 : Integer;
      lMetadata@1000000020 : ARRAY [5] OF Text[50];
      lFileName@1000000006 : Text[1024];
      lInStream@1000000008 : InStream;
    BEGIN
      // IMH11.PC

      IF ISCLEAR(pXmlNodeList) THEN BEGIN
        EXIT;
      END;

      lLength := pXmlNodeList.length;
      IF lLength = 0 THEN BEGIN
        EXIT;
      END;

      FOR i := 1 TO lLength DO BEGIN
        lXmlDOMNode := pXmlNodeList.item(i - 1);
        FOR j := 1 TO 5 DO BEGIN
          lMetadata[j] := '';
        END;

        // R‚cup‚artion des Metadata
        lXmlDOMNode1 := lXmlDOMNode.selectSingleNode('ns2:metadata');
        IF NOT ISCLEAR(lXmlDOMNode1) THEN BEGIN
          // ID PJ
          lXmlDOMNode2 := lXmlDOMNode1.selectSingleNode('ns2:idPJ');
          IF NOT ISCLEAR(lXmlDOMNode2) THEN BEGIN
            lMetadata[1]  := lXmlDOMNode2.text;
          END;
          // LIBELLE PJ
          lXmlDOMNode2 := lXmlDOMNode1.selectSingleNode('ns2:libellePJ');
          IF NOT ISCLEAR(lXmlDOMNode2) THEN BEGIN
            lMetadata[2]  := lXmlDOMNode2.text;
          END;
          // NATURE PJ
          lXmlDOMNode2 := lXmlDOMNode1.selectSingleNode('ns2:naturePJ');
          IF NOT ISCLEAR(lXmlDOMNode2) THEN BEGIN
            lMetadata[3]  := lXmlDOMNode2.text;
          END;
          // FORMAT PJ
          lXmlDOMNode2 := lXmlDOMNode1.selectSingleNode('ns2:formatPJ');
          IF NOT ISCLEAR(lXmlDOMNode2) THEN BEGIN
            lMetadata[4]  := lXmlDOMNode2.text;
          END;
          // VERSION PJ
          lXmlDOMNode2 := lXmlDOMNode1.selectSingleNode('ns2:versionPJ');
          IF NOT ISCLEAR(lXmlDOMNode2) THEN BEGIN
            lMetadata[5]  := lXmlDOMNode2.text;
          END;
        END;

        lXmlDOMNode1 := lXmlDOMNode.selectSingleNode('ns2:fluxPJ');
        IF NOT ISCLEAR(lXmlDOMNode1) THEN BEGIN
          lXmlDOMNode1.dataType := 'bin.base64';
          CLEAR(lAdoStream);
          CREATE(lAdoStream);
          lAdoStream.Open;
          lAdoStream.Type := 1;
          lAdoStream.Write(lXmlDOMNode1.nodeTypedValue);
          lTempFile.CREATETEMPFILE;
          lFileName := COPYSTR(lTempFile.NAME,1,STRLEN(lTempFile.NAME) - 4) + '.' + lMetadata[4];
          lTempFile.CLOSE;
          lAdoStream.SaveToFile(lFileName);
          lAdoStream.Close;
        END;

        // sauvegarde Document Attachment
        lDocumentAttachmentBuffer.SETRANGE("Service Document No.",pServiceHeader."No.");
        lDocumentAttachmentBuffer."Darva ID" := lMetadata[1];
        lDocumentAttachmentBuffer.Description := lMetadata[2];
        lDocumentAttachmentBuffer."Description 2" := lMetadata[3];
        lDocumentAttachmentBuffer."File Extension" := lMetadata[4];
        lDocumentAttachmentBuffer."Darva Attachment Version" := lMetadata[5];
        lSourceAttchMgt.CreateAttachment(lDocumentAttachmentBuffer,lFileName,lAttachmentTemp);
      END;
    END;

    PROCEDURE GetEAIRcvdPJ@1000000005(pEAIRcvdMQBuffer@1000000000 : Record 8016721;VAR pXmlNodeList@1000000003 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF82-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNodeList");
    VAR
      lEAIRcvdMQBufferPJ@1000000006 : Record 8016721;
      lEAIAct@1000000005 : Record 8016706;
      lEAIMQBufferMgt@1000000007 : Codeunit 8016708;
      lXmlDom@1000000004 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";
      lLength@1000000002 : Integer;
      lInStream@1000000001 : InStream;
    BEGIN
      // IMH11.PC

      CLEAR(pXmlNodeList);

      // On r‚cupŠre le flux DARVA_PJ
      lEAIAct.GET(pEAIRcvdMQBuffer."EAI Act Type Code",pEAIRcvdMQBuffer."EAI Act Code");
      lEAIMQBufferMgt.GetSpecificAct(
        pEAIRcvdMQBuffer,
        lEAIAct."Next Act Type Code Af. Val.",lEAIAct."Next Act Code After Validation",
        lEAIRcvdMQBufferPJ);

      lEAIRcvdMQBufferPJ.CALCFIELDS("Response XML Source");
      lEAIRcvdMQBufferPJ."Response XML Source".CREATEINSTREAM(lInStream);
      CREATE(lXmlDom);
      lXmlDom.load(lInStream);
      lEAIRcvdMQBufferPJ.DELETE;
      pXmlNodeList := lXmlDom.getElementsByTagName('ns2:pieceJointe');
      lLength := pXmlNodeList.length;
      IF lLength = 0 THEN BEGIN
        EXIT;
      END;

      IF pEAIRcvdMQBuffer."Attachment Nb" <> lLength THEN BEGIN
        ERROR(Text013,pEAIRcvdMQBuffer."Attachment Nb",lLength);
      END;
    END;

    BEGIN
    {
      /** @r IMH13 @d 24/02/14 @a ISAT.ZW @v IMH6.01.4985 @s R‚organisation DPS @c Gestion de code corbeille service par d‚faut */
      /** @r IMH12 @d 25/06/13 @a ISAT.ACO @v IMH6.01.350 @s SystŠme d'aiguillage
          @c Affection champs "Origin Entry No." et "Origin EAI Act Code" depuis flux origine
          @c Utilisation matrice aiguillage DARVA */
      /** @r IMH11 @d 23/05/13 @a ISAT.PC @v IMH6.01.222 @s Uniformisation DARVA @c Ajout de la gestion de la piŠce jointe
          @c Ajout fonction CheckAttachment, CreateAttachmentDemand, GetEAIRcvdPJ, CreateAttachment
          @c Suppression des zones de code en commentaires */
      /** @r IMH10 @d 07/05/13 @a ISAT.PC @v IMH6.01.222 @s Uniformisation Darva @c Epuration des commentaires */
      /** @r IMH11 @d 06/11/12 @a ISAT.PC @v IMH6.01.222 @s Uniformisation Darva @c Modification */
      /** @r IMH10 @d 25/09/12 @a ISAT.PC @v IMH6.01.228 @s NEP00011 @h 3763 @h 3906 */
      /** @r IMH09 @d 24/09/12 @a ISAT.PC @v IMH6.01.228 @s NEP00011 @h 3931 */
      /** @r IMH08 @d 21/09/12 @a ISAT.PC @v IMH6.01.228 @s NEP00011 @h 3903  */
      /** @r IMH07 @d 15/03/12 @a ISAT.LLT @v IMH6.01.134 @s DARVA Management @c Correctif @h 3067  */
      /** @r IMH06 @d 15/03/12 @a ISAT.LLT @v IMH6.01.134 @s DARVA Management @c Correctif @h 2760  */
      /** @r IMH05 @d 12/03/12 @a ISAT.ZW @v IMH6.01.134 @s DARVA Management @c Correctif nø GC */
      /** @r IMH04 @d 03/01/12 @a ISAT.PC @v IMH6.01.134 @s DARVA Management @c Correctif */
      /** @r IMH03 @d 23/12/11 @a ISAT.PC @v IMH6.01.134 @s DARVA Management @c Correctif sur le nø de grand compte */
      /** @r IMH02 @d 30/11/11 @a ISAT.EBA @v IMH6.01.134 @s DARVA Management
          @c MAJ champs activit‚ corbeille */GR
      /** @r IMH01 @d 13/07/11 @a ISAT.MA @v IMH6.01.134 @s DARVA Management @s Cr‚ation */
    }
    END.
  }
}