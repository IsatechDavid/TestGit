OBJECT Codeunit 50233 Fusion Inv. Jrnl. Post Batch
{
  OBJECT-PROPERTIES
  {
    Date=21/01/13;
    Time=14:20:11;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    TableNo=50187;
    OnRun=BEGIN
            Code(Rec);
          END;

  }
  CODE
  {
    VAR
      FUSIONInvoiceMgt@1100281000 : Codeunit 50180;
      WDialog@1100281001 : Dialog;
      TotalCounter@1100281004 : Integer;
      ModuloCounter@1100281003 : Integer;
      CurrentCounter@1100281002 : Integer;
      Text001@1100281005 : TextConst 'ENU=Processing @1@@@@@@@@@@@@@@@@@@@@;FRA=Traitement en cours @1@@@@@@@@@@@@@@@@@@@@';
      BeginDateTime@1000000000 : DateTime;
      Text002@1100281006 : TextConst 'ENU=Processing end. \Begin : %1\End : %2;FRA=Traitement termin‚.\D‚but : %1 \Fin : %2';

    PROCEDURE Code@1100281001(VAR pFusionInvoicingJournalLine@1100281000 : Record 50187);
    VAR
      lFusionInvoicingJournalLine@1100281001 : Record 50187;
    BEGIN
      BeginDateTime := CURRENTDATETIME;
      pFusionInvoicingJournalLine.SETRANGE("In Error", FALSE);
      IF GUIALLOWED THEN BEGIN
        WDialog.OPEN(Text001);
        TotalCounter := pFusionInvoicingJournalLine.COUNT;
        ModuloCounter := ROUND(TotalCounter * 5 / 100,1,'>');
        CurrentCounter := 0;
      END;

      IF pFusionInvoicingJournalLine.FINDSET THEN
        REPEAT
          IF GUIALLOWED THEN BEGIN
            CurrentCounter += 1;
            IF CurrentCounter MOD ModuloCounter = 1 THEN BEGIN
              WDialog.UPDATE(1,ROUND(CurrentCounter / TotalCounter * 9999,1,'>'));
            END;
          END;

          lFusionInvoicingJournalLine := pFusionInvoicingJournalLine;
          FUSIONInvoiceMgt.PostLine(lFusionInvoicingJournalLine);
          lFusionInvoicingJournalLine.DELETE(TRUE);
          COMMIT;
        UNTIL pFusionInvoicingJournalLine.NEXT = 0;

      pFusionInvoicingJournalLine.SETRANGE("In Error");

      IF GUIALLOWED THEN BEGIN
        WDialog.CLOSE;
        MESSAGE(Text002, FORMAT(BeginDateTime), FORMAT(CURRENTDATETIME));
      END;
    END;

    BEGIN
    {
      /** @r IMH01 @d 05/12/12 @a ISAT.ZW @v IMH6.01.220 @s Projet Facturation Fusion @c Cr‚ation */
    }
    END.
  }
}