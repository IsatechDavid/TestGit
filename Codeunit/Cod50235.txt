OBJECT Codeunit 50235 Test WS
{
  OBJECT-PROPERTIES
  {
    Date=13/06/13;
    Time=11:41:13;
    Modified=Yes;
    Version List=IMH6.01,MAI;
  }
  PROPERTIES
  {
    Permissions=TableData 50011=rm;
    OnRun=VAR
            lWinHttpService@1000000000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{AFBA6B42-5692-48EA-8141-DC517DCF0EF1}:'Microsoft XML, v6.0'.ServerXMLHTTP";
            lXmlDoc@1100281001 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument";
            ErrorText@1100281000 : Text[1024];
          BEGIN
            IF (AddressWS = '') THEN BEGIN
              EXIT;
            END;

            CREATE(lWinHttpService,TRUE,FALSE);
            lWinHttpService.open('POST',AddressWS + FunctionWS,0);
            lWinHttpService.setRequestHeader('Content-type','text/xml; charset=UTF-8');
            lWinHttpService.setRequestHeader('SOAPAction','""');
            CreateXMLDoc(lXmlDoc);
            lWinHttpService.send(lXmlDoc);
            IF NOT lWinHttpService.waitForResponse(60) THEN BEGIN
              ERROR('Request timed out.');
            END;

            IF lWinHttpService.status <> 200 THEN BEGIN
              ErrorText := COPYSTR(STRSUBSTNO(Text001,AddressWS,FunctionWS,lWinHttpService.responseText),1,1024);
              ERROR(ErrorText);
            END;
          END;

  }
  CODE
  {
    VAR
      AddressWS@1000000000 : Text[1024];
      Text001@1000000002 : TextConst 'ENU=WS %1 - %2 unavailable (%3);FRA=WS %1 - %2 indisponible (%3)';
      FunctionWS@1000000001 : Text[1024];

    LOCAL PROCEDURE CreateXMLDoc@1100281023(VAR pXmlDoc@1100281000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{F6D90F11-9C73-11D3-B32E-00C04F990BB4}:'Microsoft XML, v6.0'.DOMDocument");
    VAR
      lXmlMgt@1100281021 : Codeunit 8016710;
      lXmlProcessingInst@1100281022 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF89-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMProcessingInstruction";
      lNewChild@1100281023 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lCurNode@1100281024 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
      lCurNode2@1100281025 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{2933BF80-7B36-11D2-B20E-00C04F983E60}:'Microsoft XML, v6.0'.IXMLDOMNode";
    BEGIN
      // Demande auprŠs du web service Geoconcept le temps de trajet
      CREATE(pXmlDoc);
      lXmlMgt.SetNormalCase;
      lXmlProcessingInst := pXmlDoc.createProcessingInstruction('xml','version="1.0" encoding="UTF-8" standalone="yes"');
      lCurNode := pXmlDoc.appendChild(lXmlProcessingInst);
      lCurNode := pXmlDoc.createElement('soapenv:Envelope');
      lCurNode := pXmlDoc.appendChild(lCurNode);
      lXmlMgt.AddAttribute(lCurNode,'xmlns:soapenv','http://schemas.xmlsoap.org/soap/envelope/');
      lXmlMgt.AddAttribute(lCurNode,'xmlns:sch','http://geoconcept.com/gc/schemas');
      lXmlMgt.AddElement3(lCurNode,'soapenv:Header','','soapenv',lNewChild);
      lXmlMgt.AddElement3(lCurNode,'soapenv:Body','','soapenv',lNewChild);
      lCurNode := lNewChild;
      lXmlMgt.AddElement3(lCurNode,'sch:SearchAroundRequest','','sch',lNewChild);
      lCurNode := lNewChild;
      lXmlMgt.AddElement3(lCurNode,'sch:Options','','sch',lNewChild);
      lCurNode2 := lNewChild;
      lXmlMgt.AddElement3(lCurNode2,'sch:SearchMethod','TIME','sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode2,'sch:Projection','MAP','sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode,'sch:Target','','sch',lNewChild);
      lCurNode2 := lNewChild;
      lXmlMgt.AddElement3(lCurNode2,'sch:Id','0','sch',lNewChild);
      // PARIS
      lXmlMgt.AddElement3(lCurNode2,'sch:X',FORMAT(600113) ,'sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode2,'sch:Y',FORMAT(2429350),'sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode2,'sch:Priority1','0','sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode2,'sch:Priority2','0','sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode,'sch:Resource','','sch',lNewChild);
      lCurNode2 := lNewChild;
      // VANNES
      lXmlMgt.AddElement3(lCurNode2,'sch:Id','Test','sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode2,'sch:X',FORMAT(217227) ,'sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode2,'sch:Y',FORMAT(2307781),'sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode2,'sch:Priority1','0','sch',lNewChild);
      lXmlMgt.AddElement3(lCurNode2,'sch:Priority2','0','sch',lNewChild);
    END;

    PROCEDURE SetWSToCheck@1100281024(pAddressWS@1000000000 : Text[1024];pFunctionWS@1000000001 : Text[1024]);
    VAR
      lProviderSetup@1100281000 : Record 50011;
    BEGIN
      AddressWS := pAddressWS;
      FunctionWS := pFunctionWS;
    END;

    BEGIN
    {
      /** @r IMH01 @d 22/04/13 @a ISAT.PC @v IMH6.01.345 @s Mantis @h 4177 @c Cr‚ation Gestion des tests WS */
    }
    END.
  }
}