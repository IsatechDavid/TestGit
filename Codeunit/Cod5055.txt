OBJECT Codeunit 5055 CustVendBank-Update
{
  OBJECT-PROPERTIES
  {
    Date=28/03/12;
    Time=16:30:19;
    Modified=Yes;
    Version List=NAVW16.00.01,IMH6.01;
  }
  PROPERTIES
  {
    TableNo=5050;
    Permissions=TableData 23=rim,
                TableData 5054=rim,
                TableData 5079=r;
    OnRun=VAR
            ContBusRel@1000 : Record 5054;
          BEGIN
            ContBusRel.SETRANGE("Contact No.", "No.");
            ContBusRel.SETFILTER("Link to Table", '<>''''');

            IF ContBusRel.FIND('-') THEN
              REPEAT
                CASE ContBusRel."Link to Table" OF
                  ContBusRel."Link to Table"::Customer: UpdateCustomer(Rec,ContBusRel);
                  ContBusRel."Link to Table"::Vendor: UpdateVendor(Rec,ContBusRel);
                  ContBusRel."Link to Table"::"Bank Account": UpdateBankAccount(Rec,ContBusRel);
                END;
              UNTIL ContBusRel.NEXT = 0;
          END;

  }
  CODE
  {
    VAR
      Cust@1001 : Record 18;
      Vend@1002 : Record 23;
      BankAcc@1003 : Record 270;
      ChangeLogMgt@1008 : Codeunit 423;
      RecRef@1007 : RecordRef;
      xRecRef@1000 : RecordRef;
      NoSerie@1004 : Code[10];
      PurchaserCode@1005 : Code[10];
      OurContactCode@1006 : Code[10];

    PROCEDURE UpdateCustomer@1(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    BEGIN
      WITH Cust DO BEGIN
        GET(ContBusRel."No.");
        xRecRef.GETTABLE(Cust);
        NoSerie := "No. Series";
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSerie;
        VALIDATE("Service Corporate"); //IMH01.ST
        MODIFY;
        RecRef.GETTABLE(Cust);
        ChangeLogMgt.LogModification(RecRef,xRecRef);
      END;
    END;

    PROCEDURE UpdateVendor@2(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    BEGIN
      WITH Vend DO BEGIN
        GET(ContBusRel."No.");
        xRecRef.GETTABLE(Vend);
        NoSerie := "No. Series";
        PurchaserCode :=  Vend."Purchaser Code";
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSerie;
        Vend."Purchaser Code" := PurchaserCode;
        //<<IMH06.JC
        IF Cont."Service Contact" = Cont."Service Contact"::Beneficiary THEN
          "Contact No." := Cont."No.";
        //>>IMH06.JC
        MODIFY;
        RecRef.GETTABLE(Vend);
        ChangeLogMgt.LogModification(RecRef,xRecRef);
      END;
    END;

    PROCEDURE UpdateBankAccount@3(VAR Cont@1000 : Record 5050;VAR ContBusRel@1001 : Record 5054);
    BEGIN
      WITH BankAcc DO BEGIN
        GET(ContBusRel."No.");
        xRecRef.GETTABLE(BankAcc);
        NoSerie := "No. Series";
        OurContactCode := "Our Contact Code";
        TRANSFERFIELDS(Cont);
        "No." := ContBusRel."No.";
        "No. Series" := NoSerie;
        "Our Contact Code" := OurContactCode;
        MODIFY;
        RecRef.GETTABLE(BankAcc);
        ChangeLogMgt.LogModification(RecRef,xRecRef);
      END;
    END;

    BEGIN
    {
      /** @r IMH03 @d 28/03/12 @a ISAT.PC @v IMH6.01.146 @s Gestion facturation
          @c Ajout des permissions suivantes :
          @c TableData Vendor=rim,
          @c TableData Contact Business Relation=rim,
          @c TableData Marketing Setup=r */
      /** @r IMH02 @d 17/03/11 @a CAP.JC @v IMH6.01.141 @s Comptabilit‚ - gestion fournisseur
          @c Charge la variable Contact No. du fournisseur */
      /** @r IHM01 @d 16/02/11 @a ISAT.ST @v IMH6.01.002 @s Gestion des contacts */
    }
    END.
  }
}