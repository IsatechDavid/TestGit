OBJECT Codeunit 5921 ServComponent-Copy from BOM
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW14.00,KITNA6.00;
  }
  PROPERTIES
  {
    TableNo=5940;
    OnRun=VAR
            TempProdBOMLine@1462000 : TEMPORARY Record 99000772;
            KitManagement@1462001 : Codeunit 25000;
            NoOfBOMComp@1462002 : Integer;
          BEGIN
            ServItem.GET(Rec."No.");
            ServItem.TESTFIELD("Item No.");

            ServItemComponent.LOCKTABLE;
            ServItemComponent.RESET;
            ServItemComponent.SETRANGE(Active,TRUE);
            ServItemComponent.SETRANGE("Parent Service Item No.",ServItem."No.");
            IF ServItemComponent.FIND('+') THEN
              LineNo := ServItemComponent."Line No."
            ELSE
              LineNo := 0;

            Item.GET(ServItem."Item No.");
            KitManagement.GetKitProdBOM(Item."Kit BOM No.",TempProdBOMLine,NoOfBOMComp);
            TempProdBOMLine.SETRANGE(Type,TempProdBOMLine.Type::Item);
            IF TempProdBOMLine.FINDSET THEN BEGIN
              REPEAT
                IF TempProdBOMLine."Quantity per" <> ROUND(TempProdBOMLine."Quantity per",1) THEN
                  ERROR(Text001,ServItem.TABLECAPTION,TempProdBOMLine.FIELDCAPTION("Quantity per"));
                FOR Index := 1 TO TempProdBOMLine."Quantity per" DO BEGIN
                  LineNo := LineNo + 10000;
                  Item.GET(TempProdBOMLine."No.");
                  CLEAR(ServItemComponent);
                  ServItemComponent.Active := TRUE;
                  ServItemComponent.INIT;
                  ServItemComponent."Parent Service Item No.":= ServItem."No.";
                  ServItemComponent."Line No." := LineNo;
                  ServItemComponent.Type := ServItemComponent.Type::Item;
                  ServItemComponent."No." := TempProdBOMLine."No.";
                  ServItemComponent."Variant Code" := TempProdBOMLine."Variant Code";
                  ServItemComponent."Date Installed" := ServItem."Installation Date";
                  ServItemComponent.Description := Item.Description;
                  ServItemComponent."Description 2" := Item."Description 2";
                  ServItemComponent."From Line No." := 0;
                  IF NOT ServItemComponent.INSERT THEN
                    ServItemComponent.MODIFY;
                END;
              UNTIL TempProdBOMLine.NEXT = 0;
            END ELSE
              ERROR(
                Text000,
                TempProdBOMLine.TABLECAPTION,ServItem.FIELDCAPTION("Item No."),ServItem."Item No.");
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 for %2 %3 cannot be found.;FRA=Impossible de trouver un(e) %1 pour l''%2 %3.';
      ServItem@1001 : Record 5940;
      ServItemComponent@1003 : Record 5941;
      Item@1004 : Record 27;
      LineNo@1005 : Integer;
      Index@1006 : Integer;
      Text001@1007 : TextConst 'ENU=You cannot copy the component list for this %1 from BOM. The %2 of one or more BOM components is not a whole number.;FRA=Vous ne pouvez pas copier la liste des composants de ce(t)(tte) %1 depuis la nomenclature. Le (la) %2 d''un ou plusieurs composants de la nomenclature n''est pas un entier.';

    BEGIN
    END.
  }
}