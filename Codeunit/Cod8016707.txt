OBJECT Codeunit 8016707 EAI Document Entry Mgt
{
  OBJECT-PROPERTIES
  {
    Date=27/12/11;
    Time=17:51:08;
    Modified=Yes;
    Version List=ISAEAI1.00;
  }
  PROPERTIES
  {
    OnRun=BEGIN
          END;

  }
  CODE
  {

    PROCEDURE AddEntryFromSentBuffer@1100481002(pSourceType@1100481000 : Integer;pSourceSubtype@1100481001 : Integer;pSourceID@1100481002 : Code[20];pExtDocNo@1100481006 : Code[20];pSourceLineNo@1100281000 : Integer;pEAISentMQBuffer@1100481003 : Record 8016720);
    VAR
      lEAIDocumentEntry@1100481004 : Record 8016753;
      lMessageQueue@1100481005 : Record 8016704;
    BEGIN
      lMessageQueue.GET(pEAISentMQBuffer."EAI MQ Code");

      WITH lEAIDocumentEntry DO
      BEGIN
        INIT;
        "Source Type" := pSourceType;
        "Source Subtype" := pSourceSubtype;
        "Source ID" := pSourceID;
        "External Document No." := pExtDocNo;
        "EAI Act Type Code" := pEAISentMQBuffer."EAI Act Type Code";
        "EAI Act Code" := pEAISentMQBuffer."EAI Act Code";
        "MQ Table No." := lMessageQueue."Table No.";
        "Posted MQ Table No." := lMessageQueue."Posted Table No.";
        "MQ Entry No." := pEAISentMQBuffer."Entry No.";
        "Source Line No." := pSourceLineNo;  //IMH01 isat.zw
        Direction := Direction::Sent; //IMH02.ST
        IF NOT INSERT(TRUE) THEN
          MODIFY(TRUE);
      END;
    END;

    PROCEDURE AddEntryFromRcvdBuffer@1100481005(pSourceType@1100481000 : Integer;pSourceSubtype@1100481001 : Integer;pSourceID@1100481002 : Code[20];pExtDocNo@1100481006 : Code[20];pSourceLineNo@1100281000 : Integer;pEAIRcvdMQBuffer@1100481003 : Record 8016721);
    VAR
      lEAIDocumentEntry@1100481004 : Record 8016753;
      lMessageQueue@1100481005 : Record 8016704;
    BEGIN
      //<<IMH03.ST
      //lMessageQueue.GET(pEAIRcvdMQBuffer."EAI MQ Code");
      IF NOT lMessageQueue.GET(pEAIRcvdMQBuffer."EAI MQ Code") THEN;
      //>>IMH03.ST

      WITH lEAIDocumentEntry DO
      BEGIN
        INIT;
        "Source Type" := pSourceType;
        "Source Subtype" := pSourceSubtype;
        "Source ID" := pSourceID;
        "External Document No." := pExtDocNo;
        "EAI Act Type Code" := pEAIRcvdMQBuffer."EAI Act Type Code";
        "EAI Act Code" := pEAIRcvdMQBuffer."EAI Act Code";
        "MQ Table No." := lMessageQueue."Table No.";
        "Posted MQ Table No." := lMessageQueue."Posted Table No.";
        "MQ Entry No." := pEAIRcvdMQBuffer."Entry No.";
        "Source Line No." := pSourceLineNo;  //IMH01 isat.zw
        Direction := Direction::Receive; //IMH02.ST
        IF INSERT(TRUE) THEN;
      END;
    END;

    PROCEDURE IsDocAlreadySend@1100481007(pSourceType@1100481000 : Integer;pSourceSubtype@1100481001 : Integer;pSourceID@1100481002 : Code[20];pEAIActType@1100481003 : Code[20];pEAIAct@1100481006 : Code[20]) : Boolean;
    VAR
      lEAIDocumentEntry@1100481004 : Record 8016753;
      lMessageQueue@1100481005 : Record 8016704;
    BEGIN
      WITH lEAIDocumentEntry DO
      BEGIN
        SETRANGE("Source Type",pSourceType);
        SETRANGE("Source Subtype",pSourceSubtype);
        SETRANGE("Source ID",pSourceID);
        SETRANGE("EAI Act Type Code",pEAIActType);
        SETRANGE("EAI Act Code",pEAIAct);
        EXIT(FINDSET(FALSE,FALSE));
      END;
    END;

    PROCEDURE ShowEntriesByAct@1100481003(pEAIAct@1100481000 : Record 8016706);
    VAR
      lEAIDocumentEntry@1100481001 : Record 8016753;
    BEGIN
      lEAIDocumentEntry.SETCURRENTKEY("EAI Act Type Code","EAI Act Code");
      lEAIDocumentEntry.SETRANGE("EAI Act Type Code",pEAIAct."EAI Act Type Code");
      lEAIDocumentEntry.SETRANGE("EAI Act Code",pEAIAct.Code);
      FORM.RUNMODAL(0,lEAIDocumentEntry);
    END;

    PROCEDURE ShowEntriesByDocument@1100481004(pSourceType@1100481003 : Integer;pSourceSubtype@1100481002 : Integer;pSourceID@1100481001 : Code[20]);
    VAR
      lEAIDocumentEntry@1100481000 : Record 8016753;
    BEGIN
      lEAIDocumentEntry.SETCURRENTKEY("Source Type","Source Subtype","Source ID");
      lEAIDocumentEntry.SETRANGE("Source Type",pSourceType);
      lEAIDocumentEntry.SETRANGE("Source Subtype",pSourceSubtype);
      lEAIDocumentEntry.SETRANGE("Source ID",pSourceID);
      FORM.RUNMODAL(0,lEAIDocumentEntry);
    END;

    PROCEDURE ShowEntriesByMQ@1100481008(pTableNo@1100481002 : Integer;pEntryNo@1100481000 : Code[20]);
    VAR
      lEAIDocumentEntry@1100481001 : Record 8016753;
    BEGIN
      lEAIDocumentEntry.SETCURRENTKEY("MQ Table No.","MQ Entry No.");
      lEAIDocumentEntry.SETRANGE("MQ Table No.",pTableNo);
      lEAIDocumentEntry.SETRANGE("MQ Entry No.",pEntryNo);
      FORM.RUNMODAL(0,lEAIDocumentEntry);
    END;

    PROCEDURE ShowEntriesByPostedMQ@1100481010(pTableNo@1100481000 : Integer;pEntryNo@1100481001 : Code[20]);
    VAR
      lEAIDocumentEntry@1100481002 : Record 8016753;
    BEGIN
      lEAIDocumentEntry.SETCURRENTKEY("Posted MQ Table No.","MQ Entry No.");
      lEAIDocumentEntry.SETRANGE("Posted MQ Table No.",pTableNo);
      lEAIDocumentEntry.SETRANGE("MQ Entry No.",pEntryNo);
      FORM.RUNMODAL(0,lEAIDocumentEntry);
    END;

    PROCEDURE DeleteEntryFromMQ@1100481001(pTableNo@1100481000 : Integer;pEntryNo@1100481001 : Code[20]);
    VAR
      lEAIDocumentEntry@1100481002 : Record 8016753;
    BEGIN
      lEAIDocumentEntry.SETCURRENTKEY("MQ Table No.","MQ Entry No.");
      lEAIDocumentEntry.SETRANGE("MQ Table No.",pTableNo);
      lEAIDocumentEntry.SETRANGE("MQ Entry No.",pEntryNo);
      lEAIDocumentEntry.DELETEALL
    END;

    PROCEDURE DeleteEntryFromPostedMQ@1100481000(pTableNo@1100481000 : Integer;pEntryNo@1100481001 : Code[20]);
    VAR
      lEAIDocumentEntry@1100481002 : Record 8016753;
    BEGIN
      lEAIDocumentEntry.SETCURRENTKEY("Posted MQ Table No.","MQ Entry No.");
      lEAIDocumentEntry.SETRANGE("Posted MQ Table No.",pTableNo);
      lEAIDocumentEntry.SETRANGE("MQ Entry No.",pEntryNo);
      lEAIDocumentEntry.DELETEALL
    END;

    PROCEDURE DeleteEntryFromDocument@1100481006(pSourceType@1100481003 : Integer;pSourceSubtype@1100481001 : Integer;pSourceID@1100481000 : Code[20]);
    VAR
      lEAIDocumentEntry@1100481002 : Record 8016753;
    BEGIN
      lEAIDocumentEntry.SETCURRENTKEY("Source Type","Source Subtype","Source ID");
      lEAIDocumentEntry.SETRANGE("Source Type",pSourceType);
      lEAIDocumentEntry.SETRANGE("Source Subtype",pSourceSubtype);
      lEAIDocumentEntry.SETRANGE("Source ID",pSourceID);
      lEAIDocumentEntry.DELETEALL
    END;

    BEGIN
    {
      /** @r IHM03 @d 27/12/11 @a ISAT.ST @v IMH6.01.131 @s Interface EAI @c Ajout Direction */
      /** @r IHM02 @d 31/05/11 @a ISAT.ST @v IMH6.01.131 @s Interface EAI @c Ajout Direction */
      /** @r IHM01 @d 06/01/11 @a ISAT.ZW @v IMH6.01.131 @s Interface EAI @c Ajout champ Source Line No. */
      /** @r EAI00 @d 17/02/09 @a ISAT.JR @v ISAEAI5.01.00.00 @s cr‚ation */
    }
    END.
  }
}