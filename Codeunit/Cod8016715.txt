OBJECT Codeunit 8016715 EAI Task Launcher
{
  OBJECT-PROPERTIES
  {
    Date=06/02/12;
    Time=17:20:02;
    Modified=Yes;
    Version List=ISAEAI1.00;
  }
  PROPERTIES
  {
    TableNo=8016716;
    SingleInstance=Yes;
    OnRun=BEGIN
            EAIProcess.GET(Rec."EAI Process Code");
            EAIProcessTask := Rec;
            CUActive := TRUE;
            CASE "Object Type" OF
              "Object Type"::Report :
                REPORT.RUNMODAL("Object ID",FALSE);
              "Object Type"::Codeunit :
                CODEUNIT.RUN("Object ID");
            END;
            CUActive := FALSE;
            CLEAR(EAIProcessTask);
            CLEAR(EAIProcess);
          END;

  }
  CODE
  {
    VAR
      EAIProcess@1100281002 : Record 8016702;
      EAIProcessTask@1100281001 : Record 8016716;
      EAIProcHandler@1100281000 : Codeunit 8016720;
      EndDateTime@1100281004 : DateTime;
      CUActive@1100281003 : Boolean;
      Text001@1100281007 : TextConst 'ENU=Process %1 task %2 : expired.;FRA=Processus %1 tƒche %2 : expir‚.';
      Text002@1100281005 : TextConst 'ENU=Expired process;FRA=Traitement expir‚.';

    PROCEDURE RetrieveParam@1100281000() : Text[250];
    BEGIN
      EXIT(EAIProcessTask."Parameter String");
    END;

    PROCEDURE IsStoppingTime@1100281004() : Boolean;
    BEGIN
      IF NOT CUActive THEN
        EXIT(FALSE);
      IF EAIProcHandler.IsProcessExpired(EAIProcess) THEN
        EXIT(TRUE);
      IF EAIProcHandler.IsProcessTaskExpired(EAIProcessTask) THEN
        EXIT(TRUE);
      EXIT(FALSE);
    END;

    PROCEDURE IsActive@1100281002() : Boolean;
    BEGIN
      EXIT(CUActive);
    END;

    PROCEDURE SetTaskResult@1100281007(pText@1100281000 : Text[1024]);
    VAR
      lEAILogMgt@1100281001 : Codeunit 50154;
    BEGIN
      IF NOT CUActive THEN BEGIN
        MESSAGE(pText);
        EXIT;
      END;
      AddProcessLog(0, pText);
    END;

    LOCAL PROCEDURE AddProcessLog@1100281006(pType@1100281002 : 'Info,Warning,Error';pText@1100281000 : Text[1024]);
    VAR
      lEAILogMgt@1100281001 : Codeunit 8016711;
    BEGIN
      CASE pType OF
        pType::Info : lEAILogMgt.AddInfoToTaskProcessLog(
                                    EAIProcessTask."EAI Process Code",
                                    EAIProcessTask."EAI Process Task Code",
                                    pText);
        pType::Warning : lEAILogMgt.AddWarningToTaskProcessLog(
                                    EAIProcessTask."EAI Process Code",
                                    EAIProcessTask."EAI Process Task Code",
                                    pText);
        pType::Error : lEAILogMgt.AddErrorToTaskProcessLog(
                                    EAIProcessTask."EAI Process Code",
                                    EAIProcessTask."EAI Process Task Code",
                                    pText);
      END;
    END;

    PROCEDURE AddExpiredMsg@1100281005(pAddMessage@1100281000 : Text[1024]);
    VAR
      lText@1100281001 : Text[1024];
    BEGIN
      IF NOT IsActive THEN BEGIN
        MESSAGE(Text002 + pAddMessage);
        EXIT;
      END;
      lText := COPYSTR(STRSUBSTNO(Text001, EAIProcessTask."EAI Process Code", EAIProcessTask."EAI Process Task Code") + pAddMessage,
                       1, MAXSTRLEN(lText));
      AddProcessLog(1,lText);
    END;

    PROCEDURE AddErrorLog@1100281008(pText@1100281000 : Text[1024]);
    BEGIN
      IF NOT IsActive THEN BEGIN
        MESSAGE(pText);
        EXIT;
      END;
      AddProcessLog(2,pText);
    END;

    BEGIN
    {
      /** @r EAI01 @d 05/12/11 @a ISAT.ST @v ISAEAI5.01.00.00 @s Processus … tƒches @c Cr‚ation objet */
    }
    END.
  }
}