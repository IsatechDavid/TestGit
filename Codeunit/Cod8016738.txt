OBJECT Codeunit 8016738 EAI Send - Force Synchro
{
  OBJECT-PROPERTIES
  {
    Date=22/11/10;
    Time=11:40:12;
    Version List=ISAEAI1.02;
  }
  PROPERTIES
  {
    OnRun=BEGIN
            //Synchro de toutes les tables
            IF NOT CONFIRM(Text001,FALSE) THEN
              EXIT;

            Code;
          END;

  }
  CODE
  {
    VAR
      Text001@1180044000 : TextConst 'ENU="This operation will send all records from tables to synchronize. Do you want to continue? ";FRA=Cette op‚ration va envoyer tous les enregistrements des tables … synchroniser. Souhaitez-vous continuer?';
      EAISentSynchroAction@1180044001 : Codeunit 8016730;
      EAIToSynchronizeTable@1180044002 : Record 8016708;
      SourceRecRef@1180044012 : RecordRef;
      Window@1180044003 : Dialog;
      CountTable@1180044004 : Integer;
      TotalTable@1180044005 : Integer;
      CountRec@1180044006 : Integer;
      TotalRec@1180044007 : Integer;
      Text002@1180044013 : TextConst 'ENU="This operation will send all records from table %1. Do you want to continue? ";FRA=Cette op‚ration va envoyer tous les enregistrements de la table %1. Souhaitez-vous continuer?';
      Dial001@1180044008 : TextConst 'ENU=Table             #1###########\;FRA=Table             #1###########\';
      Dial002@1180044009 : TextConst 'ENU=Table in progress #2###########\;FRA=Table en cours    #2###########\';
      Dial003@1180044010 : TextConst 'ENU=#3#############################\;FRA=#3#############################\';
      Dial004@1180044011 : TextConst 'ENU=@4@@@@@@@@@@@@@@@@@@@@@@@@@@@@@;FRA=@4@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';

    PROCEDURE SendTable@1180044001(pEAIToSynchronizeTable@1180044000 : Record 8016708);
    BEGIN
      //Synchro de la table s‚lectionn‚e
      pEAIToSynchronizeTable.CALCFIELDS("Table Name");
      IF NOT CONFIRM(Text002,FALSE,pEAIToSynchronizeTable."Table Name") THEN
        EXIT;

      pEAIToSynchronizeTable.SETRECFILTER;
      EAIToSynchronizeTable.COPYFILTERS(pEAIToSynchronizeTable);

      Code;
    END;

    PROCEDURE Code@1180044005();
    BEGIN
      EAIToSynchronizeTable.SETFILTER("Synchro Group Table  Number",'<>0');
      TotalTable := EAIToSynchronizeTable.COUNT;
      CountTable := 0;

      Window.OPEN(Dial001 + Dial002 + Dial003 + Dial004);

      IF EAIToSynchronizeTable.FINDSET THEN
      REPEAT

        SourceRecRef.OPEN(EAIToSynchronizeTable."Table No.");
        CountRec := 0;
        TotalRec := SourceRecRef.COUNT;

        CountTable += 1;
        Window.UPDATE(1,STRSUBSTNO('%1 / %2',CountTable,TotalTable));
        Window.UPDATE(2,SourceRecRef.CAPTION);

        IF SourceRecRef.FINDSET THEN
        REPEAT
          CountRec += 1;

          Window.UPDATE(3,SourceRecRef.RECORDID);
          Window.UPDATE(4,ROUND(CountRec * 9999 / TotalRec,1));

          EAISentSynchroAction.LogModification(SourceRecRef,SourceRecRef);

        UNTIL SourceRecRef.NEXT=0;

        SourceRecRef.CLOSE;

      UNTIL EAIToSynchronizeTable.NEXT = 0;

      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}