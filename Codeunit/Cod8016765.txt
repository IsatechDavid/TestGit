OBJECT Codeunit 8016765 EAI Sent EDM Validation Post
{
  OBJECT-PROPERTIES
  {
    Date=20/09/12;
    Time=12:00:00;
    Modified=Yes;
    Version List=IMH6.01,GED;
  }
  PROPERTIES
  {
    TableNo=8016704;
    SingleInstance=Yes;
    OnRun=VAR
            lText000@1100481000 : TextConst 'ENU=%1 - %2 : Sending Message Queue %1 Posting Handler started;FRA=%1 - %2 : D‚marrage du handler de validation de la file d''‚mission';
          BEGIN
            IF "Table No." = 0 THEN
              EXIT;
            IF Type <> Type::Sending THEN
              EXIT;

            CurrentMessageQueue := Rec;
            RunTimer;

            MESSAGE(lText000,CurrentMessageQueue."EAI NAS Code",CurrentMessageQueue.Code);
          END;

  }
  CODE
  {
    VAR
      EAILogMgt@1100281004 : Codeunit 8016711;
      EAIMQMgt@1100281003 : Codeunit 8016704;
      CurrentMessageQueue@1100281001 : Record 8016704;
      ValidationTimer@1100281002 : Automation "{3B311C81-140E-11D5-8B18-00D0590B4EC5} 1.0:{3B311C92-140E-11D5-8B18-00D0590B4EC5}:'Navision Timer 1.0'.Timer" WITHEVENTS;
      IsRunning@1100281000 : Boolean;

    PROCEDURE MessageQueueLoop@1100281001();
    VAR
      lEAIMQBufferMgt@1100281002 : Codeunit 8016708;
      lSentMQBuffer@1100281001 : TEMPORARY Record 8016720;
      lRecRef@1100481000 : RecordRef;
      lNbMsg@1100281000 : Integer;
      lCountMsg@1100281003 : Integer;
    BEGIN
      //Traitement des messages

      lEAIMQBufferMgt.SetFilterAndKeyOnSendMQ(
         lRecRef,CurrentMessageQueue,lSentMQBuffer.Step::Validation,lSentMQBuffer.Status::Unprocessed);

      //On limite le nombre de message trait‚ en mˆme temps
      lNbMsg := CurrentMessageQueue."Max MQ Number per Loop";
      IF lNbMsg = 0 THEN
        lNbMsg := 50;

      //EAI02 lRecRef.LOCKTABLE;
      IF lRecRef.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          //On passe par un Buffer
          lEAIMQBufferMgt.TransferRecRefToSendMQBuffer(lRecRef,lSentMQBuffer);

          //Traitement unitaire
          MessageQueuePost(lSentMQBuffer);

          //On enregistre le Buffer
          lEAIMQBufferMgt.TransferSendMQBufferToSendMQ(lSentMQBuffer);

          COMMIT;

          lCountMsg += 1;

        UNTIL (lRecRef.NEXT=0) OR (lCountMsg=lNbMsg);
      END;
    END;

    PROCEDURE MessageQueuePost@1100481000(VAR pSendMQBuffer@1100281001 : Record 8016720);
    VAR
      lEAIMQBufferMgt@1100281000 : Codeunit 8016708;
      lErrorProcess@1100281002 : Boolean;
    BEGIN
      COMMIT; //N‚cessaire pour lancer le CodeUnit.Run avec valeur de retour (gestion des erreurs)

      IF pSendMQBuffer."Sleep Time" <= 0 THEN BEGIN
        pSendMQBuffer."Set To Sleep" := pSendMQBuffer."Set To Sleep"::" ";
      END;

      IF pSendMQBuffer."Set To Sleep" = pSendMQBuffer."Set To Sleep"::Yes THEN BEGIN
        pSendMQBuffer."Sleep Time" := pSendMQBuffer."Sleep Time" - 1;
      END ELSE BEGIN
        IF CODEUNIT.RUN(CODEUNIT::"EAI Sent Validation Post Line",pSendMQBuffer) THEN BEGIN
          IF pSendMQBuffer.Status <> pSendMQBuffer.Status::Warning THEN BEGIN
            pSendMQBuffer.Step := pSendMQBuffer.Step::Validation;
            pSendMQBuffer.Status := pSendMQBuffer.Status::Processing;
            pSendMQBuffer.MODIFY;
          END;
        END ELSE BEGIN
          lErrorProcess := TRUE;
          pSendMQBuffer."Comment Status" := COPYSTR(GETLASTERRORTEXT,1,250);
          IF pSendMQBuffer.Status <> pSendMQBuffer.Status::Warning THEN
            pSendMQBuffer.Status := pSendMQBuffer.Status::Error;
          pSendMQBuffer.MODIFY;
          CLEARLASTERROR;
        END;
      END;

      IF lErrorProcess THEN BEGIN
        lEAIMQBufferMgt.CheckModifSendMQBuffFromSendMQ(pSendMQBuffer);
        pSendMQBuffer.MODIFY;
      END;
    END;

    PROCEDURE MessageQueueDelete@1100481009();
    VAR
      lEAIMQBufferMgt@1100281002 : Codeunit 8016708;
      lSentMQBuffer@1100281001 : TEMPORARY Record 8016720;
      lRecRef@1100481000 : RecordRef;
    BEGIN
      //Suppression / Archivage des messages

      lEAIMQBufferMgt.SetFilterAndKeyOnSendMQ(
         lRecRef,CurrentMessageQueue,lSentMQBuffer.Step::Validation,lSentMQBuffer.Status::Processing);

      lRecRef.LOCKTABLE;
      IF lRecRef.FINDSET(FALSE,FALSE) THEN BEGIN
        REPEAT
          //On passe par un Buffer
          lEAIMQBufferMgt.TransferRecRefToSendMQBuffer(lRecRef,lSentMQBuffer);

          //Traitement unitaire
          lEAIMQBufferMgt.TransferSendMQBufferToHistoMQ(lSentMQBuffer);
        UNTIL lRecRef.NEXT=0;
      END;
    END;

    PROCEDURE RunTimer@1100281003();
    BEGIN
      IF CurrentMessageQueue."Timer Interval" = 0 THEN
        CurrentMessageQueue."Timer Interval" := 60;

      IF ISCLEAR(ValidationTimer) THEN
        CREATE(ValidationTimer);

      ValidationTimer.Interval(CurrentMessageQueue."Timer Interval" * 1000);
      ValidationTimer.Enabled(TRUE);
    END;

    EVENT ValidationTimer@1100281002::Timer@1(Milliseconds@1100281000 : Integer);
    BEGIN
      IF NOT IsRunning THEN BEGIN
        IsRunning := TRUE;
        SELECTLATESTVERSION;
        MessageQueueLoop;
        MessageQueueDelete;
        IsRunning := FALSE;
        COMMIT;
      END;
    END;

    EVENT ValidationTimer@1100281002::TimerError@2(ErrorString@1100281000 : Text[1024]);
    BEGIN
      EAILogMgt.AddErrorToSentLog(CurrentMessageQueue.Code,'','','','',COPYSTR(ErrorString,1,250));

      IsRunning := FALSE;
      RunTimer;
    END;

    BEGIN
    {
      /** @r IMH01 @d 16/07/12 @a ISAT.PC @v IMH6.01.P006 @s Interface GED @c cr‚ation */
    }
    END.
  }
}