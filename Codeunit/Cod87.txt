OBJECT Codeunit 87 Blanket Sales Order to Order
{
  OBJECT-PROPERTIES
  {
    Date=02/08/10;
    Time=15:18:03;
    Version List=NAVW16.00.01,KITNA6.00,ISA6.00;
  }
  PROPERTIES
  {
    TableNo=36;
    OnRun=VAR
            FromDocDim@1000 : Record 357;
            ToDocDim@1003 : Record 357;
            Cust@1001 : Record 18;
            TempSalesLine@1002 : TEMPORARY Record 37;
            Reservation@1005 : Form 498;
            PrepmtMgt@1004 : Codeunit 441;
          BEGIN
            TESTFIELD("Document Type","Document Type"::"Blanket Order");
            Cust.GET("Sell-to Customer No.");
            Cust.CheckBlockedCustOnDocs(Cust,"Document Type"::Order,TRUE,FALSE);

            SalesSetup.GET;

            BlanketOrderSalesLine.SETRANGE("Document Type","Document Type");
            BlanketOrderSalesLine.SETRANGE("Document No.","No.");
            BlanketOrderSalesLine.SETRANGE(Type,BlanketOrderSalesLine.Type::Item);
            BlanketOrderSalesLine.SETFILTER("No.",'<>%1','');
            IF BlanketOrderSalesLine.FINDSET THEN
              REPEAT
                IF BlanketOrderSalesLine."Qty. to Ship" > 0 THEN BEGIN
                  SalesLine := BlanketOrderSalesLine;
                  SalesLine."Line No." := 0;
                  ResetQuantityFields(SalesLine);
                  SalesLine.Quantity := BlanketOrderSalesLine."Qty. to Ship";
                  SalesLine."Quantity (Base)" := ROUND(SalesLine.Quantity * SalesLine."Qty. per Unit of Measure",0.00001);
                  SalesLine."Qty. to Ship" := SalesLine.Quantity;
                  SalesLine."Qty. to Ship (Base)" := SalesLine."Quantity (Base)";
                  SalesLine.InitOutstanding;
                  IF SalesLine.Reserve <> SalesLine.Reserve::Always THEN
                    IF NOT HideValidationDialog THEN
                      IF BlanketOrderSalesLine."Build Kit" THEN BEGIN
                        KitBlanketOrderSalesLine.SETRANGE("Document Type","Document Type");
                        KitBlanketOrderSalesLine.SETRANGE("Document No.","No.");
                        KitBlanketOrderSalesLine.SETRANGE("Document Line No.",KitBlanketOrderSalesLine."Line No.");
                        KitBlanketOrderSalesLine.SETRANGE(Type,KitBlanketOrderSalesLine.Type::Item);
                        KitBlanketOrderSalesLine.SETFILTER("No.",'<>%1','');
                        IF KitBlanketOrderSalesLine.FINDSET THEN
                          REPEAT
                            IF KitBlanketOrderSalesLine.Reserve <> KitBlanketOrderSalesLine.Reserve::Always THEN BEGIN
                              SalesLine."No." := KitBlanketOrderSalesLine."No.";
                              SalesLine."Variant Code" := KitBlanketOrderSalesLine."Variant Code";
                              SalesLine."Unit of Measure Code" := KitBlanketOrderSalesLine."Unit of Measure Code";
                              SalesLine."Qty. per Unit of Measure" := KitBlanketOrderSalesLine."Qty. per Unit of Measure";
                              SalesLine."Outstanding Quantity" :=
                                ROUND(SalesLine."Quantity (Base)" * KitBlanketOrderSalesLine."Quantity per",0.00001);
                              SalesLine."Build Kit" := FALSE;
                              ItemCheckAvail.SalesLineCheck(SalesLine);
                            END;
                          UNTIL KitBlanketOrderSalesLine.NEXT = 0;
                      END ELSE
                        ItemCheckAvail.SalesLineCheck(SalesLine);
                END;
              UNTIL BlanketOrderSalesLine.NEXT = 0;

            SalesOrderHeader := Rec;
            SalesOrderHeader."Document Type" := SalesOrderHeader."Document Type"::Order;
            IF NOT HideValidationDialog THEN
              CustCheckCreditLimit.SalesHeaderCheck(SalesOrderHeader);

            SalesOrderHeader."No. Printed" := 0;
            SalesOrderHeader.Status := SalesOrderHeader.Status::Open;
            SalesOrderHeader."No." := '';

            SalesOrderLine.LOCKTABLE;
            SalesOrderHeader.INSERT(TRUE);

            FromDocDim.SETRANGE("Table ID",DATABASE::"Sales Header");
            FromDocDim.SETRANGE("Document Type","Document Type");
            FromDocDim.SETRANGE("Document No.","No.");

            ToDocDim.SETRANGE("Table ID",DATABASE::"Sales Header");
            ToDocDim.SETRANGE("Document Type",SalesOrderHeader."Document Type");
            ToDocDim.SETRANGE("Document No.",SalesOrderHeader."No.");
            ToDocDim.DELETEALL;

            DocDim.MoveDocDimToDocDim(
              FromDocDim,
              DATABASE::"Sales Header",
              SalesOrderHeader."No.",
              SalesOrderHeader."Document Type",
              0);

            IF "Order Date" = 0D THEN
              SalesOrderHeader."Order Date" := WORKDATE
            ELSE
              SalesOrderHeader."Order Date" := "Order Date";
            IF "Posting Date" <> 0D THEN
              SalesOrderHeader."Posting Date" := "Posting Date";
            SalesOrderHeader."Document Date" := "Document Date";
            SalesOrderHeader."Shipment Date" := "Shipment Date";
            SalesOrderHeader."Shortcut Dimension 1 Code" := "Shortcut Dimension 1 Code";
            SalesOrderHeader."Shortcut Dimension 2 Code" := "Shortcut Dimension 2 Code";
            SalesOrderHeader."Outbound Whse. Handling Time" := "Outbound Whse. Handling Time";
            SalesOrderHeader."Location Code" := "Location Code";
            SalesOrderHeader."Ship-to Name" := "Ship-to Name";
            SalesOrderHeader."Ship-to Name 2" := "Ship-to Name 2";
            SalesOrderHeader."Ship-to Address" := "Ship-to Address";
            SalesOrderHeader."Ship-to Address 2" := "Ship-to Address 2";
            SalesOrderHeader."Ship-to City" := "Ship-to City";
            SalesOrderHeader."Ship-to Post Code" := "Ship-to Post Code";
            SalesOrderHeader."Ship-to County" := "Ship-to County";
            SalesOrderHeader."Ship-to Country/Region Code" := "Ship-to Country/Region Code";
            SalesOrderHeader."Ship-to Contact" := "Ship-to Contact";

            SalesOrderHeader.Reserve := Reserve; //ISA02.ST

            SalesOrderHeader."Prepayment %" := Cust."Prepayment %";
            IF SalesOrderHeader."Posting Date" = 0D THEN
              SalesOrderHeader."Posting Date" := WORKDATE;

            SalesOrderHeader.MODIFY;

            BlanketOrderSalesLine.RESET;
            BlanketOrderSalesLine.SETRANGE("Document Type","Document Type");
            BlanketOrderSalesLine.SETRANGE("Document No.","No.");

            FromDocDim.SETRANGE("Table ID",DATABASE::"Sales Line");
            ToDocDim.SETRANGE("Table ID",DATABASE::"Sales Line");

            IF BlanketOrderSalesLine.FINDSET THEN BEGIN
              TempSalesLine.DELETEALL;
              REPEAT
                SalesLine.SETCURRENTKEY("Document Type","Blanket Order No.","Blanket Order Line No.");
                //<<ISA01.ST
                SalesLine.SETFILTER("Document Type",'<>%1&<>%2',
                                  SalesLine."Document Type"::Quote, SalesLine."Document Type"::"Blanket Order");
                //>>ISA01.ST
                SalesLine.SETRANGE("Blanket Order No.",BlanketOrderSalesLine."Document No.");
                SalesLine.SETRANGE("Blanket Order Line No.",BlanketOrderSalesLine."Line No.");
                QuantityOnOrders := 0;
                IF SalesLine.FINDSET THEN
                  REPEAT
                    IF SalesLine."Document Type" IN
                        [SalesLine."Document Type"::"Return Order",
                         SalesLine."Document Type"::"Credit Memo"]
                    THEN
                      QuantityOnOrders := QuantityOnOrders - SalesLine."Outstanding Qty. (Base)"
                    ELSE
                      QuantityOnOrders := QuantityOnOrders + SalesLine."Outstanding Qty. (Base)";
                  UNTIL SalesLine.NEXT = 0;
                IF (ABS(BlanketOrderSalesLine."Qty. to Ship (Base)" + QuantityOnOrders +
                        BlanketOrderSalesLine."Qty. Shipped (Base)") >
                    ABS(BlanketOrderSalesLine."Quantity (Base)")) OR
                   (BlanketOrderSalesLine."Quantity (Base)" * BlanketOrderSalesLine."Outstanding Qty. (Base)" < 0)
                THEN
                  ERROR(
                    Text000 +
                    '%6\' +
                    '%7 - %8 = %9',
                    BlanketOrderSalesLine.FIELDCAPTION("Qty. to Ship (Base)"),
                    BlanketOrderSalesLine.Type,BlanketOrderSalesLine."No.",
                    BlanketOrderSalesLine.FIELDCAPTION("Line No."),BlanketOrderSalesLine."Line No.",
                    STRSUBSTNO(
                      Text001,
                      BlanketOrderSalesLine.FIELDCAPTION("Outstanding Qty. (Base)"),
                      BlanketOrderSalesLine.FIELDCAPTION("Qty. to Ship (Base)")),
                    BlanketOrderSalesLine."Outstanding Qty. (Base)",QuantityOnOrders,
                    BlanketOrderSalesLine."Outstanding Qty. (Base)" - QuantityOnOrders);
                SalesOrderLine := BlanketOrderSalesLine;
                ResetQuantityFields(SalesOrderLine);
                SalesOrderLine."Document Type" := SalesOrderHeader."Document Type";
                SalesOrderLine."Document No." := SalesOrderHeader."No.";
                SalesOrderLine."Blanket Order No." := "No.";
                SalesOrderLine."Blanket Order Line No." := BlanketOrderSalesLine."Line No.";
                IF (SalesOrderLine."No." <> '') AND (SalesOrderLine.Type <> 0) THEN BEGIN
                  SalesOrderLine.Amount := 0;
                  SalesOrderLine."Amount Including VAT" := 0;
                  SalesOrderLine.VALIDATE(Quantity,BlanketOrderSalesLine."Qty. to Ship");
                  SalesOrderLine.VALIDATE("Shipment Date",BlanketOrderSalesLine."Shipment Date");
                  SalesOrderLine.VALIDATE("Unit Price",BlanketOrderSalesLine."Unit Price");
                  SalesOrderLine."Allow Invoice Disc." := BlanketOrderSalesLine."Allow Invoice Disc.";
                  SalesOrderLine."Allow Line Disc." := BlanketOrderSalesLine."Allow Line Disc.";
                  SalesOrderLine.VALIDATE("Line Discount %",BlanketOrderSalesLine."Line Discount %");
                  IF SalesOrderLine.Quantity <> 0 THEN
                    SalesOrderLine.VALIDATE("Inv. Discount Amount",BlanketOrderSalesLine."Inv. Discount Amount");
                  ReserveSalesLine.TransferSaleLineToSalesLine(
                    BlanketOrderSalesLine,SalesOrderLine,BlanketOrderSalesLine."Outstanding Qty. (Base)");
                END;

                IF Cust."Prepayment %" <> 0 THEN
                  SalesOrderLine."Prepayment %" := Cust."Prepayment %";
                PrepmtMgt.SetSalesPrepaymentPct(SalesOrderLine,SalesOrderHeader."Posting Date");
                SalesOrderLine.VALIDATE("Prepayment %");

                SalesOrderLine."Shortcut Dimension 1 Code" := BlanketOrderSalesLine."Shortcut Dimension 1 Code";
                SalesOrderLine."Shortcut Dimension 2 Code" := BlanketOrderSalesLine."Shortcut Dimension 2 Code";
                SalesOrderLine.INSERT;

                IF BlanketOrderSalesLine."Build Kit" THEN BEGIN
                  KitBlanketOrderSalesLine.RESET;
                  KitBlanketOrderSalesLine.SETRANGE("Document Type","Document Type");
                  KitBlanketOrderSalesLine.SETRANGE("Document No.","No.");
                  KitBlanketOrderSalesLine.SETRANGE("Document Line No.",BlanketOrderSalesLine."Line No.");
                  IF KitBlanketOrderSalesLine.FINDSET THEN
                    REPEAT
                      KitSalesOrderLine := KitBlanketOrderSalesLine;
                      KitSalesOrderLine."Document Type" := SalesOrderHeader."Document Type";
                      KitSalesOrderLine."Document No." := SalesOrderHeader."No.";
                      KitBlanketOrderSalesLine."Shipment Date" := BlanketOrderSalesLine."Shipment Date";
                      IF KitSalesOrderLine.Type IN [KitSalesOrderLine.Type::Item,KitSalesOrderLine.Type::Resource] THEN BEGIN
                        KitSalesOrderLine."Extended Quantity" := KitSalesOrderLine."Quantity per" * SalesOrderLine."Quantity (Base)";
                        KitSalesOrderLine."Extended Quantity (Base)" :=
                          KitSalesOrderLine."Quantity per (Base)" * SalesOrderLine."Quantity (Base)";
                        KitSalesOrderLine."Outstanding Quantity" := KitSalesOrderLine."Extended Quantity";
                        KitSalesOrderLine."Outstanding Qty. (Base)" := KitSalesOrderLine."Extended Quantity (Base)";
                      END;
                      IF (KitBlanketOrderSalesLine.Type = KitBlanketOrderSalesLine.Type::Item) AND
                         (KitBlanketOrderSalesLine."No." <> '')
                      THEN
                        ReserveKitSalesLine.TransfKitSalLineToKitSalesLine(
                          KitBlanketOrderSalesLine,KitSalesOrderLine,KitBlanketOrderSalesLine."Outstanding Qty. (Base)");
                      KitSalesOrderLine."Shortcut Dimension 1 Code" := KitBlanketOrderSalesLine."Shortcut Dimension 1 Code";
                      KitSalesOrderLine."Shortcut Dimension 2 Code" := KitBlanketOrderSalesLine."Shortcut Dimension 2 Code";
                      KitSalesOrderLine.INSERT;

                      AutoReserveKit(KitSalesOrderLine);

                    UNTIL KitBlanketOrderSalesLine.NEXT = 0;
                END;

                FromDocDim.SETRANGE("Line No.",BlanketOrderSalesLine."Line No.");

                ToDocDim.SETRANGE("Line No.",SalesOrderLine."Line No.");
                ToDocDim.DELETEALL;

                DocDim.MoveDocDimToDocDim(
                  FromDocDim,
                  DATABASE::"Sales Line",
                  SalesOrderHeader."No.",
                  SalesOrderHeader."Document Type",
                  SalesOrderLine."Line No.");

                IF BlanketOrderSalesLine."Qty. to Ship" <> 0 THEN BEGIN
                  BlanketOrderSalesLine.VALIDATE("Qty. to Ship",0);
                  BlanketOrderSalesLine.MODIFY;
                  AutoReserve(SalesOrderLine,TempSalesLine);
                END;
              UNTIL BlanketOrderSalesLine.NEXT = 0;
            END;

            IF SalesSetup."Default Posting Date" = SalesSetup."Default Posting Date" :: "No Date" THEN BEGIN
              SalesOrderHeader."Posting Date" := WORKDATE;
              SalesOrderHeader.MODIFY;
            END;

            IF SalesSetup."Copy Comments Blanket to Order" THEN BEGIN
              SalesCommentLine.SETRANGE("Document Type",SalesCommentLine."Document Type"::"Blanket Order");
              SalesCommentLine.SETRANGE("No.","No.");
              IF SalesCommentLine.FINDSET THEN
                REPEAT
                  SalesCommentLine2 := SalesCommentLine;
                  SalesCommentLine2."Document Type" := SalesOrderHeader."Document Type";
                  SalesCommentLine2."No." := SalesOrderHeader."No.";
                  SalesCommentLine2.INSERT;
                UNTIL SalesCommentLine.NEXT = 0;
              SalesOrderHeader.COPYLINKS(Rec);
            END;

            COMMIT;

            IF TempSalesLine.FIND('-') THEN
              IF CONFIRM(Text003,TRUE) THEN
                REPEAT
                  CLEAR(Reservation);
                  Reservation.SetSalesLine(TempSalesLine);
                  Reservation.RUNMODAL;
                  FIND;
                UNTIL TempSalesLine.NEXT = 0;

            CLEAR(CustCheckCreditLimit);
            CLEAR(ItemCheckAvail);
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 of %2 %3 in %4 %5 cannot be more than %9.\;FRA=%1 de %2 %3 dans %4 %5 ne peut pas ˆtre sup‚rieure … %9.\';
      Text001@1001 : TextConst 'ENU="%1 - Unposted %1 = Possible %2";FRA="%1 - %1 non valid‚ = %2 possible"';
      BlanketOrderSalesLine@1003 : Record 37;
      KitBlanketOrderSalesLine@1462000 : Record 25000;
      SalesLine@1004 : Record 37;
      SalesOrderHeader@1005 : Record 36;
      SalesOrderLine@1006 : Record 37;
      KitSalesOrderLine@1462001 : Record 25000;
      SalesCommentLine@1007 : Record 44;
      SalesCommentLine2@1008 : Record 44;
      SalesSetup@1009 : Record 311;
      CustCheckCreditLimit@1010 : Codeunit 312;
      ItemCheckAvail@1011 : Codeunit 311;
      ReserveSalesLine@1012 : Codeunit 99000832;
      ReserveKitSalesLine@1462002 : Codeunit 25001;
      DocDim@1016 : Codeunit 408;
      HideValidationDialog@1014 : Boolean;
      QuantityOnOrders@1015 : Decimal;
      Text003@1017 : TextConst 'ENU=Full automatic reservation was not possible.\Reserve items manually?;FRA=La r‚servation entiŠrement automatique n''est pas possible.\Souhaitez-vous r‚server les articles manuellement ?';

    PROCEDURE ResetQuantityFields@1(VAR TempSalesLine@1000 : Record 37);
    BEGIN
      TempSalesLine.Quantity := 0;
      TempSalesLine."Quantity (Base)" := 0;
      TempSalesLine."Qty. Shipped Not Invoiced" := 0;
      TempSalesLine."Quantity Shipped" := 0;
      TempSalesLine."Quantity Invoiced" := 0;
      TempSalesLine."Qty. Shipped Not Invd. (Base)" := 0;
      TempSalesLine."Qty. Shipped (Base)" := 0;
      TempSalesLine."Qty. Invoiced (Base)" := 0;
      TempSalesLine."Outstanding Quantity" := 0;
      TempSalesLine."Outstanding Qty. (Base)" := 0;
    END;

    PROCEDURE GetSalesOrderHeader@2(VAR SalesHeader@1000 : Record 36);
    BEGIN
      SalesHeader := SalesOrderHeader;
    END;

    PROCEDURE SetHideValidationDialog@14(NewHideValidationDialog@1000 : Boolean);
    BEGIN
      HideValidationDialog := NewHideValidationDialog;
    END;

    LOCAL PROCEDURE AutoReserve@11(VAR SalesLine@1000 : Record 37;VAR TempSalesLine@1003 : TEMPORARY Record 37);
    VAR
      ReservMgt@1001 : Codeunit 99000845;
      FullAutoReservation@1002 : Boolean;
    BEGIN
      WITH SalesLine DO
        IF (Type = Type::Item) AND
           (Reserve = Reserve::Always) AND
           ("No." <> '')
        THEN BEGIN
          TESTFIELD("Shipment Date");
          ReservMgt.SetSalesLine(SalesLine);
          ReservMgt.AutoReserve(FullAutoReservation,'',"Shipment Date","Qty. to Ship");
          FIND;
          IF NOT FullAutoReservation THEN BEGIN
            TempSalesLine.TRANSFERFIELDS(SalesLine);
            TempSalesLine.INSERT();
          END;
        END;
    END;

    LOCAL PROCEDURE AutoReserveKit@1462000(VAR KitSalesLine@1462000 : Record 25000);
    VAR
      ReservMgt@1462001 : Codeunit 99000845;
      FullAutoReservation@1462002 : Boolean;
    BEGIN
      WITH KitSalesLine DO
        IF (Type = Type::Item) AND
           (Reserve = Reserve::Always) AND
           ("No." <> '')
        THEN BEGIN
          TESTFIELD("Shipment Date");
          ReservMgt.SetKitSalesLine(KitSalesLine);
          ReservMgt.AutoReserve(FullAutoReservation,'',"Shipment Date","Extended Quantity");
        END;
    END;

    BEGIN
    {
      /**@r ISA02 @d 02/08/10 @a ISAT.ST @v ISA6.00.00.09 @s Correctif MS @c KB978995 suivi r‚servation en commande */
      /**@r ISA01 @d 06/07/09 @a ISAT.ST @v ISA6.00.00.01 @s Migration ISATECH +
         @c Filtre blanket order */
    }
    END.
  }
}