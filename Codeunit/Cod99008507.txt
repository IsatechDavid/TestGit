OBJECT Codeunit 99008507 Product Catalog-Accept
{
  OBJECT-PROPERTIES
  {
    Date=15/09/04;
    Time=12:00:00;
    Version List=NAVW14.00;
  }
  PROPERTIES
  {
    TableNo=99008504;
    OnRun=VAR
            ProductCatalogLineAcc@1000 : Codeunit 99008530;
            BizTalkPartnerSetup@1001 : Record 99008513;
            InboundProductCatalogHdr@1002 : Record 99008504;
            InboundItemLine@1003 : Record 99008505;
            ItemsAccepted@1004 : Integer;
            ItemsToAccept@1005 : Integer;
          BEGIN
            ItemsAccepted := 0;
            InboundProductCatalogHdr := Rec;
            BizTalkPartnerSetup.SETCURRENTKEY("Partner Type","Partner No.");
            BizTalkPartnerSetup.SETRANGE("Partner Type", BizTalkPartnerSetup."Partner Type"::Vendor);
            BizTalkPartnerSetup.SETRANGE("Partner No.", InboundProductCatalogHdr."Vendor No.");
            IF NOT BizTalkPartnerSetup.FIND('-') THEN
              ERROR(Text000,InboundProductCatalogHdr."Vendor No.");
            InboundItemLine.SETCURRENTKEY("Inbound Document No.");
            InboundItemLine.SETRANGE("Inbound Document No.",InboundProductCatalogHdr."Inbound Document No.");
            InboundItemLine.SETRANGE("Item Imported",FALSE);
            ItemsToAccept := 0;
            IF InboundItemLine.FIND('-') THEN BEGIN
              ItemsToAccept := InboundItemLine.COUNT;
              REPEAT
                ProductCatalogLineAcc.SetData(BizTalkPartnerSetup,InboundProductCatalogHdr,InboundItemLine);
                IF ProductCatalogLineAcc.RUN THEN
                  ItemsAccepted := ItemsAccepted + 1;
              UNTIL InboundItemLine.NEXT = 0;
            END;
            IF NOT HideDialogBox THEN BEGIN
              IF (ItemsAccepted = ItemsToAccept) THEN
                MESSAGE(Text001)
              ELSE IF ItemsAccepted > 0 THEN
                MESSAGE(Text002)
              ELSE BEGIN
                MESSAGE(Text003);
                EXIT;
              END;
            END ELSE
              IF  NOT (ItemsAccepted = ItemsToAccept) THEN
                ERROR(Text007);
            IF GetNoOfUnaccepted(InboundProductCatalogHdr."Inbound Document No.") = 0 THEN BEGIN
              InboundProductCatalogHdr.Status := Status::Accepted;
              InboundProductCatalogHdr."Date Processed" := TODAY;
              InboundProductCatalogHdr."Time Processed" := TIME;
              InboundProductCatalogHdr.MODIFY;
            END;
            Rec := InboundProductCatalogHdr;
          END;

  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=BizTalk partner number %1 does not exist.;FRA=Le partenaire BizTalk nø %1 n''existe pas.';
      Text001@1001 : TextConst 'ENU=All lines have been accepted.;FRA=Toutes les lignes ont ‚t‚ accept‚es.';
      Text002@1002 : TextConst 'ENU=Some lines have not been accepted.;FRA=Certaines lignes n''ont pas ‚t‚ accept‚es.';
      Text003@1003 : TextConst 'ENU=Could not accept any lines.;FRA=Aucune ligne n''a ‚t‚ accept‚e.';
      Text004@1004 : TextConst 'ENU=You have already accepted this line.;FRA=Vous avez d‚j… accept‚ cette ligne.';
      Text005@1005 : TextConst 'ENU=The entire document has now been accepted.;FRA=L''ensemble du document a maintenant ‚t‚ accept‚.';
      Text006@1006 : TextConst 'ENU=The line has been accepted.;FRA=La ligne a ‚t‚ accept‚e.';
      HideDialogBox@1007 : Boolean;
      Text007@1008 : TextConst 'ENU=Could not accept the whole document.;FRA=Impossible d''accepter le document entier.';

    PROCEDURE AcceptProductCatalogLine@1(VAR InboundProductCatalogLine@1000 : Record 99008505);
    VAR
      InboundProductCatalogHdr@1001 : Record 99008504;
      BizTalkPartnerSetup@1002 : Record 99008513;
      ProductCatalogLineAcc@1003 : Codeunit 99008530;
    BEGIN
      IF InboundProductCatalogLine."Item Imported" THEN
        ERROR(Text004);

      InboundProductCatalogHdr.SETCURRENTKEY("Inbound Document No.");
      InboundProductCatalogHdr.SETRANGE("Inbound Document No.",InboundProductCatalogLine."Inbound Document No.");
      InboundProductCatalogHdr.FIND('-');

      BizTalkPartnerSetup.SETCURRENTKEY("Partner Type","Partner No.");
      BizTalkPartnerSetup.SETRANGE("Partner Type", BizTalkPartnerSetup."Partner Type"::Vendor);
      BizTalkPartnerSetup.SETRANGE("Partner No.", InboundProductCatalogHdr."Vendor No.");
      IF NOT BizTalkPartnerSetup.FIND('-') THEN
        ERROR(Text000,InboundProductCatalogHdr."Vendor No.");

      ProductCatalogLineAcc.SetData(BizTalkPartnerSetup,InboundProductCatalogHdr,InboundProductCatalogLine);
      ProductCatalogLineAcc.RUN;

      IF GetNoOfUnaccepted(InboundProductCatalogHdr."Inbound Document No.") = 0 THEN BEGIN
        InboundProductCatalogHdr.Status := InboundProductCatalogHdr.Status::Accepted;
        InboundProductCatalogHdr."Date Processed" := TODAY;
        InboundProductCatalogHdr."Time Processed" := TIME;
        InboundProductCatalogHdr.MODIFY;
        IF NOT HideDialogBox THEN
          MESSAGE(Text005);
      END
      ELSE
        IF NOT HideDialogBox THEN
          MESSAGE(Text006);
    END;

    LOCAL PROCEDURE GetNoOfUnaccepted@4(InboundDocumentNo@1000 : Integer) : Integer;
    VAR
      InboundProductCatalogLine@1001 : Record 99008505;
    BEGIN
      InboundProductCatalogLine.SETCURRENTKEY("Inbound Document No.");
      InboundProductCatalogLine.SETRANGE("Inbound Document No.",InboundDocumentNo);
      InboundProductCatalogLine.SETRANGE("Item Imported",FALSE);
      IF InboundProductCatalogLine.FIND('-') THEN
        EXIT(InboundProductCatalogLine.COUNT)
      ELSE
        EXIT(0);
    END;

    PROCEDURE SetHideDialogBox@3(NewHideDialogBox@1000 : Boolean);
    BEGIN
      HideDialogBox := NewHideDialogBox;
    END;

    BEGIN
    END.
  }
}