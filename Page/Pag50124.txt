OBJECT Page 50124 Create Tray Line Wizard
{
  OBJECT-PROPERTIES
  {
    Date=09/04/14;
    Time=16:29:55;
    Modified=Yes;
    Version List=IMH6.01,5005;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Create Tray Line Wizard;
               FRA=Cr‚er activit‚ corbeille];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table50048;
    DataCaptionExpr=Caption;
    PageType=NavigatePage;
    SourceTableTemporary=Yes;
    OnInit=BEGIN
             ContactSearchEnable := TRUE;
             "Intervention No.Editable" := TRUE;
             "Service Header No.Editable" := TRUE;
             "Service No.Editable" := TRUE;
             CampaignDescCtrlEditable := TRUE;
             "Corporate No.Editable" := TRUE;
             "Contact NameEditable" := TRUE;
             TeamCodeCtrlEditable := TRUE;
             DescriptionEditable := TRUE; //IMH04.ST
             TaskCodeEditable := TRUE; //IMH04.ST
             AssignedSalespersonEditable := TRUE;
           END;

    OnOpenPage=BEGIN
                 TeamCode := "Team Code";
                 IF INSERT THEN;
                 //SETRANGE("Team Code");
               END;

    OnAfterGetRecord=BEGIN
                       EditableFields;
                     END;

    ActionList=ACTIONS
    {
      { 1900000004;0 ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 39      ;1   ;Action    ;
                      Name=ContactSearch;
                      CaptionML=[ENU=Contact Search;
                                 FRA=Recherche contact];
                      ToolTipML=[ENU=Contact Search;
                                 FRA=Recherche contact];
                      Enabled=ContactSearchEnable;
                      OnAction=VAR
                                 SearchResult@1001 : Record 5113;
                                 Contact@1003 : Record 5050;
                                 ContactSearchForm@1000 : Form 5177;
                               BEGIN
                                 IF GETFILTER("Contact No.") <> '' THEN
                                   EXIT;

                                 ContactSearchForm.LOOKUPMODE(TRUE);
                                 IF ContactSearchForm.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                   ContactSearchForm.GETRECORD(SearchResult);
                                   IF Contact.GET(SearchResult."Contact No.") THEN BEGIN
                                     "Contact No." := SearchResult."Contact No.";
                                     VALIDATE("Contact No.",SearchResult."Contact No.");
                                     MODIFY;
                                     CALCFIELDS("Contact Name");
                                   END;
                                 END;
                               END;
                                }
      { 10      ;1   ;Action    ;
                      Name=Finish;
                      CaptionML=[ENU=&Finish;
                                 FRA=&Terminer];
                      InFooterBar=Yes;
                      Image=Post;
                      OnAction=BEGIN
                                 Finish;
                               END;
                                }
      { 1100281001;1 ;Action    ;
                      Name=Cancel;
                      CaptionML=[ENU=Cancel;
                                 FRA=Annuler];
                      Visible=false;
                      InFooterBar=Yes;
                      Image=Cancel;
                      OnAction=BEGIN
                                 CurrPage.CLOSE;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1900000001;0;Container;
                ContainerType=ContentArea }

    { 1100281003;1;Group  ;
                CaptionML=[ENU=Tray Line;
                           FRA=Activit‚ corbeille];
                GroupType=Group }

    { 1100281010;2;Field  ;
                CaptionML=[ENU=Select Task Code;
                           FRA=S‚lectionnez le code tƒche];
                SourceExpr="Task Code";
                Editable=TaskCodeEditable;
                OnValidate=BEGIN
                             EditableFields;
                             TeamCode := "Team Code"; //IMH07
                           END;
                            }

    { 30  ;2   ;Field     ;
                Name=TeamCodeCtrl;
                CaptionML=[ENU=Select Team Code;
                           FRA=S‚lectionnez la corbeille de destination];
                SourceExpr=TeamCode;
                TableRelation=Team.Code;
                Editable=TeamCodeCtrlEditable;
                OnValidate=VAR
                             lTrayLine@1100281000 : Record 50048;
                             lTeam@1100281002 : Record 5083;
                           BEGIN
                             lTrayLine := Rec;
                             lTrayLine."Team Code" := TeamCode;
                             //<<IMH05
                             IF lTeam.GET(TeamCode) THEN
                               lTrayLine."Team Group Code" := lTeam."Team Group Code"
                             ELSE
                               lTrayLine."Team Group Code" := '';
                             //>>IMH05
                             Rec.DELETE;
                             Rec := lTrayLine;
                             Rec.INSERT;
                             CurrPage.UPDATE(FALSE);
                           END;
                            }

    { 1100281005;2;Field  ;
                CaptionML=[ENU=Select Salesperson;
                           FRA=S‚lectionnez un destinataire];
                SourceExpr="Assigned Salesperson Code";
                Editable=AssignedSalespersonEditable }

    { 23  ;2   ;Field     ;
                CaptionML=[ENU=Task description;
                           FRA=Description de la tƒche];
                SourceExpr=Description;
                Editable=DescriptionEditable }

    { 1100281007;2;Field  ;
                SourceExpr="Short Comment";
                MultiLine=Yes }

    { 12  ;2   ;Field     ;
                SourceExpr="Expected End Date" }

    { 49  ;2   ;Field     ;
                SourceExpr="Expected End Time" }

    { 1100281016;2;Field  ;
                SourceExpr=Urgent }

    { 62  ;2   ;Field     ;
                Lookup=No;
                SourceExpr="Contact Name";
                TableRelation=Contact;
                Editable="Contact NameEditable";
                OnAssistEdit=VAR
                               Cont@1102601001 : Record 5050;
                             BEGIN
                               IF GETFILTER("Contact No.") = '' THEN BEGIN
                                 IF Cont.GET("Contact No.") THEN ;
                                 IF FORM.RUNMODAL(0,Cont) = ACTION::LookupOK THEN BEGIN
                                   VALIDATE("Contact No.",Cont."No.");
                                   CALCFIELDS("Contact Name");
                                 END;
                               END;
                             END;
                              }

    { 1100281000;2;Field  ;
                SourceExpr="Corporate No.";
                Editable="Corporate No.Editable" }

    { 1100281002;2;Field  ;
                SourceExpr="Service No.";
                Editable="Service No.Editable";
                OnAssistEdit=BEGIN
                               ShowServCard;
                             END;
                              }

    { 1100281004;2;Field  ;
                SourceExpr="Service Header No.";
                Editable="Service Header No.Editable";
                OnAssistEdit=BEGIN
                               ShowServHeaderCard;
                             END;
                              }

    { 1100281006;2;Field  ;
                SourceExpr="Intervention No.";
                Editable="Intervention No.Editable";
                OnAssistEdit=BEGIN
                               ShowIntervCard;
                             END;
                              }

  }
  CODE
  {
    VAR
      Text000@1025 : TextConst 'ENU=(Multiple);FRA=(Multiple)';
      Text001@1001 : TextConst 'ENU=untitled;FRA=sans-titre';
      TeamCode@1100281002 : Code[10];
      CampaignDesc@1100281001 : Text[80];
      IsOk@1100281000 : Boolean;
      Text002@1100281003 : TextConst 'ENU=Team code must not be void;FRA=Veuillez renseigner le code ‚quipe.';
      TeamCodeCtrlEditable@19078444 : Boolean INDATASET;
      "Contact NameEditable"@19044726 : Boolean INDATASET;
      "Corporate No.Editable"@19006468 : Boolean INDATASET;
      CampaignDescCtrlEditable@19079965 : Boolean INDATASET;
      "Service No.Editable"@19038739 : Boolean INDATASET;
      "Service Header No.Editable"@19038734 : Boolean INDATASET;
      "Intervention No.Editable"@19016971 : Boolean INDATASET;
      ContactSearchEnable@19021728 : Boolean INDATASET;
      Text003@1100281004 : TextConst 'ENU=Description code must not be void;FRA=Veuillez renseigner le champ d‚signation.';
      DescriptionEditable@1100281005 : Boolean INDATASET;
      TaskCodeEditable@1100281006 : Boolean INDATASET;
      AssignedSalespersonEditable@1100281007 : Boolean INDATASET;
      Text004@1000000000 : TextConst 'ENU=Description code must not be void;FRA=Veuillez renseigner le champ code tƒche.';

    PROCEDURE Caption@1() : Text[260];
    VAR
      lCont@1100281002 : Record 5050;
      lCampaign@1100281001 : Record 5071;
      lTeam@1100281000 : Record 5083;
      CaptionStr@1000 : Text[260];
    BEGIN
      IF lCont.GET(GETFILTER("Contact No.")) THEN
        CaptionStr := COPYSTR(lCont."No." + ' ' + lCont.Name,1,MAXSTRLEN(CaptionStr));
      //IF SalesPurchPerson.GET(GETFILTER("Salesperson Code")) THEN
      //  CaptionStr := COPYSTR(CaptionStr + ' ' + SalesPurchPerson.Code + ' ' + SalesPurchPerson.Name,1,MAXSTRLEN(CaptionStr));
      IF lTeam.GET(GETFILTER("Team Code")) THEN
        CaptionStr := COPYSTR(CaptionStr + ' ' + lTeam.Code + ' ' + lTeam.Name,1,MAXSTRLEN(CaptionStr));
      IF lCampaign.GET(GETFILTER("Campaign No.")) THEN
        CaptionStr := COPYSTR(CaptionStr + ' ' + lCampaign."No." + ' ' + lCampaign.Description,1,MAXSTRLEN(CaptionStr));
      IF CaptionStr = '' THEN
        CaptionStr := Text001;

      EXIT(CaptionStr);
    END;

    LOCAL PROCEDURE EditableFields@21();
    VAR
      lCont@1100281004 : Record 5050;
      lCampaign@1100281006 : Record 5071;
      lCustmor@1100281005 : Record 18;
      lTeam@1100281003 : Record 5083;
      lService@1100281002 : Record 50066;
      lServiceHeader@1100281001 : Record 5900;
      lServIntervLine@1100281000 : Record 50075;
    BEGIN
      //IF GETFILTER("Salesperson Code") THEN BEGIN
      //  CurrForm."Salesperson Code".EDITABLE(FALSE);
      //END;

      IF lTeam.GET(GETFILTER("Team Code")) THEN BEGIN
        TeamCodeCtrlEditable := FALSE;
      END;

      IF lCont.GET(GETFILTER("Contact No.")) THEN BEGIN
        "Contact NameEditable" := FALSE;
        ContactSearchEnable := FALSE;
      END;

      IF lCustmor.GET(GETFILTER("Corporate No.")) THEN
        "Corporate No.Editable" := FALSE;

      IF lCampaign.GET(GETFILTER("Campaign No.")) THEN
        CampaignDescCtrlEditable := FALSE;

      IF GETFILTER("Service No.") <> '' THEN
        IF lService.GET(GETRANGEMIN("Service No.")) THEN
          "Service No.Editable" := FALSE;
      IF lServiceHeader.GET(lServiceHeader."Document Type"::Invoice, GETFILTER("Service Header No.")) THEN BEGIN
        "Service No.Editable" := FALSE;
        "Service Header No.Editable" := FALSE;
      END;
      IF GETFILTER("Intervention Line No.") <> '' THEN
        IF lServIntervLine.GET(GETFILTER("Service Header No."),
                               GETRANGEMIN("Intervention Line No.")) THEN BEGIN
          "Service No.Editable" := FALSE;
          "Service Header No.Editable" := FALSE;
          "Intervention No.Editable" := FALSE;
        END;

      DescriptionEditable := (GETFILTER(Description) = '') AND ("Task Code" = '');   //IMH04.ST
      IF GETFILTER("Task Code") <> '' THEN
        TaskCodeEditable := FALSE;

      IF GETFILTER("Assigned Salesperson Code") <> '' THEN
        AssignedSalespersonEditable := FALSE;

      TeamCodeEditable;  //IMH08
    END;

    PROCEDURE TeamCodeEditable@1000000003();
    VAR
      lTrayLineFilter@1000000000 : Record 50048;
      lTrayTask@1000000001 : Record 50080;
    BEGIN
      //<<IMH08
      IF EVALUATE(lTrayTask."Source Table", GETFILTER("Linked To Table")) THEN;
      IF "Task Code" <> '' THEN
        IF lTrayTask.GET(lTrayTask."Source Table", "Task Code") THEN BEGIN
          IF lTrayTask."Team Manual Affectation" THEN BEGIN
            TeamCodeCtrlEditable := TRUE;
            EXIT;
          END;
        END;

      lTrayLineFilter.SETRANGE("Linked To Table", lTrayLineFilter."Linked To Table"::Service);
      IF GETFILTER("Linked To Table") = lTrayLineFilter.GETFILTER("Linked To Table") THEN
        TeamCodeCtrlEditable := FALSE;

      IF TeamCodeCtrlEditable THEN BEGIN
        lTrayLineFilter.SETRANGE("Linked To Table", lTrayLineFilter."Linked To Table"::ServiceHeader);
        IF GETFILTER("Linked To Table") = lTrayLineFilter.GETFILTER("Linked To Table") THEN
          TeamCodeCtrlEditable := FALSE;
      END;

      IF TeamCodeCtrlEditable THEN BEGIN
        lTrayLineFilter.SETRANGE("Linked To Table", lTrayLineFilter."Linked To Table"::Intervention);
        IF GETFILTER("Linked To Table") = lTrayLineFilter.GETFILTER("Linked To Table") THEN
          TeamCodeCtrlEditable := FALSE;
      END;

      IF TeamCodeCtrlEditable THEN BEGIN
        lTrayLineFilter.SETRANGE("Linked To Table", lTrayLineFilter."Linked To Table"::InterventionQuote);
        IF GETFILTER("Linked To Table") = lTrayLineFilter.GETFILTER("Linked To Table") THEN
          TeamCodeCtrlEditable := FALSE;
      END;
      //>>IMH08
    END;

    PROCEDURE GetIsOk@1100281002() : Boolean;
    BEGIN
      EXIT(IsOk);
    END;

    PROCEDURE CheckValue@1100281005();
    BEGIN
      IF TeamCode = '' THEN
        ERROR(Text002);
      IF Description = '' THEN
        ERROR(Text003);
      //<<IMH06 IMH.WGA
      IF  "Task Code" = '' THEN
        ERROR(Text004);
      //>>IMH06
    END;

    PROCEDURE GetValue@1100281011(VAR pTrayLine@1100281000 : Record 50048);
    BEGIN
      pTrayLine := Rec;
      pTrayLine."Team Code" := TeamCode;
    END;

    PROCEDURE Finish@1100281006();
    BEGIN
      CheckValue;
      IsOk := TRUE;
      CurrPage.CLOSE;
    END;

    PROCEDURE ShowIntervCard@1000000000();
    VAR
      lIntervLine@1000000000 : Record 50075;
    BEGIN
      IF lIntervLine.GET("Service Header No.", "Intervention Line No.") THEN
        PAGE.RUN(PAGE::"Intervention Line Card", lIntervLine);
    END;

    PROCEDURE ShowServHeaderCard@1000000001();
    VAR
      lServHeader@1000000000 : Record 5900;
    BEGIN
      IF lServHeader.GET(lServHeader."Document Type"::Invoice, "Service Header No.") THEN
        lServHeader.ShowCard(FALSE);
    END;

    PROCEDURE ShowServCard@1000000002();
    VAR
      lService@1000000000 : Record 50066;
    BEGIN
      IF lService.GET("Service No.") THEN
        PAGE.RUN(PAGE::"Service Card", lService);
    END;

    BEGIN
    {
      /** @r IMH08 @d 26/03/14 @a ISAT.ZW @v IMH6.01.5005 @s Reorganisation DPS @h 5005
          @c Gestion Editable TeamCode : ‚ditable si table li‚ <> service: cr‚ation depuis prestataire */
      /** @r IMH07 @d 25/02/14 @a ISAT.ZW @v IMH6.01.4985 @s Reorganisation DPS @h 4985 @c Ajout champ FIltre Code tache */
      /** @r IMH06 @d 12/12/13 @a IMH.WGA @v IMH6.01.3762 @s Controle Cr‚ation Activit‚ @h mantis 3762 @c Modification Fonction CheckVa */
      /** @r IMH05 @d 25/04/12 @a ISAT.ZW @v IMH6.01.120 @s Gestion des corbeilles @h mantis 3218 @c Correction bug */
      /** @r IMH04 @d 11/02/11 @a ISAT.ST @v IMH6.01.120 @s Gestion des corbeilles @c Am‚liorations */
      /** @r IMH03 @d 27/01/11 @a ISAT.LLT @v IMH6.01.013 @s Gestion des corbeilles @c Design */
      /** @r IMH02 @d 13/12/2010 @a ISAT.LLT @v IMH6.01.013 @s Gestion des corbeilles @c Corrections */
      /** @r IMH01 @d 27/10/2010 @a ISAT.ZW @v IMH6.01.013 @s Gestion des corbeilles @c Cr‚ation objet*/
    }
    END.
  }
}