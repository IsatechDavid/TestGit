OBJECT Page 50307 Send Mail
{
  OBJECT-PROPERTIES
  {
    Date=18/09/12;
    Time=13:44:17;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=[ENU=Send Mail;
               FRA=Envoi mail];
    DataCaptionExpr=PageDescription;
    OnOpenPage=BEGIN
                 IF NOT DataLoaded THEN
                   CurrPage.CLOSE;
               END;

    OnQueryClosePage=BEGIN
                       IF NOT SkipClosureTest THEN
                         EXIT(CONFIRM(Text001, TRUE))
                       ELSE
                         EXIT(TRUE);
                     END;

    ActionList=ACTIONS
    {
      { 1000000001;  ;ActionContainer;
                      ActionContainerType=ActionItems }
      { 1000000003;1 ;Action    ;
                      ShortCutKey=Ctrl+E;
                      CaptionML=[ENU=Send;
                                 FRA=Envoyer];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=SendTo;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 SendMail;
                               END;
                                }
      { 1100281002;1 ;Action    ;
                      CaptionML=[ENU=Add User;
                                 FRA=Ajouter utilisateur];
                      Promoted=Yes;
                      Image=AddContacts;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 lEmployee@1100281001 : Record 5200;
                                 lContact@1100281000 : Record 5050;
                               BEGIN
                                 //IMH03
                                 IF NOT (PAGE.RUNMODAL(PAGE::"Employee Lookup List", lEmployee) = ACTION::LookupOK) THEN
                                   EXIT;

                                 lEmployee.TESTFIELD("Contact No.");
                                 IF lContact.GET(lEmployee."Contact No.") THEN BEGIN
                                   lContact.TESTFIELD("E-Mail");
                                   CurrPage.MailRecip.FORM.AddContact('R', lContact)
                                 END;
                               END;
                                }
      { 1000000006;1 ;Action    ;
                      CaptionML=[ENU=Add attachment;
                                 FRA=Joindre un fichier];
                      Promoted=Yes;
                      Image=MailAttachment;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 CurrPage.MailAtt.FORM.AddDocument;
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1000000000;;Container;
                ContainerType=ContentArea }

    { 1000000002;1;Field  ;
                CaptionML=[ENU=From;
                           FRA=De];
                SourceExpr=FromMail;
                Editable=FALSE }

    { 1100281000;1;Part   ;
                Name=MailRecip;
                CaptionML=[ENU=Recipients;
                           FRA=Destinataires];
                PagePartID=Page50308;
                PartType=Page }

    { 1000000004;1;Field  ;
                CaptionML=[ENU=Subject;
                           FRA=Objet];
                SourceExpr=Subject }

    { 1000000005;1;Field  ;
                SourceExpr=BodyText;
                ControlAddIn=[Isatech.DynamicsNav.NotepadAddIn;PublicKeyToken=f9c11cee8793cd65] }

    { 1100281001;1;Part   ;
                Name=MailAtt;
                CaptionML=[ENU=Attachments;
                           FRA=PiŠces jointes];
                PagePartID=Page50309;
                PartType=Page }

  }
  CODE
  {
    VAR
      PageDescription@1100281000 : Text[80];
      FromMail@1000000000 : Text[80];
      Subject@1000000001 : Text[1024];
      BodyText@1000000002 : BigText;
      DataLoaded@1100281002 : Boolean;
      SegLine@1000000003 : TEMPORARY Record 5077;
      SkipClosureTest@1000000004 : Boolean;
      Text001@1000000005 : TextConst 'ENU=Message has not been sent. Do you really want to quit ?;FRA=Le message n''est pas envoy‚. Etes-vous s–r de vouloir quitter ?';
      Text002@1100281001 : TextConst 'ENU=You must set subject.;FRA=Vous devez pr‚ciser l''objet.';
      Text003@1000000006 : TextConst 'ENU=Please select a least one recipient.;FRA=Vous devez s‚lectionner au moins un destinataire.';

    PROCEDURE LoadData@1100281000(pPageDesc@1100281000 : Text[80];pSendFrom@1000000002 : Text[80];pSubject@1000000001 : Text[80];pBody@1000000003 : Text[1024];VAR pSegLine@1000000000 : Record 5077;VAR pContactList@1100281001 : Record 5050;VAR pAttachmentList@1100281002 : Record 50021);
    BEGIN
      PageDescription := pPageDesc;
      SegLine := pSegLine;
      Subject := pSubject;
      FromMail := pSendFrom;
      IF pBody <> '' THEN
        BodyText.ADDTEXT(pBody);
      CurrPage.MailAtt.FORM.InitList(pAttachmentList);
      CurrPage.MailRecip.FORM.InitList(pContactList);
      DataLoaded := TRUE;
    END;

    LOCAL PROCEDURE SendMail@1000000000();
    VAR
      lSendMail@1000000000 : Codeunit 50128;
      lTempToList@1000000001 : TEMPORARY Record 5050;
      lTempCCList@1000000002 : TEMPORARY Record 5050;
      lTempCCiList@1000000003 : TEMPORARY Record 5050;
      lTempDocAttach@1000000004 : TEMPORARY Record 50021;
    BEGIN
      // R‚cup‚ration des r‚sultats
      IF Subject = '' THEN
        ERROR(Text002);
      CurrPage.MailRecip.FORM.RetrieveMailList(0, lTempToList);
      //<<IMH02.ST
      IF lTempToList.ISEMPTY THEN
        ERROR(Text003);
      //>>IMH02.ST
      CurrPage.MailRecip.FORM.RetrieveMailList(1, lTempCCList);
      CurrPage.MailRecip.FORM.RetrieveMailList(2, lTempCCiList);
      CurrPage.MailAtt.FORM.RetrieveDocumentList(lTempDocAttach);
      lTempToList.RESET;
      lSendMail.SendMail(SegLine, FromMail,Subject,BodyText,
                         lTempToList,
                         lTempCCList,
                         lTempCCiList,
                         lTempDocAttach);
      SkipClosureTest := TRUE;
      CurrPage.CLOSE;
    END;

    BEGIN
    {
      /** @r IMH03 @d 30/08/12 @a ISAT.ZW @v IMH6.01.226 @s version 1.4 @h mantis 3445 @c Ajout fonction Ajouter utilisateur */
      /** @r IMH02 @d 04/04/12 @a ISAT.ST @v IMH6.01.108 @s Interactions @c Destinataire obligatoire @h 3270 */
      /** @r IMH01 @d 16/02/12 @a ISAT.ST @v IMH6.01.108 @s Interactions @c Cr‚ation objet */
    }
    END.
  }
}