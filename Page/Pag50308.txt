OBJECT Page 50308 Mail List
{
  OBJECT-PROPERTIES
  {
    Date=18/09/12;
    Time=13:43:53;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    Editable=Yes;
    CaptionML=[ENU=Mail List;
               FRA=Liste d'adresse];
    InsertAllowed=No;
    DeleteAllowed=No;
    SourceTable=Table5050;
    PageType=ListPart;
    SourceTableTemporary=Yes;
    OnModifyRecord=BEGIN
                     MODIFY(FALSE);
                     EXIT(FALSE);
                   END;

  }
  CONTROLS
  {
    { 1100281000;;Container;
                ContainerType=ContentArea }

    { 1100281001;1;Group  ;
                GroupType=Repeater }

    { 1100281002;2;Field  ;
                SourceExpr=Name;
                Editable=FALSE }

    { 1100281003;2;Field  ;
                ExtendedDatatype=None;
                SourceExpr="E-Mail";
                Editable=FALSE;
                OnAssistEdit=VAR
                               lSendMail@1100281000 : Codeunit 50128;
                             BEGIN
                               lSendMail.SendMailFromContNo("No.", "E-Mail");
                             END;
                              }

    { 1100281004;2;Field  ;
                CaptionML=[ENU=To;
                           FRA=A];
                SourceExpr="Preferred Contact DPR" }

    { 1100281005;2;Field  ;
                CaptionML=[ENU=CC;
                           FRA=CC];
                SourceExpr="Preferred Contact DGS" }

    { 1100281006;2;Field  ;
                CaptionML=[ENU=CCi;
                           FRA=CCi];
                SourceExpr="Preferred Contact DFC";
                Visible=FALSE;
                Enabled=FALSE;
                Editable=FALSE }

    { 1000000001;2;Field  ;
                CaptionML=[ENU=Relationship;
                           FRA=Relation];
                SourceExpr="Legal Form";
                Editable=FALSE }

    { 1000000000;2;Field  ;
                SourceExpr="Organization Level Descr.";
                Visible=FALSE;
                Editable=FALSE }

  }
  CODE
  {

    PROCEDURE RetrieveMailList@1100281000(pWhich@1100281000 : 'MailTo,MailCC,MailCCi';VAR pContactList@1000000000 : Record 5050) rValue : Text[1024];
    BEGIN
      rValue := '';
      RESET;
      CASE pWhich OF
        pWhich::MailTo : SETRANGE("Preferred Contact DPR", TRUE);
        pWhich::MailCC : SETRANGE("Preferred Contact DGS", TRUE);
        pWhich::MailCCi : SETRANGE("Preferred Contact DFC", TRUE);
      END;
      IF FINDSET THEN
        REPEAT
          pContactList := Rec;
          pContactList.INSERT;
        UNTIL NEXT = 0;
      RESET;
    END;

    PROCEDURE InitList@1100281001(VAR pContact@1100281000 : Record 5050) : Boolean;
    BEGIN
      RESET;
      DELETEALL;
      IF NOT pContact.FINDSET THEN
        EXIT(FALSE);

      REPEAT
        IF pContact."E-Mail" <> '' THEN BEGIN
          Rec := pContact;
          INSERT;
        END;
      UNTIL pContact.NEXT=0;
      EXIT(TRUE);
    END;

    PROCEDURE AddContact@1100281002(pType@1100281001 : Code[10];pContact@1100281000 : Record 5050);
    VAR
      lNo@1100281002 : Code[20];
    BEGIN
      //IMH03
      IF pContact."E-Mail" <> '' THEN BEGIN
        RESET;
        SETRANGE("E-Mail", pContact."E-Mail");
        IF NOT ISEMPTY THEN
          EXIT;
        RESET;
        SETFILTER("No.", STRSUBSTNO('%1*', pType));
        IF FINDLAST THEN
          lNo := INCSTR("No.")
        ELSE
          lNo := pType + '000000001';

        Rec := pContact;
        "No." := lNo;
        "Lookup Contact No." := pContact."No.";
        "Preferred Contact DPR" := FALSE; // To
        "Preferred Contact DGS" := FALSE; // CC
        "Preferred Contact DFC" := FALSE; // Cci
        INSERT(FALSE);
      END;
    END;

    BEGIN
    {
      /** @r IMH03 @d 30/08/12 @a ISAT.ZW @v IMH6.01.226 @s version 1.4 @h mantis 3445 @c Ajout fonction AddContact */
      /** @r IMH02 @d 17/02/12 @a ISAT.ST @v IMH6.01.108 @s Interactions @c Envoi mail / fax */
      /** @r IMH01 @d 16/02/12 @a ISAT.ST @v IMH6.01.108 @d Interactions @c Cr‚ation objet */
    }
    END.
  }
}