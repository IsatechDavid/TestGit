OBJECT Page 50359 RDD Fusion Invoicing
{
  OBJECT-PROPERTIES
  {
    Date=04/02/13;
    Time=13:59:17;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    Permissions=TableData 111=rimd;
    CaptionML=[ENU=Fusion Invoicing Journal Line;
               FRA=Ligne feuille facturation Fusion];
    InsertAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table50187;
    PageType=List;
    PromotedActionCategoriesML=[ENU=New,Process,Report,Post;
                                FRA=Nouveau,Processus,Report,Validation];
    OnAfterGetRecord=BEGIN
                       StyleUnfavorable := "In Error";
                     END;

    ActionList=ACTIONS
    {
      { 1100281046;  ;ActionContainer;
                      Name=<Action1900000004>;
                      ActionContainerType=ActionItems }
      { 1100281041;1 ;ActionGroup;
                      Name=<Action46>;
                      CaptionML=[ENU=F&unctions;
                                 FRA=Fonction&s] }
      { 1100281040;2 ;Action    ;
                      Name=<Action91>;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Fusion Document Import;
                                 FRA=Importer dossiers Fusion RDD];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Import;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 FUSIONInvoiceMgt.ImportJrnlLineByXMLPort;
                               END;
                                }
      { 1100281048;2 ;Action    ;
                      Name=<Action91>;
                      Ellipsis=Yes;
                      CaptionML=[ENU=Check lines;
                                 FRA=V‚rifier lignes s‚lectionn‚es];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=ChangeStatus;
                      PromotedCategory=Process;
                      OnAction=VAR
                                 lFusionInvoicingJournalLine@1100281001 : Record 50187;
                               BEGIN
                                 CurrPage.SETSELECTIONFILTER(lFusionInvoicingJournalLine);
                                 FUSIONInvoiceMgt.CheckLines(lFusionInvoicingJournalLine);
                               END;
                                }
      { 1100281049;2 ;Action    ;
                      Name=<Action1100281049>;
                      ShortCutKey=F9;
                      CaptionML=[ENU=P&ost;
                                 FRA=&Valider RDD];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Post;
                      PromotedCategory=Category4;
                      OnAction=BEGIN
                                 IF NOT CONFIRM(Text001) THEN
                                   EXIT;

                                 BeginDateTime := CURRENTDATETIME;
                                 PostJournal;
                                 MESSAGE('D‚but : ' + FORMAT(BeginDateTime) + '\' + 'Fin : ' + FORMAT(CURRENTDATETIME));
                               END;
                                }
      { 1100281033;  ;ActionContainer;
                      Name=<Action1100281011>;
                      ActionContainerType=RelatedInformation }
      { 1100281032;1 ;Action    ;
                      Name=ShowFusionDoc;
                      CaptionML=[ENU=Show Fusion Document;
                                 FRA=Afficher dossier Fusion];
                      Promoted=Yes;
                      PromotedIsBig=Yes;
                      Image=Edit;
                      PromotedCategory=Process;
                      OnAction=BEGIN
                                 //<<IMH02.SB
                                 ShowFusionDoc;
                                 //>>IMH02.SB
                               END;
                                }
    }
  }
  CONTROLS
  {
    { 1100281000;0;Container;
                ContainerType=ContentArea }

    { 1100281001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1100281002;2;Field  ;
                SourceExpr="Fusion Document No.";
                Style=Unfavorable;
                StyleExpr=StyleUnfavorable;
                OnLookup=BEGIN
                           ShowFusionDoc;
                         END;
                          }

    { 1100281003;2;Field  ;
                SourceExpr="Sinister No." }

    { 1100281004;2;Field  ;
                SourceExpr="Mission Order No." }

    { 1100281005;2;Field  ;
                SourceExpr="Mission Date" }

    { 1100281006;2;Field  ;
                SourceExpr="Fusion Corporate No." }

    { 1100281007;2;Field  ;
                SourceExpr="Fusion Corporate Region Code" }

    { 1100281008;2;Field  ;
                SourceExpr="Corporate No." }

    { 1100281009;2;Field  ;
                SourceExpr="Corporate Region Code" }

    { 1100281010;2;Field  ;
                SourceExpr="Siniter Nature Fusion" }

    { 1100281011;2;Field  ;
                SourceExpr="Fusion Service Type" }

    { 1100281034;2;Field  ;
                SourceExpr="Fusion Service Type Descrip" }

    { 1100281012;2;Field  ;
                SourceExpr="Fusion Status" }

    { 1100281013;2;Field  ;
                SourceExpr="New Service Order Type";
                Visible=false }

    { 1100281014;2;Field  ;
                SourceExpr="New Service Order Description" }

    { 1100281015;2;Field  ;
                SourceExpr="Old Service Order Type";
                Visible=false }

    { 1100281016;2;Field  ;
                SourceExpr="Old Service Order Description" }

    { 1100281017;2;Field  ;
                SourceExpr="Fusion Last Doc. Change Date" }

    { 1100281018;2;Field  ;
                SourceExpr="Contract Type" }

    { 1100281019;2;Field  ;
                SourceExpr="Fusion Beneficiary No." }

    { 1100281020;2;Field  ;
                SourceExpr="Fusion Beneficairy Name" }

    { 1100281021;2;Field  ;
                SourceExpr="Fusion Beneficairy Post Code" }

    { 1100281022;2;Field  ;
                SourceExpr="Fusion Beneficairy Type" }

    { 1100281023;2;Field  ;
                SourceExpr="Fusion Document GUID" }

    { 1100281024;2;Field  ;
                SourceExpr="Import Date" }

    { 1100281025;2;Field  ;
                SourceExpr="Import Line No." }

    { 1100281026;2;Field  ;
                SourceExpr="In Error";
                Style=Unfavorable;
                StyleExpr=StyleUnfavorable }

    { 1100281027;2;Field  ;
                SourceExpr="Error Message";
                Style=Unfavorable;
                StyleExpr=StyleUnfavorable }

    { 1100281028;2;Field  ;
                SourceExpr="Creation Date";
                Visible=false }

    { 1100281029;2;Field  ;
                SourceExpr="Creation User ID";
                Visible=false }

    { 1100281030;2;Field  ;
                SourceExpr="Last Modification Date";
                Visible=false }

    { 1100281031;2;Field  ;
                SourceExpr="Last Modification User ID";
                Visible=false }

  }
  CODE
  {
    VAR
      FUSIONInvoiceMgt@1100281004 : Codeunit 50180;
      StyleUnfavorable@1100281005 : Boolean INDATASET;
      Text001@1100281006 : TextConst 'ENU=Do you want to post the Fusion invoicing journal ?;FRA=Souhaitez-vous valider la feuille facturation Fusion RDD ?';
      SalesInvoicingManagment@1100281007 : Codeunit 50079;
      WDialog@1100281008 : Dialog;
      TotalCounter@1100281003 : Integer;
      ModuloCounter@1100281002 : Integer;
      CurrentCounter@1100281001 : Integer;
      BeginDateTime@1100281000 : DateTime;
      Text002@1100281009 : TextConst 'ENU=Processing @1@@@@@@@@@@@@@@@@@@@@;FRA=Traitement en cours @1@@@@@@@@@@@@@@@@@@@@';

    PROCEDURE CreateSalesOrderFromFusion@1100281030(VAR pFusionInvoicingJournalLine@1100281000 : Record 50187) rValue : Code[20];
    VAR
      lFusionInvoicingJournalLine@1100281004 : Record 50187;
      lServiceHeaderTmp@1100281011 : TEMPORARY Record 5900;
      lSalesHeader@1100281001 : Record 36;
      lSalesLine@1100281002 : Record 37;
      lInvoicingMatrixSetup@1100281003 : Record 50137;
      lServiceOrderType@1100281005 : Record 5903;
      lReleaseSalesDocument@1100281008 : Codeunit 414;
      lSalesPost@1100281009 : Codeunit 80;
      lLastLineNo@1100281006 : Integer;
      lItemNo@1100281007 : Code[20];
      lBillCust@1100281010 : Record 18;
      lSalesShipmentLine@1100281012 : Record 111;
    BEGIN
      //IMH12
      // Cr‚ation d'une commande de vente … partir d'une ligne document facturation Fusion le type de produit

      rValue := '';

      IF NOT lFusionInvoicingJournalLine.GET(pFusionInvoicingJournalLine."Fusion Document No.") THEN BEGIN
        EXIT;
      END;

      IF lServiceOrderType.GET(pFusionInvoicingJournalLine."New Service Order Type") THEN BEGIN
        //IF NOT lServiceOrderType."Sal. Order Creat. (With. Int.)" THEN BEGIN
        //  EXIT;
        //END;
      END;

      IF NOT SalesInvoicingManagment.CheckCreationAvailable(lInvoicingMatrixSetup
               ,pFusionInvoicingJournalLine."Corporate No."
               ,pFusionInvoicingJournalLine."New Service Order Type"
               ,pFusionInvoicingJournalLine."Corporate Region Code"
               ,
               //<<IMH12
               //lService."Service No.", pServiceHeader)
               '',pFusionInvoicingJournalLine."Fusion Document No.", lServiceHeaderTmp)
               //>>IMH12
      THEN BEGIN
        EXIT;
      END;

      lInvoicingMatrixSetup.FINDSET;
      REPEAT
        // Cr‚ation de l'en-tˆte
        CLEAR(lSalesHeader);
        WITH lSalesHeader DO BEGIN
          VALIDATE("Document Type","Document Type"::Order);
          INSERT(TRUE);
          rValue := "No.";
          //<<IMH13.PC
          //VALIDATE("Document Date",WORKDATE);
          VALIDATE("Posting Date",pFusionInvoicingJournalLine."Fusion Last Doc. Change Date");
          VALIDATE("Document Date",pFusionInvoicingJournalLine."Fusion Last Doc. Change Date");
          VALIDATE("Order Date",pFusionInvoicingJournalLine."Fusion Last Doc. Change Date");
          //>>IMH13.PC
          VALIDATE("Sell-to Customer No.",lInvoicingMatrixSetup."Sell-To Customer No.");
          //<<IMH04
          SetHideValidationDialog(TRUE);
          VALIDATE("Bill-to Customer No.",lInvoicingMatrixSetup."Bill-to Customer No.");
          IF lInvoicingMatrixSetup."Sale Payment Method" = '' THEN BEGIN
            lBillCust.GET(lInvoicingMatrixSetup."Bill-to Customer No.");
            VALIDATE("Payment Method Code",lBillCust."Payment Method Code");
          END ELSE
            VALIDATE("Payment Method Code",lInvoicingMatrixSetup."Sale Payment Method");

          //>>IMH04
          VALIDATE("Corporate No.",pFusionInvoicingJournalLine."Corporate No.");
          VALIDATE("Corporate Region Code",pFusionInvoicingJournalLine."Corporate Region Code");
          VALIDATE("Fusion Document No.", pFusionInvoicingJournalLine."Fusion Document No.");
          MODIFY(TRUE);
          lLastLineNo := 0;
        END;

        // Cr‚ation des lignes
        CLEAR(lSalesLine);
        WITH lSalesLine DO BEGIN
          lLastLineNo += 10000;
          VALIDATE("Document Type",lSalesHeader."Document Type");
          VALIDATE("Document No.",lSalesHeader."No.");
          VALIDATE("Line No.",lLastLineNo);
          INSERT(TRUE);
          lItemNo := GetSalesItemNo3(
                      pFusionInvoicingJournalLine."New Service Order Type",
                      pFusionInvoicingJournalLine."Corporate No.",
                      pFusionInvoicingJournalLine."Corporate Region Code",FALSE, FALSE);
          VALIDATE(Type,Type::Item);
          VALIDATE("No.",lItemNo);
          VALIDATE(Quantity,1);
          VALIDATE("Corporate No.",pFusionInvoicingJournalLine."Corporate No.");
          VALIDATE("Corporate Region Code",pFusionInvoicingJournalLine."Corporate Region Code");
          //VALIDATE("Service No.",pServiceHeader."Service No.");
          //VALIDATE("Service Header No.",pServiceHeader."No.");
          VALIDATE("Fusion Document No.", pFusionInvoicingJournalLine."Fusion Document No.");
          VALIDATE("Service Order Type",pFusionInvoicingJournalLine."New Service Order Type");
          //SB
          //VALIDATE(lSalesLine."Reference No.",lService."Reference No.");
          VALIDATE(lSalesLine."Reference No.",pFusionInvoicingJournalLine."Sinister No.");
          VALIDATE("Qty. to Invoice",0);
          MODIFY(TRUE);
        END;

        CLEAR(lReleaseSalesDocument);
        CLEAR(lSalesPost);
        lReleaseSalesDocument.RUN(lSalesHeader);
        lSalesHeader.Ship := TRUE;
        lSalesPost.SetHideDialog(TRUE);
        lSalesPost.RUN(lSalesHeader);
        lSalesLine.FIND('=');
        lSalesLine."Qty. to Invoice" := 0;
        lSalesLine.MODIFY(FALSE);

        lSalesShipmentLine.SETCURRENTKEY("Order No.","Order Line No.");
        lSalesShipmentLine.SETRANGE("Order No.", lSalesHeader."No.");
        lSalesShipmentLine.SETRANGE("Line No.", lSalesLine."Line No.");
        lSalesShipmentLine.SETRANGE("Fusion Document No.", pFusionInvoicingJournalLine."Fusion Document No.");
        IF lSalesShipmentLine.FINDFIRST THEN BEGIN
          lSalesShipmentLine.VALIDATE("Qty. Shipped Not Invoiced", 0);
          lSalesShipmentLine.MODIFY(FALSE);
        END;
      UNTIL lInvoicingMatrixSetup.NEXT = 0;
    END;

    LOCAL PROCEDURE GetSalesItemNo3@1100281031(pServiceOrderType@1100281002 : Code[10];pCorporateNo@1100281004 : Code[20];pCorporateRegionCode@1100281005 : Code[20];pAccreditedProvider@1100281003 : Boolean;pUrgence@1100281006 : Boolean) rValue : Code[20];
    VAR
      lSalesItemSetup@1100281001 : Record 50138;
      LitemFind@1100281000 : Boolean;
    BEGIN
      //<<IMH12
      // Permet de retrouver l'article qui doit descendre en fonction du nø de service
      rValue := '';

      //<<IMH02
      LitemFind := FALSE;
      lSalesItemSetup.RESET;
      lSalesItemSetup.SETRANGE("Service Order Type",pServiceOrderType);
      lSalesItemSetup.SETRANGE("Corporate No.",pCorporateNo);
      lSalesItemSetup.SETRANGE("Corporate Region Code",pCorporateRegionCode);
      //lSalesItemSetup.SETRANGE("Accredited Provider",pAccreditedProvider);
      LitemFind := lSalesItemSetup.FINDFIRST;

      IF NOT LitemFind THEN BEGIN
        lSalesItemSetup.SETRANGE("Corporate Region Code");
        LitemFind := lSalesItemSetup.FINDFIRST;
      END;

      IF NOT LitemFind THEN BEGIN
        lSalesItemSetup.SETRANGE("Corporate No.");
        LitemFind := lSalesItemSetup.FINDFIRST;
      END;

      IF NOT LitemFind THEN
        EXIT;
      //>>IMH02

      IF pUrgence THEN BEGIN
        rValue := lSalesItemSetup."Emergency Item No.";
      END ELSE BEGIN
        rValue := lSalesItemSetup."Item No.";
      END;
      //>>IMH12
    END;

    PROCEDURE PostJournal@1100281003();
    VAR
      lFusionInvoicingDocHead@1100281000 : Record 50188;
      lCount@1100281001 : Integer;
    BEGIN
      SETRANGE("In Error", FALSE);
      IF GUIALLOWED THEN BEGIN
        WDialog.OPEN(Text002);
        TotalCounter := COUNT;
        ModuloCounter := ROUND(TotalCounter * 5 / 100,1,'>');
        CurrentCounter := 0;
      END;;

      IF FINDFIRST THEN BEGIN
        REPEAT
          IF GUIALLOWED THEN BEGIN
            CurrentCounter += 1;
            IF CurrentCounter MOD ModuloCounter = 1 THEN BEGIN
              WDialog.UPDATE(1,ROUND(CurrentCounter / TotalCounter * 9999,1,'>'));
            END;
          END;
          IF NOT lFusionInvoicingDocHead.GET("Fusion Document No.") THEN BEGIN
            CreateSalesOrderFromFusion(Rec);
            CreateFusionInvoicingHistory(Rec);

            DELETE;
            IF lCount = 5 THEN BEGIN
              COMMIT;
              lCount := 0;
            END;
            lCount += 1;
          END;
        UNTIL NEXT = 0;
      END;
      SETRANGE("In Error");
      IF GUIALLOWED THEN
        WDialog.CLOSE;
    END;

    PROCEDURE CreateFusionInvoicingHistory@1100281017(pFusionInvoicingJournalLine@1100281000 : Record 50187);
    BEGIN
      //IMH04
      IF pFusionInvoicingJournalLine."New Service Order Type" = pFusionInvoicingJournalLine."Old Service Order Type" THEN
        EXIT;

      CreateFusionInvoicingDocHeader(pFusionInvoicingJournalLine);
      CreateFusionInvStatusEntry(pFusionInvoicingJournalLine);
    END;

    PROCEDURE CreateFusionInvoicingDocHeader@1100281018(pFusionInvoicingJournalLine@1100281000 : Record 50187);
    VAR
      lFusionInvoicingDocHead@1100281001 : Record 50188;
      lFusionInvoicingDocHead2@1100281002 : Record 50188;
    BEGIN
      //IMH04
      lFusionInvoicingDocHead.TRANSFERFIELDS(pFusionInvoicingJournalLine);
      lFusionInvoicingDocHead.VALIDATE("Fusion Document No.", pFusionInvoicingJournalLine."Fusion Document No.");
      lFusionInvoicingDocHead."RDD Document" := TRUE;
      lFusionInvoicingDocHead.INSERT(TRUE)
    END;

    PROCEDURE CreateFusionInvStatusEntry@1100281019(pFusionInvoicingJournalLine@1100281000 : Record 50187);
    VAR
      lFusionInvoicingStatusEntry@1100281001 : Record 50121;
    BEGIN
      //IMH04
      lFusionInvoicingStatusEntry.INIT;
      lFusionInvoicingStatusEntry.TRANSFERFIELDS(pFusionInvoicingJournalLine);
      lFusionInvoicingStatusEntry.VALIDATE("Fusion Document No.", pFusionInvoicingJournalLine."Fusion Document No.");
      lFusionInvoicingStatusEntry."Entry No." := 1;
      lFusionInvoicingStatusEntry."RDD Document" := TRUE;
      lFusionInvoicingStatusEntry.INSERT(TRUE);
    END;

    BEGIN
    {
      /** @r IMH02 @d 10/12/12 @a ISAT.SB @v IMH6.01.220 @s Projet Facturation Fusion @c Bouton afficher dossier FUSION */
      /** @r IMH01 @d 29/11/12 @a ISAT.ZW @v IMH6.01.220 @s Projet Facturation Fusion @c Cr‚ation */
    }
    END.
  }
}