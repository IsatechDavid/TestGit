OBJECT Page 50366 DARVA Send Template Subpage
{
  OBJECT-PROPERTIES
  {
    Date=12/11/13;
    Time=16:06:40;
    Modified=Yes;
    Version List=IMH6.01,MATMUT01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=DARVA send Template List Subpage;
               FRA=Sous-page liste modŠles envoi DARVA];
    InsertAllowed=No;
    DeleteAllowed=No;
    ModifyAllowed=No;
    SourceTable=Table50125;
    PageType=ListPart;
    CardFormID=Page50365;
    OnOpenPage=BEGIN
                 SETCURRENTKEY("Corporate No.", "Show To Intervention", Active);
                 SETRANGE("Corporate No.", CorporateNo);

                 IF InterventionNo = 0 THEN
                   SETRANGE("Show To Service Header", TRUE)
                 ELSE
                   SETRANGE("Show To Intervention", TRUE);

                 //<<IMH01.ACO
                 CommentsAllowed := FALSE;
                 //>>IMH01.ACO
               END;

    OnAfterGetRecord=BEGIN
                       IF NOT TempSelectedDARVATemplate.ISEMPTY THEN
                         Selected := (TempSelectedDARVATemplate.Code = Code)
                       ELSE
                         Selected := FALSE;
                     END;

  }
  CONTROLS
  {
    { 1000000000;0;Container;
                ContainerType=ContentArea }

    { 1000000001;1;Group  ;
                Name=Group;
                GroupType=Repeater }

    { 1000000002;2;Field  ;
                CaptionML=[ENU=Name;
                           FRA=Nom];
                SourceExpr=Code;
                Editable=FALSE }

    { 1000000003;2;Field  ;
                CaptionML=[ENU=Dscription;
                           FRA=Descriptif];
                SourceExpr=Description }

    { 1000000004;2;Field  ;
                CaptionML=[ENU=Select;
                           FRA=S‚lectionner];
                SourceExpr=Selected;
                OnValidate=VAR
                             lInstream@1000000001 : InStream;
                           BEGIN
                             TempSelectedDARVATemplate.DELETEALL;

                             IF Selected THEN BEGIN // D‚selection des autres templates
                               Subject := Description;
                               CALCFIELDS(Body);
                               Body.CREATEINSTREAM(lInstream);

                               BodyText.READ(lInstream);

                               CurrPage.UPDATE(FALSE);
                               SelectTemplate(Rec);
                             END
                             ELSE BEGIN
                               Subject := '';
                               CLEAR(BodyText);
                               //<<IMH01
                               CommentsAllowed := FALSE;
                               //>>IMH01
                             END;
                           END;
                            }

    { 1000000005;1;Group  ;
                GroupType=Group }

    { 1000000006;2;Field  ;
                CaptionML=FRA=Objet;
                SourceExpr=Subject }

    { 1000000007;2;Field  ;
                SourceExpr=BodyText;
                Enabled=CommentsAllowed;
                ControlAddIn=[Isatech.DynamicsNav.NotepadAddIn;PublicKeyToken=f9c11cee8793cd65] }

  }
  CODE
  {
    VAR
      Selected@1000000000 : Boolean;
      CorporateNo@1000000001 : Code[20];
      ServiceHeaderNo@1000000002 : Code[20];
      InterventionNo@1000000003 : Integer;
      Subject@1000000006 : Text[1024];
      BodyText@1000000005 : BigText;
      TempSelectedDARVATemplate@1000000007 : TEMPORARY Record 50125;
      CommentsAllowed@1000000004 : Boolean INDATASET;

    PROCEDURE LoadDARVATemplates@1000000000(pCorporateNo@1000000000 : Code[20];pServiceHeaderNo@1000000001 : Code[20];pInterventionNo@1000000002 : Integer);
    BEGIN
      CorporateNo := pCorporateNo;
      ServiceHeaderNo := pServiceHeaderNo;
      InterventionNo := pInterventionNo;
    END;

    PROCEDURE RetrieveDARVATemplate@1000000008(VAR pDARVATemplate@1000000000 : Record 50125;VAR pObject@1000000001 : Text[1024];VAR pText@1000000002 : BigText);
    BEGIN
      pDARVATemplate.DELETEALL;

      IF TempSelectedDARVATemplate.FINDSET THEN
        REPEAT
          pDARVATemplate.INIT;
          pDARVATemplate.TRANSFERFIELDS(TempSelectedDARVATemplate);
          pDARVATemplate.INSERT(FALSE);
        UNTIL TempSelectedDARVATemplate.NEXT = 0;

      pObject := Subject;
      pText := BodyText;
    END;

    LOCAL PROCEDURE SelectTemplate@1000000009(pSelectedTemplate@1000000000 : Record 50125);
    BEGIN
      TempSelectedDARVATemplate.INIT;
      TempSelectedDARVATemplate.TRANSFERFIELDS(pSelectedTemplate);
      TempSelectedDARVATemplate.INSERT(FALSE);

      //<<IMH01.ACO
      CommentsAllowed := pSelectedTemplate."Allow Comments";
      CurrPage.UPDATE(FALSE);
      //>>IMH01.ACO
    END;

    BEGIN
    {
      /** @r IMH01 @d 12/11/13 @a ISAT.ACO @v IMH6.01.348 @s DARVA MATMUT Lot 1
          @c Ajout activation des commentaires selon type flux */
      /** @r IMH00 @d 26/08/13 @a ISAT.ACO @v IMH6.01.348 @s DARVA Matmut Lot1 @c Cr‚ation */
    }
    END.
  }
}