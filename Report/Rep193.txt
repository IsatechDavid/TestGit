OBJECT Report 193 Issue Finance Charge Memos
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Issue Finance Charge Memos;
               FRA=Emettre factures d'int‚rˆts];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table302;
        DataItemTableView=SORTING(No.);
        ReqFilterHeadingML=[ENU=Finance Charge Memo;
                            FRA=Facture d'int‚rˆts];
        OnPreDataItem=BEGIN
                        IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                          ERROR(Text000);
                        NoOfRecords := COUNT;
                        IF NoOfRecords = 1 THEN
                          Window.OPEN(Text001)
                        ELSE BEGIN
                          Window.OPEN(Text002);
                          OldTime := TIME;
                        END;
                      END;

        OnAfterGetRecord=BEGIN
                           RecordNo := RecordNo + 1;
                           UpdateFinanceChargeRounding("Finance Charge Memo Header");
                           CLEAR(FinChrgMemoIssue);
                           FinChrgMemoIssue.Set("Finance Charge Memo Header",ReplacePostingDate,PostingDateReq);
                           IF NoOfRecords = 1 THEN BEGIN
                             FinChrgMemoIssue.RUN;
                             "Finance Charge Memo Header".MARK := FALSE;
                           END ELSE BEGIN
                             NewTime := TIME;
                             IF (NewTime - OldTime > 100) OR (NewTime < OldTime) THEN BEGIN
                               NewProgress := ROUND(RecordNo / NoOfRecords * 100,1);
                               IF NewProgress <> OldProgress THEN BEGIN
                                 Window.UPDATE(1,NewProgress * 100);
                                 OldProgress := NewProgress;
                               END;
                               OldTime := TIME;
                             END;
                             "Finance Charge Memo Header".MARK := NOT FinChrgMemoIssue.RUN;
                           END;

                           IF PrintDoc AND NOT "Finance Charge Memo Header".MARK THEN BEGIN
                             FinChrgMemoIssue.GetIssuedFinChrgMemo(IssuedFinChrgMemoHeader);
                             IssuedFinChrgMemoHeader.MARK := TRUE;
                           END;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;
                         COMMIT;
                         IF PrintDoc THEN BEGIN
                           IssuedFinChrgMemoHeader.MARKEDONLY := TRUE;
                           IssuedFinChrgMemoHeader.PrintRecords(FALSE);
                         END;
                         "Finance Charge Memo Header".MARKEDONLY := TRUE;
                         IF "Finance Charge Memo Header".FIND('-') THEN
                           IF CONFIRM(
                               Text003 +
                               Text004,
                               TRUE)
                           THEN
                             FORM.RUNMODAL(0,"Finance Charge Memo Header");
                       END;

        ReqFilterFields=No.;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=1540;
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;1100 ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              FRA=Date comptabilisation];
                                                   SourceExpr=PostingDateReq }
      { 21  ;Label        ;0    ;1100 ;3300 ;440  ;ParentControl=1 }
      { 2   ;CheckBox     ;3410 ;550  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Replace Posting Date;
                                                              FRA=Remplacer date comptabilisation];
                                                   SourceExpr=ReplacePostingDate }
      { 22  ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=2 }
      { 3   ;CheckBox     ;3410 ;0    ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Print;
                                                              FRA=Imprimer];
                                                   SourceExpr=PrintDoc }
      { 4   ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=3 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Print;
                             FRA=Imprimer];
                  SourceExpr=PrintDoc }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             FRA=Remplacer date comptabilisation];
                  SourceExpr=ReplacePostingDate }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             FRA=Date comptabilisation];
                  SourceExpr=PostingDateReq }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;FRA=Veuillez entrer une date de comptabilisation.';
      Text001@1001 : TextConst 'ENU=Issuing finance charge memo...;FRA=Emission des factures d''int‚rˆts...';
      Text002@1002 : TextConst 'ENU=Issuing finance charge memos @1@@@@@@@@@@@@@;FRA=Emission des factures d''int‚rˆts @1@@@@@@@@@@@@@';
      Text003@1003 : TextConst 'ENU=It was not possible to issue some of the selected finance charge memos.\\;FRA=Il n''a pas ‚t‚ possible d''‚mettre certaines factures d''int‚rˆts s‚lectionn‚es.\\';
      Text004@1004 : TextConst 'ENU=Do you want to see these finance charge memos?;FRA=Souhaitez-vous visualiser ces factures d''int‚rˆts ?';
      IssuedFinChrgMemoHeader@1005 : Record 304;
      FinChrgMemoIssue@1006 : Codeunit 395;
      Window@1007 : Dialog;
      NoOfRecords@1008 : Integer;
      RecordNo@1009 : Integer;
      NewProgress@1010 : Integer;
      OldProgress@1011 : Integer;
      NewTime@1012 : Time;
      OldTime@1013 : Time;
      PostingDateReq@1014 : Date;
      ReplacePostingDate@1015 : Boolean;
      PrintDoc@1016 : Boolean;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}