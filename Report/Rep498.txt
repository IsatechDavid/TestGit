OBJECT Report 498 Batch Post Purch. Credit Memos
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Batch Post Purch. Credit Memos;
               FRA=TPL validation avoirs achat];
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table38;
        DataItemTableView=SORTING(Document Type,No.)
                          WHERE(Document Type=CONST(Credit Memo));
        ReqFilterHeadingML=[ENU=Purchase Credit Memo;
                            FRA=Avoir achat];
        OnPreDataItem=BEGIN
                        IF ReplacePostingDate AND (PostingDateReq = 0D) THEN
                          ERROR(Text000);

                        Approvaltemplate.SETCURRENTKEY(Approvaltemplate."Table ID",Approvaltemplate."Document Type",Approvaltemplate.Enabled);
                        Approvaltemplate.SETRANGE("Table ID",DATABASE::"Purchase Header");
                        Approvaltemplate.SETRANGE("Document Type","Purchase Header"."Document Type"::"Credit Memo");
                        Approvaltemplate.SETRANGE(Enabled,TRUE);
                        IF Approvaltemplate.FINDFIRST THEN
                          "Purchase Header".SETRANGE(Status,"Purchase Header".Status::Released);

                        CounterTotal := COUNT;
                        Window.OPEN(Text001);
                      END;

        OnAfterGetRecord=BEGIN
                           IF CalcInvDisc THEN
                             CalculateInvoiceDiscount;

                           Counter := Counter + 1;
                           Window.UPDATE(1,"No.");
                           Window.UPDATE(2,ROUND(Counter / CounterTotal * 10000,1));
                           CLEAR(PurchPost);
                           PurchPost.SetPostingDate(ReplacePostingDate,ReplaceDocumentDate,PostingDateReq);
                           IF PurchPost.RUN("Purchase Header") THEN BEGIN
                             CounterOK := CounterOK + 1;
                             IF MARKEDONLY THEN
                               MARK(FALSE);
                           END;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;
                         MESSAGE(Text002,CounterOK,CounterTotal);
                       END;

        ReqFilterFields=No.,Status;
      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=2090;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   PurchSetup.GET;
                   CalcInvDisc := PurchSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1   ;TextBox      ;3410 ;0    ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              FRA=Date comptabilisation];
                                                   SourceExpr=PostingDateReq }
      { 21  ;Label        ;0    ;0    ;3300 ;440  ;ParentControl=1 }
      { 2   ;CheckBox     ;3410 ;550  ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Replace Posting Date;
                                                              FRA=Remplacer date comptabilisation];
                                                   SourceExpr=ReplacePostingDate;
                                                   OnValidate=BEGIN
                                                                IF ReplacePostingDate THEN
                                                                  MESSAGE(Text003);
                                                              END;
                                                               }
      { 22  ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=2 }
      { 3   ;CheckBox     ;3410 ;1100 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Replace Document Date;
                                                              FRA=Remplacer date document];
                                                   SourceExpr=ReplaceDocumentDate }
      { 4   ;Label        ;0    ;1100 ;3300 ;440  ;ParentControl=3 }
      { 5   ;CheckBox     ;3410 ;1650 ;440  ;440  ;ShowCaption=No;
                                                   CaptionML=[ENU=Calc. Inv. Discount;
                                                              FRA=Calcul remise facture];
                                                   SourceExpr=CalcInvDisc;
                                                   OnValidate=BEGIN
                                                                PurchSetup.GET;
                                                                PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                                                              END;
                                                               }
      { 6   ;Label        ;0    ;1650 ;3300 ;440  ;ParentControl=5 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   PurchSetup.GET;
                   CalcInvDisc := PurchSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             FRA=Date comptabilisation];
                  SourceExpr=PostingDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Posting Date;
                             FRA=Remplacer date comptabilisation];
                  SourceExpr=ReplacePostingDate;
                  OnValidate=BEGIN
                               IF ReplacePostingDate THEN
                                 MESSAGE(Text003);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Replace Document Date;
                             FRA=Remplacer date document];
                  SourceExpr=ReplaceDocumentDate }

      { 5   ;2   ;Field     ;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             FRA=Calculer remise facture];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               PurchSetup.GET;
                               PurchSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;FRA=Veuillez entrer une date de comptabilisation.';
      Text001@1001 : TextConst 'ENU=Posting credit memos  #1########## @2@@@@@@@@@@@@@;FRA=Validation des avoirs        #1########## @2@@@@@@@@@@@@@';
      Text002@1002 : TextConst 'ENU=%1 credit memos out of a total of %2 have now been posted.;FRA=%1 avoir(s) sur %2 enregistr‚(s).';
      PurchLine@1003 : Record 39;
      PurchSetup@1004 : Record 312;
      Approvaltemplate@1013 : Record 464;
      PurchCalcDisc@1005 : Codeunit 70;
      PurchPost@1006 : Codeunit 90;
      Window@1007 : Dialog;
      PostingDateReq@1008 : Date;
      CounterTotal@1009 : Integer;
      Counter@1010 : Integer;
      CounterOK@1011 : Integer;
      ReplacePostingDate@1014 : Boolean;
      ReplaceDocumentDate@1015 : Boolean;
      CalcInvDisc@1016 : Boolean;
      Text003@1012 : TextConst 'ENU=The exchange rate associated with the new posting date on the purchase header will not apply to the purchase lines.;FRA=Le taux de change associ‚ … la nouvelle date de comptabilisation sur l''en-tˆte achat ne s''applique pas aux lignes achat.';

    PROCEDURE CalculateInvoiceDiscount@1();
    BEGIN
      PurchLine.RESET;
      PurchLine.SETRANGE("Document Type","Purchase Header"."Document Type");
      PurchLine.SETRANGE("Document No.","Purchase Header"."No.");
      IF PurchLine.FIND('-') THEN
        IF PurchCalcDisc.RUN(PurchLine) THEN BEGIN
          "Purchase Header".GET("Purchase Header"."Document Type","Purchase Header"."No.");
          COMMIT;
        END;
    END;

    BEGIN
    END.
  }
  RDLDATA
  {
  }
}