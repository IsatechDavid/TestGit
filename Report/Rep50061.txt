OBJECT Report 50061 Customer Invoicing
{
  OBJECT-PROPERTIES
  {
    Date=05/12/11;
    Time=14:21:33;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Customer Invoicing;
               FRA=Facturation client];
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF NOT GUIALLOWED THEN BEGIN
                    IF PostingDateReq = 0D THEN
                      PostingDateReq := WORKDATE;
                    IF DocDateReq = 0D THEN
                      DocDateReq := WORKDATE;
                  END;
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table50137;
        DataItemTableView=SORTING(Corporate No.,Invoicing frequency,Invoice Regroup Method);
        DataItemVarName=InvMatrixSetup;
        OnPreDataItem=BEGIN
                        //IF PostingDateReq = 0D THEN
                        //  ERROR(Text000);

                        IF DocDateReq = 0D THEN
                          ERROR(Text001);

                        IF GUIALLOWED THEN BEGIN
                          Window.OPEN(
                            Text002 +
                            Text003 +
                            Text004 +
                            Text005);
                        END ELSE
                          SETRANGE("Invoicing frequency", "Invoicing frequency"::Daily);
                      END;

        OnAfterGetRecord=BEGIN
                           RefDate := PostingDateReq;
                           RefDay := DATE2DMY(RefDate,1);

                           IF (PostingDateReq <> CALCDATE('<CM>',PostingDateReq)) THEN
                           BEGIN
                             CASE "Invoicing frequency" OF
                               "Invoicing frequency"::Daily :
                                 BEGIN
                                   PostingDateReq := RefDate;
                                 END;

                               "Invoicing frequency"::Decade :
                                 BEGIN
                                 //IMH04
                                 IF RefDay < 11 THEN
                                   //IF RefDay < 10 THEN
                                   //IMH04
                                     PostingDateReq := CALCDATE('<-1M+CM+10D>',RefDate)
                                     //PostingDateReq := CALCDATE('<-1M+CM>',RefDate)
                                 //IMH04
                                 ELSE IF RefDay < 21 THEN
                                   //ELSE IF RefDay < 20 THEN
                                   //IMH04
                                     PostingDateReq := CALCDATE('<-1M+CM+20D>',RefDate)
                                     //PostingDateReq := CALCDATE('<D10>',RefDate)
                                   ELSE
                                   //IMH04
                                     PostingDateReq := CALCDATE('<CM>',RefDate)
                                     //PostingDateReq := CALCDATE('<D20>',RefDate)
                                 END;

                               "Invoicing frequency"::Week :
                                 BEGIN
                                 //IMH04
                                   PostingDateReq := CALCDATE('<CWD>',RefDate);
                                   //PostingDateReq := CALCDATE('<cWD-7D>',RefDate);
                                 END;

                               "Invoicing frequency"::Fortnight :
                                 BEGIN
                                 //IMH04
                                   IF RefDay < 16 THEN
                                   //IF RefDay < 15 THEN
                                   //IMH04
                                     PostingDateReq := CALCDATE('<-1M+CM+15D>',RefDate)
                                     //PostingDateReq := CALCDATE('<-1M + CM>',RefDate)
                                   ELSE
                                     //PostingDateReq := CALCDATE('<D15>',RefDate)
                                     PostingDateReq := CALCDATE('<CM>',RefDate)
                                 END;

                               "Invoicing frequency"::Monthly :
                                 BEGIN
                                 //IMH04
                                   PostingDateReq := CALCDATE('<CM>',RefDate);
                                   //PostingDateReq := CALCDATE('<1M+CM>',RefDate);
                                 END;
                             END;
                           END;
                         END;

        OnPostDataItem=VAR
                         lCust@1100281000 : Record 18;
                       BEGIN
                         CurrReport.LANGUAGE := GLOBALLANGUAGE;
                         IF GUIALLOWED THEN
                           Window.CLOSE;

                         IF SalesHeader."No." <> '' THEN BEGIN // Not the first time
                           FinalizeSalesInvHeader;
                           IF GUIALLOWED THEN BEGIN
                             IF (NoOfSalesInvErrors = 0) AND NOT HideDialog  THEN BEGIN
                               IF NoOfskippedShiment > 0 THEN BEGIN
                                 MESSAGE(
                                   Text010 + Text011,
                                   NoOfSalesInv,NoOfskippedShiment)
                               END ELSE BEGIN
                                 MESSAGE(
                                   Text010,
                                   NoOfSalesInv);
                               END;
                             END ELSE
                               IF NOT HideDialog THEN
                                 MESSAGE(
                                   Text007,
                                   NoOfSalesInvErrors)
                           END;
                         END ELSE
                           IF (NOT HideDialog) AND GUIALLOWED THEN
                             MESSAGE(Text008);


                         IF GeneratePDFInv THEN BEGIN
                           TempSalesInvHeader.RESET;
                           IF TempSalesInvHeader.FINDSET THEN
                             REPEAT
                               lCust.GET(TempSalesInvHeader."Bill-to Customer No.");

                               PDFMgt.ClearInstance;
                               PDFMgt.CreatePDFCreator;
                               //ligne mise en commentaire car impossible de compiler
                               PDFMgt.PrintSalesInvoice(TempSalesInvHeader,lCust."PDF Invoice Directory",TempSalesInvHeader."No.");
                             UNTIL TempSalesInvHeader.NEXT = 0;
                         END;
                       END;

        ReqFilterFields=Corporate No.,Service Order Type,Corporate Region Code,Invoicing frequency,Invoice Regroup Method;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
    { PROPERTIES
      {
        DataItemIndent=1;
        DataItemTable=Table111;
        DataItemTableView=SORTING(Document No.,Line No.)
                          WHERE(Build Kit=CONST(No));
        OnPreDataItem=BEGIN
                        SETCURRENTKEY("Corporate No.","Service Order Type","Corporate Region Code");
                        SETRANGE("Corporate No.",InvMatrixSetup."Corporate No.");
                        IF InvMatrixSetup."Service Order Type" <> '' THEN
                          SETRANGE("Service Order Type",InvMatrixSetup."Service Order Type");

                        IF InvMatrixSetup."Corporate Region Code" <> '' THEN
                          SETRANGE("Corporate Region Code",InvMatrixSetup."Corporate Region Code");

                        SETFILTER("Qty. Shipped Not Invoiced",'<>0');
                      END;

        OnAfterGetRecord=VAR
                           lInvMatrixSetup@1100281000 : Record 50137;
                         BEGIN
                           IF (InvMatrixSetup."Service Order Type" = '') OR (InvMatrixSetup."Corporate Region Code" = '') THEN BEGIN
                             CASE InvMatrixSetup."Invoice Regroup Method" OF
                               InvMatrixSetup."Invoice Regroup Method"::" " :
                                 BEGIN
                                   lInvMatrixSetup.SETRANGE("Corporate No.","Corporate No.");
                                   lInvMatrixSetup.SETRANGE("Service Order Type","Service Order Type");
                                   lInvMatrixSetup.SETFILTER("Corporate Region Code",'=%1|=%2','',"Corporate Region Code");
                                   lInvMatrixSetup.SETRANGE("Invoice Regroup Method",lInvMatrixSetup."Invoice Regroup Method"::ByServiceOrderType);
                                   IF lInvMatrixSetup.FINDFIRST THEN
                                     CurrReport.SKIP;
                                 END;
                               InvMatrixSetup."Invoice Regroup Method"::ByServiceOrderType :
                                 BEGIN
                                   lInvMatrixSetup.SETRANGE("Corporate No.","Corporate No.");
                                   lInvMatrixSetup.SETRANGE("Service Order Type","Service Order Type");
                                   lInvMatrixSetup.SETFILTER("Corporate Region Code",'=%1|=%2','',"Corporate Region Code");
                                   lInvMatrixSetup.SETRANGE("Invoice Regroup Method",lInvMatrixSetup."Invoice Regroup Method"::" ");
                                   IF lInvMatrixSetup.FINDFIRST THEN
                                     CurrReport.SKIP;
                                 END;
                             END;
                           END;

                           IF SalesOrderHeader."No." <> "Order No." THEN BEGIN
                             SalesOrderHeader.GET(SalesOrderHeader."Document Type"::Order,"Order No.");
                             CurrReport.LANGUAGE := Language.GetLanguageID(SalesOrderHeader."Language Code");

                             FromDocDim.SETRANGE("Table ID",DATABASE::"Sales Header");
                             FromDocDim.SETRANGE("Document Type",SalesOrderHeader."Document Type");
                             FromDocDim.SETRANGE("Document No.",SalesOrderHeader."No.");
                           END;


                           IF SalesShptHeader."No." <> "Document No." THEN
                             SalesShptHeader.GET("Document No.");

                           {
                           IF SalesShptHeader.Canceled THEN BEGIN
                             CurrReport.SKIP;
                           END;
                           }


                           IF (SalesShptHeader."Posting Date" > PostingDateReq) THEN
                             CurrReport.SKIP;

                           IF OnlyStdPmtTerms THEN BEGIN
                             Cust.GET("Bill-to Customer No.");
                             PmtTerms.GET(Cust."Payment Terms Code");
                             IF PmtTerms.Code = SalesShptHeader."Payment Terms Code" THEN BEGIN
                               DueDate := CALCDATE(PmtTerms."Due Date Calculation",SalesShptHeader."Document Date");
                               PmtDiscDate := CALCDATE(PmtTerms."Discount Date Calculation",SalesShptHeader."Document Date");
                               "PmtDisc%" := PmtTerms."Discount %";
                               IF (DueDate <> SalesShptHeader."Due Date") OR
                                 (PmtDiscDate <> SalesShptHeader."Pmt. Discount Date") OR
                                 ("PmtDisc%" <> SalesShptHeader."Payment Discount %")
                               THEN BEGIN
                                 //NoOfskippedShiment := NoOfskippedShiment + 1;
                                 CurrReport.SKIP;
                               END;
                             END ELSE BEGIN
                               //NoOfskippedShiment := NoOfskippedShiment + 1;
                               CurrReport.SKIP;
                             END;
                           END;

                           IF GUIALLOWED THEN BEGIN
                             Window.UPDATE(1,SalesOrderHeader."Bill-to Customer No.");
                             Window.UPDATE(2,SalesOrderHeader."No.");
                             Window.UPDATE(3,SalesShptHeader."No.");
                           END;

                           SalesInvLine.RESET;
                           SalesInvLine.SETCURRENTKEY("Document Type","Shipment No.","Shipment Line No.");
                           SalesInvLine.SETRANGE("Document Type",SalesInvLine."Document Type"::Invoice);
                           SalesInvLine.SETRANGE("Shipment No.","Sales Shipment Line"."Document No.");
                           SalesInvLine.SETRANGE("Shipment Line No.","Sales Shipment Line"."Line No.");
                           SalesInvLine.SETRANGE(Quantity,"Sales Shipment Line"."Qty. Shipped Not Invoiced");
                           IF SalesInvLine.FIND('-') THEN
                             CurrReport.SKIP;

                           IF "Sales Shipment Line".Type = 0 THEN BEGIN
                             IF ((NOT CopyTextLines) OR ("Sales Shipment Line"."Attached to Line No." <> 0)) THEN
                               CurrReport.SKIP;
                           END;

                           IF "Authorized for Credit Card" THEN
                             CurrReport.SKIP;

                           IF ("Qty. Shipped Not Invoiced" <> 0) OR ("Sales Shipment Line".Type = 0) THEN BEGIN
                             IF ("Bill-to Customer No." <> Cust."No.") AND
                                ("Sales Shipment Line"."Sell-to Customer No." <> '') THEN
                               Cust.GET("Bill-to Customer No.");
                             IF NOT (Cust.Blocked IN [Cust.Blocked::All,Cust.Blocked::Invoice]) THEN BEGIN
                               IF (SalesOrderHeader."Bill-to Customer No." <> SalesHeader."Bill-to Customer No.") OR
                                  (SalesOrderHeader."Currency Code" <> SalesHeader."Currency Code") OR
                                  (NOT VerifyDimAnalogy) OR
                                  ((InvMatrixSetup."Invoice Regroup Method" = InvMatrixSetup."Invoice Regroup Method"::ByServiceOrderType) AND
                                   ("Service Order Type" <>  SalesLine."Service Order Type")) //IMH04  //OR
                                  //(InvFrequency <> InvMatrixSetup."Invoicing frequency")
                               THEN BEGIN
                                 IF SalesHeader."No." <> '' THEN
                                   FinalizeSalesInvHeader;
                                 InsertSalesInvHeader;
                                 SalesLine.SETRANGE("Document Type",SalesHeader."Document Type");
                                 SalesLine.SETRANGE("Document No.",SalesHeader."No.");
                                 SalesLine."Document Type" := SalesHeader."Document Type";
                                 SalesLine."Document No." := SalesHeader."No.";
                               END;
                               SalesShptLine := "Sales Shipment Line";
                               HasAmount := HasAmount OR ("Qty. Shipped Not Invoiced" <>0);
                               SalesShptLine.InsertInvLineFromShptLine(SalesLine,TempFromLineDim);
                               DimMgt.MoveTempFromDimToTempToDim(TempFromLineDim,TempToLineDim);
                             END ELSE
                               NoOfSalesInvErrors := NoOfSalesInvErrors + 1;
                           END;
                         END;

        OnPostDataItem=VAR
                         SalesGetShpt@1000 : Codeunit 64;
                       BEGIN
                         SalesGetShpt.GetItemChargeAssgnt("Sales Shipment Line");

                         InvFrequency := InvMatrixSetup."Invoicing frequency";
                       END;

      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5060;
      Height=4510;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   IF PostingDateReq = 0D THEN
                     PostingDateReq := WORKDATE;
                   IF DocDateReq = 0D THEN
                     DocDateReq := WORKDATE;
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                 END;

      OnAfterGetCurrRecord=BEGIN
                             RequestOptionsForm.GeneratePDFInv.EDITABLE(PostInv);
                           END;

    }
    CONTROLS
    {
      { 2   ;CheckBox     ;3410 ;1100 ;440  ;440  ;InPage=-1;
                                                   ShowCaption=No;
                                                   CaptionML=[ENU=Calc. Inv. Discount;
                                                              FRA=Calcul remise facture];
                                                   SourceExpr=CalcInvDisc;
                                                   OnValidate=BEGIN
                                                                SalesSetup.GET;
                                                                SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                                                              END;
                                                               }
      { 22  ;Label        ;0    ;1100 ;3300 ;440  ;ParentControl=2 }
      { 3   ;CheckBox     ;3410 ;1650 ;440  ;440  ;InPage=-1;
                                                   ShowCaption=No;
                                                   CaptionML=[ENU=Post Invoices;
                                                              FRA=Validation des factures];
                                                   SourceExpr=PostInv;
                                                   OnAfterValidate=BEGIN
                                                                     IF NOT PostInv THEN
                                                                       GeneratePDFInv := FALSE;
                                                                   END;
                                                                    }
      { 23  ;Label        ;0    ;1650 ;3300 ;440  ;ParentControl=3 }
      { 4   ;TextBox      ;3410 ;550  ;1650 ;440  ;InPage=-1;
                                                   CaptionML=[ENU=Document Date;
                                                              FRA=Date document];
                                                   SourceExpr=DocDateReq }
      { 5   ;Label        ;0    ;550  ;3300 ;440  ;ParentControl=4 }
      { 6   ;CheckBox     ;3410 ;2200 ;440  ;440  ;InPage=-1;
                                                   ShowCaption=No;
                                                   CaptionML=[ENU=Only Std. Payment Terms;
                                                              FRA=Conditions paiement standard uniquement];
                                                   SourceExpr=OnlyStdPmtTerms }
      { 7   ;Label        ;0    ;2200 ;3300 ;440  ;ParentControl=6 }
      { 8   ;CheckBox     ;3410 ;2750 ;440  ;440  ;InPage=-1;
                                                   ShowCaption=No;
                                                   SourceExpr=CopyTextLines }
      { 9   ;Label        ;0    ;2750 ;3300 ;440  ;ParentControl=8;
                                                   CaptionML=[ENU=Copy Text Lines;
                                                              FRA=Copier lignes texte] }
      { 1100281000;CheckBox;3410;3410 ;440  ;440  ;Name=GeneratePDFInv;
                                                   InPage=-1;
                                                   ShowCaption=No;
                                                   CaptionML=[ENU=Generate PDF Invoice;
                                                              FRA=G‚n‚rer facture PDF];
                                                   SourceExpr=GeneratePDFInv }
      { 1100281001;Label  ;0    ;3410 ;3300 ;440  ;ParentControl=1100281000 }
      { 1100281002;Label  ;0    ;0    ;3300 ;440  ;ParentControl=1100281003 }
      { 1100281003;TextBox;3410 ;0    ;1650 ;440  ;CaptionML=[ENU=Posting Date;
                                                              FRA=Date comptabilisation];
                                                   SourceExpr=PostingDateReq }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
      OnOpenPage=BEGIN
                   IF PostingDateReq = 0D THEN
                     PostingDateReq := WORKDATE;
                   IF DocDateReq = 0D THEN
                     DocDateReq := WORKDATE;
                   SalesSetup.GET;
                   CalcInvDisc := SalesSetup."Calc. Inv. Discount";
                 END;

    }
    CONTROLS
    {
      { 1900000001;0;Container;
                  ContainerType=ContentArea }

      { 1900000002;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options] }

      { 1   ;2   ;Field     ;
                  CaptionML=[ENU=Posting Date;
                             FRA=Date comptabilisation];
                  SourceExpr=PostingDateReq }

      { 4   ;2   ;Field     ;
                  CaptionML=[ENU=Document Date;
                             FRA=Date document];
                  SourceExpr=DocDateReq }

      { 2   ;2   ;Field     ;
                  CaptionML=[ENU=Calc. Inv. Discount;
                             FRA=Calculer remise facture];
                  SourceExpr=CalcInvDisc;
                  OnValidate=BEGIN
                               SalesSetup.GET;
                               SalesSetup.TESTFIELD("Calc. Inv. Discount",FALSE);
                             END;
                              }

      { 3   ;2   ;Field     ;
                  CaptionML=[ENU=Post Invoices;
                             FRA=Validation des factures];
                  SourceExpr=PostInv }

      { 6   ;2   ;Field     ;
                  CaptionML=[ENU=Only Std. Payment Terms;
                             FRA=Conditions paiement standard uniquement];
                  SourceExpr=OnlyStdPmtTerms }

      { 8   ;2   ;Field     ;
                  CaptionML=[ENU=Copy Text Lines;
                             FRA=Copier lignes texte];
                  SourceExpr=CopyTextLines }

    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Please enter the posting date.;FRA=Veuillez entrer une date de comptabilisation.';
      Text001@1001 : TextConst 'ENU=Please enter the document date.;FRA=Veuillez entrer la date du document.';
      Text002@1002 : TextConst 'ENU=Combining shipments...\\;FRA=Regroupement des B.L...\\';
      Text003@1003 : TextConst 'ENU=Customer No.    #1##########\;FRA=Nø client       #1##########\';
      Text004@1004 : TextConst 'ENU=Order No.       #2##########\;FRA=Nø commande     #2##########\';
      Text005@1005 : TextConst 'ENU=Shipment No.    #3##########;FRA=Nø livraison    #3##########';
      Text007@1006 : TextConst 'ENU=Not all the invoices were posted. A total of %1 invoices were not posted.;FRA=Toutes les factures n''ont pas ‚t‚ valid‚es (%1 non valid‚e(s)).';
      Text008@1007 : TextConst 'ENU=There is nothing to combine.;FRA=Il n''y a rien … regrouper.';
      Text010@1008 : TextConst 'ENU=The shipments are now combined, and the number of invoice(s) created is %1.;FRA=Les exp‚ditions sont maintenant regroup‚es, et le nombre de factures cr‚‚es est de %1.';
      SalesHeader@1010 : Record 36;
      SalesLine@1011 : Record 37;
      SalesOrderHeader@1100281004 : Record 36;
      SalesShptHeader@1100281001 : Record 110;
      SalesShptLine@1012 : Record 111;
      SalesInvLine@1100281000 : Record 37;
      SalesInvHeader@1100281009 : Record 112;
      TempSalesInvHeader@1100281010 : TEMPORARY Record 112;
      SalesSetup@1013 : Record 311;
      FromDocDim@1014 : Record 357;
      ToDocDim@1016 : Record 357;
      TempFromLineDim@1031 : TEMPORARY Record 357;
      TempToLineDim@1032 : TEMPORARY Record 357;
      Cust@1018 : Record 18;
      Language@1009 : Record 8;
      PmtTerms@1017 : Record 3;
      SalesCalcDisc@1019 : Codeunit 60;
      SalesPost@1020 : Codeunit 80;
      DimMgt@1033 : Codeunit 408;
      PDFMgt@1100281011 : Codeunit 50127;
      Window@1021 : Dialog;
      PostingDateReq@1022 : Date;
      DocDateReq@1023 : Date;
      RefDate@1100281002 : Date;
      RefDay@1100281003 : Integer;
      CalcInvDisc@1024 : Boolean;
      PostInv@1025 : Boolean;
      OnlyStdPmtTerms@1015 : Boolean;
      HasAmount@1102601000 : Boolean;
      HideDialog@1034 : Boolean;
      NextLineNo@1026 : Integer;
      NoOfSalesInvErrors@1027 : Integer;
      NoOfSalesInv@1028 : Integer;
      Text011@1029 : TextConst 'ENU=\%2 Shipments with non-standard Payment Terms have not been combined.;FRA=\%2 exp‚ditions avec des conditions de paiement non standard n''ont pas ‚t‚ combin‚es.';
      NoOfskippedShiment@1030 : Integer;
      CopyTextLines@1035 : Boolean;
      DueDate@1100281007 : Date;
      PmtDiscDate@1100281006 : Date;
      "PmtDisc%"@1100281005 : Decimal;
      GeneratePDFInv@1100281008 : Boolean;
      InvFrequency@1100281012 : Option;

    LOCAL PROCEDURE FinalizeSalesInvHeader@2();
    VAR
      lCust@1100281000 : Record 18;
    BEGIN
      WITH SalesHeader DO BEGIN
        IF NOT(HasAmount) THEN BEGIN
          SalesHeader.DELETE(TRUE);
          TempToLineDim.DELETEALL;
          EXIT;
        END;
        IF CalcInvDisc THEN
          SalesCalcDisc.RUN(SalesLine);
        SalesHeader.FIND;
        DimMgt.TransferTempToDimToDocDim(TempToLineDim);
        COMMIT;
        CLEAR(SalesCalcDisc);
        CLEAR(SalesPost);
        NoOfSalesInv := NoOfSalesInv + 1;
        IF PostInv THEN BEGIN
          CLEAR(SalesPost);
          IF SalesPost.RUN(SalesHeader) THEN BEGIN
            lCust.GET(SalesHeader."Bill-to Customer No.");

            IF lCust."Generate PDF Invoice" AND (lCust."PDF Invoice Directory" <> '') THEN BEGIN
              IF SalesInvHeader.GET(SalesHeader."Last Posting No.") THEN BEGIN
                TempSalesInvHeader := SalesInvHeader;
                TempSalesInvHeader.INSERT;
              END;
            END;
            //<<IMH02.isat.PC
            // Suppression de la commande
            IF SalesHeader.DELETE(TRUE) THEN;
            //>>IMH02.isat.PC
          END ELSE
            NoOfSalesInvErrors := NoOfSalesInvErrors + 1

        END;
      END;
    END;

    LOCAL PROCEDURE InsertSalesInvHeader@1();
    BEGIN
      WITH SalesHeader DO BEGIN
        INIT;
        "Document Type" := "Document Type"::Invoice;
        "No." := '';
        INSERT(TRUE);
        VALIDATE("Sell-to Customer No.",SalesOrderHeader."Bill-to Customer No.");
        IF "Bill-to Customer No." <> "Sell-to Customer No." THEN
          VALIDATE("Bill-to Customer No.",SalesOrderHeader."Bill-to Customer No.");
        VALIDATE("Posting Date",PostingDateReq);
        VALIDATE("Document Date",DocDateReq);
        //<<IMH03
          VALIDATE("Payment Method Code",SalesOrderHeader."Payment Method Code");
        //>>IMH03
        VALIDATE("Currency Code",SalesOrderHeader."Currency Code");
        "Salesperson Code" := SalesOrderHeader."Salesperson Code";
        "Shortcut Dimension 1 Code" := SalesOrderHeader."Shortcut Dimension 1 Code";
        "Shortcut Dimension 2 Code" := SalesOrderHeader."Shortcut Dimension 2 Code";
        "Corporate No." := SalesOrderHeader."Corporate No.";
        "Service Order Type" := InvMatrixSetup."Service Order Type";
        "Corporate Region Code" := SalesOrderHeader."Corporate Region Code";
        //<<IMH05.ISAT.SB
        IF SalesOrderHeader."Prices Including VAT" THEN
            SalesHeader."Prices Including VAT" := TRUE
        ELSE
            SalesHeader."Prices Including VAT" := FALSE;
        //>>IMH05.ISAT.SB
        MODIFY;
        COMMIT;
        HasAmount := FALSE;

        ToDocDim.SETRANGE("Document Type","Document Type");
        ToDocDim.SETRANGE("Document No.","No.");
        ToDocDim.DELETEALL;

        IF FromDocDim.FIND('-') THEN BEGIN
          REPEAT
            ToDocDim.INIT;
            ToDocDim."Table ID" := DATABASE::"Sales Header";
            ToDocDim."Document Type" := "Document Type";
            ToDocDim."Document No." := "No.";
            ToDocDim."Line No." := 0;
            ToDocDim."Dimension Code" := FromDocDim."Dimension Code";
            ToDocDim."Dimension Value Code" := FromDocDim."Dimension Value Code";
            ToDocDim.INSERT;
          UNTIL FromDocDim.NEXT = 0;
        END;

        ToDocDim.SETRANGE("Table ID",DATABASE::"Sales Line");

        NextLineNo := 10000;
      END;
    END;

    PROCEDURE VerifyDimAnalogy@3() : Boolean;
    BEGIN
      IF NOT FromDocDim.FIND('-') THEN BEGIN
        FromDocDim.INIT;
        FromDocDim."Dimension Code" := '';
      END;

      ToDocDim.SETRANGE("Table ID",DATABASE::"Sales Header");
      ToDocDim.SETRANGE("Document Type",SalesHeader."Document Type");
      ToDocDim.SETRANGE("Document No.",SalesHeader."No.");
      IF NOT ToDocDim.FIND('-') THEN BEGIN
        ToDocDim.INIT;
        ToDocDim."Dimension Code" := '';
      END;

      IF FromDocDim.FIND('-') THEN
        REPEAT
          IF (FromDocDim."Dimension Code" <> ToDocDim."Dimension Code") OR
             (FromDocDim."Dimension Value Code" <> ToDocDim."Dimension Value Code")
          THEN
            EXIT(FALSE);
        UNTIL (FromDocDim.NEXT = 0) AND (ToDocDim.NEXT = 0);

      EXIT(TRUE);
    END;

    PROCEDURE InitializeReport@5(PostingDate@1002 : Date;DocDate@1003 : Date);
    BEGIN
      PostingDateReq := PostingDate;
      DocDateReq := DocDate;
    END;

    PROCEDURE SetHideDialog@7(NewHideDialog@1000 : Boolean);
    BEGIN
      HideDialog := NewHideDialog;
    END;

    BEGIN
    {
      /** @r IMH05 @d 30/09/11 @a ISAT.SB @v IMH6.01.146 @s Gestion des prix TTC */
      /** @r IMH04 @d 16/09/11 @a ISAT.SB @v IMH6.01.146 @s Ajustement "Invoicing frequency" */
      /** @r IMH03 @d 08/06/11 @a ISAT.SB @v IMH6.01.146 @s Code mode de rŠglement de la commande de vente */
      /** @r IMH02 @d 08/06/11 @a ISAT.PC @v IMH6.01.146 @s Ajustement */
      /** @r IMH01 @d 03/05/11 @a ISAT.EBA @v IMH6.01.146 @s Gestion de la facturation client */
    }
    END.
  }
  RDLDATA
  {
  }
}