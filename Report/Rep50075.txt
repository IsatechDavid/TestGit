OBJECT Report 50075 Report 34:Nb d'interv. par CR
{
  OBJECT-PROPERTIES
  {
    Date=01/02/12;
    Time=11:50:52;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    CaptionML=FRA=Report 34 : Nombre d'interventions par CR;
    ProcessingOnly=Yes;
    OnPreReport=BEGIN
                  IF StartDate = 0D THEN
                    ERROR(Text001);

                  IF NbWeek = 0 THEN
                    ERROR(Text002);

                  EndDate := CALCDATE(STRSUBSTNO('<%1W>',NbWeek),StartDate);

                  OtherFaultReason := 'Autres';
                  TotalCode := 'Total';

                  NbMainFaultReason := 5;
                END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table50000;
        DataItemTableView=SORTING(Regional Coordinator,Post Code);
        OnPreDataItem=BEGIN
                        //<<IMH02
                        //Window.OPEN(Text003,Provider."No.");
                        Window.OPEN(Text003);
                        TotalCpt := COUNT;
                        //>>IMH02
                      END;

        OnAfterGetRecord=VAR
                           lServIntervLine@1100281002 : Record 50075;
                           lService@1100281001 : Record 50066;
                           lTempFaultReason@1100281000 : TEMPORARY Record 5917;
                         BEGIN
                           //<<IMH02
                           //Window.UPDATE;
                           CurrCpt += 1;
                           IF CurrCpt >= 10 THEN BEGIN
                             Cpt += CurrCpt;
                             Window.UPDATE(1,ROUND(Cpt / TotalCpt * 9999,1));
                             CurrCpt := 0;
                           END;
                           //>>IMH02
                           IF "Regional Coordinator" = '' THEN
                             CurrReport.SKIP;

                           TempUserSetup."User ID" := "Regional Coordinator";
                           IF TempUserSetup.INSERT THEN;

                           lServIntervLine.RESET;
                           //lServIntervLine.SETCURRENTKEY("Assigned Date","Provider No.");
                           lServIntervLine.SETCURRENTKEY("Provider No.");
                           lServIntervLine.SETRANGE("Provider No.","No.");
                           lServIntervLine.SETRANGE("Assigned Date",StartDate,EndDate);
                           IF lServIntervLine.FINDSET THEN BEGIN
                             REPEAT
                               lService.GET(lServIntervLine."Service No.");

                               TempByDate.RESET;
                               TempByDate.SETRANGE("Code 1","Regional Coordinator");
                               TempByDate.SETRANGE("Code 2",lService."Service Cause");
                               TempByDate.SETRANGE("Date 1",lServIntervLine."Assigned Date");
                               IF TempByDate.FINDFIRST THEN BEGIN
                                 TempByDate."Data Integer 1" := TempByDate."Data Integer 1" + 1;
                                 TempByDate.MODIFY;
                               END ELSE BEGIN
                                 TempByDate."Code 1" := "Regional Coordinator";
                                 TempByDate."Code 2" := lService."Service Cause";
                                 TempByDate."Date 1" := lServIntervLine."Assigned Date";
                                 TempByDate."Data Integer 1" := 1;
                                 TempByDate.INSERT;
                               END;

                               TempByFaultReason.RESET;
                               TempByFaultReason.SETRANGE("Code 1","Regional Coordinator");
                               TempByFaultReason.SETRANGE("Code 2",lService."Service Cause");
                               IF TempByFaultReason.FINDFIRST THEN BEGIN
                                 TempByFaultReason."Data Integer 1" := TempByFaultReason."Data Integer 1" + 1;
                                 TempByFaultReason.MODIFY;
                               END ELSE BEGIN
                                 TempByFaultReason."Code 1" := "Regional Coordinator";
                                 TempByFaultReason."Code 2" := lService."Service Cause";
                                 TempByFaultReason."Data Integer 1" := 1;
                                 TempByFaultReason.INSERT;
                               END;
                             UNTIL lServIntervLine.NEXT = 0;
                           END;
                         END;

        OnPostDataItem=BEGIN
                         Window.CLOSE;

                         ExportToExcel;
                       END;

        ReqFilterFields=Regional Coordinator;
      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=6050;
      Height=2090;
      SaveValues=Yes;
      OnOpenForm=BEGIN
                   OneSheet := TRUE;//IMH02
                 END;

    }
    CONTROLS
    {
      { 1100281000;TextBox;3630 ;330  ;1700 ;440  ;InPage=-1;
                                                   CaptionML=[ENU=Starting Date;
                                                              FRA=Date de d‚but];
                                                   SourceExpr=StartDate }
      { 1100281001;Label  ;220  ;330  ;3300 ;440  ;ParentControl=1100281000 }
      { 1100281002;TextBox;3630 ;880  ;1700 ;440  ;InPage=-1;
                                                   CaptionML=[ENU=Number of Week;
                                                              FRA=Nombre de semaine];
                                                   SourceExpr=NbWeek }
      { 1100281003;Label  ;220  ;880  ;3300 ;440  ;ParentControl=1100281002 }
      { 1000000000;CheckBox;3630;1430 ;440  ;440  ;InPage=-1;
                                                   CaptionML=[ENU=Display on one sheet;
                                                              FRA=Afficher sur une feuille];
                                                   SourceExpr=OneSheet }
      { 1000000001;Label  ;220  ;1430 ;3300 ;440  ;ParentControl=1000000000 }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
      SaveValues=Yes;
    }
    CONTROLS
    {
      { 1000000000;;Container;
                  ContainerType=ContentArea }

      { 1000000001;1;Group  ;
                  GroupType=Group }

      { 1000000002;2;Field  ;
                  CaptionML=[ENU=Starting Date;
                             FRA=Date de d‚but];
                  SourceExpr=StartDate }

      { 1000000003;2;Field  ;
                  CaptionML=[ENU=Number of Week;
                             FRA=Nombre de semaine];
                  SourceExpr=NbWeek }

      { 1000000004;2;Field  ;
                  CaptionML=[ENU=Display on one sheet;
                             FRA=Afficher sur une feuille];
                  SourceExpr=OneSheet }

    }
  }
  CODE
  {
    VAR
      TempByDate@1100281002 : TEMPORARY Record 50143;
      TempByFaultReason@1100281014 : TEMPORARY Record 50143;
      TempUserSetup@1100281004 : TEMPORARY Record 91;
      ExcelBuffer@1100281025 : TEMPORARY Record 370;
      Window@1100281018 : Dialog;
      ExcelTemplatePath@1100281024 : Text[250];
      ExcelSheetName@1100281023 : Text[50];
      StartRowNo@1100281022 : Integer;
      StartColNo@1100281021 : Integer;
      Text000@1100281012 : TextConst 'ENU=Excel not found.;FRA=Programme Excel non trouv‚.';
      Text001@1100281007 : TextConst 'FRA=Veuillez saisir la date de d‚but.';
      Text002@1100281008 : TextConst 'FRA=Veuillez saisir le nombre de semaine.';
      Text003@1100281019 : TextConst 'ENU=Provider No. : #1##############;FRA=Avancement\@1@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@';
      Text005@1100281005 : TextConst 'FRA=Nombre d''interventions par CR';
      StartDate@1100281000 : Date;
      EndDate@1100281001 : Date;
      NbWeek@1100281006 : Integer;
      OtherFaultReason@1100281003 : Code[10];
      TotalCode@1100281015 : Code[10];
      Text006@1100281016 : TextConst 'ENU=Week %1;FRA=Semaine %1';
      NbMainFaultReason@1100281020 : Integer;
      OneSheet@1000000000 : Boolean;
      TotalCpt@1000000001 : Integer;
      CurrCpt@1000000002 : Integer;
      Cpt@1000000003 : Integer;
      Text007@1000000004 : TextConst 'FRA=CR';

    PROCEDURE InitValues@1100281005();
    VAR
      lReportSetup@1100281000 : Record 50115;
      lFound@1100281001 : Boolean;
      lReportID@1100281002 : Integer;
    BEGIN
      lReportID := 50075;
      lFound := lReportSetup.GET(lReportSetup."Object Type"::Report,lReportID,USERID);
      IF NOT lFound THEN
        lFound := lReportSetup.GET(lReportSetup."Object Type"::Report,lReportID,'');

      IF lFound THEN BEGIN
        ExcelTemplatePath := lReportSetup."Excel Template Path";
        ExcelSheetName := lReportSetup."Excel Template Tabs";
        StartRowNo := lReportSetup."First Line";
        StartColNo := lReportSetup."First Column";
      END ELSE BEGIN
        ExcelTemplatePath := '';
        ExcelSheetName  := 'Page1';
      END;

      IF StartRowNo <= 0 THEN
        StartRowNo := 8;

      IF StartColNo <= 0 THEN
        StartColNo := 1;
    END;

    PROCEDURE ExportToExcel@1100281001();
    VAR
      lDate@1100281006 : Record 2000000007;
      lTempMainFaultReason@1100281007 : TEMPORARY Record 50143;
      lTempByDateOther@1100281009 : TEMPORARY Record 50143;
      lRowNo@1100281004 : Integer;
      lColNo@1100281003 : Integer;
      lNbInterv@1100281002 : Integer;
      lNbSheet@1100281001 : Integer;
      lCpt@1100281000 : Integer;
      lEntryNo@1100281008 : Integer;
      lFromCellID@1100281010 : Text[10];
      lToCellID@1100281012 : Text[10];
      lFirstRowNo@1100281013 : Integer;
      lWeekNo@1100281011 : Integer;
      lDayNo@1100281016 : Integer;
      lEndWeekNo@1100281017 : Integer;
      lEndDayNo@1100281018 : Integer;
      lPrevWeekNo@1100281014 : Integer;
      lLen@1100281015 : Integer;
      lCellID@1100281019 : Text[10];
      lFirstLine@1000000000 : Boolean;
    BEGIN
      InitValues;

      IF ExcelTemplatePath <> '' THEN BEGIN
        ExcelBuffer.OpenBookWithoutSheet(ExcelTemplatePath);
      END ELSE BEGIN
        ExcelBuffer.CreateBookWithoutSheet;
      END;

      IF TempUserSetup.FINDLAST THEN BEGIN
        //<<IMH02
        lFirstLine := TRUE;
        IF OneSheet THEN
          lNbSheet := 1
        ELSE //>>IMH02
          lNbSheet := TempUserSetup.COUNT;
        REPEAT

          lEntryNo := 0;
          lTempMainFaultReason.RESET;
          lTempMainFaultReason.DELETEALL;

          lTempByDateOther.RESET;
          lTempByDateOther.DELETEALL;

          TempByFaultReason.RESET;
          TempByFaultReason.SETRANGE("Code 1",TempUserSetup."User ID");
          IF TempByFaultReason.FINDSET THEN
            REPEAT

              lEntryNo := lEntryNo + 1;
              lTempMainFaultReason.INIT;
              lTempMainFaultReason."Integer 1" := TempByFaultReason."Data Integer 1";
              lTempMainFaultReason."Entry No." := lEntryNo;
              lTempMainFaultReason."Data Code 2" := TempByFaultReason."Code 2";
              lTempMainFaultReason."Data Integer 1" := TempByFaultReason."Data Integer 1";
              lTempMainFaultReason.INSERT;
            UNTIL TempByFaultReason.NEXT = 0;

          lTempMainFaultReason.RESET;
          IF lTempMainFaultReason.FINDLAST THEN BEGIN
            lCpt := 0;
            REPEAT
              lCpt := lCpt + 1;
              IF lCpt > NbMainFaultReason THEN BEGIN
                TempByDate.RESET;
                TempByDate.SETRANGE("Code 1",TempUserSetup."User ID");
                TempByDate.SETRANGE("Code 2",lTempMainFaultReason."Data Code 2");
                IF TempByDate.FINDSET THEN BEGIN
                  REPEAT
                    lTempByDateOther.RESET;
                    lTempByDateOther.SETRANGE("Code 1",TempByDate."Code 1");
                    lTempByDateOther.SETRANGE("Code 2",OtherFaultReason);
                    lTempByDateOther.SETRANGE("Date 1",TempByDate."Date 1");
                    IF lTempByDateOther.FINDFIRST THEN BEGIN
                      lTempByDateOther."Data Integer 1" := lTempByDateOther."Data Integer 1" + TempByDate."Data Integer 1";
                      lTempByDateOther.MODIFY;
                    END ELSE BEGIN
                      lTempByDateOther."Code 1" := TempByDate."Code 1";
                      lTempByDateOther."Code 2" := OtherFaultReason;
                      lTempByDateOther."Date 1" := TempByDate."Date 1";
                      lTempByDateOther."Data Integer 1" := TempByDate."Data Integer 1";
                      lTempByDateOther.INSERT;
                    END;

                  UNTIL TempByDate.NEXT = 0;

                  TempByDate.DELETEALL;
                END;

                lTempMainFaultReason.DELETE;
              END;
            UNTIL lTempMainFaultReason.NEXT(-1) = 0;

            lEntryNo := lEntryNo + 1;
            lTempMainFaultReason.INIT;
            lTempMainFaultReason."Integer 1" := - 1;
            lTempMainFaultReason."Entry No." := lEntryNo;
            lTempMainFaultReason."Data Code 2" := OtherFaultReason;
            lTempMainFaultReason.INSERT;

            lTempByDateOther.RESET;
            IF lTempByDateOther.FINDSET THEN
              REPEAT
                TempByDate := lTempByDateOther;
                TempByDate.INSERT;
              UNTIL lTempByDateOther.NEXT = 0;


            lEntryNo := lEntryNo + 1;
            lTempMainFaultReason.INIT;
            lTempMainFaultReason."Integer 1" := - 2;
            lTempMainFaultReason."Entry No." := lEntryNo;
            lTempMainFaultReason."Data Code 2" := TotalCode;
            lTempMainFaultReason.INSERT;
          END;


          //InsertRow(1,1,STRSUBSTNO(Text005),'','');
          IF NOT OneSheet THEN BEGIN//IMH02
            //Export
            ExcelBuffer.DELETEALL;
            InsertRow(3,1,TempUserSetup."User ID",'','');
          END ELSE BEGIN
         // InsertRow(lRowNo,1,Text007,'','');
          END;
          lColNo := StartColNo;
          //<<IMH02
          IF OneSheet THEN BEGIN
            lColNo += 1;
          END;//>>IMH02



          lEndWeekNo := DATE2DWY(EndDate, 2);
          lEndDayNo := DATE2DWY(EndDate, 1);
          IF (OneSheet AND lFirstLine) OR (NOT OneSheet) THEN BEGIN //IMH02
          lRowNo := StartRowNo;
          lDate.SETRANGE("Period Type",lDate."Period Type"::Date);
          lDate.SETRANGE("Period Start",StartDate,EndDate);
          IF lDate.FINDSET THEN BEGIN
            REPEAT
              lColNo := lColNo + 1;

              lWeekNo := DATE2DWY(lDate."Period Start", 2);
              IF lWeekNo <> lPrevWeekNo THEN BEGIN
                InsertRow(lRowNo - 1,lColNo,STRSUBSTNO(Text006,lWeekNo),'','');

                lPrevWeekNo := lWeekNo;
              END;

              InsertRow(lRowNo,lColNo,FORMAT(lDate."Period Start",0,'<Day,2>-<Month Text>'),'','');
            UNTIL lDate.NEXT = 0;

            lColNo := lColNo + 1;
            InsertRow(lRowNo,lColNo,TotalCode,'','');
            lFirstLine := FALSE;//IMH02
          END;
          END;
          lTempMainFaultReason.RESET;
          IF lTempMainFaultReason.FINDLAST THEN BEGIN
            lFirstRowNo := lRowNo + 1;
            REPEAT
              lColNo := StartColNo; //1
              lRowNo := lRowNo + 1;
              //<<IMH02
              IF OneSheet THEN BEGIN
                InsertRow(lRowNo,lColNo,TempUserSetup."User ID",'','');
                lColNo += 1;
              END;//>>IMH02
              InsertRow(lRowNo,lColNo,lTempMainFaultReason."Data Code 2",'','');

              lDate.SETRANGE("Period Type",lDate."Period Type"::Date);
              lDate.SETRANGE("Period Start",StartDate,EndDate);
              IF lDate.FINDSET THEN BEGIN
                REPEAT
                  lColNo := lColNo + 1;
                  lNbInterv := 0;
                  IF lTempMainFaultReason."Data Code 2" <> TotalCode THEN BEGIN
                    TempByDate.RESET;
                    TempByDate.SETRANGE("Code 1",TempUserSetup."User ID");
                    TempByDate.SETRANGE("Code 2",lTempMainFaultReason."Data Code 2");
                    TempByDate.SETRANGE("Date 1",lDate."Period Start");
                    IF TempByDate.FINDFIRST THEN
                      lNbInterv := TempByDate."Data Integer 1";

                    InsertRow(lRowNo,lColNo,FORMAT(lNbInterv),'','');
                  END ELSE BEGIN
                    lFromCellID := GetCellID(lFirstRowNo,lColNo);
                    lToCellID := GetCellID(lRowNo - 1,lColNo);
                    InsertRow(lRowNo,lColNo,'',STRSUBSTNO('=SOMME(%1:%2)',lFromCellID,lToCellID),'');
                  END;
                UNTIL lDate.NEXT = 0;

                lFromCellID := GetCellID(lRowNo,2);
                lToCellID := GetCellID(lRowNo,lColNo);
                lColNo := lColNo + 1;
                InsertRow(lRowNo,lColNo,'',STRSUBSTNO('=SOMME(%1:%2)',lFromCellID,lToCellID),'');
              END;

            UNTIL lTempMainFaultReason.NEXT(-1) = 0;
          END;

          IF NOT OneSheet THEN//IMH02
            ExcelSheetName := TempUserSetup."User ID";
          IF ExcelTemplatePath <> '' THEN BEGIN
            IF ExcelBuffer.GetSheet(ExcelSheetName) THEN BEGIN
              ExcelBuffer.CreateSheet(ExcelSheetName,ExcelSheetName,'','');
            END ELSE
              ExcelBuffer.AddSheet(ExcelSheetName,ExcelSheetName,'','');
          END ELSE
            ExcelBuffer.AddSheet(ExcelSheetName,ExcelSheetName,'','');

        UNTIL TempUserSetup.NEXT(-1) = 0;
      END;

      ViewExcel;
    END;

    PROCEDURE InsertRow@1100281004(RowNo@1000000000 : Integer;ColumnNo@1000000001 : Integer;CellValue@1000000002 : Text[250];CellFormula@1100281000 : Text[250];NbFormat@1000000003 : Text[30]);
    BEGIN
      ExcelBuffer.INIT;
      ExcelBuffer.VALIDATE("Row No.",RowNo);
      ExcelBuffer.VALIDATE("Column No.",ColumnNo);
      ExcelBuffer."Cell Value as Text" := CellValue;
      ExcelBuffer.NumberFormat := NbFormat;
      ExcelBuffer.Formula := CellFormula; //'';
      ExcelBuffer.INSERT;
    END;

    PROCEDURE ViewExcel@1000000002();
    BEGIN

      //IF ExcelTemplatePath <> '' THEN
      //  ExcelBuffer.OpenBook(ExcelTemplatePath,ExcelSheetName)
      //ELSE
      //  ExcelBuffer.CreateBook;

      //ExcelBuffer.CreateSheet(ExcelSheetName,ExcelSheetName,'','');

      ExcelBuffer.GiveUserControl;
      CLEAR(ExcelBuffer);
    END;

    PROCEDURE GetCellID@1100281002(RowNo@1100281001 : Integer;ColumnNo@1100281000 : Integer) : Text[30];
    VAR
      lExcelBuffer@1100281002 : TEMPORARY Record 370;
    BEGIN
      lExcelBuffer.VALIDATE("Row No.",RowNo);
      lExcelBuffer.VALIDATE("Column No.",ColumnNo);
      EXIT(lExcelBuffer.xlColID+lExcelBuffer.xlRowID);
    END;

    BEGIN
    {
      /** @r IMH02 @d 01/02/12 @a ISAT.LLT @v IMH6.01.201 @s Reporting DPR @c Regroupement sur un onglet pour utilisation modŠle */
      /** @r IMH01 @d 28/11/11 @a ISAT.EBA @v IMH6.01.201 @s Reporting DPR @c Report 34 : Nombre d'interventions par CR */
    }
    END.
  }
  RDLDATA
  {
  }
}