OBJECT Report 50167 Delete Storage DT
{
  OBJECT-PROPERTIES
  {
    Date=07/11/13;
    Time=11:06:40;
    Modified=Yes;
    Version List=IMH6.01,MATMUT01;
  }
  PROPERTIES
  {
    ProcessingOnly=Yes;
  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table50124;
        DataItemTableView=SORTING(Service No.,Service Header No.,Entry No.,Tag Entry No.);
        OnPreDataItem=BEGIN
                        DARVASetup.GET;
                        IF FORMAT(DARVASetup."Delete Time DT Storage") = '' THEN BEGIN
                          CurrReport.QUIT;
                        END;
                        DateSituation := CALCDATE(DARVASetup."Delete Time DT Storage",TODAY);

                        StartDate := CURRENTDATETIME;
                        TotalCounter := COUNT;
                        IF GUIALLOWED THEN BEGIN
                          WDialog.OPEN(Text001);
                          WDialog.UPDATE(1,Text004);
                          IF NOT CONFIRM(STRSUBSTNO(Text002,TotalCounter)) THEN BEGIN
                            ERROR(Text003);
                          END;
                        END;
                        ModuloCounter := ROUND(TotalCounter * 5 / 100,1,'>');
                        CurrentCounter := 0;
                      END;

        OnAfterGetRecord=VAR
                           lVendorLedgerEntry@1100281000 : Record 25;
                           lDARVADTStorage@1100281001 : Record 50124;
                           lService@1100281002 : Record 50066;
                         BEGIN
                           CurrentCounter += 1;
                           IF CurrentCounter MOD ModuloCounter = 1 THEN BEGIN
                             IF GUIALLOWED THEN BEGIN
                               WDialog.UPDATE(2,ROUND(CurrentCounter / TotalCounter * 9999,1,'>'));
                             END;
                             COMMIT;
                           END;

                           IF lService.GET("Service No.") THEN BEGIN
                             IF ServiceTemp.GET("Service No.") THEN BEGIN
                               IF ServiceTemp."Corporate Declaration" THEN BEGIN
                                 DELETE(TRUE);
                               END;
                             END ELSE BEGIN
                               IF (lService.Status = lService.Status::Finished) AND (DT2DATE(lService."Closed DateTime") < DateSituation) THEN BEGIN
                                 lVendorLedgerEntry.SETCURRENTKEY("Service No.","Service Header No.","Intervention Line No.");
                                 lVendorLedgerEntry.SETRANGE("Service No.", lService."Service No.");
                                 lVendorLedgerEntry.SETRANGE("DRT Generate", FALSE);
                                 ServiceTemp."Service No." := lService."Service No.";
                                 ServiceTemp."Corporate Declaration" := lVendorLedgerEntry.ISEMPTY;
                                 ServiceTemp.INSERT;
                               END ELSE BEGIN
                                 ServiceTemp."Service No." := lService."Service No.";
                                 ServiceTemp."Corporate Declaration" := FALSE;
                                 ServiceTemp.INSERT;
                               END;
                               IF ServiceTemp."Corporate Declaration" THEN BEGIN
                                 DELETE(TRUE);
                               END;
                             END;
                           END;
                           IF TaskHandler.IsStoppingTime THEN BEGIN
                             TaskHandler.AddExpiredMsg('');
                             CurrReport.QUIT;
                           END;
                         END;

        OnPostDataItem=BEGIN
                         IF GUIALLOWED THEN BEGIN
                           WDialog.CLOSE;
                         END;
                         EndDate := CURRENTDATETIME;
                         MESSAGE(Text004 + ' ' + Text005 + ' ' + FORMAT(StartDate) + ' - ' + Text006 + ' ' + FORMAT(EndDate));
                       END;

      }
      SECTIONS
      {
        { PROPERTIES
          {
            SectionType=Body;
            SectionWidth=12000;
            SectionHeight=846;
          }
          CONTROLS
          {
          }
           }
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=9020;
      Height=3410;
    }
    CONTROLS
    {
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
    }
  }
  CODE
  {
    VAR
      ServiceTemp@1100281000 : TEMPORARY Record 50066;
      DARVASetup@1100281002 : Record 50126;
      TaskHandler@1100281015 : Codeunit 8016715;
      DateSituation@1100281001 : Date;
      WDialog@1100281008 : Dialog;
      TotalCounter@1100281007 : Integer;
      ModuloCounter@1100281006 : Integer;
      CurrentCounter@1100281005 : Integer;
      StartDate@1100281004 : DateTime;
      EndDate@1100281003 : DateTime;
      Text001@1100281014 : TextConst 'ENU=Processing #1###########\@2@@@@@@@@@@@@@@@@@@@@;FRA=Traitement #1###########\@2@@@@@@@@@@@@@@@@@@@@';
      Text002@1100281013 : TextConst 'ENU=%1 lines found\Do you want to continue ?;FRA="%1 Lignes trouv‚es.\Souhaitez-vous continuer ? "';
      Text003@1100281012 : TextConst 'ENU=Processing canceled;FRA=Traitement annul‚';
      Text004@1100281011 : TextConst 'ENU=Delete DT datas;FRA=Suppression des donn‚es DT';
      Text005@1100281010 : TextConst 'ENU=Starting Date;FRA=Date de d‚but';
      Text006@1100281009 : TextConst 'ENU=Ending Date;FRA=Date de fin';

    BEGIN
    {
      /** @r IMH01 @d 25/07/13 @a ISAT.ACO @v IMH6.01.348 @s DARVA Matmut Lot1 t @c Cr‚ation */
    }
    END.
  }
  RDLDATA
  {
  }
}