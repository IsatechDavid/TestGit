OBJECT Report 8016602 Update Dimension
{
  OBJECT-PROPERTIES
  {
    Date=08/07/09;
    Time=16:22:25;
    Version List=ISA6.00,SUPP;
  }
  PROPERTIES
  {
    Permissions=TableData 17=rm,
                TableData 355=rimd;
    CaptionML=[ENU=Update Dimension;
               FRA=Modif. axes ‚critures comptables];
    ProcessingOnly=Yes;
    OnInitReport=BEGIN
                   GeneralLedgerSetup.GET;
                 END;

  }
  DATAITEMS
  {
    { PROPERTIES
      {
        DataItemTable=Table17;
        DataItemTableView=SORTING(Entry No.);
        OnPreDataItem=BEGIN
                        "G/L Entry".COPY(GlobalGLEntry);

                        IF "G/L Entry".COUNT > 50 THEN BEGIN
                          ERROR (Text001,50);
                        END;
                      END;

        OnAfterGetRecord=BEGIN
                           IF ("Global Dimension 1 Code" = BeforeDimension1) AND
                              ("Global Dimension 2 Code" = BeforeDimension2)
                           THEN BEGIN
                             "Global Dimension 1 Code" := AfterDimension1;
                             "Global Dimension 2 Code" := AfterDimension2;
                             AddShortcut(17,"Entry No.","Global Dimension 1 Code","Global Dimension 2 Code");
                             UpdateAnalysisViewEntry("G/L Entry");
                             MODIFY;
                             NbLedgerEntries += 1;
                           END;
                         END;

        OnPostDataItem=BEGIN
                         MESSAGE(Text002,NbLedgerEntries);
                       END;

      }
      SECTIONS
      {
      }
       }
  }
  REQUESTFORM
  {
    PROPERTIES
    {
      Width=5940;
      Height=3740;
    }
    CONTROLS
    {
      { 1000000000;TextBox;3080 ;770  ;2640 ;440  ;SourceExpr=BeforeDimension1;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1)) }
      { 1000000001;Label  ;220  ;220  ;2750 ;440  ;ParentControl=1000000000;
                                                   CaptionML=[ENU=Init Values;
                                                              FRA=Valeurs initiales] }
      { 1000000002;TextBox;3080 ;1320 ;2640 ;440  ;SourceExpr=BeforeDimension2;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2)) }
      { 1000000004;TextBox;3080 ;2530 ;2640 ;440  ;SourceExpr=AfterDimension1;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1)) }
      { 1000000005;Label  ;220  ;1980 ;2750 ;440  ;ParentControl=1000000004;
                                                   CaptionML=[ENU=To replace by;
                                                              FRA=A remplacer par] }
      { 1000000006;TextBox;3080 ;3080 ;2640 ;440  ;SourceExpr=AfterDimension2;
                                                   TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2)) }
      { 1000000003;TextBox;440  ;770  ;2530 ;440  ;Editable=No;
                                                   Focusable=No;
                                                   SourceExpr=GeneralLedgerSetup."Global Dimension 1 Code" }
      { 1000000007;TextBox;440  ;1320 ;2530 ;440  ;Editable=No;
                                                   Focusable=No;
                                                   SourceExpr=GeneralLedgerSetup."Global Dimension 2 Code" }
      { 1000000010;TextBox;440  ;2530 ;2530 ;440  ;Editable=No;
                                                   Focusable=No;
                                                   SourceExpr=GeneralLedgerSetup."Global Dimension 1 Code" }
      { 1000000011;TextBox;440  ;3080 ;2530 ;440  ;Editable=No;
                                                   Focusable=No;
                                                   SourceExpr=GeneralLedgerSetup."Global Dimension 2 Code" }
    }
  }
  REQUESTPAGE
  {
    PROPERTIES
    {
    }
    CONTROLS
    {
      { 1100481000;;Container;
                  ContainerType=ContentArea }

      { 1100481001;1;Group  ;
                  CaptionML=[ENU=Options;
                             FRA=Options];
                  GroupType=Group }

      { 1100481002;2;Group  ;
                  CaptionML=[ENU=From Values;
                             FRA=Valeurs initiales];
                  GroupType=Group }

      { 1100481003;3;Field  ;
                  SourceExpr=BeforeDimension1;
                  CaptionClass='1,1,1';
                  TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1)) }

      { 1100481004;3;Field  ;
                  SourceExpr=BeforeDimension2;
                  CaptionClass='1,1,2';
                  TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2)) }

      { 1100481005;2;Group  ;
                  CaptionML=[ENU=To Values;
                             FRA=A remplacer par];
                  GroupType=Group }

      { 1100481006;3;Field  ;
                  SourceExpr=AfterDimension1;
                  CaptionClass='1,1,1';
                  TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(1)) }

      { 1100481007;3;Field  ;
                  SourceExpr=AfterDimension2;
                  CaptionClass='1,1,2';
                  TableRelation="Dimension Value".Code WHERE (Global Dimension No.=CONST(2)) }

    }
  }
  CODE
  {
    VAR
      BeforeDimension1@1000000000 : Code[20];
      BeforeDimension2@1000000001 : Code[20];
      AfterDimension1@1000000002 : Code[20];
      AfterDimension2@1000000003 : Code[20];
      Text001@1000000004 : TextConst 'ENU=You can''t select same time more than %1 ledger entries.;FRA=Vous ne pouvez pas s‚lectionner plus de %1 ‚critures en mˆme temps.';
      GeneralLedgerSetup@1000000005 : Record 98;
      DimensionValue@1000000006 : Record 349;
      NbLedgerEntries@1000000007 : Integer;
      Text002@1000000008 : TextConst 'ENU=You had modified %1 ledger entries.;FRA=Vous avez modifi‚ %1 ‚critures.';
      LedgerEntryDim@1000000009 : Record 355;
      GlobalGLEntry@1000000010 : Record 17;
      Text003@1000000011 : TextConst 'ENU=Date compression is active on analysis view.\Analysis view can''t be update.;FRA=La compression de date est activ‚e sur certaines vues d''analyse.\Vous devez mettre … jour manuellement vos vues.';

    PROCEDURE AddShortcut@1000000001(TableNo@1000000000 : Integer;EntryNo@1000000002 : Integer;DimValue1@1000000003 : Code[20];DimValue2@1000000004 : Code[20]);
    BEGIN
      LedgerEntryDim.SETRANGE("Table ID",TableNo);
      LedgerEntryDim.SETRANGE("Entry No.",EntryNo);

      IF GeneralLedgerSetup."Global Dimension 2 Code" = '' THEN BEGIN
        LedgerEntryDim.SETFILTER("Dimension Code",STRSUBSTNO('=%1',GeneralLedgerSetup."Global Dimension 1 Code"));
      END;

      IF GeneralLedgerSetup."Global Dimension 1 Code" = '' THEN BEGIN
        LedgerEntryDim.SETFILTER("Dimension Code",STRSUBSTNO('=%1',GeneralLedgerSetup."Global Dimension 2 Code"));
      END;

      IF (GeneralLedgerSetup."Global Dimension 1 Code" <> '') AND
         (GeneralLedgerSetup."Global Dimension 2 Code" <> '')
      THEN BEGIN
        LedgerEntryDim.SETFILTER(
          "Dimension Code",
          STRSUBSTNO('=%1|=%2',
            GeneralLedgerSetup."Global Dimension 1 Code",
            GeneralLedgerSetup."Global Dimension 2 Code"));
      END;

      IF LedgerEntryDim.FIND('-') THEN BEGIN
        LedgerEntryDim.DELETEALL;
      END;

      IF DimValue1 <> '' THEN BEGIN
        LedgerEntryDim."Table ID" := TableNo;
        LedgerEntryDim."Entry No." := EntryNo;
        LedgerEntryDim."Dimension Code" := GeneralLedgerSetup."Global Dimension 1 Code";
        LedgerEntryDim."Dimension Value Code" := DimValue1;
        IF LedgerEntryDim.FIND('=') THEN BEGIN
          LedgerEntryDim.MODIFY;
        END ELSE BEGIN
          LedgerEntryDim.INSERT;
        END;
      END;

      IF DimValue2 <> '' THEN BEGIN
        LedgerEntryDim."Table ID" := TableNo;
        LedgerEntryDim."Entry No." := EntryNo;
        LedgerEntryDim."Dimension Code" := GeneralLedgerSetup."Global Dimension 2 Code";
        LedgerEntryDim."Dimension Value Code" := DimValue2;
        IF LedgerEntryDim.FIND('=') THEN BEGIN
          LedgerEntryDim.MODIFY;
        END ELSE BEGIN
          LedgerEntryDim.INSERT;
        END;
      END;
    END;

    PROCEDURE SetReport@1000000002(VAR locGLEntry@1000000002 : Record 17;parDimension1@1000000001 : Code[20];parDimension2@1000000000 : Code[20]);
    BEGIN
      BeforeDimension1 := parDimension1;
      BeforeDimension2 := parDimension2;
      AfterDimension1 := parDimension1;
      AfterDimension2 := parDimension2;
      GlobalGLEntry.COPY(locGLEntry);
    END;

    PROCEDURE UpdateAnalysisViewEntry@1000000003(GLEntry@1000000003 : Record 17);
    VAR
      AnalysisView@1000000000 : Record 363;
      AnalysisViewEntry@1000000001 : Record 365;
      AnalysisViewEntryNew@1000000002 : Record 365;
    BEGIN
      AnalysisView.RESET;
      AnalysisView.SETRANGE("Date Compression",AnalysisView."Date Compression"::None);
      IF AnalysisView.FIND('-') THEN BEGIN
        REPEAT
          AnalysisViewEntry.RESET;
          AnalysisViewEntry.SETRANGE("Analysis View Code",AnalysisView.Code);
          AnalysisViewEntry.SETRANGE("Entry No.",GLEntry."Entry No.");
          IF AnalysisViewEntry.FIND('-') THEN BEGIN
            REPEAT
              AnalysisViewEntryNew := AnalysisViewEntry;

              IF GeneralLedgerSetup."Global Dimension 1 Code" <> '' THEN BEGIN
                CASE GeneralLedgerSetup."Global Dimension 1 Code" OF
                  AnalysisView."Dimension 1 Code" :
                    AnalysisViewEntryNew."Dimension 1 Value Code" := GLEntry."Global Dimension 1 Code";
                  AnalysisView."Dimension 2 Code" :
                    AnalysisViewEntryNew."Dimension 2 Value Code" := GLEntry."Global Dimension 1 Code";
                  AnalysisView."Dimension 3 Code" :
                    AnalysisViewEntryNew."Dimension 3 Value Code" := GLEntry."Global Dimension 1 Code";
                  AnalysisView."Dimension 4 Code" :
                    AnalysisViewEntryNew."Dimension 4 Value Code" := GLEntry."Global Dimension 1 Code";
                 END;
              END;

              IF GeneralLedgerSetup."Global Dimension 2 Code" <> '' THEN BEGIN
                CASE GeneralLedgerSetup."Global Dimension 2 Code" OF
                  AnalysisView."Dimension 1 Code" :
                    AnalysisViewEntryNew."Dimension 1 Value Code" := GLEntry."Global Dimension 2 Code";
                  AnalysisView."Dimension 2 Code" :
                    AnalysisViewEntryNew."Dimension 2 Value Code" := GLEntry."Global Dimension 2 Code";
                  AnalysisView."Dimension 3 Code" :
                    AnalysisViewEntryNew."Dimension 3 Value Code" := GLEntry."Global Dimension 2 Code";
                  AnalysisView."Dimension 4 Code" :
                    AnalysisViewEntryNew."Dimension 4 Value Code" := GLEntry."Global Dimension 2 Code";
                 END;
              END;

              IF AnalysisViewEntryNew.INSERT THEN BEGIN
                AnalysisViewEntry.DELETE
              END;

            UNTIL AnalysisViewEntry.NEXT = 0;
          END;
        UNTIL AnalysisView.NEXT = 0;
      END ELSE BEGIN
        MESSAGE(Text003);
      END;
    END;

    BEGIN
    {
      /** @r ISA01 @d 07/07/09 @a ISAT.MA @v ISA6.00.00.01 @s Migration v6 **/
    }
    END.
  }
  RDLDATA
  {
  }
}