OBJECT Table 10865 Payment Header
{
  OBJECT-PROPERTIES
  {
    Date=19/08/11;
    Time=12:00:00;
    Modified=Yes;
    Version List=NAVFR6.00,SEPA;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 IF FORM.RUNMODAL(FORM::"Payment Class List", GetProcess) = ACTION::LookupOK THEN
                   Process := GetProcess;
                 Process.TESTFIELD("Header No. Series");
                 NoSeriesMgt.InitSeries(Process."Header No. Series", xRec."No. Series", 0D, "No.","No. Series");
                 VALIDATE("Payment Class", Process.Code);
               END;
               InitHeader;
             END;

    OnDelete=BEGIN
               IF "Status No.">0 THEN
                ERROR(Text000);

               RegLine.SETRANGE(RegLine."No.","No.");
               RegLine.SETFILTER("Copied To No.",'<>''''');
               IF RegLine.FIND('-') THEN
                ERROR(Text000);
               RegLine.SETRANGE("Copied To No.");
               RegLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Payment Header;
               FRA=En-tˆte bordereau];
    PasteIsValid=No;
    LookupFormID=Form10870;
    DrillDownFormID=Form10870;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;OnValidate=BEGIN
                                                                IF "No." <> xRec."No." THEN BEGIN
                                                                  Process := GetProcess;
                                                                  NoSeriesMgt.TestManual(Process."Header No. Series");
                                                                  "No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              FRA=Nø];
                                                   Editable=No }
    { 2   ;   ;Currency Code       ;Code10        ;TableRelation=Currency;
                                                   OnValidate=VAR
                                                                PaymentLine@1120000 : Record 10866;
                                                                CompanyBank@1120001 : Record 270;
                                                              BEGIN
                                                                IF "Account Type" = "Account Type"::"Bank Account" THEN
                                                                  IF CompanyBank.GET("Account No.") THEN
                                                                    IF CompanyBank."Currency Code" <> '' THEN
                                                                      ERROR(Text008,CompanyBank."Currency Code");

                                                                IF CurrFieldNo <> FIELDNO("Currency Code") THEN
                                                                  UpdateCurrencyFactor
                                                                ELSE BEGIN
                                                                  IF "Currency Code" <> xRec."Currency Code" THEN BEGIN
                                                                    PaymentLine.SETRANGE("No.","No.");
                                                                    IF PaymentLine.FIND('-') THEN
                                                                     ERROR(Text002);
                                                                    UpdateCurrencyFactor;
                                                                END ELSE
                                                                    IF "Currency Code" <> '' THEN BEGIN
                                                                      UpdateCurrencyFactor;
                                                                      IF "Currency Factor" <> xRec."Currency Factor" THEN
                                                                        ConfirmUpdateCurrencyFactor;
                                                                    END;
                                                                END;
                                                                IF "Currency Code" <> xRec."Currency Code" THEN BEGIN
                                                                  PaymentLine.INIT;
                                                                  PaymentLine.SETRANGE("No.", "No.");
                                                                  PaymentLine.MODIFYALL("Currency Code", "Currency Code");
                                                                  PaymentLine.MODIFYALL("Currency Factor", "Currency Factor");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Currency Code;
                                                              FRA=Code devise] }
    { 3   ;   ;Currency Factor     ;Decimal       ;OnValidate=VAR
                                                                PaymentLine@1120000 : Record 10866;
                                                              BEGIN
                                                                PaymentLine.SETRANGE("No.","No.");
                                                                IF PaymentLine.FIND('-') THEN
                                                                  REPEAT
                                                                    PaymentLine."Currency Factor" := "Currency Factor";
                                                                    PaymentLine.VALIDATE(Amount);
                                                                    PaymentLine.MODIFY;
                                                                  UNTIL PaymentLine.NEXT = 0;
                                                              END;

                                                   CaptionML=[ENU=Currency Factor;
                                                              FRA=Facteur devise];
                                                   DecimalPlaces=0:15 }
    { 4   ;   ;Posting Date        ;Date          ;OnValidate=BEGIN
                                                                IF "Posting Date" <> xRec."Posting Date" THEN BEGIN
                                                                  CLEAR(RegLine);
                                                                  RegLine.SETRANGE("No.", "No.");
                                                                  RegLine.MODIFYALL("Posting Date", "Posting Date");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Posting Date;
                                                              FRA=Date comptabilisation] }
    { 5   ;   ;Document Date       ;Date          ;OnValidate=BEGIN
                                                                IF "Document Date" <> xRec."Document Date" THEN BEGIN
                                                                  CLEAR(RegLine);
                                                                  RegLine.SETRANGE("No.", "No.");
                                                                  IF RegLine.FIND('-') THEN
                                                                    REPEAT
                                                                      RegLine.UpdateDueDate("Document Date");
                                                                    UNTIL RegLine.NEXT = 0;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Document Date;
                                                              FRA=Date document] }
    { 6   ;   ;Payment Class       ;Text30        ;TableRelation="Payment Class";
                                                   OnValidate=BEGIN
                                                                VALIDATE("Status No.");
                                                              END;

                                                   CaptionML=[ENU=Payment Class;
                                                              FRA=Type de rŠglement] }
    { 7   ;   ;Status No.          ;Integer       ;TableRelation="Payment Status".Line WHERE (Payment Class=FIELD(Payment Class));
                                                   OnValidate=VAR
                                                                PaymentStep@1120000 : Record 10862;
                                                                PaymentStatus@1120001 : Record 10861;
                                                              BEGIN
                                                                PaymentStep.SETRANGE("Payment Class", "Payment Class");
                                                                PaymentStep.SETFILTER("Next Status", '>%1', "Status No.");
                                                                PaymentStep.SETRANGE(PaymentStep."Action Type", PaymentStep."Action Type"::Ledger);
                                                                IF PaymentStep.FIND('-') THEN
                                                                 IF PaymentStep."Source Code"<>'' THEN //ISA01.ST
                                                                  "Source Code" := PaymentStep."Source Code";
                                                                PaymentStatus.GET("Payment Class", "Status No.");
                                                                "Archiving Authorized" := PaymentStatus."Archiving Authorized";
                                                              END;

                                                   CaptionML=[ENU=Status No.;
                                                              FRA=Nø statut] }
    { 8   ;   ;Status Name         ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Payment Status".Name WHERE (Payment Class=FIELD(Payment Class),
                                                                                                   Line=FIELD(Status No.)));
                                                   CaptionML=[ENU=Status Name;
                                                              FRA=Nom statut];
                                                   Editable=No }
    { 9   ;   ;Shortcut Dimension 1 Code;Code20   ;OnValidate=BEGIN
                                                                ValidateShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                                MODIFY;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupShortcutDimCode(1,"Shortcut Dimension 1 Code");
                                                            END;

                                                   CaptionML=[ENU=Shortcut Dimension 1 Code;
                                                              FRA=Code raccourci axe 1];
                                                   CaptionClass='1,2,1' }
    { 10  ;   ;Shortcut Dimension 2 Code;Code20   ;OnValidate=BEGIN
                                                                ValidateShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                                MODIFY;
                                                              END;

                                                   OnLookup=BEGIN
                                                              LookupShortcutDimCode(2,"Shortcut Dimension 2 Code");
                                                            END;

                                                   CaptionML=[ENU=Shortcut Dimension 2 Code;
                                                              FRA=Code raccourci axe 2];
                                                   CaptionClass='1,2,2' }
    { 11  ;   ;Payment Class Name  ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Payment Class".Name WHERE (Code=FIELD(Payment Class)));
                                                   CaptionML=[ENU=Payment Class Name;
                                                              FRA=Nom type de rŠglement];
                                                   Editable=No }
    { 12  ;   ;No. Series          ;Code10        ;CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø] }
    { 13  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   CaptionML=[ENU=Source Code;
                                                              FRA=Code journal] }
    { 14  ;   ;Account Type        ;Option        ;OnValidate=BEGIN
                                                                IF "Account Type" <> xRec."Account Type" THEN BEGIN
                                                                  VALIDATE("Account No.", '');
                                                                  DimensionDelete;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Account Type;
                                                              FRA=Type compte];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset;
                                                                    FRA=G‚n‚ral,Client,Fournisseur,Banque,Immobilisation];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset }
    { 15  ;   ;Account No.         ;Code20        ;TableRelation=IF (Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Account Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Account No." <> xRec."Account No." THEN BEGIN
                                                                  DimensionDelete;
                                                                  IF "Account No." <> '' THEN
                                                                    DimensionSetup;
                                                                END;
                                                                IF "Account Type" = "Account Type"::"Bank Account" THEN BEGIN
                                                                  IF CompanyBank.GET("Account No.") THEN BEGIN
                                                                //<<ISA01.ST
                                                                    IF NOT CompanyBank."RIB Checked" THEN
                                                                      MESSAGE(Text8016600);
                                                                    IF CompanyBank."Source Code" = '' THEN
                                                                      MESSAGE(Text8016601);
                                                                    VALIDATE("Source Code",CompanyBank."Source Code");
                                                                //>>ISA01.ST
                                                                    IF "Currency Code" = '' THEN
                                                                      IF CompanyBank."Currency Code" <> '' THEN
                                                                        ERROR(Text006);
                                                                    IF "Currency Code" <> '' THEN
                                                                      IF (CompanyBank."Currency Code" <> "Currency Code") AND (CompanyBank."Currency Code" <> '') THEN
                                                                        ERROR(Text007,"Currency Code");
                                                                    "Bank Branch No." := CompanyBank."Bank Branch No.";
                                                                    "Bank Account No." := CompanyBank."Bank Account No.";
                                                                    IBAN := CompanyBank.IBAN;
                                                                    "SWIFT Code" := CompanyBank."SWIFT Code";
                                                                    "Bank Country/Region Code" := CompanyBank."Country/Region Code";
                                                                    "Agency Code" := CompanyBank."Agency Code";
                                                                    "RIB Key" := CompanyBank."RIB Key";
                                                                    "RIB Checked" := CompanyBank."RIB Checked";
                                                                    "Bank Name" := CompanyBank.Name;
                                                                    "Bank Post Code" := CompanyBank."Post Code";
                                                                    "Bank City" := CompanyBank.City;
                                                                    "Bank Name 2" := CompanyBank."Name 2";
                                                                    "Bank Address" := CompanyBank.Address;
                                                                    "Bank Address 2" := CompanyBank."Address 2";
                                                                    "National Issuer No." := CompanyBank."National Issuer No.";
                                                                  END ELSE
                                                                    InitBankAccount;
                                                                END ELSE
                                                                   InitBankAccount;
                                                              END;

                                                   CaptionML=[ENU=Account No.;
                                                              FRA=Nø compte] }
    { 16  ;   ;Amount (LCY)        ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Payment Line"."Amount (LCY)" WHERE (No.=FIELD(No.)));
                                                   CaptionML=[ENU=Amount (LCY);
                                                              FRA=Montant DS];
                                                   Editable=No }
    { 17  ;   ;Amount              ;Decimal       ;FieldClass=FlowField;
                                                   CalcFormula=Sum("Payment Line".Amount WHERE (No.=FIELD(No.)));
                                                   CaptionML=[ENU=Amount;
                                                              FRA=Montant];
                                                   Editable=No }
    { 18  ;   ;Bank Branch No.     ;Text20        ;OnValidate=BEGIN
                                                                "RIB Checked" := RibKey.Check("Bank Branch No.","Agency Code","Bank Account No.","RIB Key");
                                                              END;

                                                   CaptionML=[ENU=Bank Branch No.;
                                                              FRA=Code ‚tablissement] }
    { 19  ;   ;Bank Account No.    ;Text30        ;OnValidate=BEGIN
                                                                "RIB Checked" := RibKey.Check("Bank Branch No.","Agency Code","Bank Account No.","RIB Key");
                                                              END;

                                                   CaptionML=[ENU=Bank Account No.;
                                                              FRA=Nø compte bancaire] }
    { 20  ;   ;Agency Code         ;Text20        ;OnValidate=BEGIN
                                                                "RIB Checked" := RibKey.Check("Bank Branch No.","Agency Code","Bank Account No.","RIB Key");
                                                              END;

                                                   CaptionML=[ENU=Agency Code;
                                                              FRA=Code agence] }
    { 21  ;   ;RIB Key             ;Integer       ;OnValidate=BEGIN
                                                                "RIB Checked" := RibKey.Check("Bank Branch No.","Agency Code","Bank Account No.","RIB Key");
                                                              END;

                                                   CaptionML=[ENU=RIB Key;
                                                              FRA=Cl‚ RIB] }
    { 22  ;   ;RIB Checked         ;Boolean       ;CaptionML=[ENU=RIB Checked;
                                                              FRA=V‚rification RIB];
                                                   Editable=No }
    { 23  ;   ;Bank Name           ;Text50        ;CaptionML=[ENU=Bank Name;
                                                              FRA=Nom de la banque] }
    { 24  ;   ;Bank Post Code      ;Code20        ;TableRelation="Post Code";
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode("Bank City","Bank Post Code","Bank Country/Region Code");        //IMH01 Ajout Code pays
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode("Bank City","Bank Post Code","Bank Country/Region Code",TRUE);    //IMH01 Ajout Code pays
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Bank Post Code;
                                                              FRA=Code postal banque] }
    { 25  ;   ;Bank City           ;Text30        ;OnValidate=BEGIN
                                                                PostCode.ValidatePostCode("Bank City","Bank Post Code","Bank Country/Region Code");       //IMH01 Ajout Code pays
                                                              END;

                                                   OnLookup=BEGIN
                                                              PostCode.LookUpPostCode("Bank City","Bank Post Code","Bank Country/Region Code",TRUE);   //IMH01 Ajout Code pays
                                                            END;

                                                   CaptionML=[ENU=Bank City;
                                                              FRA=Ville banque] }
    { 26  ;   ;Bank Name 2         ;Text50        ;CaptionML=[ENU=Bank Name 2;
                                                              FRA=Nom 2 banque] }
    { 27  ;   ;Bank Address        ;Text50        ;CaptionML=[ENU=Bank Address;
                                                              FRA=Adresse banque] }
    { 28  ;   ;Bank Address 2      ;Text50        ;CaptionML=[ENU=Bank Address 2;
                                                              FRA=Adresse banque (2Šme ligne)] }
    { 29  ;   ;Bank Contact        ;Text50        ;CaptionML=[ENU=Bank Contact;
                                                              FRA=Contact banque] }
    { 30  ;   ;Bank County         ;Text30        ;CaptionML=[ENU=Bank County;
                                                              FRA=R‚gion banque] }
    { 31  ;   ;Bank Country/Region Code;Code10    ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Bank Country/Region Code;
                                                              FRA=Code pays/r‚gion banque] }
    { 32  ;   ;National Issuer No. ;Code6         ;CaptionML=[ENU=National Issuer No.;
                                                              FRA=Nø ‚metteur national];
                                                   Numeric=Yes }
    { 40  ;   ;Parameter           ;Boolean       ;CaptionML=[ENU=Parameter;
                                                              FRA=ParamŠtre];
                                                   Editable=No }
    { 41  ;   ;No. of Lines        ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Payment Line" WHERE (No.=FIELD(No.)));
                                                   CaptionML=[ENU=No. of Lines;
                                                              FRA=Nbre de lignes];
                                                   Editable=No }
    { 42  ;   ;No. of Unposted Lines;Integer      ;FieldClass=FlowField;
                                                   CalcFormula=Count("Payment Line" WHERE (No.=FIELD(No.),
                                                                                           Posted=CONST(No)));
                                                   CaptionML=[ENU=No. of Unposted Lines;
                                                              FRA=Nombre de lignes non valid‚es];
                                                   Editable=No }
    { 43  ;   ;Archiving Authorized;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Payment Status"."Archiving Authorized" WHERE (Payment Class=FIELD(Payment Class),
                                                                                                                     Line=FIELD(Status No.)));
                                                   CaptionML=[ENU=Archiving Authorized;
                                                              FRA=Archivage autoris‚];
                                                   Editable=No }
    { 50  ;   ;IBAN                ;Code50        ;OnValidate=VAR
                                                                CompanyInfo@1120000 : Record 79;
                                                              BEGIN
                                                                CompanyInfo.CheckIBAN(IBAN);
                                                              END;

                                                   CaptionML=[ENU=IBAN;
                                                              FRA=Nø compte international (IBAN)] }
    { 51  ;   ;SWIFT Code          ;Code20        ;CaptionML=[ENU=SWIFT Code;
                                                              FRA=Code SWIFT] }
    { 8016602;;Sales Document Type ;Code1         ;CaptionML=[ENU=Sales document type;
                                                              FRA=Type de document vente];
                                                   Description=ISA5.00.00.00 }
    { 8016603;;Sales Document No.  ;Code20        ;CaptionML=[ENU=Sales Document No.;
                                                              FRA=Nø document vente];
                                                   Description=ISA5.00.00.00 }
    { 8016605;;Payment Method Code ;Code10        ;TableRelation="Payment Method".Code;
                                                   CaptionML=[ENU=Payment Method Code;
                                                              FRA=Code mode rŠglement] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Posting Date                             }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.                                      }
  }
  CODE
  {
    VAR
      Text000@1120000 : TextConst 'ENU=Deleting not allowed;FRA=Suppression impossible';
      Text001@1120001 : TextConst 'ENU=There is no line to treat;FRA=Il n''y aucune ligne … traiter';
      Text002@1120002 : TextConst 'ENU=You cannot modify Currency Code because the Payment Header contains lines;FRA=Impossible de modifier le code devise sur un bordereau contenant des lignes';
      Text003@1120003 : TextConst 'ENU=Payment;FRA=Traite';
      DimManagement@1120004 : Codeunit 408;
      Process@1120005 : Record 10860;
      NoSeriesMgt@1120006 : Codeunit 396;
      ReglHeader@1120007 : Record 10865;
      ListeProcess@1120008 : Form 10860;
      GetProcess@1120009 : Record 10860;
      CurrencyDate@1120010 : Date;
      CurrExchRate@1120011 : Record 330;
      RegLine@1120012 : Record 10866;
      CompanyBank@1120014 : Record 270;
      RibKey@1120015 : Codeunit 10801;
      PostCode@1120013 : Record 225;
      DefaultDimension@1120016 : Record 352;
      Text004@1120018 : TextConst 'ENU=You have changed a dimension.\\;FRA=Vous avez modifi‚ un axe analytique.\\';
      Text005@1120017 : TextConst 'ENU=Do you want to update the lines?;FRA=Souhaitez-vous mettre les lignes … jour ?';
      ConfirmDialog@1120019 : Boolean;
      DoOperation@1120020 : Boolean;
      Text006@1120021 : TextConst 'ENU=The currency code for the document is the LCY Code.\\Please select a bank for which the currency code is the LCY Code.;FRA=Le code devise du document est la devise soci‚t‚.\\S‚lectionnez une banque dont le code devise est la devise soci‚t‚.';
      Text007@1120022 : TextConst 'ENU=The currency code for the document is %1.\\Please select a bank for which the currency code is %1 or the LCY Code.;FRA=Le code devise du document est %1.\\S‚lectionnez une banque dont le code devise est %1 ou la devise soci‚t‚.';
      Text008@1120023 : TextConst 'ENU=Your bank''s currency code is %1.\\You must change the bank account code before modifying the currency code.;FRA=La devise de votre banque est %1.\\Vous devez changer de code banque avant de modifier le code devise.';
      Text8016600@8016600 : TextConst 'ENU=RIB hasn''t been checked.;FRA=Le RIB n''a pas ‚t‚ v‚rifi‚.';
      Text8016601@8016601 : TextConst 'ENU=Bank source code is empty.;FRA=Le code journal de la banque n''est pas renseign‚.';

    PROCEDURE LookupShortcutDimCode@1120004(FieldNo@1120000 : Integer;VAR ShortcutDimCode@1120001 : Code[20]);
    BEGIN
      DimManagement.LookupDimValueCode(FieldNo,ShortcutDimCode);
      IF "No." <> '' THEN
        DimManagement.SaveDocDim(
          DATABASE::"Payment Header",6,"No.",0,FieldNo,ShortcutDimCode)
      ELSE
        DimManagement.SaveTempDim(FieldNo,ShortcutDimCode);
    END;

    PROCEDURE ValidateShortcutDimCode@1120003(FieldNo@1120000 : Integer;VAR ShortcutDimCode@1120001 : Code[20]);
    BEGIN
      DimManagement.ValidateDimValueCode(FieldNo,ShortcutDimCode);
      IF "No." <> '' THEN
        DimManagement.SaveDocDim(
          DATABASE::"Payment Header",6,"No.",0,FieldNo,ShortcutDimCode)
      ELSE
        DimManagement.SaveTempDim(FieldNo,ShortcutDimCode);
    END;

    PROCEDURE AssistEdit@1120002(OldReglHeader@1120000 : Record 10865) : Boolean;
    BEGIN
      WITH ReglHeader DO BEGIN
        ReglHeader := Rec;
        Process := GetProcess;

        Process.TESTFIELD("Header No. Series");
        IF NoSeriesMgt.SelectSeries(Process."Header No. Series",OldReglHeader."No. Series","No. Series") THEN BEGIN
        Process := GetProcess;

          Process.TESTFIELD("Header No. Series");
          NoSeriesMgt.SetSeries("No.");
          Rec := ReglHeader;
          EXIT(TRUE);
        END;
      END;
    END;

    LOCAL PROCEDURE UpdateCurrencyFactor@1120001();
    BEGIN
      IF "Currency Code" <> '' THEN BEGIN
        CurrencyDate := WORKDATE;
        "Currency Factor" := CurrExchRate.ExchangeRate(CurrencyDate,"Currency Code");
      END ELSE
        "Currency Factor" := 1;
    END;

    LOCAL PROCEDURE ConfirmUpdateCurrencyFactor@1120000();
    BEGIN
      {IF HideValidationDialog THEN
        Confirmed := TRUE
      ELSE
        Confirmed := CONFIRM(Text021,FALSE);
      IF Confirmed THEN
        VALIDATE("Currency Factor")
      ELSE
      }  "Currency Factor" := xRec."Currency Factor";
    END;

    PROCEDURE InitBankAccount@1120005();
    BEGIN
      "Bank Branch No." := '';
      "Bank Account No." := '';
      IBAN := '';
      "SWIFT Code" := '';
      "Agency Code" := '';
      "RIB Key" := 0;
      "RIB Checked" := FALSE;
      "Bank Name" := '';
      "Bank Post Code" := '';
      "Bank City" := '';
      "Bank Name 2" := '';
      "Bank Address" := '';
      "Bank Address 2" := '';
      "Bank Contact" := '';
      "Bank County" := '';
      "Bank Country/Region Code" := '';
      "National Issuer No." := '';
    END;

    PROCEDURE TestNbOfLines@1120006();
    BEGIN
      CALCFIELDS("No. of Lines");
      IF "No. of Lines" = 0 THEN
        ERROR(Text001);
    END;

    PROCEDURE InitHeader@1120007();
    VAR
      CompanyInformation@1120000 : Record 79;
    BEGIN
      "Posting Date" := WORKDATE;
      "Document Date" := WORKDATE;
      VALIDATE("Account Type", "Account Type"::"Bank Account");
      {
      CompanyInformation.GET;
      VALIDATE("Account No.", CompanyInformation."Default Bank Account No.");
      }
      //<<ISA03.ST
      IF Process."Default Payment Method Code" <> '' THEN
        VALIDATE("Payment Method Code", Process."Default Payment Method Code");
      //>>ISA03.ST
    END;

    PROCEDURE DimensionSetup@1120010();
    VAR
      PaymentManagement@1120000 : Codeunit 10860;
    BEGIN
      CLEAR(DefaultDimension);
      DimensionCreate;
    END;

    PROCEDURE DimensionCreate@1120009();
    VAR
      DocumentDimension@1120000 : Record 357;
      TableID@1120002 : ARRAY [10] OF Integer;
      No@1120001 : ARRAY [10] OF Code[20];
      PaymentManagement@1120003 : Codeunit 10860;
    BEGIN
      TableID[1] := DimManagement.TypeToTableID1("Account Type");
      No[1] := "Account No.";
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
      DimManagement.GetDefaultDim(
        TableID,No,"Source Code",
        "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");
      IF "No." <> '' THEN
        DimManagement.UpdateDocDefaultDim(
          DATABASE::"Payment Header",6,"No.",0,
          "Shortcut Dimension 1 Code","Shortcut Dimension 2 Code");

      DocumentDimension.SETRANGE("Table ID",DATABASE::"Payment Header");
      DocumentDimension.SETRANGE("Document Type",DocumentDimension."Document Type"::" ");
      DocumentDimension.SETRANGE("Document No.","No.");
      IF DocumentDimension.FIND('-') THEN
        REPEAT
          UpdateLineDim(DocumentDimension);
          ConfirmDialog := FALSE;
        UNTIL DocumentDimension.NEXT = 0;
    END;

    PROCEDURE DimensionDelete@1120008();
    VAR
      DocumentDimension@1120000 : Record 357;
    BEGIN
      ConfirmDialog := TRUE;
      WITH DocumentDimension DO BEGIN
        SETRANGE("Table ID", DATABASE::"Payment Header");
        SETRANGE("Document Type", DocumentDimension."Document Type"::" ");
        SETRANGE("Document No.", "No.");
        SETRANGE("Line No.", 0);
        IF FIND('-') THEN
          REPEAT
            Rec.DeleteLineDim(DocumentDimension);
            ConfirmDialog := FALSE;
            DELETE;
          UNTIL NEXT = 0;
      END;
      "Shortcut Dimension 1 Code" := '';
      "Shortcut Dimension 2 Code" := '';
    END;

    PROCEDURE DeleteLineDim@1120011(DocumentDimension@1120001 : Record 357);
    VAR
      DeleteDocDim@1120002 : Record 357;
    BEGIN
      RegLine.SETRANGE("No.","No.");
      IF NOT RegLine.FIND('-') THEN
        EXIT;
      IF ConfirmDialog THEN
        DoOperation := CONFIRM(Text004+Text005);
      WITH DeleteDocDim DO BEGIN
        SETRANGE("Table ID",DATABASE::"Payment Line");
        SETRANGE("Document Type",DocumentDimension."Document Type");
        SETRANGE("Document No.",DocumentDimension."Document No.");
        SETRANGE("Dimension Code",DocumentDimension."Dimension Code");
        IF DoOperation AND FIND('-') THEN
          DeleteDocDim.DELETE;
      END;
    END;

    PROCEDURE UpdateLineDim@1120014(DocumentDimension@1120000 : Record 357);
    VAR
      NewDocDim@1120001 : Record 357;
    BEGIN
      RegLine.SETRANGE("No.","No.");
      IF NOT RegLine.FIND('-') THEN
        EXIT;
      IF ConfirmDialog THEN
        DoOperation := CONFIRM(Text004+Text005);
      IF DoOperation THEN
        WITH NewDocDim DO BEGIN
          "Table ID" := DATABASE::"Payment Line";
          "Document Type" := DocumentDimension."Document Type";
          "Document No." := DocumentDimension."Document No.";
          "Dimension Code" := DocumentDimension."Dimension Code";
          "Dimension Value Code" := DocumentDimension."Dimension Value Code";
          IF RegLine.FIND('-') THEN
            REPEAT
              NewDocDim."Line No." := RegLine."Line No.";
              IF NOT NewDocDim.MODIFY THEN
                NewDocDim.INSERT(TRUE);
            UNTIL RegLine.NEXT = 0;
        END;
    END;

    PROCEDURE GetAccountName@8016602() : Text[50];
    VAR
      lGLAccount@8016602 : Record 15;
      LCustomer@8016603 : Record 18;
      Lvendor@8016604 : Record 23;
      LFixedAsset@8016605 : Record 5600;
    BEGIN
            //ISA02.ST
            CASE "Account Type" OF
              "Account Type"::"G/L Account" :
                IF lGLAccount.GET("Account No.") THEN
                  EXIT(lGLAccount.Name);
              "Account Type"::Customer :
                IF LCustomer.GET("Account No.") THEN
                  EXIT(LCustomer.Name);
              "Account Type"::Vendor :
                IF Lvendor.GET("Account No.") THEN
                  EXIT(Lvendor.Name);
              "Account Type"::"Bank Account" :
                EXIT("Bank Name");
              "Account Type"::"Fixed Asset" :
                IF LFixedAsset.GET("Account No.") THEN
                  EXIT(LFixedAsset.Description);
            END; //CASE
            EXIT('');
    END;

    BEGIN
    {
      /** @r IHM01 @d 10/02/11 @a ISAT.ZW @v IMH6.01.001 @s Normalisation des donn‚es
          @c Associer les Pays au code postaux - descendre le code pays automatiquement*/
      /**@r ISA03 @d 22/07/09 @a ISAT.ST @v ISA6.00.00.04 @s Suivi rŠglement
         @c Ajout code mode rŠglement + init */
      /**@r ISA02 @d 28/01/10 @a ISAT.ST @v ISA6.00.00.06 @s Gestion bordereaux
         @c Ajout GetAccountName */
      /**@r ISA01 @d 06/07/09 @a ISAT.ST @v ISA6.00.00.01 @s migration ISATECH +
         @c v‚rification RIB, suivi Source Code
         @c Ajout champ type doc vente, nø doc vente */
    }
    END.
  }
}