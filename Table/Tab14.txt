OBJECT Table 14 Location
{
  OBJECT-PROPERTIES
  {
    Date=11/02/11;
    Time=11:14:57;
    Modified=Yes;
    Version List=NAVW16.00.01,IMH6.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Code,Name;
    OnDelete=VAR
               TransferRoute@1000 : Record 5742;
             BEGIN
               WMSCheckWarehouse;

               TransferRoute.SETRANGE("Transfer-from Code",Code);
               TransferRoute.DELETEALL;
               TransferRoute.RESET;
               TransferRoute.SETRANGE("Transfer-to Code",Code);
               TransferRoute.DELETEALL;
             END;

    CaptionML=[ENU=Location;
               FRA=Magasin];
    LookupFormID=Form15;
    DrillDownFormID=Form15;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Text30        ;CaptionML=[ENU=Name;
                                                              FRA=Nom] }
    { 5700;   ;Name 2              ;Text30        ;CaptionML=[ENU=Name 2;
                                                              FRA=Nom 2] }
    { 5701;   ;Address             ;Text50        ;CaptionML=[ENU=Address;
                                                              FRA=Adresse] }
    { 5702;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              FRA=Adresse (2Šme ligne)] }
    { 5703;   ;City                ;Text30        ;OnValidate=BEGIN
                                                                Postcode.ValidateCity(City,"Post Code","Country/Region Code"); //IMH02 Ajout Code pays
                                                              END;

                                                   OnLookup=BEGIN
                                                              Postcode.LookUpCity(City,"Post Code","Country/Region Code", TRUE);  //IMH02 Ajout Code pays
                                                            END;

                                                   CaptionML=[ENU=City;
                                                              FRA=Ville] }
    { 5704;   ;Phone No.           ;Text30        ;OnValidate=VAR
                                                                lPhoneStandardization@1100281000 : Codeunit 50099;
                                                              BEGIN
                                                                //<<IMH01
                                                                lPhoneStandardization.ValidatePhoneNo(DATABASE::Location,5704,"Country/Region Code","Phone No.");
                                                                //>>IMH01
                                                              END;

                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              FRA=Nø t‚l‚phone] }
    { 5705;   ;Phone No. 2         ;Text30        ;OnValidate=VAR
                                                                lPhoneStandardization@1100281000 : Codeunit 50099;
                                                              BEGIN
                                                                //<<IMH01
                                                                lPhoneStandardization.ValidatePhoneNo(DATABASE::Location,5705,"Country/Region Code","Phone No. 2");
                                                                //>>IMH01
                                                              END;

                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No. 2;
                                                              FRA=Nø t‚l‚phone 2] }
    { 5706;   ;Telex No.           ;Text30        ;OnValidate=VAR
                                                                lPhoneStandardization@1100281000 : Codeunit 50099;
                                                              BEGIN
                                                                //<<IMH01
                                                                lPhoneStandardization.ValidatePhoneNo(DATABASE::Location,5706,"Country/Region Code","Telex No.");
                                                                //>>IMH01
                                                              END;

                                                   CaptionML=[ENU=Telex No.;
                                                              FRA=Nø t‚lex] }
    { 5707;   ;Fax No.             ;Text30        ;OnValidate=VAR
                                                                lPhoneStandardization@1100281000 : Codeunit 50099;
                                                              BEGIN
                                                                //<<IMH01
                                                                lPhoneStandardization.ValidatePhoneNo(DATABASE::Location,5707,"Country/Region Code","Fax No.");
                                                                //>>IMH01
                                                              END;

                                                   CaptionML=[ENU=Fax No.;
                                                              FRA=Nø t‚l‚copie] }
    { 5713;   ;Contact             ;Text50        ;CaptionML=[ENU=Contact;
                                                              FRA=Contact] }
    { 5714;   ;Post Code           ;Code20        ;TableRelation="Post Code".Code;
                                                   OnValidate=BEGIN
                                                                Postcode.ValidatePostCode(City,"Post Code", "Country/Region Code"); //IMH02 Ajout Code pays
                                                              END;

                                                   OnLookup=BEGIN
                                                              Postcode.LookUpPostCode(City,"Post Code","Country/Region Code", TRUE); //IMH02 Ajout Code pays
                                                            END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              FRA=Code postal] }
    { 5715;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              FRA=R‚gion] }
    { 5718;   ;E-Mail              ;Text80        ;OnValidate=VAR
                                                                lEmailStandardization@1100281000 : Codeunit 50096;
                                                              BEGIN
                                                                lEmailStandardization.ValidateEmail("E-Mail"); //IMH03
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=E-Mail;
                                                              FRA=E-mail] }
    { 5719;   ;Home Page           ;Text90        ;ExtendedDatatype=URL;
                                                   CaptionML=[ENU=Home Page;
                                                              FRA=Page d'accueil] }
    { 5720;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   CaptionML=[ENU=Country/Region Code;
                                                              FRA=Code pays/r‚gion] }
    { 5724;   ;Use As In-Transit   ;Boolean       ;OnValidate=BEGIN
                                                                IF "Use As In-Transit" THEN BEGIN
                                                                  TESTFIELD("Require Put-away",FALSE);
                                                                  TESTFIELD("Require Pick",FALSE);
                                                                  TESTFIELD("Use Cross-Docking",FALSE);
                                                                  TESTFIELD("Require Receive",FALSE);
                                                                  TESTFIELD("Require Shipment",FALSE);
                                                                  TESTFIELD("Bin Mandatory",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Use As In-Transit;
                                                              FRA=Magasin de transit] }
    { 5726;   ;Require Put-away    ;Boolean       ;OnValidate=VAR
                                                                WhseActivHeader@1000 : Record 5766;
                                                                WhseRcptHeader@1001 : Record 7316;
                                                              BEGIN
                                                                WhseRcptHeader.SETCURRENTKEY("Location Code");
                                                                WhseRcptHeader.SETRANGE("Location Code",Code);
                                                                IF WhseRcptHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Require Put-away"),xRec."Require Put-away",WhseRcptHeader.TABLECAPTION);

                                                                IF NOT "Require Put-away" THEN BEGIN
                                                                  TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  WhseActivHeader.SETRANGE(Type,WhseActivHeader.Type::"Put-away");
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Put-away"),TRUE,WhseActivHeader.TABLECAPTION);
                                                                  "Use Cross-Docking" := FALSE;
                                                                  "Cross-Dock Bin Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Require Put-away;
                                                              FRA=Rangement requis] }
    { 5727;   ;Require Pick        ;Boolean       ;OnValidate=VAR
                                                                WhseActivHeader@1000 : Record 5766;
                                                                WhseShptHeader@1001 : Record 7320;
                                                              BEGIN
                                                                WhseShptHeader.SETCURRENTKEY("Location Code");
                                                                WhseShptHeader.SETRANGE("Location Code",Code);
                                                                IF WhseShptHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Require Pick"),xRec."Require Pick",WhseShptHeader.TABLECAPTION);

                                                                IF NOT "Require Pick" THEN BEGIN
                                                                  TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  WhseActivHeader.SETRANGE(Type,WhseActivHeader.Type::Pick);
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Pick"),TRUE,WhseActivHeader.TABLECAPTION);
                                                                  "Use Cross-Docking" := FALSE;
                                                                  "Cross-Dock Bin Code" := '';
                                                                  "Pick According to FEFO" := FALSE;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Require Pick;
                                                              FRA=Pr‚lŠvement requis] }
    { 5728;   ;Cross-Dock Due Date Calc.;DateFormula;
                                                   CaptionML=[ENU=Cross-Dock Due Date Calc.;
                                                              FRA=D‚lai transbordement] }
    { 5729;   ;Use Cross-Docking   ;Boolean       ;OnValidate=BEGIN
                                                                TESTFIELD("Require Receive");
                                                                TESTFIELD("Require Shipment");
                                                                TESTFIELD("Require Put-away");
                                                                TESTFIELD("Require Pick");
                                                                IF NOT "Use Cross-Docking" THEN
                                                                  "Cross-Dock Bin Code" := '';
                                                              END;

                                                   CaptionML=[ENU=Use Cross-Docking;
                                                              FRA=Utiliser transbordement] }
    { 5730;   ;Require Receive     ;Boolean       ;OnValidate=VAR
                                                                WhseRcptHeader@1000 : Record 7316;
                                                                WhseActivHeader@1001 : Record 5766;
                                                              BEGIN
                                                                IF NOT "Require Receive" THEN BEGIN
                                                                  TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  WhseRcptHeader.SETCURRENTKEY("Location Code");
                                                                  WhseRcptHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseRcptHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Receive"),TRUE,WhseRcptHeader.TABLECAPTION);
                                                                  "Receipt Bin Code" := '';
                                                                  "Use Cross-Docking" := FALSE;
                                                                  "Cross-Dock Bin Code" := '';
                                                                END ELSE BEGIN
                                                                  WhseActivHeader.SETRANGE(Type,WhseActivHeader.Type::"Put-away");
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Receive"),FALSE,WhseActivHeader.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Require Receive;
                                                              FRA=R‚ception requise] }
    { 5731;   ;Require Shipment    ;Boolean       ;OnValidate=VAR
                                                                WhseShptHeader@1000 : Record 7320;
                                                                WhseActivHeader@1001 : Record 5766;
                                                              BEGIN
                                                                IF NOT "Require Shipment" THEN BEGIN
                                                                  TESTFIELD("Directed Put-away and Pick",FALSE);
                                                                  WhseShptHeader.SETCURRENTKEY("Location Code");
                                                                  WhseShptHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseShptHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Shipment"),TRUE,WhseShptHeader.TABLECAPTION);
                                                                  "Shipment Bin Code" := '';
                                                                  "Use Cross-Docking" := FALSE;
                                                                  "Cross-Dock Bin Code" := '';
                                                                END ELSE BEGIN
                                                                  WhseActivHeader.SETRANGE(Type,WhseActivHeader.Type::Pick);
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Require Shipment"),FALSE,WhseActivHeader.TABLECAPTION);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Require Shipment;
                                                              FRA=Exp‚dition requise] }
    { 5732;   ;Bin Mandatory       ;Boolean       ;OnValidate=VAR
                                                                ItemLedgEntry@1004 : Record 32;
                                                                WhseEntry@1000 : Record 7312;
                                                                WhseActivHeader@1001 : Record 5766;
                                                                WhseShptHeader@1002 : Record 7320;
                                                                WhseRcptHeader@1003 : Record 7316;
                                                                Window@1005 : Dialog;
                                                              BEGIN
                                                                IF "Bin Mandatory" AND NOT xRec."Bin Mandatory" THEN BEGIN
                                                                  Window.OPEN(Text010);
                                                                  ItemLedgEntry.SETCURRENTKEY("Item No.",Open,"Variant Code",Positive,"Location Code");
                                                                  IF ItemLedgEntry.FINDFIRST THEN
                                                                    REPEAT
                                                                      ItemLedgEntry.SETRANGE("Item No.",ItemLedgEntry."Item No.");
                                                                      ItemLedgEntry.SETRANGE(Open,TRUE);
                                                                      ItemLedgEntry.SETRANGE("Location Code",Code);
                                                                      IF ItemLedgEntry.FINDLAST THEN
                                                                        ERROR(Text009,FIELDCAPTION("Bin Mandatory"));
                                                                      ItemLedgEntry.SETRANGE(Open);
                                                                      ItemLedgEntry.SETRANGE("Location Code");
                                                                      ItemLedgEntry.FINDLAST;
                                                                      ItemLedgEntry.SETRANGE("Item No.");
                                                                    UNTIL ItemLedgEntry.NEXT = 0;

                                                                  "Default Bin Selection" := "Default Bin Selection"::"Fixed Bin";
                                                                END;

                                                                WhseActivHeader.SETRANGE("Location Code",Code);
                                                                IF WhseActivHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Bin Mandatory"),xRec."Bin Mandatory",WhseActivHeader.TABLECAPTION);

                                                                WhseRcptHeader.SETCURRENTKEY("Location Code");
                                                                WhseRcptHeader.SETRANGE("Location Code",Code);
                                                                IF WhseRcptHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Bin Mandatory"),xRec."Bin Mandatory",WhseRcptHeader.TABLECAPTION);

                                                                WhseShptHeader.SETCURRENTKEY("Location Code");
                                                                WhseShptHeader.SETRANGE("Location Code",Code);
                                                                IF WhseShptHeader.FINDFIRST THEN
                                                                  ERROR(Text008,FIELDCAPTION("Bin Mandatory"),xRec."Bin Mandatory",WhseShptHeader.TABLECAPTION);

                                                                IF NOT "Bin Mandatory" AND xRec."Bin Mandatory" THEN BEGIN
                                                                  WhseEntry.SETCURRENTKEY("Location Code");
                                                                  WhseEntry.SETRANGE("Location Code",Code);
                                                                  WhseEntry.CALCSUMS("Qty. (Base)");
                                                                  IF WhseEntry."Qty. (Base)" <> 0 THEN
                                                                    ERROR(Text002,FIELDCAPTION("Bin Mandatory"));
                                                                END;

                                                                IF NOT "Bin Mandatory" THEN BEGIN
                                                                  "Outbound BOM Bin Code" := '';
                                                                  "Open Shop Floor Bin Code" := '';
                                                                  "Inbound Production Bin Code" := '';
                                                                  "Outbound Production Bin Code" := '';
                                                                  "Adjustment Bin Code" := '';
                                                                  "Receipt Bin Code" := '';
                                                                  "Shipment Bin Code" := '';
                                                                  "Cross-Dock Bin Code" := '';
                                                                  "Outbound BOM Bin Code" := '';
                                                                  "Inbound BOM Bin Code" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Bin Mandatory;
                                                              FRA=Emplacement obligatoire] }
    { 5733;   ;Directed Put-away and Pick;Boolean ;OnValidate=VAR
                                                                WhseActivHeader@1002 : Record 5766;
                                                                WhseShptHeader@1001 : Record 7320;
                                                                WhseRcptHeader@1000 : Record 7316;
                                                              BEGIN
                                                                IF "Directed Put-away and Pick" THEN BEGIN
                                                                  TESTFIELD("Use As In-Transit",FALSE);
                                                                  TESTFIELD("Bin Mandatory");
                                                                  VALIDATE("Require Receive",TRUE);
                                                                  VALIDATE("Require Shipment",TRUE);
                                                                  VALIDATE("Require Put-away",TRUE);
                                                                  VALIDATE("Require Pick",TRUE);
                                                                  VALIDATE("Use Cross-Docking",TRUE);
                                                                  "Default Bin Selection" := "Default Bin Selection"::" ";
                                                                END ELSE BEGIN
                                                                  WhseActivHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseActivHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Directed Put-away and Pick"),xRec."Directed Put-away and Pick",WhseActivHeader.TABLECAPTION);

                                                                  WhseRcptHeader.SETCURRENTKEY("Location Code");
                                                                  WhseRcptHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseRcptHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Directed Put-away and Pick"),xRec."Directed Put-away and Pick",WhseRcptHeader.TABLECAPTION);

                                                                  WhseShptHeader.SETCURRENTKEY("Location Code");
                                                                  WhseShptHeader.SETRANGE("Location Code",Code);
                                                                  IF WhseShptHeader.FINDFIRST THEN
                                                                    ERROR(Text008,FIELDCAPTION("Directed Put-away and Pick"),xRec."Directed Put-away and Pick",WhseShptHeader.TABLECAPTION);

                                                                  VALIDATE("Adjustment Bin Code",'');
                                                                END;

                                                                IF (NOT "Directed Put-away and Pick") AND xRec."Directed Put-away and Pick" THEN BEGIN
                                                                  "Default Bin Selection" := "Default Bin Selection"::"Fixed Bin";
                                                                  "Use Put-away Worksheet" := FALSE;
                                                                  VALIDATE("Use Cross-Docking",FALSE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Directed Put-away and Pick;
                                                              FRA=Pr‚lŠv. et rangement sugg‚r‚s] }
    { 5734;   ;Default Bin Selection;Option       ;OnValidate=BEGIN
                                                                IF "Default Bin Selection" = "Default Bin Selection"::" " THEN
                                                                  TESTFIELD("Directed Put-away and Pick");
                                                              END;

                                                   CaptionML=[ENU=Default Bin Selection;
                                                              FRA=S‚lection emplacement par d‚f.];
                                                   OptionCaptionML=[ENU=" ,Fixed Bin,Last-Used Bin";
                                                                    FRA=" ,Emplacement fixe,Dernier emplacement utilis‚"];
                                                   OptionString=[ ,Fixed Bin,Last-Used Bin] }
    { 5790;   ;Outbound Whse. Handling Time;DateFormula;
                                                   CaptionML=[ENU=Outbound Whse. Handling Time;
                                                              FRA=D‚lai d‚senlogement] }
    { 5791;   ;Inbound Whse. Handling Time;DateFormula;
                                                   CaptionML=[ENU=Inbound Whse. Handling Time;
                                                              FRA=D‚lai enlogement] }
    { 7305;   ;Put-away Template Code;Code10      ;TableRelation="Put-away Template Header";
                                                   CaptionML=[ENU=Put-away Template Code;
                                                              FRA=Code modŠle rangement] }
    { 7306;   ;Use Put-away Worksheet;Boolean     ;CaptionML=[ENU=Use Put-away Worksheet;
                                                              FRA=Utiliser feuille rangement] }
    { 7307;   ;Pick According to FEFO;Boolean     ;CaptionML=[ENU=Pick According to FEFO;
                                                              FRA=Pr‚lŠvement selon FEFO] }
    { 7308;   ;Allow Breakbulk     ;Boolean       ;CaptionML=[ENU=Allow Breakbulk;
                                                              FRA=Autoriser d‚conditionnement] }
    { 7309;   ;Bin Capacity Policy ;Option        ;CaptionML=[ENU=Bin Capacity Policy;
                                                              FRA=Politique capacit‚ empl.];
                                                   OptionCaptionML=[ENU=Never Check Capacity,Allow More Than Max. Capacity,Prohibit More Than Max. Cap.;
                                                                    FRA=Ne pas v‚rifier la capacit‚,Autoriser d‚passement capacit‚ max.,Interdire d‚passement capacit‚ max.];
                                                   OptionString=Never Check Capacity,Allow More Than Max. Capacity,Prohibit More Than Max. Cap. }
    { 7313;   ;Open Shop Floor Bin Code;Code20    ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "Open Shop Floor Bin Code" <> xRec."Open Shop Floor Bin Code" THEN
                                                                  CheckEmptyBin(
                                                                    xRec."Open Shop Floor Bin Code",FIELDCAPTION("Open Shop Floor Bin Code"));
                                                              END;

                                                   CaptionML=[ENU=Open Shop Floor Bin Code;
                                                              FRA=Code empl. atelier ouvert] }
    { 7314;   ;Inbound Production Bin Code;Code20 ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "Inbound Production Bin Code" <> xRec."Inbound Production Bin Code" THEN
                                                                  CheckEmptyBin(
                                                                    xRec."Inbound Production Bin Code",FIELDCAPTION("Inbound Production Bin Code"));
                                                              END;

                                                   CaptionML=[ENU=Inbound Production Bin Code;
                                                              FRA=Code empl. enlogement] }
    { 7315;   ;Outbound Production Bin Code;Code20;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "Outbound Production Bin Code" <> xRec."Outbound Production Bin Code" THEN
                                                                  CheckEmptyBin(
                                                                    xRec."Outbound Production Bin Code",FIELDCAPTION("Outbound Production Bin Code"));
                                                              END;

                                                   CaptionML=[ENU=Outbound Production Bin Code;
                                                              FRA=Code empl. d‚senlogement] }
    { 7317;   ;Adjustment Bin Code ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   OnValidate=BEGIN
                                                                IF "Adjustment Bin Code" <> xRec."Adjustment Bin Code" THEN BEGIN
                                                                  IF "Adjustment Bin Code" = '' THEN
                                                                    CheckEmptyBin(
                                                                      xRec."Adjustment Bin Code",FIELDCAPTION("Adjustment Bin Code"))
                                                                  ELSE
                                                                    CheckEmptyBin(
                                                                      "Adjustment Bin Code",FIELDCAPTION("Adjustment Bin Code"));

                                                                  CheckWhseAdjmtJnl;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Adjustment Bin Code;
                                                              FRA=Code empl. ajustement] }
    { 7319;   ;Always Create Put-away Line;Boolean;CaptionML=[ENU=Always Create Put-away Line;
                                                              FRA=Toujours cr‚er ligne rangement] }
    { 7320;   ;Always Create Pick Line;Boolean    ;CaptionML=[ENU=Always Create Pick Line;
                                                              FRA=Toujours cr‚er ligne pr‚lŠv.] }
    { 7321;   ;Special Equipment   ;Option        ;CaptionML=[ENU=Special Equipment;
                                                              FRA=Equipement sp‚cial];
                                                   OptionCaptionML=[ENU=" ,According to Bin,According to SKU/Item";
                                                                    FRA=" ,Selon emplacement,Selon point de stock/article"];
                                                   OptionString=[ ,According to Bin,According to SKU/Item] }
    { 7323;   ;Receipt Bin Code    ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[ENU=Receipt Bin Code;
                                                              FRA=Code empl. r‚ception] }
    { 7325;   ;Shipment Bin Code   ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[ENU=Shipment Bin Code;
                                                              FRA=Code empl. exp‚dition] }
    { 7326;   ;Cross-Dock Bin Code ;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[ENU=Cross-Dock Bin Code;
                                                              FRA=Code empl. transbord.] }
    { 7327;   ;Outbound BOM Bin Code;Code20       ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[ENU=Outbound BOM Bin Code;
                                                              FRA=Code empl. d‚senlog. nomencl.] }
    { 7328;   ;Inbound BOM Bin Code;Code20        ;TableRelation=Bin.Code WHERE (Location Code=FIELD(Code));
                                                   CaptionML=[ENU=Inbound BOM Bin Code;
                                                              FRA=Code empl. enlogement nomencl.] }
    { 7600;   ;Base Calendar Code  ;Code10        ;TableRelation="Base Calendar";
                                                   CaptionML=[ENU=Base Calendar Code;
                                                              FRA=Code calendrier principal] }
    { 7700;   ;Use ADCS            ;Boolean       ;CaptionML=[ENU=Use ADCS;
                                                              FRA=Saisie automatis‚e] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    { No ;Name                                    ;KeyGroups=SearchCol }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Bin@1000 : Record 7354;
      Postcode@1001 : Record 225;
      WhseSetup@1002 : Record 5769;
      InvtSetup@1003 : Record 313;
      Location@1004 : Record 14;
      Text000@1005 : TextConst 'ENU=You cannot delete the %1 %2, because they contain items.;FRA=Vous ne pouvez pas supprimer l''enregistrement %1 %2, car il contient des articles.';
      Text001@1006 : TextConst 'ENU=You cannot delete the %1 %2, because one or more Warehouse Activity Lines exist for this %1.;FRA=Vous ne pouvez pas supprimer le %1 %2, car il existe au moins une ligne activit‚ entrep“t pour ce %1.';
      Text002@1007 : TextConst 'ENU=%1 must be Yes, because the bins contain items.;FRA=La valeur du champ %1 doit ˆtre Oui car les emplacements contiennent des articles.';
      Text003@1009 : TextConst 'ENU=Cancelled.;FRA=Annul‚.';
      Text004@1010 : TextConst 'ENU=The total quantity of items in the warehouse is 0, but the Adjustment Bin contains a negative quantity and other bins contain a positive quantity.\;FRA=La quantit‚ totale d''articles dans le entrep“t est 0, mais l''emplacement ajustement comporte une quantit‚ n‚gative et d''autres emplacements une quantit‚ positive.\';
      Text005@1012 : TextConst 'ENU=Do you still want to delete this %1?;FRA=Souhaitez-vous quand mˆme supprimer cet enregistrement %1?';
      Text006@1011 : TextConst 'ENU=You cannot change the %1 until the inventory stored in %2 %3 is 0.;FRA=Vous ne pouvez pas modifier la valeur du champ %1 tant que le stock du %2 %3 est 0.';
      Text007@1013 : TextConst 'ENU=You have to delete all Adjustment Warehouse Journal Lines first before you can change the %1.;FRA=Vous devez supprimer toutes les lignes feuille entrep“t ajustement avant de pouvoir modifier la valeur du champ %1.';
      Text008@1008 : TextConst 'ENU=%1 must be %2, because one or more %3 exist.;FRA=La valeur %1 doit ˆtre %2, car il existe au moins un %3.';
      Text009@1014 : TextConst 'ENU=You cannot change %1 because there are one or more open ledger entries on this location.;FRA=Vous ne pouvez pas modifier la valeur %1 car il existe une ou plusieurs ‚critures comptables ouvertes pour cet emplacement.';
      Text010@1015 : TextConst 'ENU=Checking item ledger entries for open entries...;FRA=V‚rification des ‚critures comptables article pour les ‚critures ouvertes...';
      Text011@1016 : TextConst 'ENU=You cannot change the %1 to %2 until the inventory stored in this bin is 0.;FRA=Vous ne pouvez pas remplacer %1 par %2 tant que le stock plac‚ dans cet emplacement n''est pas de 0.';
      Text012@1017 : TextConst 'ENU=Before you can use Online Map, you must fill in the Online Map Setup window.\See Setting Up Online Map in Help.;FRA=Avant de pouvoir utiliser Online Map, vous devez compl‚ter la fenˆtre Configuration Online Map.\Consultez la section Configuration d''Online Map dans l''Aide.';

    PROCEDURE RequireShipment@5(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF Location.GET(LocationCode) THEN
        EXIT(Location."Require Shipment");
      WhseSetup.GET;
      EXIT(WhseSetup."Require Shipment");
    END;

    PROCEDURE RequirePicking@1(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF Location.GET(LocationCode) THEN
        EXIT(Location."Require Pick");
      WhseSetup.GET;
      EXIT(WhseSetup."Require Pick");
    END;

    PROCEDURE RequireReceive@4(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF Location.GET(LocationCode) THEN
        EXIT(Location."Require Receive");
      WhseSetup.GET;
      EXIT(WhseSetup."Require Receive");
    END;

    PROCEDURE RequirePutaway@2(LocationCode@1000 : Code[10]) : Boolean;
    BEGIN
      IF Location.GET(LocationCode) THEN
        EXIT(Location."Require Put-away");
      WhseSetup.GET;
      EXIT(WhseSetup."Require Put-away");
    END;

    PROCEDURE GetLocationSetup@3(LocationCode@1000 : Code[10];VAR Location2@1001 : Record 14) : Boolean;
    BEGIN
      IF NOT GET(LocationCode) THEN BEGIN
        WITH Location2 DO BEGIN
          INIT;
          WhseSetup.GET;
          Code := LocationCode;
          "Use As In-Transit" := FALSE;
          "Require Put-away" := WhseSetup."Require Put-away";
          "Require Pick" := WhseSetup."Require Pick";
          "Outbound Whse. Handling Time" := InvtSetup."Outbound Whse. Handling Time";
          "Inbound Whse. Handling Time" := InvtSetup."Inbound Whse. Handling Time";
          "Require Receive" := WhseSetup."Require Receive";
          "Require Shipment" := WhseSetup."Require Shipment";
          EXIT(FALSE);
        END;
      END;
      Location2 := Rec;
      EXIT(TRUE);
    END;

    LOCAL PROCEDURE WMSCheckWarehouse@7300();
    VAR
      Zone@1005 : Record 7300;
      Bin@1006 : Record 7354;
      BinContent@1004 : Record 7302;
      WhseActivLine@1003 : Record 5767;
      WarehouseEntry@1002 : Record 7312;
      WarehouseEntry2@1001 : Record 7312;
      WhseJnlLine@1000 : Record 7311;
    BEGIN
      WarehouseEntry.SETCURRENTKEY("Location Code");
      WarehouseEntry.SETRANGE("Location Code",Code);
      WarehouseEntry.CALCSUMS("Qty. (Base)");
      IF WarehouseEntry."Qty. (Base)" = 0 THEN BEGIN
        IF "Adjustment Bin Code" <> '' THEN BEGIN
          WarehouseEntry2.SETCURRENTKEY("Bin Code","Location Code");
          WarehouseEntry2.SETRANGE("Bin Code","Adjustment Bin Code");
          WarehouseEntry2.SETRANGE("Location Code",Code);
          WarehouseEntry2.CALCSUMS("Qty. (Base)");
          IF WarehouseEntry2."Qty. (Base)" < 0 THEN
            IF NOT CONFIRM(Text004 + Text005,FALSE,TABLECAPTION) THEN
              ERROR(Text003)
        END;
      END ELSE
        ERROR(Text000,TABLECAPTION,Code);

      WhseActivLine.SETCURRENTKEY("Location Code","Activity Type");
      WhseActivLine.SETRANGE("Location Code",Code);
      WhseActivLine.SETRANGE("Activity Type",WhseActivLine."Activity Type"::Movement);
      WhseActivLine.SETFILTER("Qty. Outstanding",'<>0');
      IF WhseActivLine.FINDFIRST THEN
        ERROR(Text001,TABLECAPTION,Code);

      WhseJnlLine.SETRANGE("Location Code",Code);
      WhseJnlLine.SETFILTER(Quantity,'<>0');
      IF WhseJnlLine.FINDFIRST THEN
        ERROR(Text001,TABLECAPTION,Code);

      Zone.SETRANGE("Location Code",Code);
      Zone.DELETEALL;
      Bin.SETRANGE("Location Code",Code);
      Bin.DELETEALL;
      BinContent.SETRANGE("Location Code",Code);
      BinContent.DELETEALL;
    END;

    LOCAL PROCEDURE CheckEmptyBin@7302(BinCode@1001 : Code[20];CaptionOfField@1002 : Text[30]);
    VAR
      WarehouseEntry@1003 : Record 7312;
      WhseEntry2@1000 : Record 7312;
    BEGIN
      WarehouseEntry.SETCURRENTKEY("Bin Code","Location Code","Item No.");
      WarehouseEntry.SETRANGE("Bin Code",BinCode);
      WarehouseEntry.SETRANGE("Location Code",Code);
      IF WarehouseEntry.FINDFIRST THEN
        REPEAT
          WarehouseEntry.SETRANGE("Item No.",WarehouseEntry."Item No.");

          WhseEntry2.SETCURRENTKEY("Item No.","Bin Code","Location Code");
          WhseEntry2.COPYFILTERS(WarehouseEntry);
          WhseEntry2.CALCSUMS("Qty. (Base)");
          IF WhseEntry2."Qty. (Base)" <> 0 THEN BEGIN
            IF (BinCode = "Adjustment Bin Code") AND (xRec."Adjustment Bin Code" = '') THEN
              ERROR(Text011,CaptionOfField,BinCode)
            ELSE
              ERROR(Text006,CaptionOfField,Bin.TABLECAPTION,BinCode);
          END;

          WarehouseEntry.FINDLAST;
          WarehouseEntry.SETRANGE("Item No.");
        UNTIL WarehouseEntry.NEXT = 0;
    END;

    LOCAL PROCEDURE CheckWhseAdjmtJnl@7303();
    VAR
      WhseJnlTemplate@1002 : Record 7309;
      WhseJnlLine@1003 : Record 7311;
    BEGIN
      WhseJnlTemplate.SETRANGE(Type,WhseJnlTemplate.Type::Item);
      IF WhseJnlTemplate.FINDFIRST THEN
        REPEAT
          WhseJnlLine.SETRANGE("Journal Template Name",WhseJnlTemplate.Name);
          WhseJnlLine.SETRANGE("Location Code",Code);
          IF WhseJnlLine.FINDFIRST THEN
            ERROR(
              Text007,
              FIELDCAPTION("Adjustment Bin Code"));
        UNTIL WhseJnlTemplate.NEXT = 0;
    END;

    PROCEDURE GetRequirementText@6(FieldNumber@1000 : Integer) : Text[50];
    VAR
      Text000@1002 : TextConst 'ENU=Shipment,Receive,Pick,Put-Away;FRA=Exp‚dition,R‚ception,Pr‚lŠvement,Rangement';
    BEGIN
      CASE FieldNumber OF
        FIELDNO("Require Shipment"):
          EXIT(SELECTSTR(1,Text000));
        FIELDNO("Require Receive"):
          EXIT(SELECTSTR(2,Text000));
        FIELDNO("Require Pick"):
          EXIT(SELECTSTR(3,Text000));
        FIELDNO("Require Put-away"):
          EXIT(SELECTSTR(4,Text000));
      END;
    END;

    PROCEDURE DisplayMap@7();
    VAR
      MapPoint@1001 : Record 800;
      MapMgt@1000 : Codeunit 802;
    BEGIN
      IF MapPoint.FIND('-') THEN
        MapMgt.MakeSelection(DATABASE::Location,GETPOSITION)
      ELSE
        MESSAGE(Text012);
    END;

    BEGIN
    {
      /** @r IMH03 @d 11/02/2011 @a IMH.ZW @v IMH6.01.001 @s Normalisation des donn‚es @c normalisation Email */
      /** @r IHM02 @d 10/02/11 @a ISAT.ZW @v IMH6.01.001 @s Normalisation des donn‚es
          @c Associer les Pays au code postaux - descendre le code pays automatiquement*/
      /** @r IMH01 @d 23/09/2010 @a IMH.LLT @v IMH6.01.001 @s Normalisation des donn‚es
          @c Appel de la fonction de normalisation sur les champs de type t‚l‚phonie  */
    }
    END.
  }
}