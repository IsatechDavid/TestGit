OBJECT Table 232 Gen. Journal Batch
{
  OBJECT-PROPERTIES
  {
    Date=14/10/09;
    Time=12:00:00;
    Version List=NAVW16.00,ISA6.00;
  }
  PROPERTIES
  {
    DataCaptionFields=Name,Description;
    OnInsert=BEGIN
               LOCKTABLE;
               GenJnlTemplate.GET("Journal Template Name");
               IF GenJnlTemplate."Copy VAT Setup to Jnl. Lines" = FALSE THEN
                 "Copy VAT Setup to Jnl. Lines" := FALSE;
             END;

    OnDelete=BEGIN
               GenJnlAlloc.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlAlloc.SETRANGE("Journal Batch Name",Name);
               GenJnlAlloc.DELETEALL;
               GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
               GenJnlLine.SETRANGE("Journal Batch Name",Name);
               GenJnlLine.DELETEALL(TRUE);
             END;

    CaptionML=[ENU=Gen. Journal Batch;
               FRA=Nom feuille comptabilit‚];
    LookupFormID=Form251;
  }
  FIELDS
  {
    { 1   ;   ;Journal Template Name;Code10       ;TableRelation="Gen. Journal Template";
                                                   CaptionML=[ENU=Journal Template Name;
                                                              FRA=Nom modŠle feuille];
                                                   NotBlank=Yes }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              FRA=Nom];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 4   ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   OnValidate=BEGIN
                                                                IF "Reason Code" <> xRec."Reason Code" THEN BEGIN
                                                                  ModifyLines(FIELDNO("Reason Code"));
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reason Code;
                                                              FRA=Code motif] }
    { 5   ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=[ENU=Bal. Account Type;
                                                              FRA=Type compte contrepartie];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset;
                                                                    FRA=G‚n‚ral,Client,Fournisseur,Banque,Immobilisation];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset }
    { 6   ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=[ENU=Bal. Account No.;
                                                              FRA=Nø compte contrepartie] }
    { 7   ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  GenJnlTemplate.GET("Journal Template Name");
                                                                  IF GenJnlTemplate.Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    VALIDATE("Posting No. Series",'');
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø] }
    { 8   ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                                ModifyLines(FIELDNO("Posting No. Series"));
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              FRA=Souches de nø validation] }
    { 9   ;   ;Copy VAT Setup to Jnl. Lines;Boolean;
                                                   InitValue=Yes;
                                                   CaptionML=[ENU=Copy VAT Setup to Jnl. Lines;
                                                              FRA=Copier paramŠtres TVA sur les lignes feuille] }
    { 10  ;   ;Allow VAT Difference;Boolean       ;OnValidate=BEGIN
                                                                IF "Allow VAT Difference" THEN BEGIN
                                                                  GenJnlTemplate.GET("Journal Template Name");
                                                                  GenJnlTemplate.TESTFIELD("Allow VAT Difference",TRUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Allow VAT Difference;
                                                              FRA=Autoriser diff‚rence TVA] }
    { 21  ;   ;Template Type       ;Option        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Gen. Journal Template".Type WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Template Type;
                                                              FRA=Type modŠle];
                                                   OptionCaptionML=[ENU=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs;
                                                                    FRA=G‚n‚ral,Ventes,Achats,RŠglements,Paiements,Immobilisations,Intersoci‚t‚,Projets];
                                                   OptionString=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs;
                                                   Editable=No }
    { 22  ;   ;Recurring           ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Gen. Journal Template".Recurring WHERE (Name=FIELD(Journal Template Name)));
                                                   CaptionML=[ENU=Recurring;
                                                              FRA=Abonnement];
                                                   Editable=No }
    { 8016602;;Default Line Description;Text50    ;CaptionML=[ENU=Default Line Description;
                                                              FRA=D‚signation ligne par d‚faut] }
    { 8016605;;Default Payment Method Code;Code10 ;TableRelation="Payment Method".Code;
                                                   CaptionML=[ENU=Default Payment Method Code;
                                                              FRA=Code mode rŠglement par d‚faut] }
  }
  KEYS
  {
    {    ;Journal Template Name,Name              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;FRA=Seul le champ %1 est … renseigner dans les feuilles abonnement.';
      Text001@1001 : TextConst 'ENU=must not be %1;FRA=ne doit pas ˆtre %1';
      GenJnlTemplate@1002 : Record 80;
      GenJnlLine@1003 : Record 81;
      GenJnlAlloc@1004 : Record 221;
      Text8016602@8016601 : TextConst 'ENU=Shortcut definition :\;FRA=D‚finition des raccourcis :\';
      Text8016603@8016600 : TextConst 'ENU="%1 = %2\";FRA="%1 = %2\"';
      Text8011604@8016602 : TextConst 'ENU=Account Name;FRA=Nom du compte';

    PROCEDURE SetupNewBatch@1();
    BEGIN
      GenJnlTemplate.GET("Journal Template Name");
      "Bal. Account Type" := GenJnlTemplate."Bal. Account Type";
      "Bal. Account No." := GenJnlTemplate."Bal. Account No.";
      "No. Series" := GenJnlTemplate."No. Series";
      "Posting No. Series" := GenJnlTemplate."Posting No. Series";
      "Reason Code" := GenJnlTemplate."Reason Code";
      "Copy VAT Setup to Jnl. Lines" := GenJnlTemplate."Copy VAT Setup to Jnl. Lines";
      "Allow VAT Difference" := GenJnlTemplate."Allow VAT Difference";
    END;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    PROCEDURE ModifyLines@3(i@1000 : Integer);
    BEGIN
      GenJnlLine.LOCKTABLE;
      GenJnlLine.SETRANGE("Journal Template Name","Journal Template Name");
      GenJnlLine.SETRANGE("Journal Batch Name",Name);
      IF GenJnlLine.FIND('-') THEN
        REPEAT
          CASE i OF
            FIELDNO("Reason Code"):
              GenJnlLine.VALIDATE("Reason Code","Reason Code");
            FIELDNO("Posting No. Series"):
              GenJnlLine.VALIDATE("Posting No. Series","Posting No. Series");
          END;
          GenJnlLine.MODIFY(TRUE);
        UNTIL GenJnlLine.NEXT = 0;
    END;

    PROCEDURE AssistDefaultLineDescription@8016601();
    VAR
      lText@8016600 : Text[1024];
    BEGIN
      lText := Text8016602;
      lText := lText + STRSUBSTNO(Text8016603,1,GenJnlLine.FIELDCAPTION("Account No."));
      lText := lText + STRSUBSTNO(Text8016603,2,Text8011604);
      lText := lText + STRSUBSTNO(Text8016603,3,GenJnlLine.FIELDCAPTION("Posting Date"));
      lText := lText + STRSUBSTNO(Text8016603,4,GenJnlLine.FIELDCAPTION("Due Date"));
      lText := lText + STRSUBSTNO(Text8016603,5,GenJnlLine.FIELDCAPTION("Payment Method Code"));
      MESSAGE(lText);
    END;

    BEGIN
    {
      /**@r ISA02 @d 22/07/09 @a ISAT.ST @v ISA6.00.00.04 @s Suivi rŠglement
         @c Ajout code mode rŠglement par d‚faut */
      /**@r ISA01 @d 22/07/09 @a ISAT.ST @v ISA6.00.00.05 @s Ajout description ligne par d‚faut
         @c Ajout fonction AssistLineDescription */
    }
    END.
  }
}