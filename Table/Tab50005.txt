OBJECT Table 50005 Specialization
{
  OBJECT-PROPERTIES
  {
    Date=24/01/14;
    Time=14:32:37;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lNoSeriesMgt@1100281000 : Codeunit 396;
             BEGIN
               ProviderSetup.GET;

               IF Code = '' THEN BEGIN
                 ProviderSetup.TESTFIELD("Specialization Nos.");
                 lNoSeriesMgt.InitSeries(ProviderSetup."Specialization Nos.",xRec."No. Series",0D,Code,"No. Series");
               END;

               TouchRecord(TRUE);
             END;

    OnModify=BEGIN
               TouchRecord(FALSE);
             END;

    OnDelete=VAR
               lProviderSpeciality@1100281000 : Record 50006;
               lAbilityGeoCampaignProvider@1100281001 : Record 50015;
               lAbilityGeoProvider@1100281002 : Record 50016;
               lCompletnessTaskSetup@1100281003 : Record 50003;
               lSpecialBuildTrade@1100281004 : Record 50031;
             BEGIN
               //<<IMH05.ST
               lSpecialBuildTrade.SETCURRENTKEY("Specialization Code");
               lSpecialBuildTrade.SETRANGE("Specialization Code", Code);
               IF NOT lSpecialBuildTrade.ISEMPTY THEN
                 lSpecialBuildTrade.DELETEALL;
               //>>IMH05.ST

               // Sp‚cialit‚ prestataire
               lProviderSpeciality.SETCURRENTKEY("Specialization Code");
               lProviderSpeciality.SETRANGE("Specialization Code",Code);
               IF NOT lProviderSpeciality.ISEMPTY THEN BEGIN
                 lProviderSpeciality.DELETEALL;
               END;

               // Comp‚tence g‚ographique campaigne
               //lAbilityGeoCampaignProvider.SETCURRENTKEY("Specialization Code");
               //lAbilityGeoCampaignProvider.SETRANGE("Specialization Code",Code);
               IF NOT lAbilityGeoCampaignProvider.ISEMPTY THEN BEGIN
                 lAbilityGeoCampaignProvider.DELETEALL;
               END;

               // Comp‚tence g‚ographique
               lAbilityGeoProvider.SETCURRENTKEY("Specialization Code");
               lAbilityGeoProvider.SETRANGE("Specialization Code",Code);
               IF NOT lAbilityGeoProvider.ISEMPTY THEN BEGIN
                 lAbilityGeoProvider.DELETEALL;
               END;

               // paramŠtres compl‚tude
               lCompletnessTaskSetup.SETRANGE("Specialization Code",Code);
               IF NOT lCompletnessTaskSetup.ISEMPTY THEN BEGIN
                 lCompletnessTaskSetup.DELETEALL;
               END;
             END;

    OnRename=BEGIN
               TouchRecord(FALSE);
             END;

    CaptionML=[ENU=Specialization;
               FRA=Sp‚cialit‚];
    LookupFormID=Form50005;
  }
  FIELDS
  {
    { 1   ;   ;Code                ;Code10        ;AltSearchField=Search Name;
                                                   CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 7   ;   ;Search Name         ;Code50        ;CaptionML=[ENU=Search Name;
                                                              FRA=Nom de recherche] }
    { 10  ;   ;Description         ;Text50        ;OnValidate=BEGIN
                                                                IF ("Search Name" = UPPERCASE(xRec.Description)) OR ("Search Name" = '') THEN BEGIN
                                                                  "Search Name" := Description;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 105 ;   ;Qualification Mandatory;Boolean    ;CaptionML=[ENU=Qualification Mandatory;
                                                              FRA=Qualification obligatoire] }
    { 107 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø] }
    { 350 ;   ;Average Cost        ;Decimal       ;CaptionML=[ENU=Average Cost;
                                                              FRA=Co–t moyen] }
    { 351 ;   ;Emergency Using     ;Boolean       ;CaptionML=FRA=Missionnable en urgence }
    { 352 ;   ;Average Time        ;Decimal       ;CaptionML=[ENU=Average Time;
                                                              FRA=D‚lai moyen] }
    { 400 ;   ;Blocked On Interv. Creation;Boolean;CaptionML=[ENU=Blocked On Interv. Creation;
                                                              FRA=Bloqu‚ en cr‚ation interv.] }
    { 50000;  ;Purchase Item No.   ;Code20        ;TableRelation=Item;
                                                   CaptionML=FRA=Nø article achat;
                                                   Description=IMH6.01.143 }
    { 50001;  ;Emergency Purchase Item No.;Code20 ;TableRelation=Item;
                                                   CaptionML=FRA=Nø article achat urgence;
                                                   Description=IMH6.01.143 }
    { 50002;  ;Show On DAD         ;Boolean       ;CaptionML=FRA=Afficher dans la DAD }
    { 50003;  ;Average Time Objective;Decimal     ;CaptionML=[ENU=Average Time Objective;
                                                              FRA=D‚lai moyen objectif] }
    { 50004;  ;% Increase Service Nb / N-1;Decimal;CaptionML=[ENU=Increase Pct Objective;
                                                              FRA=% augmentation mission / N-1] }
    { 50500;  ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              FRA=Date de cr‚ation];
                                                   Editable=No }
    { 50501;  ;Creation User ID    ;Text30        ;CaptionML=[ENU=Creation User ID;
                                                              FRA=Utilisateur cr‚ation];
                                                   Editable=No }
    { 50502;  ;Last Modification Date;DateTime    ;CaptionML=[ENU=Last Modification Date;
                                                              FRA=Date derniŠre modification];
                                                   Editable=No }
    { 50503;  ;Last Modification User ID;Text30   ;ValidateTableRelation=No;
                                                   CaptionML=[ENU=Last Modification User ID;
                                                              FRA=Utilisateur derniŠre modification];
                                                   Editable=No }
    { 56010;  ;DARVA Code          ;Code20        ;CaptionML=[ENU=DARVA Code;
                                                              FRA=Code DARVA] }
  }
  KEYS
  {
    {    ;Code                                    ;Clustered=Yes }
    {    ;Search Name                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Search Name,Code                         }
  }
  CODE
  {
    VAR
      Text005@1100281000 : TextConst 'ENU=Total %1 is missing a matching Titre.;FRA=Il manque un Titre pour le Total %1.';
      ProviderSetup@1100281001 : Record 50011;

    PROCEDURE TouchRecord@1100281006(pInsert@1100281000 : Boolean);
    BEGIN
      IF pInsert THEN BEGIN
        "Creation Date" := CURRENTDATETIME;
        "Creation User ID" := USERID;
      END;

      "Last Modification Date" := CURRENTDATETIME;
      "Last Modification User ID" := USERID;
    END;

    PROCEDURE GetBuildingTrade@1100281000(pForSpeciality@1100281000 : Code[10];VAR pBuildingTrade@1100281001 : Code[20]) : Boolean;
    VAR
      lSpecialBuildTrade@1100281002 : Record 50031;
    BEGIN
      //IMH05.ST S‚lection du corps de m‚tiers sur la sp‚cialit‚
      lSpecialBuildTrade.SETCURRENTKEY("Specialization Code");
      lSpecialBuildTrade.FILTERGROUP(2);
      lSpecialBuildTrade.SETRANGE("Specialization Code", pForSpeciality);
      lSpecialBuildTrade.FILTERGROUP(0);
      IF lSpecialBuildTrade.ISEMPTY THEN
        EXIT(FALSE);
      IF lSpecialBuildTrade.COUNT = 1 THEN BEGIN
        lSpecialBuildTrade.FINDFIRST;
        pBuildingTrade := lSpecialBuildTrade."Building Trade No.";
        EXIT(TRUE);
      END ELSE BEGIN
        IF FORM.RUNMODAL(0, lSpecialBuildTrade) = ACTION::LookupOK THEN BEGIN
          pBuildingTrade := lSpecialBuildTrade."Building Trade No.";
          EXIT(TRUE);
        END;
      END;
      EXIT(FALSE);
    END;

    BEGIN
    {
      /** @r IMH11 @d 12/03/2012 @a ISAT.ZW @v IMH6.01.023 @s Etat prestataire missionable
          @c Ajout du champ 50004 % Increase Service Nb / N-1 */
      /** @r IMH11 @d 01/02/2012 @a ISAT.ZW @v IMH6.01.023 @s Specialite
          @c Ajout du champ 352 Average Time */
      /** @r IMH10 @d 03/01/2012 @a ISAT.LLT @v IMH6.01.023 @s Specialite
          @c Ajout du champ 50003 Average Time Objective */
      /** @r IMH09 @d 27/12/11 @a ISAT.EBA @v IMH6.01.023 @s Specialite
          @c Ajout du champ 400 "Blocked On Interv. Creation" */
      /** @r IMH08 @d 25/03/11 @a ISAT.ZW @v IMH6.01.136 @s Interface Fusion @c Ajout champ 56010 DARVA Code */
      /** @r IMH07 @d 25/03/11 @a ISAT.PC @v IMH6.01.022 @s Comp‚tence geographique
          @c Modification du dropdown Search Name,Code */
      /** @r IMH06 @d 16/03/11 @a CAP.JC @v IMH6.01.143 @s Comptabilit‚ - Gestion des articles
          @c Ajout champ 50000, Purchase Item No.
          @c Ajout champ 50001, Emergency Purchase Item No.*/
      /** @r IMH05 @d 05/01/11 @a ISAT.ST @v IMH6.01.023 @s Specialit‚ @c Ajout fonction GetBuildingTrade */
      /** @r IMH04 @d 05/01/11 @a ISAT.PC @v IMH6.01.110 @s Finalisation prestataire @c ajout champ 350, Average Cost */
      /** @r IMH03 @d 21/10/10 @a ISAT.cc @v IMH6.01.021 @s Compl‚tude @c Ajout de la fonction InitTempSpecialization */
      /** @r IMH02 @d 05/10/10 @a ISAT.cc @v IMH6.01.021 @s Compl‚tude @c Suppression des enregistremants li‚e */
      /** @r IMH01 @d 30/09/10 @a ISAT.NFO @v IMH6.01.023 @s Specialite @c Ajout du champ Selected */
      /** @r IMH01 @d 23/09/10 @a ISAT.PC @v IMH6.01.023 @s Specialite @c Creation */
    }
    END.
  }
}