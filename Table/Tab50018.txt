OBJECT Table 50018 Attachment Treeview Structure
{
  OBJECT-PROPERTIES
  {
    Date=27/10/11;
    Time=14:24:33;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lAttachTreeview@1100281000 : Record 50017;
               lAttachTreeviewStructure@1100481000 : Record 50018;
             BEGIN
               IF Level = 0 THEN BEGIN
                 lAttachTreeviewStructure.SETRANGE("Attachment Treeview Code", "Attachment Treeview Code");
                 IF lAttachTreeviewStructure.FINDLAST THEN BEGIN
                   Level := lAttachTreeviewStructure.Level + 1;
                 END ELSE BEGIN
                   Level := 1;
                 END;
               END;


               lAttachTreeview.GET("Attachment Treeview Code");
               IF "Referent Table No." <> lAttachTreeview."Referent Table No." THEN
                 VALIDATE("Referent Table No.", lAttachTreeview."Referent Table No.");
               TouchRecord(TRUE);
             END;

    OnModify=BEGIN
               TouchRecord(FALSE);
             END;

    OnDelete=VAR
               lAttachmentTreeview@1100281000 : Record 50017;
               lAttachmentTreeviewCondition@1100481000 : Record 50019;
             BEGIN
               lAttachmentTreeview.GET("Attachment Treeview Code");
               lAttachmentTreeview.CALCFIELDS("Attachments Exist");
               IF lAttachmentTreeview."Attachments Exist" THEN
                 ERROR(Text004);

               lAttachmentTreeviewCondition.SETRANGE("Attachment Treeview Code", "Attachment Treeview Code");
               lAttachmentTreeviewCondition.SETRANGE(Level,Level);
               lAttachmentTreeviewCondition.DELETEALL;
             END;

    OnRename=BEGIN
               ERROR(Text001);
             END;

    CaptionML=[ENU=Attachment Treeview Structure;
               FRA=Structure arborescence doc. joint];
  }
  FIELDS
  {
    { 1   ;   ;Attachment Treeview Code;Code10    ;TableRelation="Attachment Treeview".Code;
                                                   OnValidate=VAR
                                                                lAttachTreeview@1100281000 : Record 50017;
                                                              BEGIN
                                                                lAttachTreeview.GET("Attachment Treeview Code");
                                                                VALIDATE("Referent Table No.", lAttachTreeview."Referent Table No.");
                                                              END;

                                                   CaptionML=[ENU=Attachment Treeview Code;
                                                              FRA=Code arborescence document joint] }
    { 2   ;   ;Referent Table No.  ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Referent Table No.;
                                                              FRA=Nø table r‚f‚rence] }
    { 5   ;   ;Level               ;Integer       ;CaptionML=[ENU=Level;
                                                              FRA=Niveau];
                                                   MinValue=1 }
    { 15  ;   ;Type                ;Option        ;CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=Constant,Field;
                                                                    FRA=Constante,Champ];
                                                   OptionString=Constant,Field }
    { 16  ;   ;Field No.           ;Integer       ;TableRelation=IF (Type=CONST(Field)) Field.No. WHERE (TableNo=FIELD(Referent Table No.));
                                                   OnValidate=VAR
                                                                lAttachmentTreeviewCondition@1100281000 : Record 50019;
                                                              BEGIN
                                                                TESTFIELD(Type,Type::Field);

                                                                lAttachmentTreeviewCondition.SETRANGE("Attachment Treeview Code", "Attachment Treeview Code");
                                                                lAttachmentTreeviewCondition.SETRANGE(Level, Level);
                                                                IF lAttachmentTreeviewCondition.FINDFIRST THEN
                                                                  REPEAT
                                                                    IF lAttachmentTreeviewCondition."Field No." <> "Field No." THEN BEGIN
                                                                      lAttachmentTreeviewCondition."Field No." := "Field No.";
                                                                      lAttachmentTreeviewCondition.MODIFY(TRUE);
                                                                    END;
                                                                  UNTIL lAttachmentTreeviewCondition.NEXT = 0;
                                                              END;

                                                   OnLookup=VAR
                                                              lAttachmentTreeview@1100281001 : Record 50017;
                                                              lFieldNo@1100281000 : Integer;
                                                            BEGIN
                                                              lAttachmentTreeview.GET("Attachment Treeview Code");
                                                              IF AttachmentMgt.LookupFieldReferentTable(lAttachmentTreeview."Referent Table No.",lFieldNo) THEN
                                                                VALIDATE("Field No.", lFieldNo);
                                                            END;

                                                   CaptionML=[ENU=Field No.;
                                                              FRA=Nø champ] }
    { 20  ;   ;Constante Value     ;Text20        ;OnValidate=BEGIN
                                                                TESTFIELD(Type,Type::Constant);
                                                              END;

                                                   CaptionML=[ENU=Constante Value;
                                                              FRA=Valeur constante] }
    { 30  ;   ;Conditions          ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Attachment Treeview Condition" WHERE (Attachment Treeview Code=FIELD(Attachment Treeview Code),
                                                                                                            Level=FIELD(Level)));
                                                   CaptionML=[ENU=Conditions;
                                                              FRA=Conditions];
                                                   Editable=No }
    { 40  ;   ;Folder For Year     ;Boolean       ;OnValidate=BEGIN
                                                                CheckFieldType;
                                                              END;

                                                   CaptionML=[ENU=Folder For Year;
                                                              FRA=Dossier par ann‚e] }
    { 41  ;   ;Folder For Quarter  ;Boolean       ;OnValidate=BEGIN
                                                                CheckFieldType;
                                                              END;

                                                   CaptionML=[ENU=Folder For Quarter;
                                                              FRA=Dossier par trimestre] }
    { 42  ;   ;Folder For Month    ;Boolean       ;OnValidate=BEGIN
                                                                CheckFieldType;
                                                              END;

                                                   CaptionML=[ENU=Folder For Month;
                                                              FRA=Dossier par mois] }
    { 43  ;   ;Folder For Week     ;Boolean       ;OnValidate=BEGIN
                                                                CheckFieldType;
                                                              END;

                                                   CaptionML=[ENU=Folder For Week;
                                                              FRA=Dossier par semaine] }
    { 44  ;   ;Folder For Day      ;Boolean       ;OnValidate=BEGIN
                                                                CheckFieldType;
                                                              END;

                                                   CaptionML=[ENU=Folder For Day;
                                                              FRA=Dossier par jour] }
    { 45  ;   ;Folder For Year and Month;Boolean  ;OnValidate=BEGIN
                                                                CheckFieldType;
                                                              END;

                                                   CaptionML=[ENU=Folder For Year and Month;
                                                              FRA=Dossier par ann‚e et mois] }
    { 100 ;   ;Referent Table Name ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object ID=FIELD(Referent Table No.)));
                                                   CaptionML=[ENU=Referent Table Name;
                                                              FRA=Nom table r‚f‚rence];
                                                   Editable=No }
    { 101 ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Referent Table No.),
                                                                                             No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              FRA=Nom champ];
                                                   Editable=No }
    { 50500;  ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              FRA=Date de cr‚ation];
                                                   Editable=No }
    { 50501;  ;Creation User ID    ;Text30        ;TableRelation=User."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Creation User ID;
                                                              FRA=Utilisateur cr‚ation];
                                                   Editable=No }
    { 50502;  ;Last Modification Date;DateTime    ;CaptionML=[ENU=Last Modification Date;
                                                              FRA=Date derniŠre modification];
                                                   Editable=No }
    { 50503;  ;Last Modification User ID;Text30   ;TableRelation=User."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Last Modification User ID;
                                                              FRA=Utilisateur derniŠre modification];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Attachment Treeview Code,Level          ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text001@1100481000 : TextConst 'ENU=Impossible to rename;FRA=Impossible de renommer';
      Text002@1000000000 : TextConst 'ENU=Field must be date or datetime;FRA=Le champ doit ˆtre de type date ou datetime';
      AttachmentMgt@1100281000 : Codeunit 50002;
      Text004@1100281001 : TextConst 'ENU=You can''t delete the treeview before detele the attachments.;FRA=Vous ne pouvez pas supprimer l''arborescence avant de supprimer les documents joint li‚s.';

    PROCEDURE CheckFieldType@1000000000();
    VAR
      lField@1000000000 : Record 2000000041;
    BEGIN
      lField.GET("Referent Table No.","Field No.");
      IF NOT (lField.Type IN [
           lField.Type::Date,
           lField.Type::DateTime])
      THEN BEGIN
        ERROR(Text002);
      END;
    END;

    PROCEDURE TouchRecord@1000000001(pInsert@1000000000 : Boolean);
    BEGIN
      IF pInsert THEN BEGIN
        "Creation Date" := CURRENTDATETIME;
        "Creation User ID" := USERID;
      END;
      "Last Modification Date" := CURRENTDATETIME;
      "Last Modification User ID" := USERID;
    END;

    BEGIN
    {
      /** @r IMH02 @d 14/02/11 @a ISAT.LLT @v IMH6.01.003 @s Attach. Management
          @c Ajout champ 45 Folder for year and Month */
      /** @r IMH01 @d 23/09/10 @a ISAT.ZW @v IMH6.01.003 @s Attach. Management @c Creation */
    }
    END.
  }
}