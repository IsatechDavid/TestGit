OBJECT Table 50019 Attachment Treeview Condition
{
  OBJECT-PROPERTIES
  {
    Date=21/07/11;
    Time=08:54:48;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lAttachmentTreeviewCondition@1000000000 : Record 50019;
               lAttachmentTreeviewStructure@1100281000 : Record 50018;
             BEGIN
               IF "Line No." = 0 THEN BEGIN
                 lAttachmentTreeviewCondition.SETRANGE("Attachment Treeview Code", "Attachment Treeview Code");
                 lAttachmentTreeviewCondition.SETRANGE(Level,Level);
                 lAttachmentTreeviewCondition.SETRANGE("Field No.","Field No.");
                 IF lAttachmentTreeviewCondition.FINDLAST THEN BEGIN
                   "Line No." := lAttachmentTreeviewCondition."Line No." + 10000;
                 END ELSE BEGIN
                   "Line No." := 10000;
                 END;
               END;

               IF "Field No." = 0 THEN BEGIN
                 lAttachmentTreeviewStructure.GET("Attachment Treeview Code", Level);
                 "Field No." := lAttachmentTreeviewStructure."Field No.";
               END;
               TouchRecord(TRUE);
             END;

    OnModify=BEGIN
               TouchRecord(FALSE);
             END;

    OnDelete=VAR
               lAttachmentTreeview@1100281000 : Record 50017;
             BEGIN
               lAttachmentTreeview.CALCFIELDS("Attachments Exist");
               IF lAttachmentTreeview."Attachments Exist" THEN
                 ERROR(Text004);
             END;

    OnRename=BEGIN
               TouchRecord(FALSE);
             END;

    CaptionML=[ENU=Attachment Treeview Condition;
               FRA=Condition arborescence doc. joint];
    LookupFormID=Form50010;
    DrillDownFormID=Form50010;
  }
  FIELDS
  {
    { 1   ;   ;Attachment Treeview Code;Code10    ;TableRelation="Attachment Treeview".Code;
                                                   OnValidate=VAR
                                                                lAttachTreeview@1100281000 : Record 50017;
                                                              BEGIN
                                                                lAttachTreeview.GET("Attachment Treeview Code");
                                                                VALIDATE("Referent Table No.", lAttachTreeview."Referent Table No.");
                                                              END;

                                                   CaptionML=[ENU=Attachment Treeview Code;
                                                              FRA=Code arborescence document joint] }
    { 2   ;   ;Referent Table No.  ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Referent Table No.;
                                                              FRA=Nø table r‚f‚rence] }
    { 4   ;   ;Level               ;Integer       ;CaptionML=[ENU=Level;
                                                              FRA=Niveau];
                                                   MinValue=1 }
    { 5   ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Referent Table No.));
                                                   CaptionML=[ENU=Field No.;
                                                              FRA=Nø champ];
                                                   NotBlank=Yes }
    { 7   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              FRA=Nø ligne] }
    { 9   ;   ;Reference Field No. ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Referent Table No.));
                                                   OnLookup=VAR
                                                              lFieldNo@1100281000 : Integer;
                                                            BEGIN
                                                              IF AttachmentMgt.LookupFieldReferentTable("Referent Table No.",lFieldNo) THEN
                                                                VALIDATE("Reference Field No.", lFieldNo);
                                                            END;

                                                   CaptionML=[ENU=Reference Field No.;
                                                              FRA=Nø champ r‚f‚rence] }
    { 20  ;   ;Reference Value     ;Text30        ;OnValidate=VAR
                                                                lFieldRef@1100481000 : FieldRef;
                                                              BEGIN
                                                                IF "Reference Value" <> '' THEN BEGIN
                                                                  EvaluateField("Referent Table No.","Reference Field No.","Reference Value");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Reference Value;
                                                              FRA=Valeur r‚f‚rence] }
    { 21  ;   ;Replace Value Type  ;Option        ;CaptionML=[ENU=Replace Value Type;
                                                              FRA=Type valeur remplacement];
                                                   OptionCaptionML=[ENU=" ,Constante";
                                                                    FRA=" ,Constante"];
                                                   OptionString=[ ,Constante] }
    { 22  ;   ;Replace Value       ;Text250       ;CaptionML=[ENU=Replace Value;
                                                              FRA=Valeur de remplacement] }
    { 101 ;   ;Field Name          ;Text50        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(Referent Table No.),
                                                                                             No.=FIELD(Reference Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              FRA=Nom champ];
                                                   Editable=No }
    { 50500;  ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              FRA=Date de cr‚ation];
                                                   Editable=No }
    { 50501;  ;Creation User ID    ;Text30        ;TableRelation=User."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Creation User ID;
                                                              FRA=Utilisateur cr‚ation];
                                                   Editable=No }
    { 50502;  ;Last Modification Date;DateTime    ;CaptionML=[ENU=Last Modification Date;
                                                              FRA=Date derniŠre modification];
                                                   Editable=No }
    { 50503;  ;Last Modification User ID;Text30   ;TableRelation=User."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Last Modification User ID;
                                                              FRA=Utilisateur derniŠre modification];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Attachment Treeview Code,Level,Line No. ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      AttachmentMgt@1100281000 : Codeunit 50002;
      Text004@1100281001 : TextConst 'ENU=You can''t delete the treeview before detele the attachments.;FRA=Vous ne pouvez pas supprimer l''arborescence avant de supprimer les documents joint li‚s.';

    PROCEDURE OptionAssistEdit@1100281000(pTableNo@1100481002 : Integer;pFieldNo@1100481001 : Integer);
    VAR
      lRecRef@1100481000 : RecordRef;
      lFieldRef@1100281009 : FieldRef;
      lOptionString@1100281007 : Text[1024];
      lSelection@1100281005 : Integer;
    BEGIN
      // Permet d'obtenir la liste des options s‚lectionnable

      IF pTableNo = 0 THEN BEGIN
        EXIT;
      END;

      IF pFieldNo = 0 THEN BEGIN
        EXIT;
      END;

      lRecRef.OPEN(pTableNo);
      lFieldRef := lRecRef.FIELD(pFieldNo);

      IF UPPERCASE(FORMAT(lFieldRef.TYPE)) <> 'OPTION' THEN BEGIN
        EXIT;
      END;

      lOptionString := lFieldRef.OPTIONCAPTION;
      lSelection := STRMENU(lOptionString, 0);
      IF lSelection <> 0 THEN BEGIN
        VALIDATE("Reference Value",FORMAT(lSelection - 1,0,1));
      END;
    END;

    PROCEDURE EvaluateField@1100481002(pTableNo@1100481012 : Integer;pFieldNo@1100481000 : Integer;VAR pValue@1100481001 : Text[1024]);
    VAR
      lRecRef@1100481010 : RecordRef;
      lFieldRef@1100481011 : FieldRef;
      lBool@1100481002 : Boolean;
      lInt@1100481003 : Integer;
      lDec@1100481004 : Decimal;
      lText@1100481005 : Text[1024];
      lCode@1100481006 : Code[1024];
      lDate@1100481007 : Date;
      lTime@1100481008 : Time;
      lDateTime@1100481009 : DateTime;
    BEGIN
      // Permet de formater la valeur de r‚f‚rence

      IF pTableNo = 0 THEN BEGIN
        EXIT;
      END;

      IF pFieldNo = 0 THEN BEGIN
        EXIT;
      END;

      lRecRef.OPEN(pTableNo);
      lFieldRef := lRecRef.FIELD(pFieldNo);

      CASE UPPERCASE(FORMAT(lFieldRef.TYPE)) OF
        'BOOLEAN' : BEGIN
          EVALUATE(lBool, pValue);
          pValue := FORMAT(lBool);
        END;
        'INTEGER', 'DURATION' : BEGIN
          EVALUATE(lInt, pValue);
          pValue := FORMAT(lInt);
        END;
        'DECIMAL' : BEGIN
          EVALUATE(lDec, pValue);
          pValue := FORMAT(lDec);
        END;
        'CODE' : BEGIN
          EVALUATE(lCode, pValue);
          pValue := lCode;
        END;
        'DATE' : BEGIN
          EVALUATE(lDate, pValue);
          pValue := FORMAT(lDate);
        END;
        'TIME' : BEGIN
          EVALUATE(lTime, pValue);
          pValue := FORMAT(lTime);
        END;
        'DATETIME' : BEGIN
          EVALUATE(lDateTime, pValue);
          pValue := FORMAT(lDateTime);
        END;
        'OPTION' : BEGIN
          lFieldRef.SETFILTER(pValue);
          pValue := lFieldRef.GETFILTER;
        END;
      END;
    END;

    PROCEDURE TouchRecord@1000000001(pInsert@1000000000 : Boolean);
    BEGIN
      IF pInsert THEN BEGIN
        "Creation Date" := CURRENTDATETIME;
        "Creation User ID" := USERID;
      END;
      "Last Modification Date" := CURRENTDATETIME;
      "Last Modification User ID" := USERID;
    END;

    BEGIN
    {
      /** @r IMH01 @d 23/09/10 @a ISAT.ZW @v IMH6.01.003 @s Attach. Management @c Creation */
    }
    END.
  }
}