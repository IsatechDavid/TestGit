OBJECT Table 50028 Provider Business Hours
{
  OBJECT-PROPERTIES
  {
    Date=17/09/12;
    Time=14:07:16;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Default Value" := IsDefaultValue(TRUE); // IMH02.ISAT.MA
               SetUrgencyModification(FALSE); //IMH03.ISAT.VA
             END;

    OnModify=BEGIN
               "Default Value" := IsDefaultValue(FALSE); // IMH02.ISAT.MA
               SetUrgencyModification(FALSE); //IMH03.ISAT.VA
             END;

    OnDelete=BEGIN
               SetUrgencyModification(TRUE); //IMH03.ISAT.VA
             END;

    CaptionML=[ENU=Provider Business Hours;
               FRA=Heures d'ouverture prestataire];
    LookupFormID=Form50147;
    DrillDownFormID=Form50147;
  }
  FIELDS
  {
    { 1   ;   ;Provider No.        ;Code20        ;TableRelation=Provider;
                                                   CaptionML=[ENU=Provider No.;
                                                              FRA=Nø prestataire] }
    { 2   ;   ;Emergency           ;Boolean       ;CaptionML=[ENU=Emergency;
                                                              FRA=Urgence] }
    { 3   ;   ;Day                 ;Option        ;CaptionML=[ENU=Day;
                                                              FRA=Jour];
                                                   OptionCaptionML=[ENU=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday;
                                                                    FRA=Lundi,Mardi,Mercredi,Jeudi,Vendredi,Samedi,Dimanche];
                                                   OptionString=Monday,Tuesday,Wednesday,Thursday,Friday,Saturday,Sunday }
    { 10  ;   ;Start Time          ;Time          ;CaptionML=[ENU=Start Time;
                                                              FRA=Heure d‚but] }
    { 11  ;   ;End Time            ;Time          ;CaptionML=[ENU=Edn Time;
                                                              FRA=Heure fin] }
    { 20  ;   ;Default Value       ;Boolean       ;CaptionML=[ENU=Default Value;
                                                              FRA=Valeur par d‚faut];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Provider No.,Emergency,Day              ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      ProviderSetup@1100281001 : Record 50011;
      ProviderSetupGet@1100281000 : Boolean;

    PROCEDURE IsDefaultValue@1100281003(pInsert@1100281000 : Boolean) : Boolean;
    BEGIN
      // IMH02.ISAT.MA
      // D‚termine si la valeur est celle par d‚faut

      GetProviderSetup;
      CASE Day OF
        Day::Monday : BEGIN
          EXIT(
            ("Start Time" = ProviderSetup."Start Monday Business Hours") AND
            ("End Time" = ProviderSetup."End Monday Business Hours"));
        END;
        Day::Tuesday : BEGIN
          EXIT(
            ("Start Time" = ProviderSetup."Start Tuesday Business Hours") AND
            ("End Time" = ProviderSetup."End Tuesday Business Hours"));
        END;
        Day::Wednesday : BEGIN
          EXIT(
            ("Start Time" = ProviderSetup."Start Wednes. Business Hours") AND
            ("End Time" = ProviderSetup."End Wednes. Business Hours"));
        END;
        Day::Thursday : BEGIN
          EXIT(
            ("Start Time" = ProviderSetup."Start Thursday Business Hours") AND
            ("End Time" = ProviderSetup."End Thursday Business Hours"));
        END;
        Day::Friday: BEGIN
          EXIT(
            ("Start Time" = ProviderSetup."Start Friday Business Hours") AND
            ("End Time" = ProviderSetup."End Friday Business Hours"));
        END;
        Day::Saturday : BEGIN
          EXIT(
            ("Start Time" = ProviderSetup."Start Satur. Business Hours") AND
            ("End Time" = ProviderSetup."End Satur. Business Hours"));
        END;
        Day::Sunday : BEGIN
          EXIT(
            ("Start Time" = ProviderSetup."Start Sunday Business Hours") AND
            ("End Time" = ProviderSetup."End Sunday Business Hours"));
        END;
      END;
    END;

    PROCEDURE SetUrgencyModification@1100281001(ToDelete@1100281001 : Boolean);
    VAR
      lProviderBusinessHours@1100281000 : Record 50028;
      lProvider@1100281002 : Record 50000;
    BEGIN
      //IMH03
      IF NOT Emergency THEN BEGIN
        EXIT;
      END;

      lProvider.GET("Provider No.");
      IF NOT lProvider."Copy Calendar" THEN BEGIN
        EXIT;
      END;

      lProviderBusinessHours := Rec;
      //lProviderBusinessHours."Default Value"
      lProviderBusinessHours.Emergency := FALSE;
      IF NOT lProviderBusinessHours.INSERT(TRUE) THEN
        lProviderBusinessHours.MODIFY(TRUE);
      IF (ToDelete) THEN
        IF lProviderBusinessHours.DELETE(TRUE) THEN;
    END;

    PROCEDURE GetProviderSetup@1100281000();
    BEGIN
      // IMH02.ISAT.MA
      IF ProviderSetupGet THEN
        EXIT;
      ProviderSetup.GET;
      ProviderSetupGet := TRUE;
    END;

    BEGIN
    {
      /** @r IHM03 @d 22/06/11 @a ISAT.VA @v IMH6.01.177 @s Calendrier @c Gestion des copies de calandrier */
      /** @r IMH02 @d 08/04/11 @a ISAT.MA @v IMH6.01.020 @s Gestion prestataire @h 535 @c Ajout champ 20  */
      /** @r IMH01 @d 30/11/10 @a ISAT.PC @v IMH6.01.020 @s Gestion prestataire @c Creation  */
    }
    END.
  }
}