OBJECT Table 50058 Corporate Region
{
  OBJECT-PROPERTIES
  {
    Date=17/07/13;
    Time=11:44:36;
    Modified=Yes;
    Version List=IMH6.01,MATMUT01;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TESTFIELD("Sell-to Customer No.");  //IMH05
               TouchRecord(TRUE);
             END;

    OnModify=BEGIN
               TouchRecord(FALSE);
             END;

    OnDelete=VAR
               lPartnerCrossRef@1100281000 : Record 8016748;
               lCorpExpert@1100281001 : Record 50158;
             BEGIN
               lPartnerCrossRef.DropFromSourceTable2(DATABASE::"Corporate Region", "Corporate No.", "Region Code"); //IMH03.ST
               lCorpExpert.SETRANGE("Corporate No.", "Corporate No.");
               lCorpExpert.SETRANGE("Region Code", "Region Code");
               IF NOT lCorpExpert.ISEMPTY THEN
                 lCorpExpert.DELETEALL;
             END;

    OnRename=BEGIN
               TouchRecord(FALSE);
             END;

    CaptionML=[ENU=Corporate Region;
               FRA=R‚gion grand comptes];
    LookupFormID=Form50058;
    DrillDownFormID=Form50058;
  }
  FIELDS
  {
    { 1   ;   ;Corporate No.       ;Code20        ;TableRelation=Customer.No. WHERE (Service Contact=CONST(Corporate Customer),
                                                                                     Service Corporate=CONST(Yes));
                                                   CaptionML=[ENU=Corporate No.;
                                                              FRA=Nø grand compte];
                                                   NotBlank=Yes }
    { 2   ;   ;Region Code         ;Code10        ;CaptionML=[ENU=Region Code;
                                                              FRA=Code r‚gion];
                                                   NotBlank=Yes }
    { 3   ;   ;Name                ;Text50        ;CaptionML=[ENU=Name;
                                                              FRA=Nom] }
    { 10  ;   ;Sell-to Customer No.;Code20        ;TableRelation=Customer.No.;
                                                   CaptionML=[ENU=Sell-to Customer No.;
                                                              FRA=Nø client donneur d'odre] }
    { 20  ;   ;Main Contact No.    ;Code20        ;TableRelation=Contact.No.;
                                                   OnLookup=VAR
                                                              lContact@1100281000 : Record 5050;
                                                            BEGIN
                                                              lContact.SETRANGE("Corporate No.","Corporate No.");
                                                              lContact.SETFILTER("Corporate Region Code",'%1|%2','',"Region Code");
                                                              IF lContact.ISEMPTY THEN EXIT;
                                                              IF ISSERVICETIER THEN BEGIN
                                                                IF PAGE.RUNMODAL(0,lContact) = ACTION::LookupOK THEN
                                                                  "Main Contact No." := lContact."No.";
                                                              END ELSE BEGIN
                                                                IF FORM.RUNMODAL(0,lContact) = ACTION::LookupOK THEN
                                                                  "Main Contact No." := lContact."No.";
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Main Contact No.;
                                                              FRA=Nø contact principal] }
    { 50  ;   ;Phone No.           ;Text30        ;OnValidate=VAR
                                                                lCust@1100281001 : Record 18;
                                                                lPhoneStandardization@1100281000 : Codeunit 50099;
                                                              BEGIN
                                                                IF lCust.GET("Corporate No.") THEN
                                                                  lPhoneStandardization.ValidatePhoneNo(DATABASE::Contact,9,lCust."Country/Region Code","Phone No.");
                                                              END;

                                                   ExtendedDatatype=Phone No.;
                                                   CaptionML=[ENU=Phone No.;
                                                              FRA=Nø t‚l‚phone] }
    { 51  ;   ;Fax No.             ;Text30        ;OnValidate=VAR
                                                                lCust@1100281001 : Record 18;
                                                                lPhoneStandardization@1100281000 : Codeunit 50099;
                                                              BEGIN
                                                                IF lCust.GET("Corporate No.") THEN
                                                                  lPhoneStandardization.ValidatePhoneNo(DATABASE::Contact,84,lCust."Country/Region Code","Fax No.");
                                                              END;

                                                   CaptionML=[ENU=Fax No.;
                                                              FRA=Nø t‚l‚copie] }
    { 52  ;   ;E-Mail              ;Text80        ;OnValidate=VAR
                                                                lEmailStandardization@1100281000 : Codeunit 50096;
                                                              BEGIN
                                                                lEmailStandardization.ValidateEmail("E-Mail");
                                                              END;

                                                   ExtendedDatatype=E-Mail;
                                                   CaptionML=[ENU=E-Mail;
                                                              FRA=E-mail] }
    { 60  ;   ;Corporate Agreement ;Boolean       ;CaptionML=[ENU=Corporate Agreement;
                                                              FRA=Agr‚ment grand compte] }
    { 100 ;   ;Address             ;Text50        ;CaptionML=[ENU=Address;
                                                              FRA=Adresse] }
    { 101 ;   ;Address 2           ;Text50        ;CaptionML=[ENU=Address 2;
                                                              FRA=Adresse (2Šme ligne)] }
    { 102 ;   ;City                ;Text30        ;TableRelation=IF (Post Code=FILTER(<>'')) "Post Code".City WHERE (Code=FIELD(Post Code))
                                                                 ELSE IF (Post Code=FILTER('')) "Post Code".City;
                                                   OnValidate=BEGIN
                                                                PostCode.ValidateCity(City,"Post Code", "Country/Region Code"); //IMH24 Ajout Code pays
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=City;
                                                              FRA=Ville] }
    { 103 ;   ;Post Code           ;Code20        ;TableRelation="Post Code".Code;
                                                   OnValidate=BEGIN
                                                                PostCode.ValidatePostCode(City,"Post Code", "Country/Region Code");  //IMH24 Ajout Code pays
                                                              END;

                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Post Code;
                                                              FRA=Code postal] }
    { 105 ;   ;County              ;Text30        ;CaptionML=[ENU=County;
                                                              FRA=R‚gion] }
    { 110 ;   ;Country/Region Code ;Code10        ;TableRelation=Country/Region;
                                                   OnValidate=VAR
                                                                lAddressStandardization@1100281000 : Codeunit 50098;
                                                                lCountryRegion@1100281001 : Record 9;
                                                              BEGIN
                                                              END;

                                                   CaptionML=[ENU=Country/Region Code;
                                                              FRA=Code pays/r‚gion] }
    { 120 ;   ;SDR64 Provider No.  ;Code20        ;CaptionML=[ENU=SDR64 Provider No.;
                                                              FRA=Nø prestataire SDR64] }
    { 130 ;   ;DARVA Subscriber No.;Code20        ;CaptionML=[ENU=DARVA Subscriber No.;
                                                              FRA=Nø abonn‚ DARVA] }
    { 200 ;   ;Expert Companies    ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Corporate Expert" WHERE (Corporate No.=FIELD(Corporate No.),
                                                                                               Region Code=FIELD(Region Code)));
                                                   CaptionML=[ENU=Expert Companies;
                                                              FRA=Cabinets d'expertise];
                                                   Editable=No }
    { 50500;  ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              FRA=Date de cr‚ation];
                                                   Editable=No }
    { 50501;  ;Creation User ID    ;Text30        ;TableRelation="User Setup"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Creation User ID;
                                                              FRA=Utilisateur cr‚ation];
                                                   Editable=No }
    { 50502;  ;Last Modification Date;DateTime    ;CaptionML=[ENU=Last Modification Date;
                                                              FRA=Date derniŠre modification];
                                                   Editable=No }
    { 50503;  ;Last Modification User ID;Text30   ;TableRelation="User Setup"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Last Modification User ID;
                                                              FRA=Utilisateur derniŠre modification];
                                                   Editable=No }
    { 54080;  ;Extranet            ;Boolean       ;CaptionML=[ENU=Extranet;
                                                              FRA=Extranet] }
    { 56010;  ;Fusion Corporate Region Code;Code50;CaptionML=[ENU=Fusion Corporate Region Code;
                                                              FRA=Code r‚gion grand compte Fusion] }
    { 58000;  ;Show SDR64 Button   ;Boolean       ;CaptionML=[ENU=Show SDR64 Button;
                                                              FRA=Afficher bouton SDR64] }
    { 58001;  ;Show DARVA Button   ;Boolean       ;CaptionML=[ENU=Show DARVA Button;
                                                              FRA=Afficher bouton DARVA] }
  }
  KEYS
  {
    {    ;Corporate No.,Region Code               ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Region Code,Name,Corporate No.           }
  }
  CODE
  {
    VAR
      PostCode@1100281000 : Record 225;

    PROCEDURE TouchRecord@1100281006(pInsert@1100281000 : Boolean);
    BEGIN
      IF pInsert THEN BEGIN
        "Creation Date" := CURRENTDATETIME;
        "Creation User ID" := USERID;
      END;

      "Last Modification Date" := CURRENTDATETIME;
      "Last Modification User ID" := USERID;
    END;

    PROCEDURE OpenCustCard@1100281000();
    VAR
      lCust@1100281000 : Record 18;
    BEGIN
      IF NOT lCust.GET("Sell-to Customer No.") THEN
        EXIT;
      IF ISSERVICETIER THEN
        PAGE.RUNMODAL(PAGE::"Customer Card",lCust)
      ELSE
        FORM.RUNMODAL(FORM::"Customer Card",lCust);
    END;

    PROCEDURE UpdateGTACode@1100281001(pCode@1100281000 : Code[30]);
    VAR
      lPartnerCrossRef@1100281001 : Record 8016748;
    BEGIN
      lPartnerCrossRef.SETRANGE("Table ID", DATABASE::"Corporate Region");
      lPartnerCrossRef.SETRANGE("Source No.", "Corporate No.");
      lPartnerCrossRef.SETRANGE("Source No. 2", "Region Code");
      lPartnerCrossRef.SETFILTER("Partner Code", '=%1', '');
      IF lPartnerCrossRef.FINDFIRST THEN BEGIN
        IF lPartnerCrossRef."Partner Reference" <> pCode THEN BEGIN
          lPartnerCrossRef."Partner Reference" := pCode;
          lPartnerCrossRef.MODIFY(TRUE);
        END;
      END
      ELSE BEGIN
        lPartnerCrossRef.INIT;
        lPartnerCrossRef."Table ID" := DATABASE::"Corporate Region";
        lPartnerCrossRef."Source No." := "Corporate No.";
        lPartnerCrossRef."Source No. 2" := "Region Code";
        lPartnerCrossRef."Partner Code" := '';
        lPartnerCrossRef."Partner Reference" := pCode;
        lPartnerCrossRef.INSERT(TRUE);
      END;
    END;

    PROCEDURE GetGTACode@1100281002() : Code[30];
    VAR
      lPartnerCrossRef@1100281000 : Record 8016748;
    BEGIN
      lPartnerCrossRef.SETRANGE("Table ID", DATABASE::"Corporate Region");
      lPartnerCrossRef.SETRANGE("Source No.", "Corporate No.");
      lPartnerCrossRef.SETRANGE("Source No. 2", "Region Code");
      lPartnerCrossRef.SETFILTER("Partner Code", '=%1', '');
      IF lPartnerCrossRef.FINDFIRST THEN
        EXIT(lPartnerCrossRef."Partner Reference");
    END;

    BEGIN
    {
      /** @r IMH09 @d 17/07/13 @a ISAT.PC @v IMH6.01.348 @s DARVA MATMUT
          @c Ajout champ 58000, Show SDR64 Button
          @c Ajout champ 58001, Show DARVA Button */
      /** @r IMH08 @d 29/11/12 @a ISAT.ZW @v IMH6.01.220 @s Projet Facturation Fusion @c Ajout champ 56010 Fusion Corporate Region Code */
      /** @r IMH07 @d 21/12/11 @a ISAT.EBA @v IMH6.01.018 @s Gestion des agr‚ments
          @c Ajout champ 60 Corporate Agreement */
      /** @r IMH06 @d 12/07/11 @a ISAT.MA @v IMH6.01.132 @s DARVA Management
          @c Ajout champs 120 et 130 */
      /** @r IMH05 @d 26/04/11 @a ISAT.ZW @v IMH6.01.131 @s Gestion des contacts
          @c Client donneur d'ordre FlowField --> Vrai champ */
      /** @r IMH05 @d 26/04/11 @a ISAT.ZW @v IMH6.01.131 @s Gestion des contacts @c Client donneur d'ordre obligatoire */
      /** @r IMH04 @d 24/03/11 @a ISAT.ZW @v IMH6.01.131 @s Moteur EAI @c Ajout champ Phone no + fax no + email */
      /** @r IMH03 @d 28/02/11 @a ISAT.ST @v IMH6.01.131 @s Moteur EAI @c Suppression r‚f‚rence EAI */
      /** @r IMH02 @d 07/12/10 @a ISAT.LLT @v IMH6.01.002 @s Gestion des contacts @c Ajout du champ 20 Main Contact No. */
      /** @r IMH01 @d 24/09/10 @a ISAT.LLT @v IMH6.01.002 @s Gestion des contacts @c Cr‚ation */
    }
    END.
  }
}