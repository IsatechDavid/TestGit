OBJECT Table 50063 Service Catalog Exception
{
  OBJECT-PROPERTIES
  {
    Date=16/02/11;
    Time=15:18:52;
    Modified=Yes;
    Version List=IMH6.01;
  }
  PROPERTIES
  {
    DataCaptionFields=Corporate No.,Service Order Type;
    OnInsert=BEGIN
               TouchRecord(TRUE);
               InitRecFilters;
             END;

    OnModify=BEGIN
               TouchRecord(FALSE);
             END;

    OnRename=BEGIN
               TouchRecord(FALSE);
             END;

    CaptionML=[ENU=Service Catalog Exception;
               FRA=Cas particulier produit];
    LookupFormID=Form50063;
    DrillDownFormID=Form50063;
  }
  FIELDS
  {
    { 1   ;   ;Corporate No.       ;Code20        ;TableRelation=Customer.No. WHERE (Service Contact=CONST(Corporate Customer),
                                                                                     Service Corporate=CONST(Yes));
                                                   CaptionML=[ENU=Corporate No.;
                                                              FRA=Nø grand compte];
                                                   NotBlank=Yes }
    { 2   ;   ;Service Order Type  ;Code10        ;TableRelation="Service Order Type";
                                                   CaptionML=[ENU=Service Order Type;
                                                              FRA=Type produit];
                                                   NotBlank=Yes }
    { 3   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              FRA=Nø ligne] }
    { 10  ;   ;Service Filter      ;Text250       ;CaptionML=[ENU=Service Filter;
                                                              FRA=Filtre ligne produit] }
    { 30  ;   ;Set Quote           ;Boolean       ;CaptionML=[ENU=Set Quote;
                                                              FRA=Devis] }
    { 31  ;   ;Set Urgent          ;Boolean       ;CaptionML=[ENU=Set Urgent;
                                                              FRA=Urgent] }
    { 50500;  ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              FRA=Date de cr‚ation];
                                                   Editable=No }
    { 50501;  ;Creation User ID    ;Text30        ;TableRelation="User Setup"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Creation User ID;
                                                              FRA=Utilisateur cr‚ation];
                                                   Editable=No }
    { 50502;  ;Last Modification Date;DateTime    ;CaptionML=[ENU=Last Modification Date;
                                                              FRA=Date derniŠre modification];
                                                   Editable=No }
    { 50503;  ;Last Modification User ID;Text30   ;TableRelation="User Setup"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Last Modification User ID;
                                                              FRA=Utilisateur derniŠre modification];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Corporate No.,Service Order Type,Line No.;
                                                   Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text002@1100281000 : TextConst;

    PROCEDURE TouchRecord@1000000001(pInsert@1000000000 : Boolean);
    BEGIN
      IF pInsert THEN BEGIN
        "Creation Date" := CURRENTDATETIME;
        "Creation User ID" := USERID;
      END;
      "Last Modification Date" := CURRENTDATETIME;
      "Last Modification User ID" := USERID;
    END;

    PROCEDURE InitRecFilters@1100281004();
    BEGIN
      SaveFilter('WHERE (Field50018=1('+ "Corporate No."+'),Field5904=1(' + "Service Order Type" + '))');
    END;

    PROCEDURE GetRecFilters@1100281003(pWithName@1100281000 : Boolean) ReturnValue : Text[1024];
    VAR
      lRecordRef@1100281001 : RecordRef;
    BEGIN
      IF ("Service Filter" = '') THEN
        EXIT('');
      ReturnValue := "Service Filter";
      IF pWithName THEN BEGIN
        lRecordRef.OPEN(DATABASE::"Service Header", TRUE);
        lRecordRef.SETVIEW(ReturnValue);
        IF STRLEN(lRecordRef.GETVIEW(TRUE)) > MAXSTRLEN(ReturnValue) THEN
          ReturnValue := COPYSTR(lRecordRef.GETVIEW(TRUE), 1, MAXSTRLEN(ReturnValue)-5) + '[...]'
        ELSE
          ReturnValue := lRecordRef.GETVIEW(TRUE);
      END;
    END;

    PROCEDURE SaveFilter@1100281007(pText@1100281000 : Text[1024]);
    BEGIN
      "Service Filter" := GetChars(pText, MAXSTRLEN("Service Filter"));
      IF pText <> '' THEN
        ERROR(Text002);
    END;

    LOCAL PROCEDURE GetChars@1100281008(VAR pText@1100281000 : Text[1024];pStrLen@1100281001 : Integer) ReturnValue : Text[1024];
    BEGIN
      IF pText = '' THEN
        EXIT;
      IF STRLEN(pText) <= pStrLen THEN BEGIN
        ReturnValue := COPYSTR(pText, 1, pStrLen);
        pText := '';
      END
      ELSE BEGIN
        ReturnValue := COPYSTR(pText, 1, pStrLen);
        pText := COPYSTR(pText, pStrLen + 1, STRLEN(pText));
      END;
    END;

    PROCEDURE IsSelectable@1100281001(pObject@1100281000 : Record 2000000001) : Boolean;
    VAR
      lTempObject@1100281001 : TEMPORARY Record 2000000001;
    BEGIN
      IF pObject.Type <> pObject.Type::Table THEN
        EXIT;
      lTempObject := pObject;
      lTempObject.INSERT;
      lTempObject.SETFILTER(ID, GetSelectableTable);
      EXIT(NOT lTempObject.ISEMPTY);
    END;

    PROCEDURE GetSelectableTable@1100281000() : Text[1024];
    BEGIN
      EXIT('5900');
    END;

    PROCEDURE PreviewRec@1100281002(pForUpdate@1100281002 : Boolean);
    VAR
      lServHeaderList@1100281013 : Form 50075;
      lServHeader@1100281014 : Record 5900;
      lViewFilter@1100281001 : Text[1024];
      lRecRef@1100281004 : RecordRef;
    BEGIN
      lViewFilter := GetRecFilters(FALSE);

      lServHeader.SETVIEW(lViewFilter);
      lServHeaderList.SETTABLEVIEW(lServHeader);
      IF pForUpdate THEN BEGIN
        lServHeaderList.RUNMODAL;
        lServHeaderList.CopyRec(lServHeader);
        SaveFilter(lServHeader.GETVIEW(FALSE));
      END
      ELSE
        lServHeaderList.RUN;
      EXIT;
    END;

    BEGIN
    {
      /** @r IMH01 @d 01/10/10 @a ISAT.LLT @v IMH6.01.010 @s Gestion du catalogue @c Cr‚ation */
    }
    END.
  }
}