OBJECT Table 50079 Code
{
  OBJECT-PROPERTIES
  {
    Date=15/04/14;
    Time=09:56:26;
    Modified=Yes;
    Version List=IMH6.01,IMH6.01.P011;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lCodeUsage@1100281000 : Record 50078;
               lNoSeriesMgt@1100281001 : Codeunit 396;
             BEGIN
               IF (GETFILTER("Table ID") <> '') THEN
                 IF EVALUATE("Table ID",GETFILTER("Table ID")) THEN
                   "Table ID" := "Table ID";

               IF (GETFILTER("Field ID") <> '') THEN
                 IF EVALUATE("Field ID",GETFILTER("Field ID")) THEN
                   "Field ID" :=  "Field ID";

               //<<IMH02.isat.PC
               //IF Code = '' THEN
               //  ERROR(STRSUBSTNO(Text002, FIELDCAPTION(Code)));
               IF Code = '' THEN BEGIN
                 IF lCodeUsage.GET("Table ID","Field ID") THEN;
                 IF lCodeUsage."Code Nos." <> '' THEN BEGIN
                   IF Code <> '' THEN BEGIN
                     lNoSeriesMgt.TestManual(lCodeUsage."Code Nos.");
                   END;
                   lNoSeriesMgt.InitSeries(lCodeUsage."Code Nos.",xRec."No. Series",0D,Code,"No. Series");
                 END ELSE BEGIN
                   ERROR(STRSUBSTNO(Text002, FIELDCAPTION(Code)));
                 END;
               END;
               //>>IMH02.isat.PC
             END;

    OnDelete=VAR
               lCodeUsage@1100281000 : Record 50078;
               lRecRef@1100281001 : Integer;
             BEGIN
               IF lCodeUsage.GET("Table ID", "Field ID") THEN
                 IF lCodeUsage."Delete Action" = lCodeUsage."Delete Action"::DropRecord THEN
                   DeleteAttachedRec;
             END;

    OnRename=BEGIN
               IF Code = '' THEN
                 ERROR(STRSUBSTNO(Text002, FIELDCAPTION(Code)));
             END;

    CaptionML=[ENU=Code;
               FRA=Code];
    LookupFormID=Form50099;
    DrillDownFormID=Form50099;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=[ENU=TableID;
                                                              FRA=ID Table] }
    { 2   ;   ;Field ID            ;Integer       ;CaptionML=[ENU=FieldID;
                                                              FRA=ID champ] }
    { 3   ;   ;Code                ;Code10        ;CaptionML=[ENU=Code;
                                                              FRA=Code] }
    { 4   ;   ;Description         ;Text50        ;AltSearchField=Search Code;
                                                   CaptionML=[ENU=Description;
                                                              FRA=Description] }
    { 5   ;   ;Search Code         ;Code10        ;AltSearchField=Search Code;
                                                   CaptionML=[ENU=Search Code;
                                                              FRA=Code de recherche] }
    { 20  ;   ;Home Service Retractation;Boolean  ;CaptionML=[ENU=Home Service Retractation;
                                                              FRA=R‚tractation service … l'habitat] }
    { 30  ;   ;Comment Mandatory   ;Boolean       ;CaptionML=[ENU=Comment Mandatory;
                                                              FRA=Commentaire obligatoire] }
    { 33  ;   ;Comment             ;Text100       ;CaptionML=[ENU=Comment;
                                                              FRA=Commentaires] }
    { 99  ;   ;Loading Index No.   ;Integer       ;CaptionML=[ENU=Loading Index No.;
                                                              FRA=Nø index chargement];
                                                   BlankZero=Yes }
    { 100 ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø] }
    { 110 ;   ;Hide For Extranet   ;Boolean       ;CaptionML=[ENU=Hide For Extranet;
                                                              FRA=Masquer sur extranet] }
    { 50060;  ;Show To DGS         ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                VALIDATE("Showing Filter");
                                                              END;

                                                   CaptionML=[ENU=Show To Contact;
                                                              FRA=Afficher pour le DGS] }
    { 50061;  ;Show To DPR         ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                VALIDATE("Showing Filter");
                                                              END;

                                                   CaptionML=[ENU=Show To Service;
                                                              FRA=Afficher pour le DPR] }
    { 50062;  ;Show To DFC         ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                VALIDATE("Showing Filter");
                                                              END;

                                                   CaptionML=[ENU=Show To DFC;
                                                              FRA=Afficher pour le DFC] }
    { 50063;  ;Show To DQP         ;Boolean       ;InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                VALIDATE("Showing Filter");
                                                              END;

                                                   CaptionML=[ENU=Show To DQP;
                                                              FRA=Afficher pour le DQP] }
    { 50069;  ;Showing Filter      ;Code4         ;InitValue=1111;
                                                   OnValidate=BEGIN
                                                                IF "Show To DGS" THEN
                                                                  "Showing Filter"[1] := '1'
                                                                ELSE
                                                                  "Showing Filter"[1] := '0';
                                                                IF "Show To DPR" THEN
                                                                  "Showing Filter"[2] := '1'
                                                                ELSE
                                                                  "Showing Filter"[2] := '0';
                                                                IF "Show To DFC" THEN
                                                                  "Showing Filter"[3] := '1'
                                                                ELSE
                                                                  "Showing Filter"[3] := '0';
                                                                IF "Show To DQP" THEN
                                                                  "Showing Filter"[4] := '1'
                                                                ELSE
                                                                  "Showing Filter"[4] := '0';
                                                              END;

                                                   CaptionML=[ENU=Showing Filter;
                                                              FRA=Filtre affichage] }
  }
  KEYS
  {
    {    ;Table ID,Field ID,Code                  ;Clustered=Yes }
    {    ;Code,Description                         }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description                         }
  }
  CODE
  {
    VAR
      Text001@1000000000 : TextConst 'ENU=Field definition is missing (T%1 F%2);FRA=D‚finition du champ manquante (T%1 F%2)';
      Text002@1100281000 : TextConst 'ENU=%1 can''t be empty.;FRA=%1 ne peut ˆtre vide.';

    PROCEDURE GetCaption@1000000000() : Text[250];
    VAR
      lField@1000000002 : Record 2000000041;
    BEGIN
      IF (GETFILTER("Table ID") = '') OR (GETFILTER("Field ID") = '') THEN BEGIN
         FILTERGROUP(2);
         IF (GETFILTER("Table ID") = '') OR (GETFILTER("Field ID") = '') THEN BEGIN
           FILTERGROUP(3);
           IF (GETFILTER("Table ID") = '') OR (GETFILTER("Field ID") = '') THEN BEGIN
             FILTERGROUP(4);
             IF (GETFILTER("Table ID") = '') OR (GETFILTER("Field ID") = '') THEN BEGIN
               FILTERGROUP(0);
               EXIT('');
             END;
           END;
         END;
      END;
      EVALUATE(lField.TableNo,GETFILTER("Table ID"));
      EVALUATE(lField."No.",GETFILTER("Field ID"));
      FILTERGROUP(0);
      IF NOT lField.GET(lField.TableNo,lField."No.") THEN
        MESSAGE(Text001,lField."No.",lField.TableNo);
      EXIT(lField."Field Caption");
    END;

    PROCEDURE GetDescription@1100281001(pTableID@1100281002 : Integer;pFieldID@1100281001 : Integer;pCode@1100281003 : Code[10]) : Text[50];
    VAR
      lCode@1100281000 : Record 50079;
    BEGIN
      // Permet de r‚cup‚rer la designation du code

      IF lCode.GET(pTableID,pFieldID,pCode) THEN BEGIN
        EXIT(lCode.Description);
      END;
    END;

    LOCAL PROCEDURE DeleteAttachedRec@1100281000();
    VAR
      lRecRef@1100281000 : RecordRef;
      lFieldRef@1100281001 : FieldRef;
    BEGIN
      IF ("Table ID" = 0) OR ("Field ID" = 0) THEN
        EXIT;
      lRecRef.OPEN("Table ID");
      lFieldRef := lRecRef.FIELD("Field ID");
      lFieldRef.SETRANGE(Code);
      IF NOT lRecRef.ISEMPTY THEN
        lRecRef.DELETEALL(TRUE);
    END;

    BEGIN
    {
      /** @r IMH10 @d 15/04/14 @a ISAT.PC @v IMH6.01.P011 @s New Provider Search
          @c Add field 30, Comment Mandatory
          @c Add field 33, Comment */
      /** @r IMH09 @d 16/12/13 @a IMH.ZW @v IMH6.01.4905 @s Gestion affichage code @h 4905
          @c Ajout champs 50061 - 50069 */
      /** @r IMH06 @d 15/10/11 @a ISAT.EBA @v IMH6.01.011 @s Gestion du service
          @c Ajout champ 20 "Home Service Retractation" */
      /** @r IMH05 @d 25/07/11 @a ISAT.ZW @v IMH6.01.001 @s Normalisation des donn‚es @c Ajout Champ description 2*/
      /** @r IMH04 @d 16/06/11 @a ISAT.PC @v IMH6.01.001 @s Normalisation des donn‚es @c Ajout cl‚ Code,Description - gestion DropDown*/
      /** @r IMH03 @d 27/01/11 @a ISAT.ST @v IMH6.01.011 @s Gestion du service @c Ajout champ 99 Loading Index No. */
      /** @r IMH02 @d 09/12/10 @a ISAT.PC @v IMH6.01.005 @s Annexes @c Ajout champ 100, Series No. @c Gestion de la souche */
      /** @r IMH01 @d 23/09/10 @a ISAT.ST @v IMH6.01.001 @s Normalisation des donn‚es @c Cr‚ation objet */
    }
    END.
  }
}