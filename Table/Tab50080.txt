OBJECT Table 50080 Tray Task
{
  OBJECT-PROPERTIES
  {
    Date=10/06/14;
    Time=16:43:49;
    Modified=Yes;
    Version List=IMH6.01,5005,IMH6.01.P010;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               IF "Source Table" = "Source Table"::" " THEN
                 FIELDERROR("Source Table");
               TouchRecord(TRUE);

               VALIDATE("Showing Filter");   //IMH12
             END;

    OnModify=BEGIN
               TouchRecord(FALSE);

               VALIDATE("Showing Filter");   //IMH12
             END;

    OnDelete=VAR
               lTrayTaskCorp@1100281000 : Record 50081;
               lTeamTrayTask@1100281001 : Record 50129;
               lTrayTask@1100281002 : Record 50080;
             BEGIN
               IF TrayLineExist THEN
                 ERROR(Text001);
               lTrayTaskCorp.SETRANGE("Source Table", "Source Table");
               lTrayTaskCorp.SETRANGE("Task Code", "Task Code");
               IF NOT lTrayTaskCorp.ISEMPTY THEN
                 lTrayTaskCorp.DELETEALL;

               //<<IMH12
               lTrayTask.SETRANGE("Task Code", "Task Code");
               IF lTrayTask.COUNT = 1 THEN BEGIN
                 lTeamTrayTask.SETRANGE("Task Code", "Task Code");
                 IF NOT lTeamTrayTask.ISEMPTY THEN
                   lTeamTrayTask.DELETEALL;
               END;
               //>>IMH12
             END;

    OnRename=BEGIN
               TouchRecord(FALSE);
             END;

    CaptionML=[ENU=Tray Task;
               FRA=Tƒches corbeilles];
    LookupFormID=Form50162;
    DrillDownFormID=Form50162;
  }
  FIELDS
  {
    { 1   ;   ;Source Table        ;Option        ;OnValidate=BEGIN
                                                                CASE "Source Table" OF
                                                                  "Source Table"::" " : "Source Table No." := 0;
                                                                  "Source Table"::Contact : "Source Table No." := DATABASE::Contact;
                                                                  "Source Table"::Service : "Source Table No." := DATABASE::Service;
                                                                  "Source Table"::ServiceHeader : "Source Table No." := DATABASE::"Service Header";
                                                                  "Source Table"::Intervention : "Source Table No." := DATABASE::"Service Intervention Line";
                                                                  "Source Table"::DocumentQueue : "Source Table No." := DATABASE::"Document Queue";
                                                                  "Source Table"::CompletnessTask : "Source Table No." := DATABASE::"Completness Task";
                                                                  "Source Table"::InterventionQuote : "Source Table No." := DATABASE::"Service Intervention Quote";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Source Table;
                                                              FRA=Table origine];
                                                   OptionCaptionML=[ENU=" ,Contact,Service,Service Header,Intervention,Document Queue,Completness Task,InterventionQuote";
                                                                    FRA=" ,Contact,Service,Ligne produit,Intervention,File de document,Tƒche compl‚tude,Devis intervention"];
                                                   OptionString=[ ,Contact,Service,ServiceHeader,Intervention,DocumentQueue,CompletnessTask,InterventionQuote] }
    { 2   ;   ;Task Code           ;Code10        ;CaptionML=[ENU=Task Code;
                                                              FRA=Code tƒche];
                                                   NotBlank=Yes }
    { 10  ;   ;Source Table No.    ;Integer       ;CaptionML=[ENU=Source Table No;
                                                              FRA=Nø table origine] }
    { 11  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 20  ;   ;Default Line Description;Text80    ;CaptionML=[ENU=Default Line Description;
                                                              FRA=Description ligne par d‚faut] }
    { 21  ;   ;End Date Base       ;Option        ;CaptionML=[ENU=End Date Base;
                                                              FRA=Base calcul ‚cheance];
                                                   OptionCaptionML=[ENU=Workdate;
                                                                    FRA=Date de travail];
                                                   OptionString=Workdate }
    { 22  ;   ;End Date Formula    ;DateFormula   ;CaptionML=[ENU=End Fate Formula;
                                                              FRA=Calcul date ‚ch‚ance] }
    { 23  ;   ;End Time Formula    ;Integer       ;CaptionML=[ENU=End Fate Formula;
                                                              FRA=Calcul heure ‚ch‚ance];
                                                   BlankZero=Yes }
    { 30  ;   ;Assist Action       ;Option        ;OnValidate=BEGIN
                                                                CASE "Assist Action" OF
                                                                  "Assist Action"::CreateIntervention :
                                                                    TESTFIELD("Source Table", "Source Table"::ServiceHeader);
                                                                  "Assist Action"::CreateInteraction :
                                                                    IF "Source Table" = "Source Table"::DocumentQueue THEN
                                                                      FIELDERROR("Assist Action");
                                                                END; // CASE
                                                              END;

                                                   CaptionML=[ENU=Assist Action;
                                                              FRA=Action assist‚e];
                                                   OptionCaptionML=[ENU=Open Card,Create Interaction,Find Provider,Create Intervention,Create Service,To Qualify,DT To Treat,Affect document,,Affect Create Doc,Create Claim Service,Create Provider, SD45 To Treat,Show Attachment;
                                                                    FRA=Ouvrir fiche,Cr‚er interaction,Recherche prestataire,Cr‚er intervention,Cr‚er service,A qualifier,DT … traiter,Affecter document service,Affecter/Cr‚er document service,Cr‚er LP r‚clamation,Cr‚er prestataire,SD45 … traiter,Afficher PJ];
                                                   OptionString=OpenCard,CreateInteraction,FindProvider,CreateIntervention,CreateService,ToQualify,DTToTreat,AffectDoc,AffectCreateDoc,CreateClaimServ,CreateProvider,SD45ToTreat,ShowAttach }
    { 31  ;   ;End Date Delay Alert (hour);Integer;CaptionML=[ENU=End Date Delay Alert (hour);
                                                              FRA=D‚lai alerte post ‚ch‚ance (h)];
                                                   BlankZero=Yes }
    { 32  ;   ;Response Expected   ;Boolean       ;CaptionML=[ENU=Response Expected;
                                                              FRA=R‚ponse attendue] }
    { 50  ;   ;Corporate Due Date  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Tray Task Due Date" WHERE (Source Table=FIELD(Source Table),
                                                                                                 Task Code=FIELD(Task Code)));
                                                   CaptionML=[ENU=Due Date Spec.;
                                                              FRA=Ech‚ances grand compte];
                                                   Editable=No }
    { 60  ;   ;Team Code Filter    ;Code10        ;FieldClass=FlowFilter;
                                                   TableRelation=Team.Code;
                                                   CaptionML=[ENU=Team Code Filter;
                                                              FRA=Filtre code corbeille] }
    { 61  ;   ;End Date Filter     ;Date          ;FieldClass=FlowFilter;
                                                   CaptionML=[ENU=End Date Filter;
                                                              FRA=Filtre date d'‚ch‚ance] }
    { 62  ;   ;Tray Lines          ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Tray Line" WHERE (Task Code=FIELD(Task Code),
                                                                                        Source Table No.=FIELD(Source Table No.),
                                                                                        Team Code=FIELD(Team Code Filter),
                                                                                        Closed=CONST(No),
                                                                                        Expected End Date=FIELD(End Date Filter)));
                                                   CaptionML=[ENU=Tray Lines;
                                                              FRA=Lignes corbeilles];
                                                   Editable=No }
    { 70  ;   ;Default Short Comment;Text120      ;CaptionML=[ENU=Default Comment;
                                                              FRA=Commentaire par d‚faut] }
    { 100 ;   ;Enable              ;Boolean       ;CaptionML=[ENU=Enable;
                                                              FRA=Actif] }
    { 50005;  ;Show To DGS         ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("Showing Filter");//IMH08
                                                              END;

                                                   CaptionML=[ENU=Show To Contact;
                                                              FRA=Afficher pour le DGS] }
    { 50006;  ;Show To DPR         ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("Showing Filter");//IMH08
                                                              END;

                                                   CaptionML=[ENU=Show To Service;
                                                              FRA=Afficher pour le DPR] }
    { 50007;  ;Show To DFC         ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("Showing Filter");//IMH08
                                                              END;

                                                   CaptionML=[ENU=Show To DFC;
                                                              FRA=Afficher pour le DFC] }
    { 50008;  ;Show To DQP         ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE("Showing Filter");//IMH08
                                                              END;

                                                   CaptionML=[ENU=Show To DQP;
                                                              FRA=Afficher pour le DQP] }
    { 50010;  ;Showing Filter      ;Code4         ;OnValidate=BEGIN
                                                                //IMH08
                                                                IF "Show To DGS" THEN
                                                                  "Showing Filter"[1] := '1'
                                                                ELSE
                                                                  "Showing Filter"[1] := '0';
                                                                IF "Show To DPR" THEN
                                                                  "Showing Filter"[2] := '1'
                                                                ELSE
                                                                  "Showing Filter"[2] := '0';
                                                                IF "Show To DFC" THEN
                                                                  "Showing Filter"[3] := '1'
                                                                ELSE
                                                                  "Showing Filter"[3] := '0';
                                                                IF "Show To DQP" THEN
                                                                  "Showing Filter"[4] := '1'
                                                                ELSE
                                                                  "Showing Filter"[4] := '0';
                                                              END;

                                                   CaptionML=[ENU=Showing Filter;
                                                              FRA=Filtre affichage] }
    { 50011;  ;Urgent              ;Boolean       ;CaptionML=[ENU=Urgent;
                                                              FRA=Urgent] }
    { 50012;  ;Tray Line Hist. Delay;DateFormula  ;CaptionML=[ENU=Tray Line Hist. Delay;
                                                              FRA=D‚lai historisation activit‚ corbeille] }
    { 50020;  ;Default Team Code   ;Code10        ;TableRelation=Team.Code;
                                                   CaptionML=[ENU=Default Team Code;
                                                              FRA=Code corbeille par d‚faut] }
    { 50030;  ;Team Manual Affectation;Boolean    ;CaptionML=[ENU=Team Manual Affectation;
                                                              FRA=Affectation corbeille manuelle] }
    { 50040;  ;Team Tray Task Nb.  ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("Team Tray Task" WHERE (Task Code=FIELD(Task Code)));
                                                   CaptionML=[ENU=Team Tray Task Nb.;
                                                              FRA=Nb. paramŠtres corbeilles];
                                                   Editable=No }
    { 50500;  ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              FRA=Date de cr‚ation];
                                                   Editable=No }
    { 50501;  ;Creation User ID    ;Text30        ;TableRelation="User Setup"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Creation User ID;
                                                              FRA=Utilisateur cr‚ation];
                                                   Editable=No }
    { 50502;  ;Last Modification Date;DateTime    ;CaptionML=[ENU=Last Modification Date;
                                                              FRA=Date derniŠre modification];
                                                   Editable=No }
    { 50503;  ;Last Modification User ID;Text30   ;TableRelation="User Setup"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Last Modification User ID;
                                                              FRA=Utilisateur derniŠre modification];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Source Table,Task Code                  ;Clustered=Yes }
    {    ;Source Table,Description                 }
    {    ;Task Code                                }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Task Code,Description                    }
  }
  CODE
  {
    VAR
      Text001@1100281000 : TextConst 'ENU=You can''t delete this task because tray activities exists.;FRA=Vous ne pouvez pas supprimer cette tƒche car des lignes d''activit‚s existent.';

    PROCEDURE TouchRecord@1100281006(pInsert@1100281000 : Boolean);
    BEGIN
      IF pInsert THEN BEGIN
        "Creation Date" := CURRENTDATETIME;
        "Creation User ID" := USERID;
      END;
      "Last Modification Date" := CURRENTDATETIME;
      "Last Modification User ID" := USERID;
    END;

    PROCEDURE TrayLineExist@1100281000() : Boolean;
    VAR
      lTrayLine@1100281000 : Record 50048;
    BEGIN
      lTrayLine.SETRANGE("Task Code", "Task Code");
      lTrayLine.SETRANGE("Source Table No.", "Source Table No.");
      EXIT(NOT lTrayLine.ISEMPTY);
    END;

    PROCEDURE GetTrayTask@1100281001(pTrayLine@1100281000 : Record 50048;VAR pTaskToApply@1100281001 : Record 50080) rValue : Boolean;
    VAR
      lTrayTask@1100281002 : Record 50080;
      lTrayTaskSpec@1100281003 : Record 50081;
    BEGIN
      CLEAR(pTaskToApply);
      rValue := FALSE;
      IF pTrayLine."Task Code" = '' THEN
        EXIT;
      IF NOT lTrayTask.GET(pTrayLine."Linked To Table", pTrayLine."Task Code") THEN
        EXIT;
      pTaskToApply := lTrayTask;
      rValue := TRUE;
      IF pTrayLine."Corporate No." = '' THEN
        EXIT;
      lTrayTaskSpec.SETRANGE("Source Table", lTrayTask."Source Table");
      lTrayTaskSpec.SETRANGE("Task Code", lTrayTask."Task Code");
      lTrayTaskSpec.SETRANGE("Corporate No.", pTrayLine."Corporate No.");
      IF lTrayTaskSpec.ISEMPTY THEN
        EXIT;
      pTrayLine.CALCFIELDS("Service Order Type");
      IF pTrayLine."Service Order Type" <> '' THEN BEGIN
        lTrayTaskSpec.SETRANGE("Service Order Type", pTrayLine."Service Order Type");
        IF lTrayTaskSpec.FINDFIRST THEN BEGIN
          CopyTaskSpecToTask(lTrayTaskSpec, pTaskToApply);
          EXIT;
        END;
      END;
      lTrayTaskSpec.SETFILTER("Service Order Type", '=%1', '');
      IF lTrayTaskSpec.FINDFIRST THEN BEGIN
        CopyTaskSpecToTask(lTrayTaskSpec, pTaskToApply);
        EXIT;
      END;
      EXIT;
    END;

    LOCAL PROCEDURE CopyTaskSpecToTask@1100281002(pTrayTaskSpec@1100281001 : Record 50081;VAR pTrayTask@1100281000 : Record 50080);
    BEGIN
      pTrayTask."End Date Base" := pTrayTaskSpec."End Date Base";
      pTrayTask."End Date Formula" := pTrayTaskSpec."End Date Formula";
      pTrayTask."End Time Formula" := pTrayTaskSpec."End Time Formula";
    END;

    BEGIN
    {
      /** @r IMH13 @d 10/06/14 @a ISAT.ZW @v IMH6.01.P010 @s Projet PMU @c Ajout action assit‚ Afficher PJ */
      /** @r IMH12 @d 26/03/14 @a ISAT.ZW @v IMH6.01.5005 @s Reorganisation DPS @h 5005
          @c Ajout champ 50020 + 50012 Tray Line Hist. Delay + 50030 Team Manual Affectation */
      /** @r IMH11 @d 17/10/13 @a ISAT.ACO @v IMH6.01.348 @s DARVA MATMUT Lot 1 @h 4768
          @c Ajout option "SD45 To treat" dans champ "Assist Action" */
      /** @r IMH10 @d 25/07/12 @a ISAT.PC @v IMH6.01.226 @s version 1.4 @h 3469
          @c ajout champ 100, Enable*/
      /** @r IMH09 @d 19/06/12 @a ISAT.ZW @v IMH6.01.226 @s V1.4 @h 1141
          @c Ajout cl‚ Code tache */
      /** @r IMH08 @d 03/05/12 @a ISAT.LLT @v IMH6.01.120 @s Gestion corbeille  @h 1141
          @c Ajout champ 50008
          @c Ajout champ 50010 */
      /** @r IMH07 @d 07/03/12 @a ISAT.ZW @v IMH6.01.120 @s Gestion corbeille @c Ajout action assit‚e "Ev‚nement climatique" */
      /** @r IMH06 @d 23/02/12 @a ISAT.ZW @v IMH6.01.120 @s Gestion corbeille @c Ajout action assit‚e "cr‚er prestataire" */
      /** @r IMH05 @d 16/01/11 @a ISAT.ZW @v IMH6.01.120 @s Gestion corbeille @c Ajout fonction cr‚er LP r‚cla */
      /** @r IMH04 @d 08/07/11 @a ISAT.ST @v IMH6.01.120 @s Gestion corbeille @c Ajout commentaire par d‚faut */
      /** @r IMH03 @d 10/06/2011 @a ISAT.ST @v IMH6.01.120 @s Gestion des corbeilles
          @c Ajout champs 60 Team Code Filter, 61 End Date Filter et 62 Tray Lines */
      /** @r IMH02 @d 02/04/2011 @a ISAT.ST @v IMH6.01.120 @s Gestion des corbeilles
          @c DropDown limit‚ … description seulement + Ajout index Source Table,Description pour le DropDown */
      /** @r IMH01 @d 10/12/2010 @a ISAT.ST @v IMH6.01.013 @s Gestion des corbeilles
          @c Cr‚ation Objet */
    }
    END.
  }
}