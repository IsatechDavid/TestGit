OBJECT Table 50150 Extranet Modif. Journal Line
{
  OBJECT-PROPERTIES
  {
    Date=15/10/13;
    Time=18:19:57;
    Modified=Yes;
    Version List=IMH6.01.08,NEPIMH4;
  }
  PROPERTIES
  {
    OnInsert=VAR
               lExtratModifJournalLine@1100281000 : Record 50150;
             BEGIN
               //<<IMH04
               IF "Entry No." = 0 THEN BEGIN
                 lExtratModifJournalLine.SETRANGE("Source Table ID","Source Table ID");
                 lExtratModifJournalLine.SETRANGE("Source No.","Source No.");
                 lExtratModifJournalLine.SETRANGE("Source No. 2","Source No. 2");
                 lExtratModifJournalLine.SETRANGE("Source No. 3","Source No. 3");
                 lExtratModifJournalLine.SETRANGE("Source No. 4","Source No. 4");
                 lExtratModifJournalLine.SETRANGE("Source No. 5", "Source No. 5");
                 lExtratModifJournalLine.SETRANGE("Source Boolean", "Source Boolean");
                 lExtratModifJournalLine.SETRANGE("Source Date", "Source Date");
                 lExtratModifJournalLine.LOCKTABLE;
                 IF NOT lExtratModifJournalLine.FINDLAST THEN
                   "Entry No." := 1
                 ELSE
                   "Entry No." := lExtratModifJournalLine."Entry No." + 1;
               END;

               //IMH02
               IF "Source Boolean" THEN
                 "Source Boolean Int" := 1
               ELSE
                 "Source Boolean Int" :=0;
               //IMH02

               SetRecordId(TRUE);
               TouchRecord(TRUE);
             END;

    OnModify=BEGIN
               //IMH02
               IF "Source Boolean" THEN
                 "Source Boolean Int" := 1
               ELSE
                 "Source Boolean Int" :=0;
               //IMH02
               TouchRecord(FALSE);
             END;

    OnDelete=VAR
               lExtranetModifJournalField@1100281000 : Record 50156;
             BEGIN
               lExtranetModifJournalField.SETRANGE("Source Table ID","Source Table ID");
               lExtranetModifJournalField.SETRANGE("Source No.","Source No.");
               lExtranetModifJournalField.SETRANGE("Source No. 2","Source No. 2");
               lExtranetModifJournalField.SETRANGE("Source No. 3","Source No. 3");
               lExtranetModifJournalField.SETRANGE("Source No. 4","Source No. 4");
               lExtranetModifJournalField.SETRANGE("Source No. 5", "Source No. 5");
               lExtranetModifJournalField.SETRANGE("Source Boolean", "Source Boolean");
               lExtranetModifJournalField.SETRANGE("Source Date", "Source Date");
               lExtranetModifJournalField.SETRANGE("Entry No.", "Entry No.");
               IF NOT lExtranetModifJournalField.ISEMPTY THEN BEGIN
                 lExtranetModifJournalField.DELETEALL;
               END;
             END;

    OnRename=BEGIN
               SetRecordId(FALSE);
               TouchRecord(FALSE);
             END;

    CaptionML=[ENU=Extrat Modif. Journal Line;
               FRA=Ligne feuille modification Extranet];
    LookupFormID=Form50278;
    DrillDownFormID=Form50278;
  }
  FIELDS
  {
    { 1   ;   ;Source Table ID     ;Integer       ;TableRelation=AllObj."Object ID" WHERE (Object Type=CONST(Table));
                                                   CaptionML=[ENU=Source Table ID;
                                                              FRA=Nø table origine] }
    { 2   ;   ;Source No.          ;Code20        ;TableRelation=IF (Source Table ID=CONST(50000)) Provider.No.
                                                                 ELSE IF (Source Table ID=CONST(5900)) "Service Header".No.
                                                                 ELSE IF (Source Table ID=CONST(50075)) "Service Intervention Line"."Service Header No."
                                                                 ELSE IF (Source Table ID=CONST(50076)) "Service Intervention Line"."Service Header No."
                                                                 ELSE IF (Source Table ID=CONST(5050)) Contact.No.;
                                                   CaptionML=[ENU=Source No.;
                                                              FRA=Nø origine] }
    { 3   ;   ;Source No. 2        ;Code20        ;CaptionML=[ENU=Source No. 2;
                                                              FRA=Nø origine 2] }
    { 4   ;   ;Source No. 3        ;Integer       ;CaptionML=[ENU=Source No. 3;
                                                              FRA=Nø origine 3] }
    { 5   ;   ;Source No. 4        ;Integer       ;CaptionML=[ENU=Source No. 4;
                                                              FRA=Nø origine 4] }
    { 6   ;   ;Entry No.           ;Integer       ;CaptionML=[ENU=Entry No.;
                                                              FRA=Nø s‚quence] }
    { 7   ;   ;Source No. 5        ;Integer       ;CaptionML=[ENU=Source No. 5;
                                                              FRA=Nø origine 5] }
    { 8   ;   ;Source Boolean      ;Boolean       ;OnValidate=BEGIN
                                                                //IMH02
                                                                IF "Source Boolean" THEN
                                                                  "Source Boolean Int" := 1
                                                                ELSE
                                                                  "Source Boolean Int" :=0;
                                                                //IMH02
                                                              END;

                                                   CaptionML=[ENU=Source Boolean;
                                                              FRA=Boolean origine] }
    { 9   ;   ;Source Date         ;Date          ;CaptionML=[ENU=Source Date;
                                                              FRA=Date origine] }
    { 10  ;   ;Source Table Name   ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Source Table ID)));
                                                   CaptionML=[ENU=Source Table Name;
                                                              FRA=Nom table origine] }
    { 11  ;   ;Field No.           ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table ID));
                                                   CaptionML=[ENU=Field No.;
                                                              FRA=Nø champ] }
    { 12  ;   ;Field Name          ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Source Table ID),
                                                                                                   No.=FIELD(Field No.)));
                                                   CaptionML=[ENU=Field Name;
                                                              FRA=Nom du champ] }
    { 13  ;   ;Type of Change      ;Option        ;CaptionML=[ENU=Type of Change;
                                                              FRA=Type modification];
                                                   OptionCaptionML=[ENU=Insertion,Modification,Deletion;
                                                                    FRA=Insertion,Modification,Suppression];
                                                   OptionString=Insertion,Modification,Deletion }
    { 15  ;   ;New Value           ;Text250       ;OnValidate=VAR
                                                                lFieldRef@1100281000 : FieldRef;
                                                              BEGIN
                                                                IF "New Value" <> '' THEN BEGIN
                                                                  GetField("Source Table ID",lFieldRef,"Field No.");
                                                                  EvaluateField(lFieldRef,"New Value");

                                                                  IF UPPERCASE(FORMAT(lFieldRef.TYPE)) IN ['BOOLEAN', 'OPTION'] THEN
                                                                    "New Int Value" := lFieldRef.VALUE
                                                                  ELSE
                                                                    "New Value" := FORMAT(lFieldRef.VALUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=New Value;
                                                              FRA=Nouvelle valeur] }
    { 16  ;   ;New Int Value       ;Integer       ;CaptionML=[ENU=New Int Value;
                                                              FRA=Nouvelle valeur int] }
    { 20  ;   ;Extranet Contact No.;Code20        ;CaptionML=[ENU=Extranet Contact No.;
                                                              FRA=Nø contact Extranet] }
    { 21  ;   ;Extranet Contact Name;Text30       ;CaptionML=[ENU=Extranet Contact Name;
                                                              FRA=Nom contact Extranet] }
    { 22  ;   ;Extranet Contact First Name;Text30 ;CaptionML=[ENU=Extranet Contact First Name;
                                                              FRA=Pr‚nom contact Extranet] }
    { 30  ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              FRA=Statut];
                                                   OptionCaptionML=[ENU=" ,To Post,Posted,Error";
                                                                    FRA=" ,A valider,Valid‚,Erreur"];
                                                   OptionString=[ ,To Post,Posted,Error] }
    { 31  ;   ;Description Status  ;Text250       ;CaptionML=[ENU=Description Status;
                                                              FRA=Description statut] }
    { 40  ;   ;RecordId            ;RecordID      ;CaptionML=[ENU=Record Identifiant;
                                                              FRA=Identifiant enregistrement] }
    { 50  ;   ;Condition Field No. ;Integer       ;TableRelation=Field.No. WHERE (TableNo=FIELD(Source Table ID));
                                                   CaptionML=[ENU=Condition Field No.;
                                                              FRA=Nø champ condition] }
    { 51  ;   ;Condition Field Name;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(Source Table ID),
                                                                                                   No.=FIELD(Condition Field No.)));
                                                   CaptionML=[ENU=Condition Field Name;
                                                              FRA=Nom du champ condition] }
    { 52  ;   ;Condition Field Value;Text250      ;CaptionML=[ENU=Condition Field Value;
                                                              FRA=Valeur condition] }
    { 53  ;   ;Condition Field Int Value;Integer  ;OnValidate=VAR
                                                                lFieldRef@1100281000 : FieldRef;
                                                              BEGIN
                                                                IF "Condition Field Value" <> '' THEN BEGIN
                                                                  GetField("Source Table ID",lFieldRef,"Condition Field No.");
                                                                  EvaluateField(lFieldRef,"Condition Field Value");

                                                                  IF UPPERCASE(FORMAT(lFieldRef.TYPE)) IN ['BOOLEAN', 'OPTION'] THEN
                                                                    "Condition Field Int Value" := lFieldRef.VALUE
                                                                  ELSE
                                                                    "Condition Field Value" := FORMAT(lFieldRef.VALUE);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Condition Field Int Value;
                                                              FRA=Condition valeur int] }
    { 428 ;   ;Source Boolean Int  ;Integer       ;CaptionML=[ENU=Source Boolean Int;
                                                              FRA=Boolean origine en int] }
    { 50000;  ;Provider No.        ;Code20        ;TableRelation=Provider.No.;
                                                   CaptionML=[ENU=Provider No.;
                                                              FRA=Nø prestataire] }
    { 50010;  ;Contact No.         ;Code20        ;CaptionML=[ENU=Contact No.;
                                                              FRA=Nø b‚n‚ficiaire] }
    { 50019;  ;Service No.         ;Code20        ;TableRelation=Service."Service No.";
                                                   CaptionML=[ENU=Service Header No.;
                                                              FRA=Nø service] }
    { 50020;  ;Service Header No.  ;Code20        ;TableRelation="Service Header".No. WHERE (Document Type=CONST(Invoice));
                                                   CaptionML=[ENU=Service Header No.;
                                                              FRA=Nø ligne produit] }
    { 50021;  ;Service Intervention Line No.;Integer;
                                                   CaptionML=[ENU=Service Intervention Line No.;
                                                              FRA=Nø ligne intervention service] }
    { 50022;  ;Intervention Quote Line No.;Integer;CaptionML=[ENU=Intervention Quote Line No.;
                                                              FRA=Nø devis ligne intervention] }
    { 50500;  ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              FRA=Date de cr‚ation];
                                                   Editable=No }
    { 50501;  ;Creation User ID    ;Text30        ;TableRelation="User Setup"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Creation User ID;
                                                              FRA=Utilisateur cr‚ation];
                                                   Editable=No }
    { 50502;  ;Last Modification Date;DateTime    ;CaptionML=[ENU=Last Modification Date;
                                                              FRA=Date derniŠre modification];
                                                   Editable=No }
    { 50503;  ;Last Modification User ID;Text30   ;TableRelation="User Setup"."User ID";
                                                   ValidateTableRelation=No;
                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=Last Modification User ID;
                                                              FRA=Utilisateur derniŠre modification];
                                                   Editable=No }
    { 54000;  ;Provider Name       ;Text50        ;CaptionML=[ENU=Name;
                                                              FRA=Raison sociale] }
    { 54001;  ;Provider Territory Description;Text50;
                                                   CaptionML=[ENU=IMH Territory Description;
                                                              FRA=Zone IMH] }
  }
  KEYS
  {
    {    ;Source Table ID,Source No.,Source No. 2,Source No. 3,Source No. 4,Source No. 5,Source Boolean,Source Date,Entry No.;
                                                   Clustered=Yes }
    {    ;Provider No.                             }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DisableFusion@1100281000 : Boolean;

    PROCEDURE GetFieldOldValue@1100281000() : Text[250];
    VAR
      lRecRef@1100281001 : RecordRef;
      lRecID@1100281000 : RecordID;
      lFldRef@1100281002 : FieldRef;
    BEGIN
      IF "Field No." = 0 THEN
        EXIT;
      lRecID := Rec.RecordId;
      IF lRecID.TABLENO = 0 THEN
        EXIT;
      lRecRef := lRecID.GETRECORD;
      IF NOT lRecRef.FIND('=') THEN
        EXIT;
      lFldRef := lRecRef.FIELD("Field No.");
      EXIT(lFldRef.VALUE);
    END;

    PROCEDURE SetRecordId@1100281001(pInsert@1100281007 : Boolean);
    VAR
      lRecId@1100281000 : RecordID;
      lRecRef@1100281006 : RecordRef;
      lProvider@1100281002 : Record 50000;
      lServiceHeader@1100281001 : Record 5900;
      lServiceInterventionLine@1100281003 : Record 50075;
      lServiceInterventionQuote@1100281004 : Record 50076;
      lContact@1100281005 : Record 5050;
      lContactAltAddress@1100281008 : Record 5051;
      lProviderChangeCalendar@1100281009 : Record 50007;
      lProviderBusinessHours@1100281010 : Record 50028;
    BEGIN
      IF pInsert THEN BEGIN
        lRecId := Rec.RecordId;
        IF lRecId.TABLENO <> 0 THEN
          EXIT;
      END;

      CASE "Source Table ID" OF
        DATABASE::Provider : BEGIN
          IF NOT lProvider.GET("Source No.") THEN
            EXIT;
          lRecRef.GETTABLE(lProvider);
        END;
        DATABASE::"Service Header" : BEGIN
          IF NOT lServiceHeader.GET(lServiceHeader."Document Type"::Invoice, "Source No.") THEN
            EXIT;
          lRecRef.GETTABLE(lServiceHeader);
        END;
        DATABASE::"Service Intervention Line" : BEGIN
          IF NOT lServiceInterventionLine.GET("Source No.", "Source No. 3") THEN
            EXIT;
          lRecRef.GETTABLE(lServiceInterventionLine);
        END;
        DATABASE::"Service Intervention Quote" : BEGIN
          IF NOT lServiceInterventionQuote.GET("Source No.", "Source No. 3", "Source No. 4") THEN
            EXIT;
          lRecRef.GETTABLE(lServiceInterventionQuote);
        END;
        DATABASE::"Contact Alt. Address" : BEGIN
          IF NOT lContactAltAddress.GET("Source No.", "Source No. 2") THEN
            EXIT;
          lRecRef.GETTABLE(lContactAltAddress);
        END;
        DATABASE::"Provider Change Calendar" : BEGIN
          IF NOT lProviderChangeCalendar.GET(
                  "Source No.", "Source Boolean", "Source No. 2",
                  "Source No. 3", "Source Date", "Source No. 4", "Source No. 5") THEN
            EXIT;
          lRecRef.GETTABLE(lProviderChangeCalendar);
        END;
        DATABASE::"Provider Business Hours" : BEGIN
          IF NOT lProviderBusinessHours.GET(
                  "Source No.", "Source Boolean", "Source No. 3") THEN
            EXIT;
          lRecRef.GETTABLE(lProviderBusinessHours);
        END;
      END;

      Rec.RecordId := lRecRef.RECORDID;
    END;

    LOCAL PROCEDURE GetField@1100281003(pTableNo@1100281000 : Integer;VAR pFieldRef@1001 : FieldRef;pFieldNo@1003 : Integer);
    VAR
      lRecRef@1100281001 : RecordRef;
    BEGIN
      lRecRef.OPEN(pTableNo);
      pFieldRef := lRecRef.FIELD(pFieldNo);
    END;

    PROCEDURE EvaluateField@1100481002(VAR pFieldRef@1100481000 : FieldRef;VAR pFilterValue@1100481001 : Text[1024]);
    VAR
      lBool@1100481002 : Boolean;
      lInt@1100481003 : Integer;
      lDec@1100481004 : Decimal;
      lText@1100481005 : Text[1024];
      lCode@1100481006 : Code[1024];
      lDate@1100481007 : Date;
      lTime@1100481008 : Time;
      lDateTime@1100481009 : DateTime;
    BEGIN
      CASE UPPERCASE(FORMAT(pFieldRef.TYPE)) OF
        'BOOLEAN' : BEGIN
          EVALUATE(lBool, pFilterValue);
          pFieldRef.VALUE := lBool;
          pFilterValue := FORMAT(lBool);
        END;
        'INTEGER', 'DURATION' : BEGIN
          EVALUATE(lInt, pFilterValue);
          pFieldRef.VALUE := lInt;
        END;
        'DECIMAL' : BEGIN
          EVALUATE(lDec, pFilterValue);
          pFieldRef.VALUE := lDec;
        END;
        'CODE' : BEGIN
          EVALUATE(lCode, pFilterValue);
          pFieldRef.VALUE := lCode;
        END;
        'DATE' : BEGIN
          EVALUATE(lDate, pFilterValue);
          pFieldRef.VALUE := lDate;
        END;
        'TIME' : BEGIN
          EVALUATE(lTime, pFilterValue);
          pFieldRef.VALUE := lTime;
        END;
        'DATETIME' : BEGIN
          EVALUATE(lDateTime, pFilterValue);
          pFieldRef.VALUE := lDateTime;
        END;
        'OPTION' : BEGIN
          pFieldRef.SETFILTER(pFilterValue);
          pFilterValue := pFieldRef.GETFILTER;
          pFieldRef.VALUE := pFieldRef.GETRANGEMIN;
        END;
      END; //CASE
    END;

    PROCEDURE TouchRecord@1100281006(pInsert@1100281000 : Boolean);
    BEGIN
      IF pInsert THEN BEGIN
        IF "Creation Date" = 0DT THEN BEGIN
          "Creation Date" := CURRENTDATETIME;
        END;
        IF "Creation User ID" = '' THEN BEGIN
          "Creation User ID" := USERID;
        END;
      END;

      IF "Last Modification Date" = 0DT THEN BEGIN
        "Last Modification Date" := CURRENTDATETIME;
      END;
      IF "Last Modification User ID" = '' THEN BEGIN
        "Last Modification User ID" := USERID;
      END;
    END;

    BEGIN
    {
      /** @r IMH03 @d 15/10/13 @a ISAT.ST @v IMH6.01.353 @s Reorganisation region IMH @h 4760 @c Renommage CR / AR / R‚gion */
      /** @r IHM02 @d 15/03/12 @a ISAT.ZW @v IMH6.01.340 @s NEP IMH 4 @h 3757 @c ajout gestion champ 428 Source Boolean Int */
      /** @r IHM01 @d 06/07/11 @a ISAT.ZW @v IMH6.01.165 @s Extranet @c Creation */
    }
    END.
  }
}