OBJECT Table 5103 Interaction Tmpl. Language
{
  OBJECT-PROPERTIES
  {
    Date=11/07/11;
    Time=11:59:17;
    Modified=Yes;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=BEGIN
               RemoveAttachment(FALSE);
             END;

    OnRename=VAR
               InteractionTemplate@1000 : Record 5064;
             BEGIN
               InteractionTemplate.GET("Interaction Template Code");
               IF xRec."Language Code" <> "Language Code" THEN
                 IF InteractionTemplate."Language Code (Default)" = xRec."Language Code" THEN BEGIN
                   InteractionTemplate."Language Code (Default)" := "Language Code";
                   InteractionTemplate.MODIFY;
                 END;
             END;

    CaptionML=[ENU=Interaction Tmpl. Language;
               FRA=Langue modŠle interaction];
    LookupFormID=Form5154;
  }
  FIELDS
  {
    { 1   ;   ;Interaction Template Code;Code10   ;TableRelation="Interaction Template";
                                                   CaptionML=[ENU=Interaction Template Code;
                                                              FRA=Code modŠle interaction];
                                                   Editable=No }
    { 2   ;   ;Language Code       ;Code10        ;TableRelation=Language;
                                                   CaptionML=[ENU=Language Code;
                                                              FRA=Code langue] }
    { 3   ;   ;Description         ;Text50        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 4   ;   ;Attachment No.      ;Integer       ;CaptionML=[ENU=Attachment No.;
                                                              FRA=Nø document joint] }
  }
  KEYS
  {
    {    ;Interaction Template Code,Language Code ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1002 : TextConst 'ENU=You have canceled the create process.;FRA=Vous avez annul‚ la cr‚ation.';
      Text001@1001 : TextConst 'ENU=Replace existing attachment?;FRA=Remplacer le document joint existant ?';
      Text002@1000 : TextConst 'ENU=You have canceled the import process.;FRA=Vous avez annul‚ l''import.';

    PROCEDURE CreateAttachment@1();
    VAR
      Attachment@1000 : Record 5062;
      InteractTmplLanguage@1004 : Record 5103;
      AttachmentManagement@1001 : Codeunit 5052;
      WordManagement@1002 : Codeunit 5054;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;
      WordManagement.SetInteractionTemplate("Interaction Template Code"); //IMH01
      NewAttachNo := WordManagement.CreateWordAttachment("Interaction Template Code" + ' ' + Description);
      IF NewAttachNo <> 0 THEN BEGIN
        IF "Attachment No." <> 0  THEN
          RemoveAttachment(FALSE);
        "Attachment No." := NewAttachNo;
        IF InteractTmplLanguage.GET("Interaction Template Code","Language Code") THEN
          MODIFY
        ELSE
          INSERT;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE OpenAttachment@2();
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.SetInteractionTemplate("Interaction Template Code"); //IMH01
      Attachment.OpenAttachment("Interaction Template Code" + ' ' + Description,FALSE);
    END;

    PROCEDURE CopyFromAttachment@3();
    VAR
      InteractTmplLanguage@1000 : Record 5103;
      Attachment@1001 : Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
      NewAttachNo@1003 : Integer;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        Attachment.TESTFIELD("Read Only",FALSE);

      IF "Attachment No." <> 0 THEN BEGIN
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
        RemoveAttachment(FALSE);
        "Attachment No." := 0;
        MODIFY;
        COMMIT;
      END;

      InteractTmplLanguage.SETFILTER("Attachment No.",'<>%1',0);
      IF FORM.RUNMODAL(0,InteractTmplLanguage) = ACTION::LookupOK THEN BEGIN
        NewAttachNo := AttachmentManagement.InsertAttachment(InteractTmplLanguage."Attachment No.");
        IF NewAttachNo <> 0 THEN BEGIN
          "Attachment No." := NewAttachNo;
          MODIFY;
        END;
      END;
    END;

    PROCEDURE ImportAttachment@5();
    VAR
      Attachment@1000 : Record 5062;
      AttachmentManagement@1002 : Codeunit 5052;
    BEGIN
      IF "Attachment No." <> 0 THEN BEGIN
        IF Attachment.GET("Attachment No.") THEN
          Attachment.TESTFIELD("Read Only",FALSE);
        IF NOT CONFIRM(Text001,FALSE) THEN
          EXIT;
      END;

      IF Attachment.ImportAttachment('',FALSE,FALSE) THEN BEGIN
        "Attachment No." := Attachment."No.";
        MODIFY;
      END ELSE
        ERROR(Text002);
    END;

    PROCEDURE ExportAttachment@6();
    VAR
      AttachmentRecord@1000 : Record 5062;
      FileName@1001 : Text[1024];
      BLOBRef@1002 : Record 99008535;
      RBAutoMgt@1003 : Codeunit 419;
    BEGIN
      IF AttachmentRecord.GET("Attachment No.") THEN BEGIN
        WITH AttachmentRecord DO BEGIN
          CALCFIELDS(Attachment);
          IF Attachment.HASVALUE THEN BEGIN
            FileName := "Interaction Template Code"+'.'+"File Extension";
            BLOBRef.Blob := Attachment;
            RBAutoMgt.BLOBExport(BLOBRef,FileName,TRUE);
          END;
        END;
      END;
    END;

    PROCEDURE RemoveAttachment@7(Prompt@1000 : Boolean);
    VAR
      Attachment@1001 : Record 5062;
    BEGIN
      IF Attachment.GET("Attachment No.") THEN
        IF Attachment.RemoveAttachment(Prompt) THEN BEGIN
          "Attachment No." := 0;
          MODIFY;
        END;
    END;

    PROCEDURE CheckAttachWordFields@1100281000() : Boolean;
    VAR
      Attachment@1000 : Record 5062;
    BEGIN
      //IMH02
      IF "Attachment No." = 0 THEN
        EXIT;
      Attachment.GET("Attachment No.");
      Attachment.SetInteractionTemplate("Interaction Template Code"); //IMH01
      EXIT(Attachment.CheckWordFields);
    END;

    BEGIN
    {
      /**@r IMH02 @d 08/07/10 @a ISAT.ZW @v IMH6.01.108 @s Interactions @c AJout fonction CheckAttachWordFields */
      /**@r IMH01 @d 15/12/10 @a ISAT.LLT @v IMH6.01.108 @s Interactions @c gestion champs de fusion */
    }
    END.
  }
}