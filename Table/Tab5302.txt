OBJECT Table 5302 Outlook Synch. Link
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               "Search Record ID" := FORMAT("Record ID");
             END;

    OnRename=BEGIN
               IF FORMAT("Record ID") <> FORMAT(xRec."Record ID") THEN
                 "Search Record ID" := FORMAT("Record ID");
             END;

    CaptionML=[ENU=Outlook Synch. Link;
               FRA=Lien synch. Outlook];
  }
  FIELDS
  {
    { 1   ;   ;User ID             ;Code20        ;CaptionML=[ENU=User ID;
                                                              FRA=Code utilisateur];
                                                   NotBlank=Yes }
    { 2   ;   ;Record ID           ;RecordID      ;CaptionML=[ENU=Record ID;
                                                              FRA=ID d'enregistrement] }
    { 3   ;   ;Outlook Entry ID    ;BLOB          ;CaptionML=[ENU=Outlook Entry ID;
                                                              FRA=ID ‚criture Outlook] }
    { 4   ;   ;Outlook Entry ID Hash;Text32       ;CaptionML=[ENU=Outlook Entry ID Hash;
                                                              FRA=Corruption ID ‚criture Outlook] }
    { 5   ;   ;Search Record ID    ;Code250       ;CaptionML=[ENU=Search Record ID;
                                                              FRA=Rechercher l'ID d'enregistrement] }
    { 6   ;   ;Synchronization Date;DateTime      ;CaptionML=[ENU=Synchronization Date;
                                                              FRA=Date synchronisation] }
  }
  KEYS
  {
    {    ;User ID,Record ID                       ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE GetEntryID@2(VAR EntryIDContainer@1001 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{2884F16D-75BF-4E3D-A1B7-8FFF455F37ED}:'Microsoft Dynamics NAV Outlook Synchronization Common Library'.LongTextContainer") : Boolean;
    VAR
      InStrm@1000 : InStream;
      Buffer@1002 : Text[1024];
    BEGIN
      EntryIDContainer.Clear;
      EntryIDContainer.MoveFirst;

      CALCFIELDS("Outlook Entry ID");
      "Outlook Entry ID".CREATEINSTREAM(InStrm);

      WHILE NOT InStrm.EOS DO BEGIN
        InStrm.READTEXT(Buffer,1024);
        EntryIDContainer.WriteNext(Buffer);
      END;

      EXIT("Outlook Entry ID".HASVALUE);
    END;

    PROCEDURE PutEntryID@7(VAR EntryIDContainer@1000 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{2884F16D-75BF-4E3D-A1B7-8FFF455F37ED}:'Microsoft Dynamics NAV Outlook Synchronization Common Library'.LongTextContainer";OEntryIDHash@1002 : Text[32]) : Boolean;
    BEGIN
      "Outlook Entry ID Hash" := OEntryIDHash;
      FillEntryID(Rec,EntryIDContainer);
      MODIFY;
      EXIT("Outlook Entry ID".HASVALUE);
    END;

    PROCEDURE InsertOSynchLink@12(UserID@1005 : Code[20];VAR EntryIDContainer@1000 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{2884F16D-75BF-4E3D-A1B7-8FFF455F37ED}:'Microsoft Dynamics NAV Outlook Synchronization Common Library'.LongTextContainer";RecRef@1001 : RecordRef;OEntryIDHash@1006 : Text[32]);
    VAR
      RecID@1004 : RecordID;
    BEGIN
      EVALUATE(RecID,FORMAT(RecRef.RECORDID));
      IF GET(UserID,RecID) THEN
        EXIT;

      INIT;
      "User ID" := UserID;
      "Record ID" := RecID;
      "Search Record ID" := FORMAT(RecID);
      "Outlook Entry ID Hash" := OEntryIDHash;
      FillEntryID(Rec,EntryIDContainer);
      INSERT;
    END;

    LOCAL PROCEDURE FillEntryID@3(VAR OSynchLink@1001 : Record 5302;VAR EntryIDContainer@1000 : Automation "{CAF2CEDD-2636-48EC-AE94-3A2E78BDCAA4} 1.0:{2884F16D-75BF-4E3D-A1B7-8FFF455F37ED}:'Microsoft Dynamics NAV Outlook Synchronization Common Library'.LongTextContainer");
    VAR
      OutStrm@1004 : OutStream;
      Buffer@1003 : Text[1024];
    BEGIN
      OSynchLink."Outlook Entry ID".CREATEOUTSTREAM(OutStrm);
      EntryIDContainer.MoveFirst;
      WHILE EntryIDContainer.ReadNext(1024,Buffer) DO
        OutStrm.WRITETEXT(Buffer,1024);
    END;

    BEGIN
    END.
  }
}