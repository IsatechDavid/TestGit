OBJECT Table 6805 EP Web Part Request
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               EPFunctions@1000 : Codeunit 6800;
             BEGIN
               IF "Group Code" <> '' THEN
                 EPFunctions.InsertGroupReqTypeTabs(Rec);
             END;

    OnModify=BEGIN
               IF "Defined by System" THEN
                 ERROR(Text004,TABLECAPTION);
             END;

    OnDelete=VAR
               EPGrpReqTypeTables@1000000000 : Record 6811;
               EPSearchConfig@1000 : Record 6839;
             BEGIN
               IF "Defined by System" THEN
                 ERROR(Text005,TABLECAPTION);

               EPGrpReqTypeTables.SETRANGE("Group Code","Group Code");
               EPGrpReqTypeTables.SETRANGE("WP Request Code",Code);
               IF EPGrpReqTypeTables.FIND('-') THEN
                 REPEAT
                   EPGrpReqTypeTables.DELETE(TRUE);
                 UNTIL EPGrpReqTypeTables.NEXT = 0;

               EPSearchConfig.SETCURRENTKEY("Group Code","WP Request Code");
               EPSearchConfig.SETRANGE("Group Code","Group Code");
               EPSearchConfig.SETRANGE("WP Request Code",Code);
               IF EPSearchConfig.FIND('-') THEN
                 REPEAT
                   EPSearchConfig.DELETE(TRUE);
                 UNTIL EPSearchConfig.NEXT = 0;
             END;

    OnRename=BEGIN
               IF "Defined by System" THEN
                 ERROR(Text006,TABLECAPTION);
             END;

    CaptionML=[ENU=EP Web Part Request;
               FRA=Demande de composant WebPart EP];
    LookupFormID=Form6845;
  }
  FIELDS
  {
    { 1   ;   ;Group Code          ;Code20        ;TableRelation="EP Group".Code;
                                                   CaptionML=[ENU=Group Code;
                                                              FRA=Code groupe] }
    { 2   ;   ;Code                ;Code20        ;OnValidate=VAR
                                                                EPWebPartRequest@1000 : Record 6805;
                                                              BEGIN
                                                                IF "Group Code" <> '' THEN
                                                                  IF NOT EPWebPartRequest.GET('',Code) THEN
                                                                    ERROR(Text003,FIELDCAPTION(Code),Code);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 4   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation];
                                                   Editable=Yes }
    { 5   ;   ;Editable            ;Boolean       ;OnValidate=BEGIN
                                                                IF Editable THEN
                                                                  IF (Type = Type::List) OR (Type = Type::System) THEN
                                                                    ERROR(Text007,TABLECAPTION,FIELDCAPTION(Type),Type)
                                                              END;

                                                   CaptionML=[ENU=Editable;
                                                              FRA=Modifiable] }
    { 8   ;   ;Type                ;Option        ;OnValidate=VAR
                                                                EPWPRTable@1000 : Record 6811;
                                                              BEGIN
                                                                EPWPRTable.SETRANGE("Group Code",Rec."Group Code");
                                                                EPWPRTable.SETRANGE("WP Request Code",Rec.Code);
                                                                CASE Type OF
                                                                  Type::List:
                                                                    BEGIN
                                                                      TESTFIELD(Editable,FALSE);
                                                                      EPWPRTable.SETRANGE("Header or Line",EPWPRTable."Header or Line"::Header);
                                                                      IF EPWPRTable.FIND('-') THEN BEGIN
                                                                        EPWPRTable."Header or Line" := EPWPRTable."Header or Line"::Line;
                                                                        ERROR(Text001,EPWPRTable."Header or Line",
                                                                          TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                      END;
                                                                    END;
                                                                  Type::Card:
                                                                    BEGIN
                                                                      EPWPRTable.SETRANGE("Header or Line",EPWPRTable."Header or Line"::Line);
                                                                      IF EPWPRTable.FIND('-') THEN BEGIN
                                                                        EPWPRTable."Header or Line" := EPWPRTable."Header or Line"::Header;
                                                                        ERROR(Text001,EPWPRTable."Header or Line",
                                                                          TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                      END;
                                                                    END;
                                                                  Type::System:
                                                                    BEGIN
                                                                      TESTFIELD(Editable,FALSE);
                                                                      IF EPWPRTable.FIND('-') THEN
                                                                        ERROR(Text002,TABLECAPTION,FIELDCAPTION(Type),Type);
                                                                    END;
                                                                END
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=" ,List,Card,Header + Line,System";
                                                                    FRA=" ,Liste,Fiche,En-tˆte + Ligne,SystŠme"];
                                                   OptionString=[ ,List,Card,Header + Line,System] }
    { 9   ;   ;Defined by System   ;Boolean       ;CaptionML=[ENU=Defined by System;
                                                              FRA=D‚fini(e) par le systŠme];
                                                   Editable=No }
    { 10  ;   ;No. of Where-Used Groups;Integer   ;FieldClass=FlowField;
                                                   CalcFormula=Count("EP Web Part Request" WHERE (Group Code=FILTER(<>''),
                                                                                                  Code=FIELD(Code)));
                                                   CaptionML=[ENU=No. of Where-Used Groups;
                                                              FRA=Nombre de groupes cas d'emploi];
                                                   Editable=No }
    { 12  ;   ;Caption ID          ;Integer       ;CaptionML=[ENU=Caption ID;
                                                              FRA=ID l‚gende];
                                                   Editable=No }
    { 13  ;   ;Caption             ;Text80        ;CaptionML=[ENU=Caption;
                                                              FRA=L‚gende];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Group Code,Code                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Code,Description                         }
  }
  CODE
  {
    VAR
      Text001@1001 : TextConst 'ENU=Only a %1 Table can be defined for a %2 of %3 %4.;FRA=Vous ne pouvez d‚finir qu''une table %1 pour un(e) %2 de %3 %4.';
      Text002@1000 : TextConst 'ENU=No Tables can be defined for a %1 of %2 %3.;FRA=Vous ne pouvez d‚finir aucune table pour un(e) %1 de %2 %3.';
      Text003@1002 : TextConst 'ENU=No template of %1 %2 has been defined.;FRA=Vous n''avez d‚fini aucun modŠle de %1 %2.';
      Text004@1003 : TextConst 'ENU=A %1 Defined By System cannot be modified.;FRA=Impossible de modifier un(e) %1 d‚fini(e) par le systŠme.';
      Text005@1004 : TextConst 'ENU=A %1 Defined By System cannot be deleted.;FRA=Impossible de supprimer un(e) %1 d‚fini(e) par le systŠme.';
      Text006@1005 : TextConst 'ENU=A %1 Defined By System cannot be renamed.;FRA=Impossible de renommer un(e) %1 d‚fini(e) par le systŠme.';
      Text007@1006 : TextConst 'ENU=A %1 of %2 %3 cannot be editable.;FRA=Impossible de modifier un(e) %1 de %2 %3.';

    BEGIN
    END.
  }
}