OBJECT Table 6809 EP WP Request Table Action
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               EPGrpRTActionNewHeadCrit@1000000000 : Record 6824;
               EPGrpRTActionFilter@1000000001 : Record 6813;
             BEGIN
               EPGrpRTActionNewHeadCrit.RESET;
               EPGrpRTActionNewHeadCrit.SETRANGE("Group Code","Group Code");
               EPGrpRTActionNewHeadCrit.SETRANGE("WP Request Code","WP Request Code");
               EPGrpRTActionNewHeadCrit.SETRANGE("Table No.","Table No.");
               EPGrpRTActionNewHeadCrit.SETRANGE("Header or Line","Header or Line");
               EPGrpRTActionNewHeadCrit.SETRANGE("Action Type",Type);
               EPGrpRTActionNewHeadCrit.SETRANGE(Position,Position);
               IF EPGrpRTActionNewHeadCrit.FIND('-') THEN BEGIN
                 REPEAT
                   EPGrpRTActionNewHeadCrit.DELETE(TRUE);
                 UNTIL EPGrpRTActionNewHeadCrit.NEXT = 0;
               END;

               EPGrpRTActionFilter.RESET;
               EPGrpRTActionFilter.SETRANGE("Group Code","Group Code");
               EPGrpRTActionFilter.SETRANGE("WP Request Code","WP Request Code");
               EPGrpRTActionFilter.SETRANGE("Table No.","Table No.");
               EPGrpRTActionFilter.SETRANGE("Header or Line","Header or Line");
               EPGrpRTActionFilter.SETRANGE("Action Type",Type);
               EPGrpRTActionFilter.SETRANGE(Position,Position);
               IF EPGrpRTActionFilter.FIND('-') THEN BEGIN
                 REPEAT
                   EPGrpRTActionFilter.DELETE(TRUE);
                 UNTIL EPGrpRTActionFilter.NEXT = 0;
               END;
             END;

    OnRename=VAR
               EPSupportFunctions@1000000000 : Codeunit 6815;
             BEGIN
               // check for rename
               EPSupportFunctions.RenameActionType(Rec, xRec);
             END;

    CaptionML=[ENU=EP WP Request Table Action;
               FRA=Table demande de composant WP EP - Action];
  }
  FIELDS
  {
    { 1   ;   ;Group Code          ;Code20        ;TableRelation="EP Group".Code;
                                                   CaptionML=[ENU=Group Code;
                                                              FRA=Code groupe];
                                                   Editable=No }
    { 2   ;   ;WP Request Code     ;Code20        ;TableRelation="EP Web Part Request".Code WHERE (Group Code=FIELD(Group Code));
                                                   CaptionML=[ENU=WP Request Code;
                                                              FRA=Code de demande de composant WP] }
    { 3   ;   ;Code                ;Code20        ;TableRelation="EP Web Part Request".Code;
                                                   OnValidate=VAR
                                                                ReqType@1000000000 : Record 6805;
                                                              BEGIN
                                                                IF "Group Code" = '' THEN
                                                                  TESTFIELD(Type,Type::"Connect to another WP Request")
                                                                ELSE
                                                                  IF (Type= Type::"Connect to another WP Request") AND
                                                                     (Code <> '')
                                                                  THEN BEGIN
                                                                    IF Code = "WP Request Code" THEN
                                                                      ERROR(Text002,FIELDCAPTION("WP Request Code"),TABLECAPTION);
                                                                    ReqType.RESET;
                                                                    ReqType.SETRANGE("Group Code","Group Code");
                                                                    ReqType.SETRANGE(Code,Code);
                                                                    IF NOT ReqType.FIND('-') THEN
                                                                      ERROR(Text001);
                                                                  END;
                                                                IF Code <> '' THEN BEGIN
                                                                  ReqType.SETFILTER(Code, Code);
                                                                  Description := ReqType.Description;
                                                                  "Target WP Request Type" := ReqType.Type;
                                                                END ELSE BEGIN
                                                                  Description := '';
                                                                  "Target WP Request Type" := "Target WP Request Type"::" ";
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   Editable=Yes }
    { 4   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 5   ;   ;Target WP Request   ;Option        ;OnValidate=BEGIN
                                                                IF "Target WP Request" <> xRec."Target WP Request" THEN
                                                                  Code := '';

                                                                IF "Target WP Request" = "Target WP Request"::Other THEN
                                                                  TESTFIELD(Type,Type::"Connect to another WP Request");
                                                              END;

                                                   CaptionML=[ENU=Target WP Request;
                                                              FRA=Demande de composant WP cible];
                                                   OptionCaptionML=[ENU=Current,Other;
                                                                    FRA=En cours,Autre];
                                                   OptionString=Current,Other }
    { 6   ;   ;HTML Layout         ;Option        ;CaptionML=[ENU=HTML Layout;
                                                              FRA=Pr‚sentation HTML];
                                                   OptionCaptionML=[ENU=Link,Button;
                                                                    FRA=Lien,Bouton];
                                                   OptionString=Link,Button }
    { 7   ;   ;Position            ;Integer       ;CaptionML=[ENU=Position;
                                                              FRA=Position];
                                                   BlankZero=Yes }
    { 8   ;   ;Field Validation Required;Boolean  ;CaptionML=[ENU=Field Validation Required;
                                                              FRA=Validation de champ requise] }
    { 9   ;   ;Restrict to Editable;Boolean       ;CaptionML=[ENU=Restrict to Editable;
                                                              FRA=Limiter … Modifiable] }
    { 10  ;   ;Table No.           ;Integer       ;CaptionML=[ENU=Table No.;
                                                              FRA=Nø table];
                                                   Editable=No }
    { 11  ;   ;Header or Line      ;Option        ;TableRelation="EP WP Request Table"."Header or Line" WHERE (Group Code=FIELD(Group Code),
                                                                                                               WP Request Code=FIELD(WP Request Code),
                                                                                                               Table No.=FIELD(Table No.));
                                                   CaptionML=[ENU=Header or Line;
                                                              FRA=En-tˆte ou ligne];
                                                   OptionCaptionML=[ENU=" ,Header,Line";
                                                                    FRA=" ,En-tˆte,Ligne"];
                                                   OptionString=[ ,Header,Line];
                                                   Editable=No }
    { 12  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                CalcActionTypeFrontend;
                                                                CASE Type OF
                                                                  Type::"Insert Head", Type::"Insert Line":
                                                                    TESTFIELD("Header or Line","Header or Line"::Header);
                                                                  Type::"Set Line to Modify", Type::"Cancel Modify (Line)":
                                                                    TESTFIELD("Header or Line","Header or Line"::Line);
                                                                END
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=" ,Insert Head,Insert Line,Save Record,Delete Record,Set Line to Modify,Cancel Modify (Line),,Connect to another WP Request,Show Report";
                                                                    FRA=" ,Ins‚rer en-tˆte,Ins‚rer ligne,Enregistrer enregistrement,Supprimer enregistrement,D‚finir ligne sur Modifier,Annuler modification (ligne),,Se connecter … autre demande composant WP,Afficher ‚tat"];
                                                   OptionString=[ ,Insert Head,Insert Line,Save Record,Delete Record,Set Line to Modify,Cancel Modify (Line),,Connect to another WP Request,Show Report] }
    { 13  ;   ;Type ID             ;Integer       ;CaptionML=[ENU=Type ID;
                                                              FRA=ID type];
                                                   Editable=No }
    { 14  ;   ;Show in Mode        ;Option        ;CaptionML=[ENU=Show in Mode;
                                                              FRA=Afficher en mode];
                                                   OptionCaptionML=[ENU=View,Edit,Both;
                                                                    FRA=Afficher,Modifier,Les deux];
                                                   OptionString=View,Edit,Both }
    { 15  ;   ;Target WP Request Type;Option      ;CaptionML=[ENU=Target WP Request Type;
                                                              FRA=Type demande de composant WP cible];
                                                   OptionCaptionML=[ENU=" ,List,Card,Header + Line,System";
                                                                    FRA=" ,Liste,Fiche,En-tˆte + Ligne,SystŠme"];
                                                   OptionString=[ ,List,Card,Header + Line,System];
                                                   Editable=No }
    { 17  ;   ;Target Table No.    ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Table));
                                                   CaptionML=[ENU=Target Table No.;
                                                              FRA=Nø table cible];
                                                   BlankZero=Yes }
    { 18  ;   ;Caption ID          ;Integer       ;InitValue=0;
                                                   CaptionML=[ENU=Caption ID;
                                                              FRA=ID l‚gende];
                                                   Editable=No }
    { 19  ;   ;Report ID           ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[ENU=Report ID;
                                                              FRA=ID ‚tat];
                                                   BlankZero=Yes }
    { 20  ;   ;Caption             ;Text80        ;CaptionML=[ENU=Caption;
                                                              FRA=L‚gende];
                                                   Editable=No }
    { 21  ;   ;Picture Code        ;Code20        ;TableRelation="EP SharePoint Image Path";
                                                   CaptionML=[ENU=Picture Code;
                                                              FRA=Code image] }
  }
  KEYS
  {
    {    ;Group Code,WP Request Code,Table No.,Header or Line,Type,Position;
                                                   Clustered=Yes }
    {    ;Position                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      DisableActionCodeCheck@1000000000 : Boolean;
      Text001@1000 : TextConst 'ENU=The web part request code is not part of the current group.;FRA=Le code de demande de composant WebPart n''appartient pas au groupe actuel.';
      Text002@1001 : TextConst 'ENU=You cannot select this %1 because it is the same code that this %2 is related to.;FRA=Impossible de s‚lectionner ce/cette %1 car il s''agit du code auquel ce/cette %2 est associ‚(e).';

    PROCEDURE CalcActionTypeFrontend@1000000000();
    BEGIN
      DisableActionCodeCheck := TRUE;

      CASE "Header or Line" OF
        "Header or Line"::Header:
          BEGIN
            CASE Type OF
              Type::"Insert Head":
                BEGIN
                  "Type ID" := 1;
                  VALIDATE(Code,'INSERTHEAD');
                END;
              Type::"Insert Line":
                BEGIN
                  "Type ID" := 2;
                  VALIDATE(Code,'INSERTLINE');
                END;
              Type::"Save Record":
                BEGIN
                  "Type ID" := 3;
                  VALIDATE(Code,'MODIFYHEAD');
                END;
              Type::"Delete Record":
                BEGIN
                  "Type ID" := 8;
                  VALIDATE(Code,'DELETEHEAD');
                END;
              Type::"Set Line to Modify":
                "Type ID" := 0;  // Only for lines
              Type::"Cancel Modify (Line)":
                "Type ID" := 0; // Only for lines
              Type::"Show Report":
                BEGIN
                  "Type ID" := 11;
                  VALIDATE(Code,'SHOWREPORT');
                END;
              Type::"Connect to another WP Request":
                BEGIN
                  "Type ID" := 7;
                  VALIDATE(Code,'');
                END;
            END;
          END;
        "Header or Line"::Line:
          BEGIN
            CASE Type OF
              Type::"Insert Head":
                BEGIN
                  "Type ID" := 0; // Only for Head
                  VALIDATE(Code,'');
                END;
              Type::"Insert Line":
                BEGIN
                  "Type ID" := 2;
                  VALIDATE(Code,'INSERTLINE');
                END;
              Type::"Save Record":
                BEGIN
                  "Type ID" := 4;
                  VALIDATE(Code,'MODIFYLINE');
                END;
              Type::"Delete Record":
                BEGIN
                  "Type ID" := 9;
                  VALIDATE(Code,'DELETELINE');
                END;
              Type::"Set Line to Modify":
                "Type ID" := 5;
              Type::"Cancel Modify (Line)":
                "Type ID" := 6;
              Type::"Show Report":
                BEGIN
                  "Type ID" := 11;
                  VALIDATE(Code,'SHOWREPORT');
                END;
              Type::"Connect to another WP Request":
                BEGIN
                  "Type ID" := 7;
                  VALIDATE(Code,'');
                END;
            END;
          END;
      END;

      DisableActionCodeCheck := FALSE;
    END;

    BEGIN
    END.
  }
}