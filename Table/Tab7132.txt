OBJECT Table 7132 Item Budget Name
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               ItemBudgetEntry@1000 : Record 7134;
               ItemAnalysisViewBudgetEntry@1001 : Record 7156;
             BEGIN
               ItemBudgetEntry.SETCURRENTKEY("Analysis Area","Budget Name");
               ItemBudgetEntry.SETRANGE("Analysis Area","Analysis Area");
               ItemBudgetEntry.SETRANGE("Budget Name",Name);
               ItemBudgetEntry.DELETEALL(TRUE);

               ItemAnalysisViewBudgetEntry.SETRANGE("Analysis Area","Analysis Area");
               ItemAnalysisViewBudgetEntry.SETRANGE("Budget Name",Name);
               ItemAnalysisViewBudgetEntry.DELETEALL;
             END;

    CaptionML=[ENU=Item Budget Name;
               FRA=Nom budget article];
    LookupFormID=Form7132;
  }
  FIELDS
  {
    { 1   ;   ;Analysis Area       ;Option        ;CaptionML=[ENU=Analysis Area;
                                                              FRA=Zone d'analyse];
                                                   OptionCaptionML=[ENU=Sales,Purchase;
                                                                    FRA=Ventes,Achats];
                                                   OptionString=Sales,Purchase }
    { 2   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              FRA=Nom];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 4   ;   ;Blocked             ;Boolean       ;CaptionML=[ENU=Blocked;
                                                              FRA=Bloqu‚] }
    { 5   ;   ;Budget Dimension 1 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 1 Code" <> xRec."Budget Dimension 1 Code" THEN BEGIN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 1 Code",17,Name,'',"Analysis Area") THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                                  MODIFY;
                                                                  UpdateBudgetDim("Budget Dimension 1 Code",0);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 1 Code;
                                                              FRA=Code axe budget 1] }
    { 6   ;   ;Budget Dimension 2 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 2 Code" <> xRec."Budget Dimension 2 Code" THEN BEGIN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 2 Code",18,Name,'',"Analysis Area") THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                                  MODIFY;
                                                                  UpdateBudgetDim("Budget Dimension 2 Code",1);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 2 Code;
                                                              FRA=Code axe budget 2] }
    { 7   ;   ;Budget Dimension 3 Code;Code20     ;TableRelation=Dimension;
                                                   OnValidate=BEGIN
                                                                IF "Budget Dimension 3 Code" <> xRec."Budget Dimension 3 Code" THEN BEGIN
                                                                  IF Dim.CheckIfDimUsed("Budget Dimension 3 Code",19,Name,'',"Analysis Area") THEN
                                                                    ERROR(Text000,Dim.GetCheckDimErr);
                                                                  MODIFY;
                                                                  UpdateBudgetDim("Budget Dimension 3 Code",2);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Budget Dimension 3 Code;
                                                              FRA=Code axe budget 3] }
  }
  KEYS
  {
    {    ;Analysis Area,Name                      ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1\You cannot use the same dimension twice in the same budget.;FRA=%1\Vous ne pouvez pas utiliser le mˆme axe analytique deux fois dans le mˆme budget.';
      Text001@1001 : TextConst 'ENU=Updating budget entries @1@@@@@@@@@@@@@@@@@@;FRA=Mise … jour des ‚critures budget @1@@@@@@@@@@@@@@@@@@';
      Dim@1002 : Record 348;

    LOCAL PROCEDURE UpdateBudgetDim@3(NewDim@1000 : Code[20];Which@1001 : 'BudgetDim1,BudgetDim2,BudgetDim3,BudgetDim4');
    VAR
      ItemBudgetEntry@1002 : Record 7134;
      ItemBudgetDim@1003 : Record 7135;
      Window@1004 : Dialog;
      TotalCount@1005 : Integer;
      i@1006 : Integer;
    BEGIN
      WITH ItemBudgetEntry DO BEGIN
        SETCURRENTKEY("Analysis Area","Budget Name");
        SETRANGE("Analysis Area",Rec."Analysis Area");
        SETRANGE("Budget Name",Name);
        IF NewDim = '' THEN
          ItemBudgetDim.INIT;
        TotalCount := COUNTAPPROX;
        Window.OPEN(Text001);
        IF FIND('-') THEN
          REPEAT
            i := i + 1;
            IF i MOD 1000 = 0 THEN
              Window.UPDATE(1,10000 * i DIV TotalCount);
            IF NewDim <> '' THEN
              IF NOT ItemBudgetDim.GET("Entry No.",NewDim) THEN
                ItemBudgetDim.INIT;
            CASE Which OF
              Which::BudgetDim1:
                IF ItemBudgetDim."Dimension Value Code" <> "Budget Dimension 1 Code" THEN BEGIN
                  "Budget Dimension 1 Code" := ItemBudgetDim."Dimension Value Code";
                  MODIFY;
                END;
              Which::BudgetDim2:
                IF ItemBudgetDim."Dimension Value Code" <> "Budget Dimension 2 Code" THEN BEGIN
                  "Budget Dimension 2 Code" := ItemBudgetDim."Dimension Value Code";
                  MODIFY;
                END;
              Which::BudgetDim3:
                IF ItemBudgetDim."Dimension Value Code" <> "Budget Dimension 3 Code" THEN BEGIN
                  "Budget Dimension 3 Code" := ItemBudgetDim."Dimension Value Code";
                  MODIFY;
                END;
            END;
          UNTIL NEXT = 0;
      END;
      Window.CLOSE;
    END;

    BEGIN
    END.
  }
}