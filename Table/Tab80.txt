OBJECT Table 80 Gen. Journal Template
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               VALIDATE("Form ID");
             END;

    OnDelete=BEGIN
               GenJnlAlloc.SETRANGE("Journal Template Name",Name);
               GenJnlAlloc.DELETEALL;
               GenJnlLine.SETRANGE("Journal Template Name",Name);
               GenJnlLine.DELETEALL(TRUE);
               GenJnlBatch.SETRANGE("Journal Template Name",Name);
               GenJnlBatch.DELETEALL;
             END;

    CaptionML=[ENU=Gen. Journal Template;
               FRA=ModŠle feuille comptabilit‚];
    LookupFormID=Form250;
  }
  FIELDS
  {
    { 1   ;   ;Name                ;Code10        ;CaptionML=[ENU=Name;
                                                              FRA=Nom];
                                                   NotBlank=Yes }
    { 2   ;   ;Description         ;Text80        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 5   ;   ;Test Report ID      ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[ENU=Test Report ID;
                                                              FRA=ID impression test] }
    { 6   ;   ;Form ID             ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Form));
                                                   OnValidate=BEGIN
                                                                IF "Form ID" = 0 THEN
                                                                  VALIDATE(Type);
                                                              END;

                                                   CaptionML=[ENU=Form ID;
                                                              FRA=ID formulaire] }
    { 7   ;   ;Posting Report ID   ;Integer       ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[ENU=Posting Report ID;
                                                              FRA=ID rapport validation] }
    { 8   ;   ;Force Posting Report;Boolean       ;CaptionML=[ENU=Force Posting Report;
                                                              FRA=Forcer rapport validation] }
    { 9   ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                "Test Report ID" := REPORT::"General Journal - Test";
                                                                "Posting Report ID" := REPORT::"G/L Register";
                                                                SourceCodeSetup.GET;
                                                                CASE Type OF
                                                                  Type::General:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."General Journal";
                                                                      "Form ID" :=  FORM::"General Journal";
                                                                    END;
                                                                  Type::Sales:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Sales Journal";
                                                                      "Form ID" := FORM::"Sales Journal";
                                                                    END;
                                                                  Type::Purchases:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Purchase Journal";
                                                                      "Form ID" := FORM::"Purchase Journal";
                                                                    END;
                                                                  Type::"Cash Receipts":
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Cash Receipt Journal";
                                                                      "Form ID" := FORM::"Cash Receipt Journal";
                                                                    END;
                                                                  Type::Payments:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Payment Journal";
                                                                      "Form ID" := FORM::"Payment Journal";
                                                                    END;
                                                                  Type::Assets:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Fixed Asset G/L Journal";
                                                                      "Form ID" := FORM::"Fixed Asset G/L Journal";
                                                                    END;
                                                                  Type::Intercompany:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."IC General Journal";
                                                                      "Form ID" := FORM::"IC General Journal";
                                                                    END;
                                                                  Type::Jobs:
                                                                    BEGIN
                                                                      "Source Code" := SourceCodeSetup."Job G/L Journal";
                                                                      "Form ID" := FORM::"Job G/L Journal";
                                                                    END;
                                                                END;

                                                                IF Recurring THEN
                                                                  "Form ID" := FORM::"Recurring General Journal";
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs;
                                                                    FRA=G‚n‚ral,Ventes,Achats,RŠglements,Paiements,Immobilisations,Intersoci‚t‚,Projets];
                                                   OptionString=General,Sales,Purchases,Cash Receipts,Payments,Assets,Intercompany,Jobs }
    { 10  ;   ;Source Code         ;Code10        ;TableRelation="Source Code";
                                                   OnValidate=BEGIN
                                                                GenJnlLine.SETRANGE("Journal Template Name",Name);
                                                                GenJnlLine.MODIFYALL("Source Code","Source Code");
                                                                MODIFY;
                                                              END;

                                                   CaptionML=[ENU=Source Code;
                                                              FRA=Code journal] }
    { 11  ;   ;Reason Code         ;Code10        ;TableRelation="Reason Code";
                                                   CaptionML=[ENU=Reason Code;
                                                              FRA=Code motif] }
    { 12  ;   ;Recurring           ;Boolean       ;OnValidate=BEGIN
                                                                VALIDATE(Type);
                                                                IF Recurring THEN
                                                                  TESTFIELD("No. Series",'');
                                                              END;

                                                   CaptionML=[ENU=Recurring;
                                                              FRA=Abonnement] }
    { 15  ;   ;Test Report Name    ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Test Report ID)));
                                                   CaptionML=[ENU=Test Report Name;
                                                              FRA=Nom impression test];
                                                   Editable=No }
    { 16  ;   ;Form Name           ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Form),
                                                                                                                Object ID=FIELD(Form ID)));
                                                   CaptionML=[ENU=Form Name;
                                                              FRA=Nom du formulaire];
                                                   Editable=No }
    { 17  ;   ;Posting Report Name ;Text80        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Posting Report ID)));
                                                   CaptionML=[ENU=Posting Report Name;
                                                              FRA=Nom rapport validation];
                                                   Editable=No }
    { 18  ;   ;Force Doc. Balance  ;Boolean       ;InitValue=Yes;
                                                   CaptionML=[ENU=Force Doc. Balance;
                                                              FRA=Forcer ‚qu. nø doc.] }
    { 19  ;   ;Bal. Account Type   ;Option        ;OnValidate=BEGIN
                                                                "Bal. Account No." := '';
                                                              END;

                                                   CaptionML=[ENU=Bal. Account Type;
                                                              FRA=Type compte contrepartie];
                                                   OptionCaptionML=[ENU=G/L Account,Customer,Vendor,Bank Account,Fixed Asset;
                                                                    FRA=G‚n‚ral,Client,Fournisseur,Banque,Immobilisation];
                                                   OptionString=G/L Account,Customer,Vendor,Bank Account,Fixed Asset }
    { 20  ;   ;Bal. Account No.    ;Code20        ;TableRelation=IF (Bal. Account Type=CONST(G/L Account)) "G/L Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Customer)) Customer
                                                                 ELSE IF (Bal. Account Type=CONST(Vendor)) Vendor
                                                                 ELSE IF (Bal. Account Type=CONST(Bank Account)) "Bank Account"
                                                                 ELSE IF (Bal. Account Type=CONST(Fixed Asset)) "Fixed Asset";
                                                   OnValidate=BEGIN
                                                                IF "Bal. Account Type" = "Bal. Account Type"::"G/L Account" THEN
                                                                  CheckGLAcc("Bal. Account No.");
                                                              END;

                                                   CaptionML=[ENU=Bal. Account No.;
                                                              FRA=Nø compte contrepartie] }
    { 21  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF "No. Series" <> '' THEN BEGIN
                                                                  IF Recurring THEN
                                                                    ERROR(
                                                                      Text000,
                                                                      FIELDCAPTION("Posting No. Series"));
                                                                  IF "No. Series" = "Posting No. Series" THEN
                                                                    "Posting No. Series" := '';
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø] }
    { 22  ;   ;Posting No. Series  ;Code10        ;TableRelation="No. Series";
                                                   OnValidate=BEGIN
                                                                IF ("Posting No. Series" = "No. Series") AND ("Posting No. Series" <> '') THEN
                                                                  FIELDERROR("Posting No. Series",STRSUBSTNO(Text001,"Posting No. Series"));
                                                              END;

                                                   CaptionML=[ENU=Posting No. Series;
                                                              FRA=Souches de nø validation] }
    { 23  ;   ;Copy VAT Setup to Jnl. Lines;Boolean;
                                                   InitValue=Yes;
                                                   OnValidate=BEGIN
                                                                IF CONFIRM(
                                                                     Text002,
                                                                     TRUE, GenJnlBatch.FIELDCAPTION("Copy VAT Setup to Jnl. Lines"),GenJnlBatch.TABLECAPTION)
                                                                THEN BEGIN
                                                                  GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                  GenJnlBatch.MODIFYALL("Copy VAT Setup to Jnl. Lines","Copy VAT Setup to Jnl. Lines");
                                                                  MODIFY;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Copy VAT Setup to Jnl. Lines;
                                                              FRA=Copier paramŠtres TVA sur les lignes feuille] }
    { 24  ;   ;Allow VAT Difference;Boolean       ;OnValidate=VAR
                                                                Ok@1000 : Boolean;
                                                              BEGIN
                                                                IF "Allow VAT Difference" <> xRec."Allow VAT Difference" THEN
                                                                  IF CONFIRM(
                                                                       Text002,
                                                                       TRUE, GenJnlBatch.FIELDCAPTION("Allow VAT Difference"),GenJnlBatch.TABLECAPTION)
                                                                  THEN BEGIN
                                                                    GenJnlBatch.SETRANGE("Journal Template Name",Name);
                                                                    GenJnlBatch.MODIFYALL("Allow VAT Difference","Allow VAT Difference");
                                                                  END;
                                                              END;

                                                   CaptionML=[ENU=Allow VAT Difference;
                                                              FRA=Autoriser diff‚rence TVA] }
    { 25  ;   ;Cust. Receipt Report ID;Integer    ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[ENU=Cust. Receipt Report ID;
                                                              FRA=ID ‚tat r‚ception client] }
    { 26  ;   ;Cust. Receipt Report Name;Text80   ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Cust. Receipt Report ID)));
                                                   CaptionML=[ENU=Cust. Receipt Report Name;
                                                              FRA=Nom ‚tat r‚ception client];
                                                   Editable=No }
    { 27  ;   ;Vendor Receipt Report ID;Integer   ;TableRelation=Object.ID WHERE (Type=CONST(Report));
                                                   CaptionML=[ENU=Vendor Receipt Report ID;
                                                              FRA=ID ‚tat r‚ception fournisseur] }
    { 28  ;   ;Vendor Receipt Report Name;Text80  ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Report),
                                                                                                                Object ID=FIELD(Vendor Receipt Report ID)));
                                                   CaptionML=[ENU=Vendor Receipt Report Name;
                                                              FRA=Nom ‚tat r‚cept. fournisseur];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Name                                    ;Clustered=Yes }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;Name,Description,Type                    }
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=Only the %1 field can be filled in on recurring journals.;FRA=Seul le champ %1 est … renseigner dans les feuilles abonnement.';
      Text001@1001 : TextConst 'ENU=must not be %1;FRA=ne doit pas ˆtre %1';
      Text002@1002 : TextConst 'ENU=Do you want to update the %1 field on all %2es?;FRA=Souhaitez-vous mettre … jour le champ %1 sur chaque %2 ?';
      GenJnlBatch@1003 : Record 232;
      GenJnlLine@1004 : Record 81;
      GenJnlAlloc@1005 : Record 221;
      SourceCodeSetup@1006 : Record 242;

    LOCAL PROCEDURE CheckGLAcc@2(AccNo@1000 : Code[20]);
    VAR
      GLAcc@1001 : Record 15;
    BEGIN
      IF AccNo <> '' THEN BEGIN
        GLAcc.GET(AccNo);
        GLAcc.CheckGLAcc;
        GLAcc.TESTFIELD("Direct Posting",TRUE);
      END;
    END;

    BEGIN
    END.
  }
}