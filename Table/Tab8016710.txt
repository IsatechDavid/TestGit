OBJECT Table 8016710 EAI Synchro Group Table
{
  OBJECT-PROPERTIES
  {
    Date=22/11/10;
    Time=16:56:17;
    Version List=ISAEAI1.02;
  }
  PROPERTIES
  {
    DataCaptionFields=Synchro Group Code;
    OnDelete=VAR
               lSynchroGroupTableField@1100481000 : Record 8016711;
               lSynchroGroupTableFilter@1100481001 : Record 8016712;
             BEGIN
               lSynchroGroupTableField.SETRANGE("Synchro Group Code","Synchro Group Code");
               lSynchroGroupTableField.SETRANGE("To Synchronize Table No.","To Synchronize Table No.");
               lSynchroGroupTableField.SETRANGE("Synchro Group Table Alias","Synchro Group Table Alias");
               IF NOT lSynchroGroupTableField.ISEMPTY THEN
                 lSynchroGroupTableField.DELETEALL;

               lSynchroGroupTableFilter.SETRANGE("Synchro Group Code","Synchro Group Code");
               lSynchroGroupTableFilter.SETRANGE("To Synchronize Table No.","To Synchronize Table No.");
               lSynchroGroupTableFilter.SETRANGE("Synchro Group Table Alias","Synchro Group Table Alias");
               IF NOT lSynchroGroupTableFilter.ISEMPTY THEN
                 lSynchroGroupTableFilter.DELETEALL;
             END;

    CaptionML=[ENU=Synchro Group Table;
               FRA=Table de groupe de synchro];
    LookupFormID=Form8016733;
    DrillDownFormID=Form8016733;
  }
  FIELDS
  {
    { 1   ;   ;Synchro Group Code  ;Code20        ;TableRelation="EAI Synchro Group";
                                                   CaptionML=[ENU=Synchro Group Code;
                                                              FRA=Code groupe synchro] }
    { 2   ;   ;To Synchronize Table No.;Integer   ;TableRelation="EAI To Synchronize Table";
                                                   CaptionML=[ENU=To Synchronize Table No.;
                                                              FRA=Nø table … synchroniser] }
    { 3   ;   ;Synchro Group Table Alias;Code20   ;CaptionML=[ENU=Synchro Group Table Alias;
                                                              FRA=Alias table de groupe de synchro] }
    { 4   ;   ;On Insert           ;Option        ;CaptionML=[ENU=On Insert;
                                                              FRA=Sur insertion];
                                                   OptionCaptionML=[ENU=No,Yes;
                                                                    FRA=Non,Oui];
                                                   OptionString=No,Yes }
    { 5   ;   ;On Modify           ;Option        ;CaptionML=[ENU=On Modify;
                                                              FRA=Sur modification];
                                                   OptionCaptionML=[ENU=No,Selected Fields,All Fields;
                                                                    FRA=Non,Certains champs,Tous les champs];
                                                   OptionString=No,Selected Fields,All Fields }
    { 6   ;   ;On Delete           ;Option        ;CaptionML=[ENU=On Delete;
                                                              FRA=Sur suppression];
                                                   OptionCaptionML=[ENU=No,Yes;
                                                                    FRA=Non,Oui];
                                                   OptionString=No,Yes }
    { 7   ;   ;On Rename           ;Option        ;CaptionML=[ENU=On Rename;
                                                              FRA=Sur renommage];
                                                   OptionCaptionML=[ENU=No,Rename,Delete/Insert;
                                                                    FRA=Non,Renommage,Suppression/Insertion];
                                                   OptionString=No,Rename,Delete/Insert }
    { 8   ;   ;Table Name          ;Text100       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObj."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                  Object ID=FIELD(To Synchronize Table No.)));
                                                   CaptionML=[ENU=Table Caption;
                                                              FRA=Caption table] }
    { 10  ;   ;Exist Filters       ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("EAI Synchro Group Table Filter" WHERE (Synchro Group Code=FIELD(Synchro Group Code),
                                                                                                             To Synchronize Table No.=FIELD(To Synchronize Table No.),
                                                                                                             Synchro Group Table Alias=FIELD(Synchro Group Table Alias)));
                                                   CaptionML=[ENU=Filters Exist;
                                                              FRA=Filtres];
                                                   Editable=No }
    { 11  ;   ;Fields Number       ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("EAI Synchro Group Table Field" WHERE (Synchro Group Code=FIELD(Synchro Group Code),
                                                                                                            To Synchronize Table No.=FIELD(To Synchronize Table No.),
                                                                                                            Synchro Group Table Alias=FIELD(Synchro Group Table Alias)));
                                                   CaptionML=[ENU=Fields Number;
                                                              FRA=Nombre de champs];
                                                   Editable=No }
    { 12  ;   ;Filters Number      ;Integer       ;FieldClass=FlowField;
                                                   CalcFormula=Count("EAI Synchro Group Table Filter" WHERE (Synchro Group Code=FIELD(Synchro Group Code),
                                                                                                             To Synchronize Table No.=FIELD(To Synchronize Table No.),
                                                                                                             Synchro Group Table Alias=FIELD(Synchro Group Table Alias)));
                                                   CaptionML=[ENU=Filters Number;
                                                              FRA=Nombre de filtres];
                                                   Editable=No }
    { 13  ;   ;EAI Act Type Code   ;Code20        ;TableRelation="EAI Act Type";
                                                   CaptionML=[ENU=Act Type Code;
                                                              FRA=Code type acte] }
    { 14  ;   ;EAI Act Code        ;Code20        ;TableRelation="EAI Act".Code WHERE (EAI Act Type Code=FIELD(EAI Act Type Code));
                                                   CaptionML=[ENU=Act Code;
                                                              FRA=Code acte] }
  }
  KEYS
  {
    {    ;Synchro Group Code,To Synchronize Table No.,Synchro Group Table Alias;
                                                   Clustered=Yes }
    {    ;To Synchronize Table No.                 }
    {    ;EAI Act Type Code,EAI Act Code           }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE LookupFilters@1100481001();
    VAR
      lSynchroGroupTableFilter@1100481000 : Record 8016712;
    BEGIN
      lSynchroGroupTableFilter.SETRANGE("Synchro Group Code","Synchro Group Code");
      lSynchroGroupTableFilter.SETRANGE("To Synchronize Table No.","To Synchronize Table No.");
      lSynchroGroupTableFilter.SETRANGE("Synchro Group Table Alias","Synchro Group Table Alias");

      FORM.RUNMODAL(FORM::"EAI Synchro Table Filter List",lSynchroGroupTableFilter);
    END;

    PROCEDURE LookupLinks@1180044000();
    VAR
      lSynchroGroupTableLinks@1100481000 : Record 8016754;
    BEGIN
      //EAI02
      lSynchroGroupTableLinks.SETRANGE("Synchro Group Code","Synchro Group Code");
      lSynchroGroupTableLinks.SETRANGE("Source Synchro. Table No.","To Synchronize Table No.");
      lSynchroGroupTableLinks.SETRANGE("Source Synchro. Table Alias","Synchro Group Table Alias");

      FORM.RUNMODAL(0,lSynchroGroupTableLinks);
    END;

    PROCEDURE AssistEditFields@1100481002();
    VAR
      lField@1100481000 : Record 2000000041;
      lTableFieldsSelect@1100481001 : Form 8016797;
    BEGIN
      lField.FILTERGROUP(2);
      lField.SETRANGE(TableNo,"To Synchronize Table No.");
      lField.FILTERGROUP(0);

      lTableFieldsSelect.SetCurrentSynchroGroup("Synchro Group Code","To Synchronize Table No.","Synchro Group Table Alias");
      lTableFieldsSelect.SETTABLEVIEW(lField);
      lTableFieldsSelect.RUN;
    END;

    PROCEDURE SetupNewRecord@1180044001();
    VAR
      lEAISynchroGroup@1180044000 : Record 8016709;
    BEGIN
      //EAI01
      IF "Synchro Group Code" <> '' THEN
      BEGIN
        lEAISynchroGroup.GET("Synchro Group Code");
        IF lEAISynchroGroup."On Insert" THEN
          "On Insert" := "On Insert"::Yes;
        IF lEAISynchroGroup."On Modify" THEN
          "On Modify" := "On Modify"::"All Fields";
        IF lEAISynchroGroup."On Delete" THEN
          "On Delete" := "On Delete"::Yes;
        IF lEAISynchroGroup."On Rename" THEN
          "On Rename" := "On Rename"::"Delete/Insert";
        "EAI Act Type Code" := lEAISynchroGroup."EAI Act Type Code";
        "EAI Act Code" := lEAISynchroGroup."EAI Act Code";
      END
    END;

    BEGIN
    {
      /** @r EAI02 @d 15/11/10 @a ISAT.JR  @v ISAEAI5.01.00.00 @s Ajout LookupLinks */
      /** @r EAI01 @d 30/03/10 @a ISAT.JR  @v ISAEAI5.01.00.00 @s Ajout SetupNewRecord */
      /** @r EAI00 @d 12/11/08 @a ISAT.MGI @v ISAEAI5.01.00.00 @s cr‚ation */
    }
    END.
  }
}