OBJECT Table 8016748 EAI Partner Cross Reference
{
  OBJECT-PROPERTIES
  {
    Date=18/02/14;
    Time=15:34:16;
    Modified=Yes;
    Version List=ISAEAI6.01,ANSAAPMAT;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TouchRecord(TRUE);//<< IMH04.IMH.WGA
             END;

    OnModify=BEGIN
               TouchRecord(FALSE);//<< IMH04.IMH.WGA
             END;

    CaptionML=[ENU=EAI Partner Cross Reference;
               FRA=R‚f‚rences partenaires EAI];
    LookupFormID=Form8016727;
    DrillDownFormID=Form8016727;
  }
  FIELDS
  {
    { 1   ;   ;Table ID            ;Integer       ;CaptionML=[ENU=Table ID;
                                                              FRA=Nø table];
                                                   NotBlank=Yes }
    { 2   ;   ;Source No.          ;Code20        ;TableRelation=IF (Table ID=CONST(9)) Country/Region.Code
                                                                 ELSE IF (Table ID=CONST(324)) "VAT Product Posting Group".Code
                                                                 ELSE IF (Table ID=CONST(5050)) Contact.No. WHERE (Service Contact=CONST(Corporate Customer))
                                                                 ELSE IF (Table ID=CONST(50057)) "Corporate Contract"."Corporate No."
                                                                 ELSE IF (Table ID=FILTER(18|50057)) Customer.No.
                                                                 ELSE IF (Table ID=CONST(5068)) Salutation.Code
                                                                 ELSE IF (Table ID=CONST(5903)) "Service Order Type".Code
                                                                 ELSE IF (Table ID=CONST(5915)) "Fault Area".Code
                                                                 ELSE IF (Table ID=CONST(5917)) "Fault Reason Code".Code
                                                                 ELSE IF (Table ID=CONST(50005)) Specialization.Code
                                                                 ELSE IF (Table ID=CONST(50032)) "Building Trade".No.
                                                                 ELSE IF (Table ID=CONST(50058)) "Corporate Region"."Corporate No."
                                                                 ELSE IF (Table ID=CONST(50066),
                                                                          Field ID=CONST(53)) Code.Code WHERE (Table ID=CONST(5051),
                                                                                                               Field ID=CONST(50070));
                                                   CaptionML=[ENU=Nø;
                                                              FRA=Nø];
                                                   NotBlank=Yes }
    { 3   ;   ;Source No. 2        ;Code10        ;TableRelation=IF (Table ID=CONST(50058)) "Corporate Region"."Region Code" WHERE (Corporate No.=FIELD(Source No.))
                                                                 ELSE IF (Table ID=CONST(50057)) "Corporate Contract"."Contract Form Code" WHERE (Corporate No.=FIELD(Source No.))
                                                                 ELSE Code.Code WHERE (Table ID=FIELD(Table ID),
                                                                                       Field ID=FIELD(Field ID));
                                                   CaptionML=[ENU=No. 2;
                                                              FRA=Nø 2] }
    { 4   ;   ;Source Int          ;Integer       ;CaptionML=[ENU=Source Int;
                                                              FRA=Entier origine] }
    { 8   ;   ;Field ID            ;Integer       ;CaptionML=[ENU=FieldID;
                                                              FRA=ID champ] }
    { 9   ;   ;Table Name          ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(Table ID)));
                                                   CaptionML=[ENU=Table Name;
                                                              FRA=Nom table];
                                                   Editable=No }
    { 10  ;   ;Partner Code        ;Code20        ;TableRelation="EAI Partner".Code WHERE (Reference Partner Code=FILTER(''));
                                                   CaptionML=[ENU=Partner Code;
                                                              FRA=Nø partenaire] }
    { 11  ;   ;Partner Reference   ;Code50        ;CaptionML=[ENU=Partner Reference;
                                                              FRA=R‚f‚rence partenaire] }
    { 50500;  ;Creation Date       ;DateTime      ;CaptionML=[ENU=Creation Date;
                                                              FRA=Date de cr‚ation];
                                                   Editable=No }
    { 50501;  ;Creation User ID    ;Text30        ;CaptionML=[ENU=Creation User ID;
                                                              FRA=Utilisateur cr‚ation];
                                                   Editable=No }
    { 50502;  ;Last Modification Date;DateTime    ;CaptionML=[ENU=Last Modification Date;
                                                              FRA=Date derniŠre modification];
                                                   Editable=No }
    { 50503;  ;Last Modification User ID;Text30   ;CaptionML=[ENU=Last Modification User ID;
                                                              FRA=Utilisateur derniŠre modification];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Table ID,Source No.,Source No. 2,Source Int,Partner Code,Field ID;
                                                   Clustered=Yes }
    {    ;Partner Code,Table ID,Partner Reference  }
    {    ;Table ID,Partner Reference,Partner Code  }
  }
  FIELDGROUPS
  {
  }
  CODE
  {

    PROCEDURE DropFromEAIPartner@1100281001(pPartnerNo@1100281001 : Code[20]);
    VAR
      lEAICrossRef@1100281000 : Record 8016748;
    BEGIN
      IF pPartnerNo = '' THEN
        EXIT;
      lEAICrossRef.SETCURRENTKEY("Partner Code");
      lEAICrossRef.SETRANGE("Partner Code", pPartnerNo);
      IF NOT lEAICrossRef.ISEMPTY THEN
        lEAICrossRef.DELETEALL;
    END;

    PROCEDURE DropFromSourceTable1@1100281000(pSourceTable@1100281000 : Integer;pSourceNo@1100281001 : Code[20]);
    BEGIN
      DropFromSourceTable2(pSourceTable, pSourceNo, '');
    END;

    PROCEDURE DropFromSourceTable2@1100281002(pSourceTable@1100281000 : Integer;pSourceNo@1100281001 : Code[20];pSourceNo2@1100281002 : Code[20]);
    VAR
      lEAICrossRef@1100281003 : Record 8016748;
    BEGIN
      lEAICrossRef.SETRANGE("Table ID", pSourceTable);
      lEAICrossRef.SETRANGE("Source No.", pSourceNo);
      lEAICrossRef.SETRANGE("Source No. 2", pSourceNo2);
      IF NOT lEAICrossRef.ISEMPTY THEN
        lEAICrossRef.DELETEALL;
    END;

    PROCEDURE GetFromCrossRef@1100281003(pForTableID@1100281001 : Integer;pEAIPartnerNo@1100281000 : Code[20];pReferenceNo@1100281002 : Text[30];VAR pCrossReference@1100281003 : Record 8016748) : Boolean;
    VAR
      lEAICrossRef@1100281004 : Record 8016748;
    BEGIN
      IF pReferenceNo = '' THEN
        EXIT(FALSE);
      lEAICrossRef.SETCURRENTKEY("Table ID","Partner Reference","Partner Code");
      lEAICrossRef.SETRANGE("Table ID", pForTableID);
      lEAICrossRef.SETFILTER("Partner Code", '=%1|%2', pEAIPartnerNo, '');
      lEAICrossRef.SETRANGE("Partner Reference", pReferenceNo);
      IF lEAICrossRef.FINDLAST THEN BEGIN
        pCrossReference := lEAICrossRef;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE GetFromCrossRef2@1100281004(pForTableID@1100281001 : Integer;pForFieldID@1100281005 : Integer;pEAIPartnerNo@1100281000 : Code[20];pReferenceNo@1100281002 : Text[30];VAR pCrossReference@1100281003 : Record 8016748) : Boolean;
    VAR
      lEAICrossRef@1100281004 : Record 8016748;
    BEGIN
      IF pReferenceNo = '' THEN
        EXIT(FALSE);
      lEAICrossRef.SETCURRENTKEY("Table ID","Partner Reference","Partner Code");
      lEAICrossRef.SETRANGE("Table ID", pForTableID);
      lEAICrossRef.SETRANGE("Field ID", pForFieldID);
      lEAICrossRef.SETFILTER("Partner Code", '=%1|%2', pEAIPartnerNo, '');
      lEAICrossRef.SETRANGE("Partner Reference", pReferenceNo);
      IF lEAICrossRef.FINDLAST THEN BEGIN
        pCrossReference := lEAICrossRef;
        EXIT(TRUE);
      END;
      EXIT(FALSE);
    END;

    PROCEDURE GetPartnerCrossRef@1100281009() : Boolean;
    VAR
      lEAICrossRef@1100281004 : Record 8016748;
    BEGIN
      IF ("Table ID" = 0) OR ("Source No." = '') THEN
        EXIT(FALSE);
      lEAICrossRef := Rec;
      lEAICrossRef.SETRECFILTER;
      IF lEAICrossRef.FINDFIRST THEN BEGIN
        Rec := lEAICrossRef;
        EXIT(TRUE);
      END
      ELSE BEGIN
        IF "Partner Code" <> '' THEN BEGIN
          lEAICrossRef.SETRANGE("Partner Code", '');
          IF lEAICrossRef.FINDFIRST THEN BEGIN
            Rec := lEAICrossRef;
            EXIT(TRUE);
          END;
        END;
      END;
      EXIT(FALSE);
    END;

    PROCEDURE GetToCrossRef@1100281005(pFromTableID@1100281003 : Integer;pFromFieldNo@1100281004 : Integer;pEAIPartnerNo@1100281002 : Code[20];pSourceNo@1100281001 : Text[30];VAR pCrossReference@1100281000 : Record 8016748) : Boolean;
    VAR
      lEAICrossRef@1100281005 : Record 8016748;
    BEGIN
      // IMH02.ISAT.MA
      IF pSourceNo = '' THEN
        EXIT(FALSE);
      lEAICrossRef.SETCURRENTKEY("Table ID","Partner Reference","Partner Code");
      lEAICrossRef.SETRANGE("Table ID", pFromTableID);
      lEAICrossRef.SETRANGE("Field ID", pFromFieldNo);
      lEAICrossRef.SETFILTER("Partner Code", '=%1|%2', pEAIPartnerNo, '');
      lEAICrossRef.SETRANGE("Source No.", pSourceNo);
      //<<IMH05.ACO
      //IF lEAICrossRef.FINDLAST THEN BEGIN
      IF lEAICrossRef.FINDFIRST THEN BEGIN
      //>>IMH05.ACO
        pCrossReference := lEAICrossRef;
        EXIT(TRUE);
      END;
      EXIT(FALSE);

      //<< IMH04.IMH.WGA
    END;

    PROCEDURE TouchRecord@1100281029(pInsert@1100281000 : Boolean);
    BEGIN
      IF pInsert THEN BEGIN
        "Creation Date" := CURRENTDATETIME;
        "Creation User ID" := USERID;
      END;

      "Last Modification Date" := CURRENTDATETIME;
      "Last Modification User ID" := USERID;
      //>> IMH04.IMH.WGA
    END;

    BEGIN
    {
      /** @r IMH05 @d 18/02/14 @a ISAT.ACO @v IMH6.01.4986 @s Balises manquantes DRT
          @c R‚cup‚ration de la valeur portant l'entier source 0 par d‚faut */
      /** @r IMH04 @d 17/12/13 @a IMH.WGA @v IMH6.01.4649 @s Ajout Colonnes Creation/MAJ @h 4649
          @c Ajout Colonnes Date/Utilisateur Creation/Derniere MAJ */
      /** @r IMH03 @d 13/11/13 @a ISAT.ZW @v IMH6.01.4853 @s ANSRAP MATMUT @h 4853
          @c Correction TableRelation champ nø source pour la table 50057 */
      /** @r IMH02 @d 21/06/11 @a ISAT.MA @v ISAEAI6.01 @s Moteur EAI @c Ajout de la fonction GetToCrossRef() */
      /** @r IMH01 @d 17/03/11 @a ISAT.NFO @v ISAEAI6.01 @s Moteur EAI @c Ajout de la gestion de la table code */
      /** @r ISA01 @d 28/02/11 @a ISAT.ST @v ISAEAI6.01 @s Moteur EAI @c Cr‚ation objet */
    }
    END.
  }
}