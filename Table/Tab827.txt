OBJECT Table 827 DO Payment Credit Card
{
  OBJECT-PROPERTIES
  {
    Date=23/03/11;
    Time=11:08:55;
    Modified=Yes;
    Version List=NAVW16.00.10;
  }
  PROPERTIES
  {
    Permissions=TableData 828=rimd;
    OnInsert=BEGIN
               IF "No." = '' THEN BEGIN
                 PaymentConfig.GET;
                 PaymentConfig.TESTFIELD("Credit Card Nos.");
                 NoSeriesMgt.InitSeries(PaymentConfig."Credit Card Nos.",xRec."No. Series",0D,"No.","No. Series");
               END;

               VALIDATE("Customer No.",GETFILTER("Customer No."));
             END;

    OnDelete=BEGIN
               IF LogEntry.HasTransaction(Rec) THEN
                 ERROR(Text010,TABLECAPTION,"No.");
               DeleteCreditCardNumber;
             END;

    OnRename=BEGIN
               IF LogEntry.HasTransaction(Rec) THEN
                 ERROR(Text010,TABLECAPTION,"No.");
             END;

    CaptionML=[ENU=Dynamics Online Payment Credit Card;
               FRA=Carte de cr‚dit de paiement de Dynamics Online];
    LookupFormID=Form828;
    DrillDownFormID=Form828;
  }
  FIELDS
  {
    { 1   ;   ;No.                 ;Code20        ;CaptionML=[ENU=No.;
                                                              FRA=Nø] }
    { 2   ;   ;Type                ;Code20        ;TableRelation="DO Payment Card Type".Name;
                                                   OnValidate=BEGIN
                                                                IF Type <> xRec.Type THEN
                                                                  DeleteCreditCardNumber;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   NotBlank=Yes }
    { 3   ;   ;Credit Card Number  ;Text20        ;CaptionML=[ENU=Credit Card Number;
                                                              FRA=Num‚ro de carte de cr‚dit] }
    { 4   ;   ;Expiry Date         ;Code4         ;OnValidate=VAR
                                                                IntegerValue@1000 : Integer;
                                                              BEGIN
                                                                IF NOT EVALUATE(IntegerValue,"Expiry Date") OR (STRLEN("Expiry Date") <> 4) THEN
                                                                  ERROR(Text006,FIELDCAPTION("Expiry Date"));

                                                                EVALUATE(IntegerValue,COPYSTR("Expiry Date",1,2));
                                                                IF NOT (IntegerValue IN [1..12]) THEN
                                                                  ERROR(Text007);
                                                              END;

                                                   CaptionML=[ENU=Expiry Date;
                                                              FRA=Date d'expiration];
                                                   NotBlank=Yes }
    { 5   ;   ;Card Holder Name    ;Text50        ;CaptionML=[ENU=Card Holder Name;
                                                              FRA=Nom du d‚tenteur de la carte];
                                                   NotBlank=Yes }
    { 6   ;   ;Customer No.        ;Code20        ;TableRelation=Customer;
                                                   OnValidate=BEGIN
                                                                "Contact No." := FindFirstContactNo;
                                                                ValidateContact;
                                                              END;

                                                   CaptionML=[ENU=Customer No.;
                                                              FRA=Nø client];
                                                   NotBlank=Yes }
    { 7   ;   ;Contact No.         ;Code20        ;OnValidate=BEGIN
                                                                IF "Contact No." <> xRec."Contact No." THEN
                                                                  ValidateContact;
                                                              END;

                                                   OnLookup=VAR
                                                              ContactList@1000 : Form 5052;
                                                            BEGIN
                                                              IF "Contact No." <> '' THEN BEGIN
                                                                Contact.GET("Contact No.");
                                                                Contact.SETRANGE("Company No.",Contact."Company No.");
                                                              END ELSE BEGIN

                                                                IF Contact.GET(FindFirstContactNo) THEN;
                                                                Contact.SETRANGE("Company No.",Contact."Company No.");
                                                              END;
                                                              ContactList.SETTABLEVIEW(Contact);
                                                              ContactList.LOOKUPMODE(TRUE);
                                                              IF ContactList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                ContactList.GETRECORD(Contact);
                                                                VALIDATE("Contact No.",Contact."No.");
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Contact No.;
                                                              FRA=Nø contact];
                                                   NotBlank=Yes }
    { 8   ;   ;Cvc No.             ;Integer       ;CaptionML=[ENU=Cvc No.;
                                                              FRA=Nø CVC] }
    { 10  ;   ;No. Series          ;Code10        ;TableRelation="No. Series";
                                                   CaptionML=[ENU=No. Series;
                                                              FRA=Souches de nø] }
  }
  KEYS
  {
    {    ;No.                                     ;Clustered=Yes }
    {    ;Customer No.                             }
    {    ;Contact No.                              }
  }
  FIELDGROUPS
  {
    { 1   ;DropDown            ;No.,Type,Expiry Date                     }
  }
  CODE
  {
    VAR
      Text006@1010 : TextConst 'ENU=%1 must be 4 characters, for example, 1094 for October, 1994.;FRA=%1 doit contenir 4ÿcaractŠres, par exemple, 1094 pour octobreÿ1994.';
      Text007@1008 : TextConst 'ENU=Please check the month number.;FRA=V‚rifiez le mois.';
      Text010@1013 : TextConst 'ENU=You cannot delete %1 %2 because it has valid transaction log entry.;FRA=Impossible de supprimer %1 %2 car l''‚criture du registre des transactions est valide.';
      PaymentConfig@1000 : Record 826;
      LogEntry@1002 : Record 829;
      Contact@1009 : Record 5050;
      NoSeriesMgt@1001 : Codeunit 396;

    PROCEDURE AssistEdit@2(OldCreditCard@1000 : Record 827) : Boolean;
    VAR
      CreditCard@1001 : Record 827;
    BEGIN
      WITH CreditCard DO BEGIN
        CreditCard := Rec;
        PaymentConfig.GET;
        PaymentConfig.TESTFIELD("Credit Card Nos.");
        IF NoSeriesMgt.SelectSeries(PaymentConfig."Credit Card Nos.",OldCreditCard."No. Series","No. Series") THEN BEGIN
          NoSeriesMgt.SetSeries("No.");
          Rec := CreditCard;
          EXIT(TRUE);
        END;
      END;
    END;

    PROCEDURE SetCreditCardNumber@1(CreditCardNumberText@1000 : Text[30]);
    VAR
      CreditCardNumber@1001 : Record 828;
      CardValidator@1002 : Codeunit 827;
      StarString@1003 : Text[30];
    BEGIN
      CardValidator.ValidateCreditCard(CreditCardNumberText,Type);

      IF CreditCardNumber.GET("No.") THEN BEGIN
        CreditCardNumber.SetData(CreditCardNumberText);
        CreditCardNumber.MODIFY;
      END ELSE BEGIN
        CreditCardNumber.INIT;
        CreditCardNumber."No." := "No.";
        CreditCardNumber.SetData(CreditCardNumberText);
        CreditCardNumber.INSERT;
      END;

      // Set obfuscated card no
      StarString := '';
      "Credit Card Number" :=
        PADSTR(
          StarString,
          STRLEN(CreditCardNumberText) - 4,'*') + COPYSTR(CreditCardNumberText,
          STRLEN(CreditCardNumberText) - 3);
    END;

    PROCEDURE DeleteCreditCardNumber@11();
    VAR
      CardNumber@1000 : Record 828;
    BEGIN
      IF CardNumber.GET("No.") THEN BEGIN
        CardNumber.DELETE;
        "Credit Card Number" := '';
      END;
    END;

    PROCEDURE ValidateContact@10();
    BEGIN
      IF "Contact No." <> '' THEN BEGIN
        Contact.GET("Contact No.");
        IF "Card Holder Name" = '' THEN
          "Card Holder Name" := Contact.Name;
      END;
    END;

    PROCEDURE FindFirstContactNo@9() : Code[20];
    VAR
      ContBusRel@1001 : Record 5054;
    BEGIN
      ContBusRel.SETCURRENTKEY("Link to Table","No.");
      ContBusRel.SETRANGE("Link to Table",ContBusRel."Link to Table"::Customer);
      ContBusRel.SETRANGE("No.","Customer No.");
      IF ContBusRel.FINDFIRST THEN
        EXIT(ContBusRel."Contact No.");
    END;

    PROCEDURE DeleteByContact@12(Contact@1000 : Record 5050);
    VAR
      ContactCreditCards@1001 : Record 827;
    BEGIN
      ContactCreditCards.SETCURRENTKEY("Contact No."); //IMH01.ST
      ContactCreditCards.SETFILTER("Contact No.",Contact."No.");
      //<<IMH01.ST
      IF ContactCreditCards.ISEMPTY THEN EXIT;
      //IF ContactCreditCards.FINDFIRST THEN
      //>>IMH01.ST
        ContactCreditCards.DELETEALL(TRUE);
    END;

    PROCEDURE DeleteByCustomer@13(Customer@1000 : Record 18);
    VAR
      CustomerCreditCards@1001 : Record 827;
    BEGIN
      CustomerCreditCards.SETCURRENTKEY("Customer No."); //IMH01.ST
      CustomerCreditCards.SETFILTER("Customer No.",Customer."No.");
      //<<IMH01.ST
      IF CustomerCreditCards.ISEMPTY THEN EXIT;
      //IF CustomerCreditCards.FINDFIRST THEN
      //>>IMH01.ST
        CustomerCreditCards.DELETEALL(TRUE);
    END;

    PROCEDURE GetCreditCardNumber@3(CreditCardNo@1000 : Code[20]) : Text[20];
    BEGIN
      IF GET(CreditCardNo) THEN
        EXIT("Credit Card Number");

      EXIT('');
    END;

    BEGIN
    {
      /** @r IMH01 @d 23/03/11 @a ISAT.ST @v IMH6.01.138 @s Optimisations
          @c Ajout index "Contact No."
          @c Ajout de ISEMPTY pour ‚viter les locks */
    }
    END.
  }
}