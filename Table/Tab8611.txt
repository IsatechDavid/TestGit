OBJECT Table 8611 Question Area
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnDelete=VAR
               Question@1000 : Record 8612;
             BEGIN
               Question.RESET;
               Question.SETRANGE("Questionnaire Code","Questionnaire Code");
               Question.SETRANGE("Question Area Code",Code);
               Question.DELETEALL;
             END;

    OnRename=VAR
               Question@1000 : Record 8612;
             BEGIN
               ERROR(Text003);
             END;

    CaptionML=[ENU=Question Area;
               FRA=Zone question];
    LookupFormID=Form8613;
  }
  FIELDS
  {
    { 1   ;   ;Questionnaire Code  ;Code10        ;TableRelation="Setup Questionnaire";
                                                   CaptionML=[ENU=Questionnaire Code;
                                                              FRA=Code questionnaire];
                                                   NotBlank=Yes;
                                                   Editable=No }
    { 2   ;   ;Code                ;Code10        ;OnValidate=VAR
                                                                TestValue@1000 : Integer;
                                                              BEGIN
                                                                IF EVALUATE(TestValue,COPYSTR(Code,1,1)) THEN
                                                                  ERROR(Text002);
                                                              END;

                                                   CaptionML=[ENU=Code;
                                                              FRA=Code];
                                                   NotBlank=Yes }
    { 3   ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 4   ;   ;TableID             ;Integer       ;TableRelation=Object.ID WHERE (Type=FILTER(Table));
                                                   OnValidate=VAR
                                                                Question@1000 : Record 8612;
                                                                QuestArea@1001 : Record 8611;
                                                              BEGIN
                                                                IF xRec.TableID <> TableID THEN BEGIN
                                                                  Question.SETRANGE("Questionnaire Code","Questionnaire Code");
                                                                  Question.SETRANGE("Question Area Code",Code);
                                                                  IF Question.FIND('-') THEN
                                                                    ERROR(STRSUBSTNO(Text000,Code));
                                                                  QuestArea.SETRANGE("Questionnaire Code","Questionnaire Code");
                                                                  QuestArea.SETRANGE(TableID,TableID);
                                                                  IF QuestArea.FIND('-') THEN
                                                                    ERROR(STRSUBSTNO(Text001,Rec.TableID));
                                                                END;
                                                                CALCFIELDS("Table Name","Table Caption");
                                                              END;

                                                   OnLookup=VAR
                                                              Object@1000000001 : Record 2000000001;
                                                              Objects@1000000000 : Form 358;
                                                            BEGIN
                                                              Object.SETRANGE(Type,Object.Type::Table);
                                                              Objects.SETTABLEVIEW(Object);
                                                              Objects.LOOKUPMODE := TRUE;
                                                              IF Objects.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                Objects.GETRECORD(Object);
                                                                VALIDATE(TableID, Object.ID);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=TableID;
                                                              FRA=ID table] }
    { 5   ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Name" WHERE (Object Type=CONST(Table),
                                                                                                             Object ID=FIELD(TableID)));
                                                   CaptionML=[ENU=Table Name;
                                                              FRA=Nom table];
                                                   Editable=No }
    { 6   ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=CONST(Table),
                                                                                                                Object ID=FIELD(TableID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              FRA=L‚gende table] }
  }
  KEYS
  {
    {    ;Questionnaire Code,Code                 ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000000001 : TextConst 'ENU=Delete questions for question area %1 to change the table relationship.;FRA=Supprimez les questions pour la zone de question %1 afin de modifier la relation de table.';
      Text001@1000 : TextConst 'ENU=Questions for table %1 already exist.;FRA=Il existe d‚j… des questions pour la table %1.';
      Text002@1001 : TextConst 'ENU=The first character cannot be a numeric value.;FRA=Le premier caractŠre ne peut pas ˆtre un nombre.';
      Text003@1002 : TextConst 'ENU=You cannot rename a Question Area.;FRA=Vous ne pouvez pas renommer une zone de question.';

    BEGIN
    END.
  }
}