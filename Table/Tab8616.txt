OBJECT Table 8616 Migration Table Field
{
  OBJECT-PROPERTIES
  {
    Date=23/01/12;
    Time=15:04:22;
    Modified=Yes;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Migration Table Field;
               FRA=Champ table migration];
  }
  FIELDS
  {
    { 1   ;   ;TableID             ;Integer       ;CaptionML=[ENU=TableID;
                                                              FRA=ID table] }
    { 2   ;   ;FieldID             ;Integer       ;CaptionML=[ENU=FieldID;
                                                              FRA=ID champ] }
    { 3   ;   ;Field Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field.FieldName WHERE (TableNo=FIELD(TableID),
                                                                                             No.=FIELD(FieldID)));
                                                   CaptionML=[ENU=Field Name;
                                                              FRA=Nom de champ] }
    { 4   ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(TableID),
                                                                                                   No.=FIELD(FieldID)));
                                                   CaptionML=[ENU=Field Caption;
                                                              FRA=L‚gende champ] }
    { 5   ;   ;Validate            ;Boolean       ;OnValidate=BEGIN
                                                                CALCFIELDS("Field Caption");
                                                                IF xRec.Validate AND IsKeyField THEN
                                                                  ERROR(Text000,"Field Caption");

                                                                UpdateFieldErrors();
                                                              END;

                                                   CaptionML=[ENU=Validate;
                                                              FRA=Valider] }
    { 6   ;   ;Include             ;Boolean       ;OnValidate=BEGIN
                                                                CALCFIELDS("Field Caption");
                                                                IF xRec.Include AND IsKeyField THEN
                                                                  ERROR(Text000,"Field Caption");

                                                                UpdateFieldErrors();
                                                              END;

                                                   CaptionML=[ENU=Include;
                                                              FRA=Inclure] }
    { 7   ;   ;Localize            ;Boolean       ;CaptionML=[ENU=Localize;
                                                              FRA=Localiser] }
    { 50000;  ;Field Exists        ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist(Field WHERE (TableNo=FIELD(TableID),
                                                                                  No.=FIELD(FieldID)));
                                                   CaptionML=[ENU=Field Exists;
                                                              FRA=Champ existe];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;TableID,FieldID                         ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 is part of primary key and must be included.;FRA=%1 fait partie de la cl‚ primaire et doit ˆtre inclus.';
      Text001@1001 : TextConst 'ENU=%1 value ''%2'' does not exist.;FRA=La valeur %1 ''%2'' n''existe pas.';

    PROCEDURE IsKeyField@2() : Boolean;
    VAR
      FieldSelection@1004 : Record 8616;
      RecRef@1003 : RecordRef;
      KeyRef@1002 : KeyRef;
      FieldRef@1001 : FieldRef;
      KeyFieldCount@1000 : Integer;
    BEGIN
      RecRef.OPEN(TableID);
      KeyRef := RecRef.KEYINDEX(1);
      FOR KeyFieldCount := 1 TO KeyRef.FIELDCOUNT DO BEGIN
        FieldRef := KeyRef.FIELDINDEX(KeyFieldCount);
        IF FieldRef.NUMBER = FieldID THEN
          EXIT(TRUE);
      END;
    END;

    PROCEDURE UpdateFieldErrors@1000();
    VAR
      MigrationMgt@1000 : Codeunit 8611;
      MigrationData@1001 : Record 8615;
      RecRef@1002 : RecordRef;
      FieldRef@1003 : FieldRef;
    BEGIN
      RecRef.OPEN(TableID,TRUE);
      FieldRef := RecRef.FIELD(FieldID);

      MigrationData.SETRANGE(TableID,TableID);
      MigrationData.SETRANGE(FieldID,FieldID);

      IF MigrationData.FIND('-') THEN
        REPEAT
          MigrationMgt.CleanFieldError(MigrationData);
          IF Include THEN BEGIN
            MigrationMgt.FieldError(MigrationData,MigrationMgt.EvaluateValue(FieldRef,MigrationData.Value));
            IF Validate AND NOT MigrationMgt.ValidateTableRelation(FieldRef) THEN BEGIN
              MigrationMgt.FieldError(MigrationData,STRSUBSTNO(Text001,FieldRef.CAPTION,MigrationData.Value));
            END;
          END;
        UNTIL MigrationData.NEXT = 0;
    END;

    BEGIN
    END.
  }
}