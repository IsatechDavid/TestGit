OBJECT Table 8619 Data Template Line
{
  OBJECT-PROPERTIES
  {
    Date=05/11/08;
    Time=12:00:00;
    Version List=NAVW16.00;
  }
  PROPERTIES
  {
    OnInsert=VAR
               DataTemplateHeader@1000 : Record 8618;
             BEGIN
               IF DataTemplateHeader.GET("Data Template Code") THEN
                 TableID := DataTemplateHeader.TableID;
             END;

    CaptionML=[ENU=Data Template Line;
               FRA=Ligne modŠle donn‚es];
  }
  FIELDS
  {
    { 1   ;   ;Data Template Code  ;Code10        ;CaptionML=[ENU=Data Template Code;
                                                              FRA=Code modŠle donn‚es];
                                                   Editable=No }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              FRA=Nø ligne];
                                                   Editable=No }
    { 3   ;   ;Type                ;Option        ;InitValue=Field;
                                                   OnValidate=BEGIN
                                                                CASE Type OF
                                                                  Type::Field : CLEAR(Template);
                                                                  Type::Template : BEGIN
                                                                                     CLEAR("Field Name");
                                                                                     CLEAR(FieldID);
                                                                                   END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=Field,Template;
                                                                    FRA=Champ,ModŠle];
                                                   OptionString=Field,Template }
    { 4   ;   ;FieldID             ;Integer       ;TableRelation=IF (Type=CONST(Field)) Field.No. WHERE (TableNo=FIELD(TableID));
                                                   CaptionML=[ENU=FieldID;
                                                              FRA=ID champ] }
    { 5   ;   ;Field Name          ;Text30        ;FieldClass=Normal;
                                                   OnValidate=VAR
                                                                DataTemplateLine@1000 : Record 8619;
                                                                TemplateMgt@1001 : Codeunit 8612;
                                                              BEGIN
                                                                DataTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                DataTemplateLine.SETRANGE("Field Name","Field Name");
                                                                IF DataTemplateLine.FIND('-') THEN
                                                                  ERROR(STRSUBSTNO(Text004,"Field Name"));

                                                                TemplateMgt.TestHierarchy(Rec);
                                                              END;

                                                   OnLookup=VAR
                                                              DataTemplateHeader@1000000002 : Record 8618;
                                                            BEGIN
                                                              IF Type = Type::Template THEN
                                                                EXIT;

                                                              DataTemplateHeader.GET("Data Template Code");

                                                              IF DataTemplateHeader.TableID = 0 THEN
                                                                EXIT;

                                                              CLEAR(FieldList);
                                                              FieldRec.SETRANGE(FieldRec.TableNo,DataTemplateHeader.TableID);
                                                              FieldList.SETTABLEVIEW(FieldRec);
                                                              FieldList.LOOKUPMODE := TRUE;
                                                              IF FieldList.RUNMODAL = ACTION::LookupOK THEN
                                                              BEGIN
                                                                FieldList.GETRECORD(FieldRec);
                                                                TableID:=FieldRec.TableNo;
                                                                VALIDATE(FieldID,FieldRec."No.");
                                                                VALIDATE("Field Name",FieldRec.FieldName);

                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Field Name;
                                                              FRA=Nom de champ];
                                                   Editable=No }
    { 6   ;   ;TableID             ;Integer       ;CaptionML=[ENU=TableID;
                                                              FRA=ID table] }
    { 7   ;   ;Table Name          ;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FILTER(Table),
                                                                                                                Object ID=FIELD(TableID)));
                                                   CaptionML=[ENU=Table Name;
                                                              FRA=Nom table] }
    { 8   ;   ;Template            ;Code10        ;OnValidate=VAR
                                                                DataTemplateHeader@1000 : Record 8618;
                                                                DataTemplateHeader2@1001 : Record 8618;
                                                                DataTemplateLine@1003 : Record 8619;
                                                                TemplateMgt@1002 : Codeunit 8612;
                                                              BEGIN
                                                                IF (Type <> Type::Template) THEN
                                                                  ERROR(Text005);

                                                                IF Template = "Data Template Code" THEN
                                                                  ERROR(Text000);

                                                                IF DataTemplateHeader.GET(Template) THEN
                                                                  IF DataTemplateHeader2.GET("Data Template Code") THEN
                                                                    IF DataTemplateHeader.TableID <> DataTemplateHeader2.TableID THEN
                                                                      ERROR(STRSUBSTNO(Text002,DataTemplateHeader.Code,DataTemplateHeader2.TableID));

                                                                TemplateMgt.TestHierarchy(Rec);

                                                                DataTemplateLine.SETRANGE("Data Template Code","Data Template Code");
                                                                DataTemplateLine.SETRANGE(Template,Template);
                                                                IF DataTemplateLine.FIND('-') THEN
                                                                  ERROR(STRSUBSTNO(Text003,Template));
                                                              END;

                                                   OnLookup=VAR
                                                              DataTemplateHeader@1002 : Record 8618;
                                                              DataTemplate@1001 : Record 8618;
                                                              DataTemplateList@1000 : Form 8620;
                                                            BEGIN
                                                              IF Type = Type::Field THEN
                                                                EXIT;

                                                              DataTemplateHeader.GET("Data Template Code");

                                                              IF DataTemplateHeader.TableID = 0 THEN
                                                                EXIT;

                                                              DataTemplate.SETRANGE(DataTemplate.TableID,DataTemplateHeader.TableID);
                                                              DataTemplateList.SETTABLEVIEW(DataTemplate);
                                                              DataTemplateList.LOOKUPMODE := TRUE;
                                                              DataTemplateList.EDITABLE := FALSE;
                                                              IF DataTemplateList.RUNMODAL = ACTION::LookupOK THEN BEGIN
                                                                DataTemplateList.GETRECORD(DataTemplate);
                                                                IF DataTemplate.Code = "Data Template Code" THEN
                                                                  ERROR(Text000);
                                                                "Template Description" := DataTemplate.Description;
                                                                VALIDATE(Template,DataTemplate.Code);
                                                              END;
                                                            END;

                                                   CaptionML=[ENU=Template;
                                                              FRA=ModŠle] }
    { 9   ;   ;Template Description;Text30        ;FieldClass=FlowField;
                                                   CalcFormula=Lookup("Data Template Header".Description WHERE (Code=FIELD(Data Template Code)));
                                                   CaptionML=[ENU=Template Description;
                                                              FRA=D‚signation modŠle];
                                                   Editable=No }
    { 10  ;   ;Mandatory           ;Boolean       ;CaptionML=[ENU=Mandatory;
                                                              FRA=Obligatoire] }
    { 11  ;   ;Comment             ;Text30        ;CaptionML=[ENU=Comment;
                                                              FRA=Commentaires] }
    { 12  ;   ;Default Value       ;Text30        ;OnValidate=VAR
                                                                MigrationMgt@1003 : Codeunit 8611;
                                                                RecRef@1002 : RecordRef;
                                                                Field@1001 : FieldRef;
                                                                ValidationError@1000 : Text[250];
                                                              BEGIN
                                                                IF (FieldID <> 0) AND ("Default Value" <> '') THEN BEGIN
                                                                  RecRef.OPEN(TableID,TRUE);
                                                                  Field := RecRef.FIELD(FieldID);
                                                                  ValidationError := MigrationMgt.EvaluateValue(Field,"Default Value");
                                                                  IF ValidationError <> '' THEN
                                                                    ERROR(ValidationError);
                                                                  IF FORMAT(Field.TYPE) <> 'Option' THEN
                                                                    "Default Value" := FORMAT(Field.VALUE)
                                                                  ELSE
                                                                    "Default Value" := MigrationMgt.GetOptionString(Field,"Default Value");

                                                                  IF NOT MigrationMgt.ValidateTableRelation(Field) THEN
                                                                    ERROR(Text001,Field.CAPTION,"Default Value");

                                                                END
                                                              END;

                                                   CaptionML=[ENU=Default Value;
                                                              FRA=Valeur par d‚faut] }
    { 13  ;   ;Table Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(AllObjWithCaption."Object Caption" WHERE (Object Type=FILTER(Table),
                                                                                                                Object ID=FIELD(TableID)));
                                                   CaptionML=[ENU=Table Caption;
                                                              FRA=L‚gende table] }
    { 14  ;   ;Field Caption       ;Text250       ;FieldClass=FlowField;
                                                   CalcFormula=Lookup(Field."Field Caption" WHERE (TableNo=FIELD(TableID),
                                                                                                   No.=FIELD(FieldID)));
                                                   CaptionML=[ENU=Field Caption;
                                                              FRA=L‚gende champ];
                                                   Editable=No }
  }
  KEYS
  {
    {    ;Data Template Code,Line No.             ;Clustered=Yes }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=A template cannot relate to itself.;FRA=Un modŠle ne peut pas ˆtre reli‚ … lui-mˆme.';
      Text001@1001 : TextConst 'ENU=%1 value ''%2'' does not exist.;FRA=%1 valeur ® %2 ¯ n''existe pas.';
      Text002@1002 : TextConst 'ENU=Template %1 does not relate to table %2.;FRA=Le modŠle %1 n''est pas reli‚ … la table %2.';
      Text003@1003 : TextConst 'ENU=Template %1 is already in this hierarchy.;FRA=Le modŠle %1 est d‚j… dans cette hi‚rarchie.';
      Text004@1004 : TextConst 'ENU=Field %1 is already in this template.;FRA=Le champ %1 est d‚j… dans ce modŠle.';
      Text005@1005 : TextConst 'ENU=Template cannot be edited on line type Field.;FRA=Le modŠle ne peut pas ˆtre modifi‚ sur une ligne de type Champ.';
      FieldList@1006 : Form 6218;
      FieldRec@1007 : Record 2000000041;

    BEGIN
    END.
  }
}