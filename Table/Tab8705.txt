OBJECT Table 8705 Mobile Document Queue
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01;
  }
  PROPERTIES
  {
    CaptionML=[ENU=Mobile Document Queue;
               FRA=File d'attente document Mobile];
  }
  FIELDS
  {
    { 1   ;   ;Message ID          ;Code100       ;CaptionML=[ENU=Message ID;
                                                              FRA=ID message] }
    { 2   ;   ;Device ID           ;Code100       ;CaptionML=[ENU=Device ID;
                                                              FRA=ID p‚riph‚rique] }
    { 3   ;   ;Mobile User ID      ;Text65        ;TableRelation="Mobile User";
                                                   CaptionML=[ENU=Mobile User ID;
                                                              FRA=Code utilisateur Mobile] }
    { 4   ;   ;Document Type       ;Text50        ;TableRelation="Mobile Document Type";
                                                   CaptionML=[ENU=Document Type;
                                                              FRA=Type document] }
    { 5   ;   ;Process Type        ;Option        ;CaptionML=[ENU=Process Type;
                                                              FRA=Type de processus];
                                                   OptionCaptionML=[ENU=Queue,Direct;
                                                                    FRA=File d'attente,Direct];
                                                   OptionString=Queue,Direct }
    { 6   ;   ;Status              ;Option        ;CaptionML=[ENU=Status;
                                                              FRA=Statut];
                                                   OptionCaptionML=[ENU=New,Processing,Completed,Error;
                                                                    FRA=Nouveau,En cours de traitement,R‚alis‚,Erreur];
                                                   OptionString=New,Processing,Completed,Error }
    { 7   ;   ;Created Date/Time   ;DateTime      ;CaptionML=[ENU=Created Date/Time;
                                                              FRA=Date/heure cr‚ation];
                                                   Editable=Yes }
    { 8   ;   ;Answer Date/Time    ;DateTime      ;CaptionML=[ENU=Answer Date/Time;
                                                              FRA=Date/heure r‚ponse] }
    { 9   ;   ;Result Date/Time    ;DateTime      ;CaptionML=[ENU=Result Date/Time;
                                                              FRA=Date/heure r‚sultat] }
    { 10  ;   ;Request XML         ;BLOB          ;Compressed=No;
                                                   CaptionML=[ENU=Request XML;
                                                              FRA=Requˆte XML] }
    { 11  ;   ;Answer XML          ;BLOB          ;Compressed=No;
                                                   CaptionML=[ENU=Answer XML;
                                                              FRA=R‚ponse XML] }
    { 12  ;   ;Result XML          ;BLOB          ;Compressed=No;
                                                   CaptionML=[ENU=Result XML;
                                                              FRA=R‚sultat XML] }
    { 13  ;   ;NAS User ID         ;Code20        ;TableRelation=User;
                                                   OnLookup=VAR
                                                              LoginMgt@1000 : Codeunit 418;
                                                            BEGIN
                                                              LoginMgt.LookupUserID("NAS User ID");
                                                            END;

                                                   TestTableRelation=No;
                                                   CaptionML=[ENU=NAS User ID;
                                                              FRA=Code utilisateur NAS] }
    { 14  ;   ;NAS Host Name       ;Text65        ;CaptionML=[ENU=NAS Host Name;
                                                              FRA=Nom d'h“te NAS] }
    { 15  ;   ;NAS Port            ;Integer       ;CaptionML=[ENU=NAS Port;
                                                              FRA=Port NAS] }
  }
  KEYS
  {
    {    ;Message ID                              ;Clustered=Yes }
    {    ;Status,Process Type,Created Date/Time    }
    {    ;Created Date/Time,Status,Document Type,Mobile User ID }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=No XML is available.;FRA=Aucun XML disponible.';
      Text001@1001 : TextConst 'ENU=Only documents with the status New or Error can be processed manually.;FRA=Seuls les documents pr‚sentant le statut Nouveau ou Erreur peuvent ˆtre trait‚s manuellement.';
      Text002@1002 : TextConst 'ENU=Only documents with the status Processing can be reset.;FRA=Seuls les documents pr‚sentant le statut En cours de traitement peuvent ˆtre r‚tablis.';
      Text003@1003 : TextConst 'ENU=Only documents with the status New, Completed or Error can be deleted.;FRA=Seuls les documents pr‚sentant le statut Nouveau, Termin‚ ou Erreur peuvent ˆtre supprim‚s.';
      Text004@1006 : TextConst 'ENU=Import request document;FRA=Importer le document de requˆte';
      Text005@1005 : TextConst 'ENU=XML Request (*.xml)|*.xml|All files (*.*)|*.*;FRA=Requˆte XML (*.xml)|*.xml|Tous les fichiers (*.*)|*.*';
      Text007@1007 : TextConst 'ENU=Do you want to replace the request document for %1 %2?;FRA=Souhaitez-vous remplacer le document de requˆte pour %1 %2 ?';
      Text008@1008 : TextConst 'ENU=Only request documents with the status New or Error can be replaced.;FRA=Seuls les documents de requˆte pr‚sentant le statut Nouveau ou Erreur peuvent ˆtre remplac‚s.';
      Text009@1102601000 : TextConst 'ENU=Default.xml;FRA=Default.xml';

    PROCEDURE ProcessDocument@1(VAR MobDocQueue@1001 : Record 8705);
    VAR
      MobDocMgt@1000 : Codeunit 8701;
    BEGIN
      WITH MobDocQueue DO
        IF Status IN [Status::Error,Status::New] THEN BEGIN
          MobDocMgt.ProcessDocumentManual(MobDocQueue);
        END ELSE
          ERROR(Text001);
    END;

    PROCEDURE ResetDocument@5(VAR MobDocQueue@1001 : Record 8705);
    VAR
      MobDocMgt@1000 : Codeunit 8701;
    BEGIN
      WITH MobDocQueue DO
        IF Status = Status::Processing THEN BEGIN
          MobDocMgt.ResetDocument(MobDocQueue);
        END ELSE
          ERROR(Text002);
    END;

    PROCEDURE DeleteDocument@2(VAR MobDocQueue@1001 : Record 8705);
    VAR
      MobDocMgt@1000 : Codeunit 8701;
    BEGIN
      WITH MobDocQueue DO
        IF Status IN [Status::New,Status::Error,Status::Completed] THEN BEGIN
          MobDocMgt.DeleteDocument(MobDocQueue);
        END ELSE
          ERROR(Text003);
    END;

    PROCEDURE ShortUserID@10(MobileUserID@1000 : Text[65]) : Code[20];
    BEGIN
      IF STRPOS(MobileUserID,'\') IN [0,STRLEN(MobileUserID)] THEN
        IF STRLEN(MobileUserID) <= 20 THEN
          EXIT(MobileUserID)
        ELSE
          EXIT('')
      ELSE
        EXIT(COPYSTR(MobileUserID,STRPOS(MobileUserID,'\') + 1,20));
    END;

    PROCEDURE ShowXMLRequestDoc@15();
    VAR
      XMLDom@1000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
      FileName@1102601000 : Text[1024];
      ToFile@1102601001 : Text[1024];
    BEGIN
      IF LoadXMLRequestDoc(XMLDom) THEN BEGIN
        FileName := TEMPORARYPATH + 'Request.xml';
        XMLDom.save(FileName);
        IF NOT ISSERVICETIER THEN BEGIN
          HYPERLINK(FileName);
        END ELSE BEGIN
          ToFile := Text009;
          DOWNLOAD(FileName,'','','XML (*.xml)|*.xml',ToFile);
        END;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE ShowXMLAnswerDoc@14();
    VAR
      XMLDom@1000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
      FileName@1102601001 : Text[1024];
      ToFile@1102601000 : Text[1024];
    BEGIN
      IF LoadXMLAnswerDoc(XMLDom) THEN BEGIN
        FileName := TEMPORARYPATH + 'Answer.xml';
        XMLDom.save(FileName);
        IF NOT ISSERVICETIER THEN BEGIN
          HYPERLINK(FileName);
        END ELSE BEGIN
          ToFile := Text009;
          DOWNLOAD(FileName,'','','XML (*.xml)|*.xml',ToFile);
        END;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE ShowXMLResultDoc@12();
    VAR
      XMLDom@1000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
      FileName@1102601001 : Text[1024];
      ToFile@1102601000 : Text[1024];
    BEGIN
      IF LoadXMLResultDoc(XMLDom) THEN BEGIN
        FileName := TEMPORARYPATH + 'Result.xml';
        XMLDom.save(FileName);
        IF NOT ISSERVICETIER THEN BEGIN
          HYPERLINK(FileName);
        END ELSE BEGIN
          ToFile := Text009;
          DOWNLOAD(FileName,'','','XML (*.xml)|*.xml',ToFile);
        END;
      END ELSE
        ERROR(Text000);
    END;

    PROCEDURE LoadXMLRequestDoc@11(VAR XMLDom@1002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60") : Boolean;
    VAR
      XMLInStream@1102601000 : InStream;
    BEGIN
      IF NOT ISCLEAR(XMLDom) THEN
        CLEAR(XMLDom);

      CALCFIELDS("Request XML");
      IF "Request XML".HASVALUE THEN BEGIN
        "Request XML".CREATEINSTREAM(XMLInStream);
        CREATE(XMLDom);
        EXIT(XMLDom.load(XMLInStream));
      END;
      EXIT(FALSE);
    END;

    PROCEDURE LoadXMLAnswerDoc@6(VAR XMLDom@1002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60") : Boolean;
    VAR
      XMLInStream@1102601000 : InStream;
    BEGIN
      IF NOT ISCLEAR(XMLDom) THEN
        CLEAR(XMLDom);

      CALCFIELDS("Answer XML");
      IF "Answer XML".HASVALUE THEN BEGIN
        "Answer XML".CREATEINSTREAM(XMLInStream);
        CREATE(XMLDom);
        EXIT(XMLDom.load(XMLInStream));
      END;
      EXIT(FALSE);
    END;

    PROCEDURE LoadXMLResultDoc@9(VAR XMLDom@1002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60") : Boolean;
    VAR
      XMLInStream@1001 : InStream;
    BEGIN
      IF NOT ISCLEAR(XMLDom) THEN
        CLEAR(XMLDom);

      CALCFIELDS("Result XML");
      IF "Result XML".HASVALUE THEN BEGIN
        "Result XML".CREATEINSTREAM(XMLInStream);
        CREATE(XMLDom);
        EXIT(XMLDom.load(XMLInStream));
      END;
      EXIT(FALSE);
    END;

    PROCEDURE SaveXMLRequestDoc@4(VAR XMLDom@1002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60") : Boolean;
    VAR
      XMLOutStream@1102601000 : OutStream;
    BEGIN
      IF ISCLEAR(XMLDom) THEN
        EXIT(FALSE);

      "Request XML".CREATEOUTSTREAM(XMLOutStream);
      XMLDom.save(XMLOutStream);
      "Created Date/Time" := CURRENTDATETIME;
      EXIT(TRUE);
    END;

    PROCEDURE SaveXMLAnswerDoc@8(VAR XMLDom@1002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60") : Boolean;
    VAR
      XMLOutStream@1001 : OutStream;
    BEGIN
      IF ISCLEAR(XMLDom) THEN
        EXIT(FALSE);

      "Answer XML".CREATEOUTSTREAM(XMLOutStream);
      XMLDom.save(XMLOutStream);
      "Answer Date/Time" := CURRENTDATETIME;
      EXIT(TRUE);
    END;

    PROCEDURE SaveXMLResultDoc@3(VAR XMLDom@1002 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60") : Boolean;
    VAR
      MobDocMgt@1003 : Codeunit 8701;
      XMLOutStream@1001 : OutStream;
      XMLAnswerDoc@1000 : Automation "{F5078F18-C551-11D3-89B9-0000F81FE221} 6.0:{88D96A05-F192-11D4-A65F-0040963251E5}:'Microsoft XML, v6.0'.DOMDocument60";
    BEGIN
      IF ISCLEAR(XMLDom) THEN
        EXIT(FALSE);

      "Result XML".CREATEOUTSTREAM(XMLOutStream);
      XMLDom.save(XMLOutStream);
      "Result Date/Time" := CURRENTDATETIME;

      IF NOT LoadXMLAnswerDoc(XMLAnswerDoc) THEN BEGIN
        IF "Process Type" = "Process Type"::Queue THEN
          MobDocMgt.CreateXMLResponseDocReceived(XMLAnswerDoc,"Message ID")
        ELSE
          XMLAnswerDoc := XMLDom;
        SaveXMLAnswerDoc(XMLAnswerDoc);

      END;
      EXIT(TRUE);
    END;

    PROCEDURE ImporXMLRequestDoc@7();
    VAR
      BlobRef@1000 : Record 99008535;
      RBMgt@1001 : Codeunit 419;
    BEGIN
      IF NOT (Status IN [Status::New,Status::Error]) THEN
        ERROR(Text008);
      IF NOT CONFIRM(Text007,FALSE,FIELDCAPTION("Message ID"),"Message ID") THEN
        EXIT;
      CLEAR("Request XML");
      RBMgt.BLOBImport(BlobRef,'',TRUE);
      "Request XML" := BlobRef.Blob;
      MODIFY;
    END;

    PROCEDURE ExporXMLRequestDoc@18();
    VAR
      BlobRef@1000 : Record 99008535;
      RBMgt@1001 : Codeunit 419;
    BEGIN
      CALCFIELDS("Request XML");
      IF "Request XML".HASVALUE THEN BEGIN
        BlobRef.Blob:= "Request XML";
        RBMgt.BLOBExport(BlobRef,Text009,TRUE);
      END;
    END;

    BEGIN
    END.
  }
}