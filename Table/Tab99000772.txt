OBJECT Table 99000772 Production BOM Line
{
  OBJECT-PROPERTIES
  {
    Date=14/08/09;
    Time=12:00:00;
    Version List=NAVW16.00.01,KITNA6.00;
  }
  PROPERTIES
  {
    OnInsert=BEGIN
               TestStatus;
             END;

    OnModify=BEGIN
               IF Type <> Type::" " THEN
                 TestStatus;
             END;

    OnDelete=VAR
               ProdBOMComment@1000 : Record 99000776;
               PlanningAssignment@1001 : Record 99000850;
             BEGIN
               IF Type <> Type::" " THEN BEGIN
                 TestStatus;
                 IF Type = Type::Item THEN
                   PlanningAssignment.AssignPlannedOrders("No.",FALSE)
                 ELSE
                   IF Type = Type::"Production BOM" THEN
                     PlanningAssignment.OldBom("No.");
               END;

               ProdBOMComment.SETRANGE("Production BOM No.","Production BOM No.");
               ProdBOMComment.SETRANGE("BOM Line No.","Line No.");
               ProdBOMComment.SETRANGE("Version Code","Version Code");
               ProdBOMComment.DELETEALL;
             END;

    CaptionML=[ENU=Production BOM Line;
               FRA=Ligne nomenclature production];
  }
  FIELDS
  {
    { 1   ;   ;Production BOM No.  ;Code20        ;TableRelation="Production BOM Header";
                                                   CaptionML=[ENU=Production BOM No.;
                                                              FRA=Nø nomenclature production];
                                                   NotBlank=Yes }
    { 2   ;   ;Line No.            ;Integer       ;CaptionML=[ENU=Line No.;
                                                              FRA=Nø ligne] }
    { 3   ;   ;Version Code        ;Code10        ;TableRelation="Production BOM Version"."Version Code" WHERE (Production BOM No.=FIELD(Production BOM No.));
                                                   CaptionML=[ENU=Version Code;
                                                              FRA=Code version] }
    { 10  ;   ;Type                ;Option        ;OnValidate=BEGIN
                                                                TestStatus;

                                                                ProdBOMHeader.GET("Production BOM No.");
                                                                IF ((ProdBOMHeader.Type = ProdBOMHeader.Type::Manufacturing) AND
                                                                    (Type IN [Type::Resource,Type::"Setup Resource"])) OR
                                                                   ((ProdBOMHeader.Type = ProdBOMHeader.Type::Kitting) AND
                                                                    (Type = Type::"Production BOM"))
                                                                THEN
                                                                  ERROR(
                                                                    Text25000,
                                                                    FIELDCAPTION(Type),
                                                                    Type,
                                                                    ProdBOMHeader.FIELDCAPTION(Type),ProdBOMHeader.Type,
                                                                    ProdBOMHeader.TABLECAPTION);

                                                                xRec.Type := Type;

                                                                INIT;
                                                                Type := xRec.Type;
                                                              END;

                                                   CaptionML=[ENU=Type;
                                                              FRA=Type];
                                                   OptionCaptionML=[ENU=" ,Item,Production BOM,Resource,Setup Resource";
                                                                    FRA=" ,Article,Nomenclature production,Ressource,Ressource paramŠtres"];
                                                   OptionString=[ ,Item,Production BOM,Resource,Setup Resource] }
    { 11  ;   ;No.                 ;Code20        ;TableRelation=IF (Type=CONST(Item)) Item
                                                                 ELSE IF (Type=CONST(Production BOM)) "Production BOM Header"
                                                                 ELSE IF (Type=FILTER(Resource|Setup Resource)) Resource;
                                                   OnValidate=BEGIN
                                                                TESTFIELD(Type);

                                                                TestStatus;

                                                                CASE Type OF
                                                                  Type::Item:
                                                                    BEGIN
                                                                      Item.GET("No.");
                                                                      Description := Item.Description;
                                                                      Item.TESTFIELD("Base Unit of Measure");
                                                                      "Unit of Measure Code" := Item."Base Unit of Measure";
                                                                      IF "No." <> xRec."No." THEN
                                                                        "Variant Code" := '';
                                                                    END;
                                                                  Type::"Production BOM":
                                                                    BEGIN
                                                                      ProdBOMHeader.GET("No.");
                                                                      ProdBOMHeader.TESTFIELD("Unit of Measure Code");
                                                                      Description := ProdBOMHeader.Description;
                                                                      "Unit of Measure Code" := ProdBOMHeader."Unit of Measure Code";
                                                                    END;
                                                                  Type::Resource, Type::"Setup Resource" :
                                                                    BEGIN
                                                                      Res.GET("No.");
                                                                      Description := Res.Name;
                                                                      Res.TESTFIELD("Base Unit of Measure");
                                                                      "Unit of Measure Code" := Res."Base Unit of Measure";
                                                                    END;
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=No.;
                                                              FRA=Nø] }
    { 12  ;   ;Description         ;Text30        ;CaptionML=[ENU=Description;
                                                              FRA=D‚signation] }
    { 13  ;   ;Unit of Measure Code;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Unit of Measure".Code WHERE (Item No.=FIELD(No.))
                                                                 ELSE IF (Type=CONST(Production BOM)) "Unit of Measure"
                                                                 ELSE IF (Type=FILTER(Resource|Setup Resource)) "Resource Unit of Measure".Code WHERE (Resource No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF NOT (Type IN [Type::Item,Type::Resource,Type::"Setup Resource"]) THEN
                                                                  ERROR(
                                                                    Text25001,
                                                                    FIELDCAPTION(Type),
                                                                    Type::Item,Type::Resource,Type::"Setup Resource",
                                                                    FIELDCAPTION("Unit of Measure Code"));

                                                                TESTFIELD("No.");
                                                              END;

                                                   CaptionML=[ENU=Unit of Measure Code;
                                                              FRA=Code unit‚] }
    { 14  ;   ;Quantity            ;Decimal       ;CaptionML=[ENU=Quantity;
                                                              FRA=Quantit‚];
                                                   DecimalPlaces=0:5;
                                                   Editable=No }
    { 15  ;   ;Position            ;Code10        ;CaptionML=[ENU=Position;
                                                              FRA=Position] }
    { 16  ;   ;Position 2          ;Code10        ;CaptionML=[ENU=Position 2;
                                                              FRA=Position 2] }
    { 17  ;   ;Position 3          ;Code10        ;CaptionML=[ENU=Position 3;
                                                              FRA=Position 3] }
    { 18  ;   ;Production Lead Time;DateFormula   ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                              END;

                                                   CaptionML=[ENU=Production Lead Time;
                                                              FRA=D‚lai de fabrication] }
    { 19  ;   ;Routing Link Code   ;Code10        ;TableRelation="Routing Link";
                                                   OnValidate=BEGIN
                                                                IF "Routing Link Code" <> '' THEN BEGIN
                                                                  TESTFIELD(Type,Type::Item);
                                                                  TESTFIELD("No.");
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Routing Link Code;
                                                              FRA=Code lien gamme] }
    { 20  ;   ;Scrap %             ;Decimal       ;OnValidate=BEGIN
                                                                TESTFIELD("No.");
                                                              END;

                                                   CaptionML=[ENU=Scrap %;
                                                              FRA=% perte];
                                                   DecimalPlaces=0:5;
                                                   MaxValue=100;
                                                   BlankNumbers=BlankNeg }
    { 21  ;   ;Variant Code        ;Code10        ;TableRelation=IF (Type=CONST(Item)) "Item Variant".Code WHERE (Item No.=FIELD(No.));
                                                   OnValidate=BEGIN
                                                                IF "Variant Code" = '' THEN
                                                                  EXIT;
                                                                TESTFIELD(Type,Type::Item);
                                                                TESTFIELD("No.");
                                                                ItemVariant.GET("No.","Variant Code");
                                                                Description := ItemVariant.Description;
                                                              END;

                                                   CaptionML=[ENU=Variant Code;
                                                              FRA=Code variante] }
    { 22  ;   ;Comment             ;Boolean       ;FieldClass=FlowField;
                                                   CalcFormula=Exist("Production BOM Comment Line" WHERE (Production BOM No.=FIELD(Production BOM No.),
                                                                                                          Version Code=FIELD(Version Code),
                                                                                                          BOM Line No.=FIELD(Line No.)));
                                                   CaptionML=[ENU=Comment;
                                                              FRA=Commentaires];
                                                   Editable=No }
    { 28  ;   ;Starting Date       ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                IF "Starting Date" > 0D THEN
                                                                  VALIDATE("Ending Date");
                                                              END;

                                                   CaptionML=[ENU=Starting Date;
                                                              FRA=Date d‚but] }
    { 29  ;   ;Ending Date         ;Date          ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                IF ("Ending Date" > 0D) AND
                                                                   ("Starting Date" > 0D) AND
                                                                   ("Starting Date" > "Ending Date")
                                                                THEN
                                                                  ERROR(
                                                                    Text000,
                                                                    FIELDCAPTION("Ending Date"),
                                                                    FIELDCAPTION("Starting Date"));
                                                              END;

                                                   CaptionML=[ENU=Ending Date;
                                                              FRA=Date fin] }
    { 40  ;   ;Length              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Length;
                                                              FRA=Longueur];
                                                   DecimalPlaces=0:5 }
    { 41  ;   ;Width               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Width;
                                                              FRA=Largeur];
                                                   DecimalPlaces=0:5 }
    { 42  ;   ;Weight              ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Weight;
                                                              FRA=Poids];
                                                   DecimalPlaces=0:5 }
    { 43  ;   ;Depth               ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Depth;
                                                              FRA=Profondeur];
                                                   DecimalPlaces=0:5 }
    { 44  ;   ;Calculation Formula ;Option        ;OnValidate=BEGIN
                                                                TESTFIELD("No.");

                                                                CASE "Calculation Formula" OF
                                                                  "Calculation Formula"::" ":
                                                                    Quantity := "Quantity per";
                                                                  "Calculation Formula"::Length:
                                                                    Quantity := ROUND(Length * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width":
                                                                    Quantity := ROUND(Length * Width * "Quantity per",0.00001);
                                                                  "Calculation Formula"::"Length * Width * Depth":
                                                                    Quantity := ROUND(Length * Width * Depth * "Quantity per",0.00001);
                                                                  "Calculation Formula"::Weight:
                                                                    Quantity := ROUND(Weight * "Quantity per",0.00001);
                                                                END;
                                                              END;

                                                   CaptionML=[ENU=Calculation Formula;
                                                              FRA=Formule de calcul];
                                                   OptionCaptionML=[ENU=" ,Length,Length * Width,Length * Width * Depth,Weight";
                                                                    FRA=" ,Longueur,Longueur * largeur,Longueur * largeur * profondeur,Poids"];
                                                   OptionString=[ ,Length,Length * Width,Length * Width * Depth,Weight] }
    { 45  ;   ;Quantity per        ;Decimal       ;OnValidate=BEGIN
                                                                VALIDATE("Calculation Formula");
                                                              END;

                                                   CaptionML=[ENU=Quantity per;
                                                              FRA=Quantit‚ par];
                                                   DecimalPlaces=0:5 }
  }
  KEYS
  {
    {    ;Production BOM No.,Version Code,Line No.;Clustered=Yes }
    {    ;Type,No.                                 }
  }
  FIELDGROUPS
  {
  }
  CODE
  {
    VAR
      Text000@1000 : TextConst 'ENU=%1 must be later than %2.;FRA=%1 doit ˆtre post‚rieur(e) … %2.';
      Item@1001 : Record 27;
      ProdBOMHeader@1002 : Record 99000771;
      ItemVariant@1003 : Record 5401;
      Text25000@1462000 : TextConst 'ENU=%1 cannot be %2 when %3 is %4 on %5.;FRA=%1 ne peut pas ˆtre %2 lorsque %3 est %4 dans %5.';
      Res@1462001 : Record 156;
      Text25001@1462002 : TextConst 'ENU=%1 must be %2, %3 or %4 to select a %5.;FRA=%1 doit ˆtre %2, %3 ou %4 pour s‚lectionner un %5.';

    PROCEDURE TestStatus@1();
    VAR
      ProdBOMVersion@1000 : Record 99000779;
    BEGIN
      IF "Version Code" = '' THEN BEGIN
        ProdBOMHeader.GET("Production BOM No.");
        IF ProdBOMHeader.Status = ProdBOMHeader.Status::Certified THEN
          ProdBOMHeader.FIELDERROR(Status);
      END ELSE BEGIN
        ProdBOMVersion.GET("Production BOM No.","Version Code");
        IF ProdBOMVersion.Status = ProdBOMVersion.Status::Certified THEN
          ProdBOMVersion.FIELDERROR(Status);
      END;
    END;

    PROCEDURE GetQtyPerUnitOfMeasure@2() : Decimal;
    VAR
      Item@1000 : Record 27;
      UOMMgt@1001 : Codeunit 5402;
    BEGIN
      IF Type = Type::Item THEN BEGIN
        Item.GET("No.");
        EXIT(
          UOMMgt.GetQtyPerUnitOfMeasure(Item,"Unit of Measure Code"));
      END;
      EXIT(1);
    END;

    BEGIN
    END.
  }
}